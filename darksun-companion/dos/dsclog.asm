; DSCLOG.EXE - dice log helper for the Dark Sun companion.
;
; A tiny TSR that stays resident (load it high with LH) and holds a ring
; buffer plus a replacement for the game's Borland rand(). The companion runs a
; patched copy of the game (DSUNLOG.EXE) whose rand() and a few "probe" places
; start with INT instructions; this TSR answers those interrupts
; (VEC_RAND, VEC_SAVE, VEC_AC, VEC_TEXT, VEC_MSG) and the companion reads the ring buffer
; from DOSBox's memory. VEC_CHAR adds THAC0, the saving throws and thief skills to the
; game's inventory screen. VEC_RING_AC and VEC_RING_SAVE make a ring with a plus (the
; companion's Ring +1) better the AC and saving throws of whoever wears it.
;
; STUB produces exactly the numbers the original rand() would
; (seed = seed * 0x015A4E35 + 1, result = (seed >> 16) & 0x7FFF), so the game
; plays the same. For every call it also records who called and the caller's
; stack frame, which holds the dice size, THAC0, target AC and so on.
;
; It is a small .EXE rather than a .COM so that LH can load it into upper
; memory: a .COM asks DOS for a whole 64 KB block, more than DOSBox's UMB has.
;
; Build: nasm -f bin -o DSCLOG.EXE dsclog.asm

BITS 16
CPU 386

VEC_RAND equ 0x60     ; rand() (DSUNLOG.EXE: INT 60h at the start of rand())
VEC_SAVE equ 0x61     ; PROBE_SAVE
VEC_AC   equ 0x62     ; PROBE_AC
VEC_TEXT equ 0x63     ; PROBE_TEXT
VEC_MSG  equ 0x64     ; PROBE_MSG
VEC_CHAR equ 0x65     ; PROBE_CHAR
VEC_TURN equ 0xF1     ; PROBE_TURN (not 66h-6Fh: the game calls those, looking for drivers)
VEC_USE  equ 0xF2     ; PROBE_USE
VEC_VIEW equ 0xF3     ; PROBE_VIEW
VEC_WIN  equ 0xF4     ; PROBE_WIN
VEC_LOOK equ 0xF5     ; PROBE_LOOK
VEC_UNLOOK equ 0xF6   ; PROBE_UNLOOK
VEC_NEXT equ 0xF7     ; PROBE_NEXT
VEC_RING_AC equ 0xF8  ; PROBE_RING_AC
VEC_RING_SAVE equ 0xF9  ; PROBE_RING_SAVE
VEC_WEAPON equ 0xFA   ; PROBE_WEAPON
VEC_MOVE   equ 0xFB   ; PROBE_MOVE
VEC_PICK   equ 0xFC   ; PROBE_PICK
VEC_USE_ITEM equ 0xFD ; PROBE_USE_ITEM
VEC_TWO    equ 0xFE   ; PROBE_TWO
VEC_DOUBLE equ 0xF0   ; PROBE_DOUBLE
VEC_GRACE_CAST equ 0xED    ; PROBE_GRACE_CAST
VEC_GRACE_EFFECT equ 0xEE  ; PROBE_GRACE_EFFECT
VEC_GRACE_ABILITY equ 0xEF ; PROBE_GRACE_ABILITY
VEC_NAMES_SIZE equ 0xEC    ; PROBE_NAMES_SIZE
VEC_NAMES_FILL equ 0xEB    ; PROBE_NAMES_FILL
VEC_STEALTH equ 0xEA       ; PROBE_STEALTH
VEC_TYPES_SIZE equ 0xE9    ; PROBE_TYPES_SIZE
VEC_TYPES_FILL equ 0xE8    ; PROBE_TYPES_FILL
VEC_LEVEL equ 0xE7         ; PROBE_LEVEL
VEC_HD_ROLL equ 0xE6       ; PROBE_HD_ROLL
VEC_HD_CON equ 0xE5        ; PROBE_HD_CON
VEC_THIEF_SKILL equ 0xE4   ; PROBE_THIEF_SKILL
VEC_TWO_HANDED equ 0xE3    ; PROBE_TWO_HANDED
VEC_SPELL_TEXT equ 0xE2    ; PROBE_SPELL_TEXT
VEC_CHUNK_ID equ 0xE1      ; PROBE_CHUNK_ID
VEC_FLOOR_ALL equ 0xE0     ; PROBE_FLOOR (the whole view's floor)
VEC_FLOOR_RECT equ 0xDF    ; PROBE_FLOOR (a rectangle's floor)
VEC_REDRAW equ 0xDE        ; PROBE_REDRAW
VEC_REDRAW_ALL equ 0xDD    ; PROBE_REDRAW_ALL
VEC_SCROLL equ 0xDC        ; PROBE_SCROLL
VEC_HIT    equ 0xDB        ; PROBE_HIT
VEC_ITEM_BOX equ 0xDA      ; PROBE_ITEM_BOX
VEC_BELT equ 0xD9          ; PROBE_BELT
VEC_SAVE_PAGE equ 0xD8     ; PROBE_SAVE_PAGE
VEC_SAVE_CLICK equ 0xD7    ; PROBE_SAVE_CLICK
VEC_ITEM_WEAPON equ 0xD6   ; PROBE_ITEM_WEAPON
VEC_ITEM_SKIP equ 0xD5     ; PROBE_ITEM_SKIP
VEC_ITEM_ARMOUR equ 0xD4   ; PROBE_ITEM_ARMOUR
VEC_SCRIPT_RAND equ 0xD3   ; PROBE_SCRIPT_RAND
VEC_XP_NEXT equ 0xD2       ; PROBE_XP_NEXT
VEC_ATTACKS equ 0xD1       ; PROBE_ATTACKS
VEC_SPEC_DAMAGE equ 0xD0   ; PROBE_SPEC_DAMAGE
VEC_DAM_LINE equ 0xCF      ; PROBE_DAM_LINE
VEC_VIEW_DAM equ 0xCE      ; PROBE_VIEW_DAM
VEC_CAN_USE equ 0xCD       ; PROBE_CAN_USE
VEC_NO_CAST equ 0xCC       ; PROBE_NO_CAST
VEC_MC_ROLL equ 0xCB       ; PROBE_MC_ROLL
VEC_MC_CON equ 0xCA        ; PROBE_MC_CON
VEC_MC_UNCON equ 0xC9      ; PROBE_MC_UNCON
VEC_WP_DISC_WIN equ 0xC8   ; PROBE_WP_DISC_WIN
VEC_WP_SPHERE_WIN equ 0xC7 ; PROBE_WP_SPHERE_WIN
VEC_WP_DISC_CLICK equ 0xC6 ; PROBE_WP_DISC_CLICK
VEC_WP_SPHERE_CLICK equ 0xC5 ; PROBE_WP_SPHERE_CLICK
VEC_WP_SHOWN equ 0xC4      ; PROBE_WP_SHOWN
VEC_WP_CLASS equ 0xC3      ; PROBE_WP_CLASS
VEC_LV_PICK equ 0xC2       ; PROBE_LV_PICK
VEC_PK_COUNT equ 0xC1      ; PROBE_PK_COUNT
VEC_PK_WIN equ 0xC0        ; PROBE_PK_WIN
VEC_PK_LEFT equ 0xBF       ; PROBE_PK_LEFT
VEC_PK_TITLE equ 0xBE      ; PROBE_PK_TITLE
VEC_PK_FILL equ 0xBD       ; PROBE_PK_FILL
VEC_PK_CLICK equ 0xBC      ; PROBE_PK_CLICK
VEC_EF_ROWS equ 0xBB       ; PROBE_EF_ROWS
VEC_HP_BEST equ 0xBA       ; PROBE_HP_BEST
VEC_TOME equ 0xB9          ; PROBE_TOME
VEC_INIT equ 0xB8          ; PROBE_INIT
VEC_THAC0 equ 0xB7         ; PROBE_THAC0
VEC_SLOTS equ 0xB6         ; PROBE_SLOTS
VEC_SLOT_LEVEL equ 0xB5    ; PROBE_SLOT_LEVEL
VEC_PSP_USE equ 0xB4       ; PROBE_PSP_USE
VEC_PSP_TABLE equ 0xB3     ; PROBE_PSP_TABLE
VEC_PSP_DEFENCE equ 0xB2   ; PROBE_PSP_DEFENCE
VEC_CURE equ 0xB1          ; PROBE_CURE
VEC_PSP_KEEP equ 0xB0      ; PROBE_PSP_KEEP
VEC_RANGER_CAST equ 0xAF   ; PROBE_RANGER_CAST
VEC_PSP_KEEP_DX equ 0xAE   ; PROBE_PSP_KEEP_DX
VEC_HIT_ROUND equ 0xAD     ; PROBE_HIT_ROUND
VEC_CAST_LEVEL equ 0xAC    ; PROBE_CAST_LEVEL
VEC_PICK_LEVEL equ 0xAB    ; PROBE_PICK_LEVEL
VEC_PICK_LIST equ 0xAA     ; PROBE_PICK_LIST
VEC_SCROLL_LEARN equ 0xA9  ; PROBE_SCROLL_LEARN
VEC_SPELL_LEVEL equ 0xA8   ; PROBE_SPELL_LEVEL
VEC_PICK_ANY equ 0xA7      ; PROBE_PICK_ANY
VEC_RANGER_LEVEL equ 0xA6  ; PROBE_RANGER_LEVEL
VEC_HIT_DIE equ 0xA5       ; PROBE_HIT_DIE
VEC_MAX_PSP equ 0xA4       ; PROBE_MAX_PSP
VEC_CR_DIE equ 0xA3        ; PROBE_CR_DIE
VEC_CR_PSP equ 0xA2        ; PROBE_CR_PSP
VEC_EL_GRANT equ 0xA1      ; PROBE_EL_GRANT
VEC_EL_CAST equ 0xA0       ; PROBE_EL_CAST
VEC_EL_LEVEL equ 0x9F      ; PROBE_EL_LEVEL
VEC_EL_KNOW equ 0x9E       ; PROBE_EL_KNOW
VEC_DUAL_BAN equ 0x9D      ; PROBE_DUAL_BAN
TSIZE    equ 8192     ; bytes in the text buffer

NENT    equ 96          ; entries in the ring (96: the helper and it fit in upper memory)
ESIZE   equ 192         ; bytes per entry (see ENTRY LAYOUT)
STACK   equ 256         ; bytes of stack while installing

; ---- MZ header: no relocations, and only as much memory as the image needs ----
section mz start=0
        db 'MZ'
        dw file_len % 512                       ; bytes in the last page
        dw (file_len + 511) / 512               ; pages
        dw 0                                    ; relocations
        dw 2                                    ; header size in paragraphs
        dw LOAD_EXTRA                           ; min extra paragraphs: the ring's end past the image
        dw LOAD_EXTRA                           ; max extra paragraphs (so the whole fits in upper memory)
        dw 0                                    ; SS (relative to the image)
        dw image_len + STACK                    ; SP
        dw 0                                    ; checksum
        dw install                              ; IP
        dw 0                                    ; CS (relative to the image)
        dw 0x1C                                 ; relocation table offset
        dw 0                                    ; overlay
        align 32, db 0

section image follows=mz vstart=0

; ---- header, found by the companion via SIG (16-byte aligned) ----
hdr:
sig      db 'DSCLOGvY'          ; +0
seq      dw 0                   ; +8   entries written so far (wraps at 65536)
widx     dw 0                   ; +10  ring slot the next entry goes to
nent     dw NENT                ; +12
esize    dw ESIZE               ; +14
ring_off dw 0                   ; +16  offset of the ring in RING_SEG's segment
stub_off dw stub                ; +18  offset of STUB in this segment
hdr_off  dw hdr                 ; +20  offset of this header in this segment
seed_off dw 0x4122              ; +22  DS offset of the game's 32-bit rand seed
glob     dw 0, 0, 0, 0          ; +24  DS offsets of 4 game words to capture (0 = none)
nfilt    dw NFILT               ; +32  number of filters in use (0 = record every call)
filt:                           ; +34  up to 8 filters: a length (1-8), then that many bytes the
                                ;      calling code must start with to be recorded. By default,
                                ;      "rand()*N/32768" rolls (movsx eax,ax, then N) and the
                                ;      percentile check, so bursts of other randomness
                                ;      (animations) don't crowd out the rolls that matter
        db 7, 0x66,0x0F,0xBF,0xC0,0x66,0x6B,0xC0, 0     ; imul eax,eax,imm8
        db 7, 0x66,0x0F,0xBF,0xC0,0x66,0x69,0xC0, 0     ; imul eax,eax,imm32
        db 7, 0x66,0x0F,0xBF,0xC0,0x66,0xC1,0xE0, 0     ; shl eax,n
        db 8, 0x66,0x0F,0xBF,0xC0,0x66,0x0F,0xBF,0x56   ; N = word [bp+x] (the dice routines)
        db 8, 0xBB,0x64,0x00,0x99,0xF7,0xFB,0x3B,0x56   ; percentile check
NFILT   equ ($ - filt) / 9
        times (8 - NFILT) * 9 db 0
skipped  dw 0                   ; +106 calls not recorded because no filter matched
probe_save_off dw probe_save    ; +108 offset of PROBE_SAVE in this segment
probe_ac_off   dw probe_ac      ; +110 offset of PROBE_AC in this segment
vectors  db VEC_RAND, VEC_SAVE, VEC_AC, VEC_TEXT, VEC_MSG  ; +112 the interrupts the patched game uses
hooked   db 0                   ; +117 1 once the vectors are ours
int_rand_off dw int_rand        ; +118 offset of INT_RAND (VEC_RAND's handler) in this segment
gpl_hook_off dw gpl_hook        ; +120 offset of GPL_HOOK in this segment
gpl_chain dd 0                  ; +122 the game's own hook, which GPL_HOOK passes on to
tpos     dw 0                   ; +126 bytes written to the text buffer so far (wraps at 65536)
tbuf_off dw tbuf                ; +128 offset of the text buffer in this segment
tsize    dw TSIZE               ; +130 its size (a power of two)
probe_text_off dw probe_text    ; +132 offset of PROBE_TEXT in this segment
probe_msg_off dw probe_msg      ; +134 offset of PROBE_MSG in this segment
probe_char_off dw probe_char    ; +136 offset of PROBE_CHAR in this segment
turn_seq  dw 0                  ; +138 turns that have ended in a fight (PROBE_TURN counts them)
reply_seq dw 0                  ; +140 the companion sets this to TURN_SEQ once MSG_BUF is ready
popups_on dw 0                  ; +142 the companion sets 1 to have turn summaries shown
msg_off   dw msg_buf            ; +144 offset of MSG_BUF: the summary, NUL-terminated
ended     dw 0                  ; +146 the combatant whose turn just ended
slots_off dw slots_text         ; +148 offset of SLOTS_TEXT: 4 x SLOTS_SIZE bytes, one per party
                                ;      member, lines separated by "|", NUL-terminated (the companion
                                ;      keeps them up to date); PROBE_USE draws them
look_seq   dw 0                 ; +150 monsters looked at in a fight (PROBE_LOOK counts them)
look_reply dw 0                 ; +152 the companion sets this to LOOK_SEQ once LOOK_TEXT is ready
look_who   dw 0                 ; +154 the combatant looked at
look_off   dw look_text         ; +156 offset of LOOK_TEXT: up to 3 short lines for the Look box,
                                ;      separated by "|", NUL-terminated
look_full_off dw look_full      ; +158 offset of LOOK_FULL: the whole description, shown in the
                                ;      dialogue window afterwards (empty: none)
look_on    dw 0                 ; +160 the companion sets 1 to have monsters described
stats_off  dw stats             ; +162 offset of STATS: for each party member, the THAC0 and
                                ;      saves as they stand now (see STATS)
stats_stamp dw 0                ; +164 the BIOS timer when the companion last wrote STATS: older
                                ;      than STATS_FRESH, the screens show the game's own numbers
stats_req  dw 0                 ; +166 counted up when a screen is about to show STATS ...
stats_reply dw 0                ; +168 ... and set to it by the companion once STATS are up to date
rules      dw 0                 ; +170 rule changes the companion turns on (RULE_HELMS, RULE_BOOTS)
pick_seq   dw 0                 ; +172 P pressed in a conversation (PROBE_PICK counts them) ...
pick_reply dw 0                 ; +174 ... and set to it by the companion once PICK_TEXT is ready
pick_off   dw pick_text         ; +176 offset of PICK_TEXT: what came of it, NUL-terminated (empty:
                                ;      nothing to show)
pick_on    dw 0                 ; +178 the companion sets bit 0 for the thieving tools on someone to pick
                                ;      their pocket, and bit 1 too to take P in a conversation as that
use_seq    dw 0                 ; +180 an item used on something on the map (PROBE_USE_ITEM counts) ...
use_reply  dw 0                 ; +182 ... and set to it by the companion once it has had its say
use_who    dw 0                 ; +184 the object it was used on
use_taken  dw 0                 ; +186 the companion sets 1 when it was one of its own (the thieving
                                ;      tools): the game then does nothing more, and PICK_TEXT is shown;
                                ;      2 when it is used up as well (the cooked vulture, eaten)
use_item   dw 0                 ; +188 the item used (FFFFh: none)
swap_on    dw 0                 ; +190 the companion sets 1 to have the next dialogue text that
                                ;      starts with SWAP_MATCH shown as SWAP_TEXT instead (once)
swap_seq   dw 0                 ; +192 counted up when it has been
swap_off   dw swap_match        ; +194 offset of SWAP_MATCH (SWAP_SIZE bytes, NUL-terminated),
                                ;      then SWAP_TEXT (TSIZE_SWAP bytes)
names_off  dw extra_names       ; +196 offset of EXTRA_NAMES: the names past the game's own (NAMES_EXTRA
                                ;      of NAME_SIZE bytes), copied into the game's table each time it loads
names_count dw NAMES_EXTRA      ; +198 how many
names_ptr  dd 0                 ; +200 the game's name table, as last loaded with them (0: not yet)
stealth    dw 0                 ; +204 the companion sets bit N when party member N is hidden and
                                ;      unheard (RULE_STEALTH): their next attack is from behind
stealth_used dw 0               ; +206 counted up each time one is (the bit cleared)
types_off  dw extra_types       ; +208 offset of EXTRA_TYPES: item types past the game's own
                                ;      (TYPES_EXTRA of TYPE_SIZE bytes), copied in as it loads them
types_count dw TYPES_EXTRA      ; +210 how many
types_first dw 0                ; +212 the number the first of them gets (the game's own count)
types_ptr  dd 0                 ; +214 the game's item type table, as last loaded with them
objects_on dw 0                 ; +218 1 once the game has opened the companion's copy of
                                ;      SEGOBJEX.GFF (its icons: see PROBE_DOS_OPEN)
shadows_on dw 0                 ; +220 the companion sets 1 to have figures cast shadows (SHADOWS)
shadow_tab_off dw shadow_tab    ; +222 offset of SHADOW_TAB: a byte for each of the game's 520 things,
                                ;      1 for those that cast a shadow (the companion keeps it)
dark_build dw 0                 ; +224 the companion sets 1 to have DARK made again from the palette
                                ;      (after the area, so the palette, changes); 0 once it is
shadow_passes dw 0              ; +226 shadow passes drawn (counted)
scroll_on  dw 0                 ; +228 the companion sets SCROLL_MIDDLE and/or SCROLL_RIGHT to have
                                ;      a drag with that button scroll the map (SCROLLING)
pan_x      dw 0                 ; +230 the companion adds to these (wrapping) to scroll the map by
pan_y      dw 0                 ; +232   as many pixels (the mouse wheel, read in Windows)
dust_on    dw 0                 ; +234 the companion sets 1 once LIGHT is made, to have walkers raise
                                ;      dust (DUST)
light_off  dw light             ; +236 offset of LIGHT: each colour's lighter one (0: none, not ground
                                ;      dust shows on), the companion makes it from DAC
dac_off    dw dac               ; +238 offset of DAC: the palette as DARK was last made from it
dust_puffs dw 0                 ; +240 puffs of dust raised (counted)
view_redraw dw 0                ; +242 the companion sets 1 to have the view drawn again (all of it, from
                                ;      the main loop: VIEW_AGAIN), as it is once it has been
rings_on   dw 0                 ; +244 the companion sets 1 to have rings drawn under the things RING_TAB
                                ;      marks (RINGS), once RED is made
ring_tab_off dw ring_tab        ; +246 offset of RING_TAB: a byte for each of the game's 520 things: 1 a
                                ;      ring, 2 the chosen one's (brighter, thicker)
red_off    dw red               ; +248 offset of RED: each colour's reddened one (0: none), as LIGHT
target_on  dw 0                 ; +250 the companion sets 1 while Tab chooses an enemy (TARGETING)
tab_seq    dw 0                 ; +252 Tab pressed (counted) ...
back_seq   dw 0                 ; +254 ... and Shift+Tab
hit_target dw 0xFFFF            ; +256 the enemy chosen (the companion sets it; FFFFh: none)
attack_seq dw 0                 ; +258 Enter pressed on it (counted)
main_ticks dw 0                 ; +260 the map's main loop run (counted: not while a talk, menu or
                                ;      shop is open)
xp_who     dw 0xFFFF            ; +262 the companion sets the party member to be given XP_AMOUNT
                                ;      with PICK_TEXT (GIVE_XP: the game's own routine, and the
                                ;      quest's sound; FFFFh: none), DSCLOG sets it back once given
xp_amount  dw 0                 ; +264
skills_on  dw 0                 ; +266 the companion sets SKILLS_STEALTH to have an item's box name a
                                ;      cloak's and boots' bonus to hiding and moving silently
                                ;      (PROBE_ITEM_BOX), SKILLS_BELT for a worn belt's to picking
                                ;      pockets and opening locks (PROBE_BELT, and its box's line),
                                ;      SKILLS_ELVEN the Cloak and Boots of Elvenkind's chances
ring_seg   dw 0                 ; +268 the ring's segment: the paragraphs after the resident image,
                                ;      outside this one, so the ring takes none of its 64 KB (set
                                ;      when installed; RING_OFF its offset there)
rules_hi   dw 0                 ; +270 more rule changes the companion turns on (RULE_HI_KITS)

; TEXT BUFFER: what the game sends to its dialogue window, as records of
;   byte 0FEh, byte kind (the dialogue window's: 0 = a reply to choose, the
;   first of a list being its title; 1 = a portrait; 2 = text; 3 = show the
;   replies; 4 = clear. 16 = a message box),
;   dword first argument (the text's far pointer, for 0, 2 and 16), word second
;   argument, word length, then that many bytes of text (for kinds 0, 2 and 16).
; A record is complete once TPOS counts it.

; ENTRY LAYOUT (ESIZE bytes, all words little-endian)
;   +0  seq of this entry (0xFFFF while being written)
;   +2  caller IP        +4  caller CS
;   +6  rand() result    +8  caller BP     +10 SS    +12 DS
;   +14 parent BP (word at SS:BP, the caller's caller's frame)
;   +16 32 bytes from SS:BP+2   (the caller's return address, then its arguments)
;   +48 32 bytes from SS:parentBP+2 (the same for the caller's caller)
;   +80 the 4 captured game words (glob)
;   +88 16 bytes from SS:BP-10h (the caller's last local variables)
;   +104 24 bytes of code at the caller's return address (overlays move, so
;       the code is copied now rather than read later)
;   +128 16 bytes of code at the return address stored at SS:BP+2 (the
;       caller's own caller)
;   +144 40 bytes from SS:parentBP-28h (the caller's caller's local variables)
;   +184 kind: 0 = a rand() call, 1 = PROBE_SAVE (+6 = total, save value),
;        2 = PROBE_AC (+6 = the AC)
;   +186 PROBE_SAVE: the segment of the game's spell table (0 otherwise)
;
; An entry is complete once the header's seq has counted it: the entry's own
; seq is written before the header's.

; The patched rand() starts with INT VEC_RAND. Drop the interrupt frame (restoring
; the caller's flags, so interrupts are enabled again) and be rand().
int_rand:
        add sp, 4
        popf
stub:
        push eax                ; rand() leaves the upper half of EAX alone; so do we
        push si
        mov si, sp              ; SS:SI+6 = return IP, SS:SI+8 = return CS

        mov bx, [cs:seed_off]   ; DS is the game's data segment here
        mov eax, [bx]
        imul eax, eax, 0x015A4E35
        inc eax
        mov [bx], eax
        shr eax, 16
        cwd                     ; as the original: DX = sign of the high word
        and ax, 0x7FFF

        push ax
        push dx
        push di
        push es
        push cx

        mov cx, [cs:nfilt]      ; only record calls whose calling code matches a filter
        jcxz .record
        push ds
        push si
        push di
        lds si, [ss:si+6]       ; DS:SI = the code after this rand() call
        mov bx, filt
.filter:
        push si
        push cx
        push cs
        pop es
        mov di, bx
        xor cx, cx
        mov cl, [cs:bx]         ; the filter's length
        inc di
        repe cmpsb
        pop cx                  ; POP leaves the flags alone
        pop si
        je .matched
        add bx, 9
        loop .filter
        pop di
        pop si
        pop ds
        inc word [cs:skipped]
        jmp .done
.matched:
        pop di
        pop si
        pop ds

.record:
        mov word [cs:kind], 0
        call record

.done:
        pop cx
        pop es
        pop di
        pop dx
        pop ax
        mov bx, ax              ; BX is scratch for rand() callers
        pop si
        pop eax
        mov ax, bx
        retf


; RECORD: add an entry to the ring.
;   SS:SI+6 = the return address of the call being logged (IP, then CS)
;   BP      = the frame of the code that made that call
;   AX      = the value to store at +6;  [kind] = the entry kind
; Keeps every register except the flags.
record:
        push bx
        push cx
        push di
        push es
        mov es, [cs:ring_seg]
        mov di, [cs:widx]
        imul di, di, ESIZE
        mov word [es:di], 0xFFFF
        mov cx, [cs:kind]
        mov [es:di+184], cx
        mov cx, [cs:extra]
        mov [es:di+186], cx
        mov word [cs:extra], 0
        mov cx, [ss:si+6]
        mov [es:di+2], cx
        mov cx, [ss:si+8]
        mov [es:di+4], cx
        mov [es:di+6], ax
        mov [es:di+8], bp
        mov [es:di+10], ss
        mov [es:di+12], ds
        mov cx, [bp]            ; BP-relative: SS
        mov [es:di+14], cx

        push ds
        push si
        push ss
        pop ds
        lea si, [bp+2]
        add di, 16
        mov cx, 16
        rep movsw               ; +16..+47
        mov si, [bp]
        add si, 2
        mov cx, 16
        rep movsw               ; +48..+79
        pop si
        pop ds

        xor bx, bx
.glob:
        push bx
        mov bx, [cs:glob+bx]
        xor cx, cx
        or bx, bx
        jz .noglob
        mov cx, [bx]            ; game DS
.noglob:
        mov [es:di], cx
        add di, 2
        pop bx
        add bx, 2
        cmp bx, 8
        jb .glob
        push ds                 ; DI is at +72: the caller's locals
        push si
        push ss
        pop ds
        lea si, [bp-16]
        mov cx, 8
        rep movsw
        pop si
        pop ds

        push ds                 ; DI is at +88: the calling code
        push si
        lds si, [ss:si+6]       ; the return address of this rand() call
        mov cx, 12
        rep movsw
        pop si
        push si
        lds si, [ss:bp+2]       ; the return address in the caller's frame
        mov cx, 8
        rep movsw
        push ss                 ; DI is at +128: the caller's caller's locals
        pop ds
        mov si, [bp]
        sub si, 0x28
        mov cx, 20
        rep movsw
        pop si
        pop ds
        sub di, 184             ; back to the start of the entry

        mov cx, [cs:seq]        ; publish: entry seq first, then the header's
        inc cx
        mov [es:di], cx
        mov [cs:seq], cx
        mov cx, [cs:widx]
        inc cx
        cmp cx, NENT
        jb .slot
        xor cx, cx
.slot:
        mov [cs:widx], cx
        pop es
        pop di
        pop cx
        pop bx
        ret

; PROBE_SAVE: INT VEC_SAVE replaces "mov al,[bp-2] / cmp al,[bp-1]" (6 bytes:
; INT + 4 NOPs) at the end of the saving-throw function, where [bp-2] is the
; final total (d20 + modifiers) and [bp-1] the save value it must reach.
; Records both, then does the replaced instructions; RETF 2 keeps their flags.
probe_save:
        sti
        push si
        mov si, sp
        sub si, 4               ; SS:SI+6 = our return address
        push ax
        push ds
        push bx
        lds bx, [ss:si+6]       ; our return address, 2 bytes after the patch
        mov ax, [bx-SPELL_SEG]  ; the saving throw's "mov ax,<spell table>"
        mov [cs:extra], ax
        pop bx
        pop ds
        mov ax, [bp-2]          ; AL = total, AH = save value
        mov word [cs:kind], 1
        call record
        pop ax
        pop si
        mov al, [bp-2]
        cmp al, [bp-1]
        retf 2

; PROBE_AC: INT VEC_AC replaces "mov ax,[bp-6] / add ax,si" (5 bytes: INT + 3
; NOPs) at the end of the AC function. Does the replaced instructions and
; records the result: the AC the game uses for this attack.
probe_ac:
        sti
        push si                 ; the game's SI is part of the AC
        mov ax, [bp-6]
        add ax, si
        call kit_ac
        mov si, sp
        sub si, 4
        mov word [cs:kind], 2
        call record
        pop si
        retf 2

; GPL_HOOK: the game's script interpreter calls the far pointer at DS:00ACh with
; each command's number before running it. The companion points it here and
; puts the game's own hook in GPL_CHAIN. Records the command (kind 3), then
; passes on to the game's hook.
gpl_hook:
        push si
        mov si, sp
        sub si, 4               ; SS:SI+6 = our return address
        push ax
        mov ax, [ss:si+10]      ; the command number
        mov word [cs:kind], 3
        call record
        pop ax
        pop si
        jmp far [cs:gpl_chain]

; PROBE_TEXT: INT VEC_TEXT replaces "push bp / mov bp,sp" (3 bytes: INT + NOP) at
; the start of the game's routine that feeds its dialogue window
; (kind, dword, word). Copies what it's given to the text buffer, then does the
; replaced instructions for the routine.
probe_text:
        call text_enter         ; SS:BP+16 = the routine's arguments
        cmp word [cs:swap_on], 0
        je .record
        cmp byte [bp+16], 2     ; text for the window
        jne .record
        lds si, [bp+18]
        mov ax, ds
        or ax, si
        jz .record
        mov bx, swap_match
.same:  mov al, [cs:bx]
        or al, al
        jz .swap                ; all of SWAP_MATCH matched
        cmp al, [si]
        jne .record
        inc si
        inc bx
        jmp .same
.swap:  mov word [bp+18], swap_text  ; the routine is given ours instead
        mov [bp+20], cs
        mov word [cs:swap_on], 0
        inc word [cs:swap_seq]
.record:
        mov cl, [bp+16]
        lds si, [bp+18]
        mov dx, [bp+22]
        jmp text_leave

SWAP_SIZE equ 64
swap_match times SWAP_SIZE db 0
swap_text  times 240 db 0

; PROBE_MSG: the same for the game's message box routine (far pointer to the
; message), recorded as kind 16.
probe_msg:
        call text_enter
        mov cl, 16
        lds si, [bp+16]
        xor dx, dx
        jmp text_leave

text_enter:                     ; take the interrupt frame off, save registers, BP = SP
        pop word [cs:t_ret]
        pop word [cs:t_ip]      ; the routine's stack is underneath the interrupt frame
        pop word [cs:t_cs]
        pop word [cs:t_fl]
        sti
        push ax
        push bx
        push cx
        push dx
        push si
        push ds
        push bp
        mov bp, sp              ; SS:BP+14 = return address of the routine, +18 its arguments
        add bp, 2               ; ... so that +16 is the first argument
        jmp [cs:t_ret]

text_leave:                     ; record CL = kind, DS:SI = dword, DX = word, then return
        mov bx, [cs:tpos]
        mov al, 0xFE
        call tput
        mov al, cl
        call tput
        mov ax, si
        call tputw
        mov ax, ds
        call tputw
        mov ax, dx
        call tputw
        xor ax, ax
        cmp cl, 0
        je .text
        cmp cl, 2
        je .text
        cmp cl, 16
        jne .len                ; the other kinds have no text (and no pointer)
.text:
        mov ax, ds
        or ax, si
        jz .len
        push si
        xor ax, ax
.count:
        cmp byte [si], 0
        je .counted
        inc si
        inc ax
        cmp ax, 400
        jb .count
.counted:
        pop si
.len:
        mov cx, ax
        call tputw
        jcxz .done
.copy:
        lodsb
        call tput
        loop .copy
.done:
        mov [cs:tpos], bx       ; publish the record
        pop bp
        pop ds
        pop si
        pop dx
        pop cx
        pop bx
        pop ax
        push bp                 ; the replaced instructions
        mov bp, sp
        push word [cs:t_fl]
        push word [cs:t_cs]
        push word [cs:t_ip]
        iret

; PROBE_INV: INT VEC_CHAR replaces "add sp,0Eh" (3 bytes: INT + NOP) in the inventory
; screen's routine for its right-hand panel, straight after the weapon lines (AX = how many
; lines they took). Does the add, then adds in the game's own lettering: THAC0 and the five
; saving throws above STR, and for a thief the eight skills in a column right of the
; abilities (below the weapons, three of which reach the buttons, there'd be no room).
; The routine's code holds the (relocated) far address of the game's text routine at a
; fixed distance before the patch: it is read from there.
IV_PATCH  equ 0x6F6BF           ; DSUN.EXE offsets
IV_DRAW   equ 0x6F626           ; "lcall 0150h:016Dh" operand: the text routine
IV_WHO    equ 0x6F634           ; "mov ax,0348h" operand: segment of the character number (+25Bh)
THIEF_CLASS equ 17
THIEF_TABLE equ 0x3FAA - 0x4356 ; the thief tables' segment, relative to DS
probe_char:
        pop word [cs:t_ip]
        pop word [cs:t_cs]
        pop word [cs:t_fl]
        add sp, 0x0E            ; the replaced instruction
        sti
        pushad
        push es
        push fs
        push gs
        mov es, [cs:t_cs]
        mov di, [cs:t_ip]
        sub di, 2               ; ES:DI = the patch
        mov eax, [es:di + IV_DRAW - IV_PATCH]
        mov [cs:c_draw], eax
        mov fs, [es:di + IV_WHO - IV_PATCH]
        mov eax, [0x11A4]       ; the panel's window
        mov [cs:c_winptr], eax
        mov bx, [fs:0x25B]      ; the character on show
        mov [cs:c_who], bx
        imul ax, bx, 0x47
        les si, [0x1661]
        add si, ax              ; ES:SI = the character sheet
        cmp word [es:si + 0x10], 0
        je .done                ; an empty slot
        imul bx, bx, 0x3A
        lfs di, [0x1665]
        add di, bx              ; FS:DI = the creature record
        ; THAC0 and the saves
        mov al, [fs:di + 0x1F]
        mov bx, c_cells_top
        call c_cells_saves
        ; the DEX reaction adjustment (the game's table for initiative holds the same numbers),
        ; worked out now, so a change of DEX shows the next time the panel is drawn
        mov byte [cs:c_react], NO_REACT
        movzx bx, byte [fs:di + 0x23]
        cmp bx, 26
        jae .thieves
        mov al, [bx + DEX_REACTION]
        mov [cs:c_react], al
        mov al, [bx + DEX_DEFENCE]   ; and the defensive adjustment (on AC; on saves against
        mov [cs:c_defence], al       ; what can be dodged, the other way round)
.thieves:
        ; thief skills, for a character with thief levels
        xor cx, cx
        mov bx, 0x21
.cls:   cmp byte [es:si + bx], THIEF_CLASS
        je .thief
        inc bx
        inc cx
        cmp cx, 3
        jb .cls
        ; no thief levels: a ranger's move silently and hide in shadows (the companion's
        ; stealth rule), in the thief's places, when the companion sends them
        mov bx, [cs:c_who]
        call stats_for
        jc .react
        cmp byte [cs:bx + 17], STATS_RANGER
        jne .react
        mov ax, [cs:bx + 18 + 3]
        mov [cs:c_vals], ax
        mov bx, c_cells_thief + 3 * 8
        mov cx, 2
        mov byte [cs:c_signed], 0
        call c_cells
        jmp .react
.thief: mov al, [es:si + bx + 3]  ; the thief level (levels follow the classes)
        call c_thief
.react: mov al, [cs:c_react]      ; right of SP in the saves: "REAC +4"
        cmp al, NO_REACT
        je .done
        mov di, c_num
        test al, al
        jle .sign
        mov byte [cs:di], '+'
        inc di
.sign:  call c_itoa_s
        push word REACT_Y
        push word 0x113
        push cs
        push word l_react
        call c_draw_line
        push word REACT_Y
        push word REACT_VALUE_X
        push cs
        push word c_num
        call c_draw_line
        mov al, [cs:c_defence]    ; right of the AC line: "DEF -4"
        mov di, c_num
        test al, al
        jle .dsign
        mov byte [cs:di], '+'
        inc di
.dsign: call c_itoa_s
        push word DEF_Y
        push word 0x113
        push cs
        push word l_defence
        call c_draw_line
        push word DEF_Y
        push word DEF_VALUE_X
        push cs
        push word c_num
        call c_draw_line
.done:
        pop gs
        pop fs
        pop es
        popad
        push word [cs:t_fl]
        push word [cs:t_cs]
        push word [cs:t_ip]
        iret

; PROBE_VIEW: INT VEC_VIEW replaces "push dword 000B0140h" (6 bytes: INT + 4 NOPs) near the
; end of the View Character screen's routine for its upper panel, once the game has drawn
; its own lines. Adds THAC0 and the saves under the item icons, then does the push.
; The routine's code holds, at fixed distances before the patch, the (relocated) far address
; of the text routine, the segment of the panel's window handle and the segment of the
; selected character's number: they are read from there.
CH_PATCH  equ 0x8A471           ; DSUN.EXE offsets
CH_DRAW   equ 0x8A2C8           ; "lcall 0150h:016Dh" operand: the text routine
CH_WIN    equ 0x8A2BD           ; "mov ax,0430h" operand: segment of the window's far pointer
CH_WHO    equ 0x8A2D6           ; "mov ax,0348h" operand: segment of the character number (+25Bh)
probe_view:
        pop word [cs:t_ip]
        pop word [cs:t_cs]
        pop word [cs:t_fl]
        sti
        pushad
        push es
        push fs
        mov es, [cs:t_cs]
        mov di, [cs:t_ip]
        sub di, 2               ; ES:DI = the patch
        mov eax, [es:di + CH_DRAW - CH_PATCH]
        mov [cs:c_draw], eax
        mov fs, [es:di + CH_WIN - CH_PATCH]
        mov eax, [fs:0]         ; the panel's window
        mov [cs:c_winptr], eax
        mov fs, [es:di + CH_WHO - CH_PATCH]
        mov bx, [fs:0x25B]      ; the character on show
        cmp bx, 3
        ja .done
        mov [cs:c_who], bx
        imul ax, bx, 0x47
        les si, [0x1661]
        add si, ax              ; ES:SI = the character sheet
        cmp word [es:si + 0x10], 0
        je .done                ; an empty slot
        imul bx, bx, 0x3A
        lfs di, [0x1665]
        mov al, [fs:di + bx + 0x1F]  ; THAC0
        call c_load_stats       ; (or the companion's, as they stand now)
        mov di, v_thac0 + 7
        mov al, [cs:c_vals]
        call c_itoa_s
        mov di, v_saves1 + 5
        mov al, [cs:c_vals + 1]
        call v_two
        mov al, [cs:c_vals + 2]
        call v_two
        mov al, [cs:c_vals + 3]
        call v_two
        mov di, v_saves2 + 5
        mov al, [cs:c_vals + 4]
        call v_two
        mov al, [cs:c_vals + 5]
        call v_two
        push word 0x3A          ; under the item icons
        push word 0xCD
        push cs
        push word v_thac0
        call c_draw_line
        push word 0x41
        push word 0xCD
        push cs
        push word v_saves1
        call c_draw_line
        push word 0x48
        push word 0xCD
        push cs
        push word v_saves2
        call c_draw_line
.done:  pop fs
        pop es
        popad
        push dword 0x000B0140   ; the replaced instruction
        push word [cs:t_fl]
        push word [cs:t_cs]
        push word [cs:t_ip]
        iret

v_two:                          ; AL (0-99) -> two digits (a space for a leading 0) and a space
        push bx                 ; at CS:DI; DI moves on
        xor ah, ah
        mov bl, 10
        div bl
        add ax, '00'
        cmp al, '0'
        jne .tens
        mov al, ' '
.tens:  mov [cs:di], al
        mov [cs:di + 1], ah
        mov byte [cs:di + 2], ' '
        add di, 3
        pop bx
        ret

v_thac0  db 'THAC0: ', 0, 0, 0, 0
v_saves1 db 'SAVE:00 00 00 ', 0
v_saves2 db '     00 00 ', 0

; THAC0 (AL) and the saves (sheet +37h..+3Bh at ES:SI), or the companion's numbers as they
; stand now, as the cells at CS:BX say
c_cells_saves:
        call c_load_stats
        mov byte [cs:c_signed], 1  ; (THAC0 can be below 0)
        mov cx, 6
        jmp c_cells

; the eight thief skills of the character (sheet ES:SI, creature FS:DI, thief level AL):
; base + 4 a level + the race's adjustment + DEX, from the game's tables (before equipment and
; effects); the companion's numbers instead, which count those too, while it keeps STATS
c_thief:
        push si
        mov dl, al
        shl dl, 2               ; 4 a level
        mov ax, ds
        add ax, THIEF_TABLE
        mov gs, ax
        mov dh, [fs:di + 0x23]  ; DEX
        movzx di, byte [es:si + 0x18]  ; race
        shl di, 3
        add di, 8               ; +8 + race * 8
        xor bx, bx
.skill: movsx ax, byte [gs:bx]  ; base
        movzx cx, dl
        add ax, cx
        movsx cx, byte [gs:bx + di]  ; race
        add ax, cx
        push dx                 ; DEX: -5 a point below LOW, +5 a point above HIGH, -3 above TOP
        movzx dx, dh
        movzx cx, byte [gs:bx + 0x90]
        sub cx, dx
        jle .nolow
        imul cx, cx, 5
        sub ax, cx
.nolow: mov cx, dx
        push dx
        movzx dx, byte [gs:bx + 0x98]
        sub cx, dx
        pop dx
        jle .nohigh
        imul cx, cx, 5
        add ax, cx
.nohigh:
        mov cx, dx
        push dx
        movzx dx, byte [gs:bx + 0xA0]
        sub cx, dx
        pop dx
        jle .notop
        imul cx, cx, 3
        sub ax, cx
.notop: pop dx
        cmp ax, 0
        jge .pos
        xor ax, ax              ; below 0: shown as 0
.pos:   cmp ax, 255
        jbe .fits
        mov ax, 255
.fits:  mov [cs:c_vals + bx], al
        inc bx
        cmp bx, 8
        jb .skill
        pop si
        mov al, [cs:c_vals + 6] ; pick pockets, open locks, find traps, move silently and hide in
        mov [cs:c_vals + 5], al ; shadows (which the Templar's Ledger rolls: for picking pockets
                                ; and its stealth rule), and climb walls; not hear noise (one
                                ; script check in the game) or read languages (none)
        mov bx, [cs:c_who]      ; the companion's, with equipment and effects, if it keeps them
        call stats_for
        jc .ours
        cmp byte [cs:bx + 17], 0
        je .ours
        mov eax, [cs:bx + 18]
        mov [cs:c_vals], eax
        mov ax, [cs:bx + 22]
        mov [cs:c_vals + 4], ax
.ours:  mov bx, c_cells_thief
        mov cx, 6
        mov byte [cs:c_signed], 0
        ; fall through

; CX cells at CS:BX: each x, y, label offset, value's x (words); the values are C_VALS in
; order. Keeps ES, SI, DI.
c_cells:
        push es
        push si
        push di
        xor si, si
.cell:  push cx
        push bx
        push word [cs:bx + 2]   ; y
        push word [cs:bx]       ; x
        push cs
        push word [cs:bx + 4]   ; the label
        call c_draw_line
        pop bx
        push bx
        mov al, [cs:c_vals + si]
        mov di, c_num
        or si, si
        jnz .unsigned
        cmp byte [cs:c_signed], 0
        je .unsigned
        call c_itoa_s           ; the first cell of THAC0 and the saves
        jmp .number
.unsigned:
        call c_itoa
.number:
        push word [cs:bx + 2]
        push word [cs:bx + 6]   ; the value's x
        push cs
        push word c_num
        call c_draw_line
        pop bx
        pop cx
        add bx, 8
        inc si
        loop .cell
        pop di
        pop si
        pop es
        ret

c_draw_line:                    ; stack: text far pointer, x, y (near return address first)
        push bp
        mov bp, sp
        push dword [bp + 4]     ; the text, for the format's %s
        push word [0x3270]      ; the colours, as the game sets them for its AC line
        push word 0x14
        push word [0x326E]
        push dword 0x00FE00FF
        push word 0
        push ds
        push word 0x0E11        ; the format: "%C%C%C%s"
        push dword [bp + 8]     ; x, y
        push dword [cs:c_winptr]  ; the window
        call far [cs:c_draw]
        add sp, 0x1C
        pop bp
        ret 8

c_itoa:                         ; AL (unsigned) -> decimal at CS:DI, NUL-terminated; keeps BX, CX
        push bx
        push cx
        xor ah, ah
        mov bl, 10
        xor cx, cx
.div:   div bl
        push ax                 ; AH = a digit
        inc cx
        xor ah, ah
        or al, al
        jnz .div
.put:   pop ax
        add ah, '0'
        mov [cs:di], ah
        inc di
        loop .put
        mov byte [cs:di], 0
        pop cx
        pop bx
        ret

; cells: x, y, label, value's x (window coordinates: the stats' labels are at x 0ECh, their
; values at 104h, STR at y 35h, lines 7 apart)
c_cells_top:
        dw 0xEC, 0x10, l_thac0, 0x111
        dw 0xEC, 0x17, l_ppd, 0x103,  0x113, 0x17, l_rsw, 0x12A
        dw 0xEC, 0x1E, l_pp, 0x103,   0x113, 0x1E, l_bw, 0x12A
        dw 0xEC, 0x25, l_sp, 0x103
c_cells_thief:                  ; right of the abilities (whose values end by 10Eh), in the
        dw 0x113, 0x35, l_pick, 0x12D  ; saves' second column, level with STR..CHA
        dw 0x113, 0x3C, l_lock, 0x12D
        dw 0x113, 0x43, l_trap, 0x12D
        dw 0x113, 0x4A, l_move, 0x12D  ; (move silently)
        dw 0x113, 0x51, l_hide, 0x12D  ; (hide in shadows)
        dw 0x113, 0x58, l_clmb, 0x12D
l_thac0 db 'THAC0:', 0
l_ppd   db 'PPD', 0
l_rsw   db 'RSW', 0
l_pp    db 'PP', 0
l_bw    db 'BW', 0
l_sp    db 'SP', 0
l_pick  db 'PICK', 0
l_lock  db 'LOCK', 0
l_trap  db 'TRAP', 0
l_move  db 'MOVE', 0
l_hide  db 'HIDE', 0
l_clmb  db 'CLMB', 0
l_react db 'REAC', 0
l_defence db 'DEF', 0
c_react db 0
c_defence db 0
DEX_DEFENCE equ 0x07F6          ; DS: the DEX defensive adjustment (bytes, by score: on AC)
DEF_Y   equ 0x72                ; the AC line's y
DEF_VALUE_X equ 0x12D           ; (under the thief skills' values, as REAC's)
NO_REACT equ 0x80
DEX_REACTION equ 0x07DC         ; DS: the DEX reaction adjustment (bytes, by score)
REACT_Y equ 0x25                ; the saves' last row, right of SP (where BW and RSW are above)
REACT_VALUE_X equ 0x130         ; (a little right of the thief skills' values: REAC is the longer label)
c_vals  times 8 db 0
c_num   db 0, 0, 0, 0
c_draw  dd 0
c_winptr dd 0


; PROBE_TURN: INT VEC_TURN replaces "add sp,4" (3 bytes: INT + NOP) in the game's combat
; loop, straight after the call that runs combat and may pass the turn on (it is given the
; address of DS:4979h, whose turn it is). When the turn has changed and the companion wants
; summaries, note whose turn ended, wait a moment (at most TURN_WAIT timer ticks) for the
; companion to put that turn's summary in MSG_BUF, and show it with the game's own message
; window, as the game's scripts do for a narration: the emblem instead of a portrait, the
; text, then "Continue" to click (the scripts' own "Press continue"), then CLOSE.
; The dialogue window's routines are reached through the game's overlay stub for them,
; whose segment is a fixed distance from the game's data segment.
DLG_STUB  equ 0x42CA - 0x4356   ; the stub's segment (DSUN.EXE: 42CAh) less the data segment's
DLG_FEED  equ 0x25              ; the window's input: (kind, far text, word), see the text buffer
DLG_WAIT  equ 0x34              ; wait for a reply to be clicked
S_PRESS   equ 0x15E8            ; DS: "Press continue"
S_CONT    equ 0x15F7            ; DS: "Continue"
S_CLOSE   equ 0x1F11            ; DS: "CLOSE"
TURN_WAIT equ 7                 ; timer ticks (55 ms each)
probe_turn:                     ; (re-entered while the window waits: all state on the stack)
        push bp                 ; the replaced "add sp,4": move the interrupt frame (and BP)
        mov bp, sp              ; up over the 4 bytes, so IRET returns with them gone
        push ax
        mov ax, [bp + 6]
        mov [bp + 10], ax       ; flags
        mov ax, [bp + 4]
        mov [bp + 8], ax        ; CS
        mov ax, [bp + 2]
        mov [bp + 6], ax        ; IP
        mov ax, [bp]
        mov [bp + 4], ax        ; BP
        pop ax
        mov sp, bp
        add sp, 4
        pop bp
        sti
        pushad
        push es
        call turn_check
        pop es
        popad
        iret

; PROBE_NEXT: INT VEC_NEXT replaces "cmp word [bp-2],0" (4 bytes: INT + 2 NOPs, then the
; game's "jne +5") in the combat routine PROBE_TURN's call runs, once it has passed the turn on
; (DS:4979h) and before it plays the turn of a combatant the computer runs (a monster, or
; someone charmed). The game plays such a turn whole before its loop reaches PROBE_TURN, so
; without this the monster's rolls would join the summary of the turn before. Checks the turn
; as PROBE_TURN does, then goes on where the compare and the JNE would have gone.
; The combat routine is overlay code, and the summary's window is too: loading the window's
; code may move the combat routine or throw it out of memory while the window is up. The
; overlay manager then fixes up the return addresses it finds by following BP from frame to
; frame, so the way back is put in such a frame (as a far call's return address, with BP
; pointing at the game's), and taken from there afterwards: the combat routine where it is
; now, or the manager's trap that loads it again. (Returning to where it was before broke
; the game at random: the fight started over, or it stopped with "Stack overflow!")
probe_next:
        sti
        pushad
        push es
        mov bx, sp              ; the interrupt frame at BX+34: IP, CS, flags
        mov ax, [ss:bx + 34]    ; (the NOPs after the INT)
        add ax, 4               ; past the NOPs and the JNE when the compare finds 0 ...
        cmp word [bp - 2], 0    ; the replaced compare (BP: the combat routine's frame)
        je .frame
        add ax, 5               ; ... and to its target when not
.frame: push word [ss:bx + 36]
        push ax
        push bp
        mov bp, sp
        call turn_check
        pop bp
        pop ax                  ; the way back, as the overlay manager has left it
        pop dx
        mov bx, sp
        mov [ss:bx + 34], ax
        mov [ss:bx + 36], dx
        pop es
        popad
        iret

; PROBE_PICK: INT VEC_PICK replaces "jmp <ignore the key>" (3 bytes: INT + NOP) in the
; dialogue window's key handling, where a key that isn't one of the window's own (1-5, Y, N,
; the arrows...) goes, while the window waits for a reply. For P, when the companion wants it:
; count it, wait a moment for the companion to try the leader's hand at the pocket of the
; person talked to (PICK_TEXT, what came of it), and show that in the window, which goes on
; waiting for a reply as before. The window's code is overlay code: the way back is put in a
; frame the overlay manager can fix up, as for PROBE_NEXT.
PICK_JUMP equ 0x7DD70 - 0x7D9FF ; (DSUN.EXE) the JMP's target less the address after the INT
PICK_KEY  equ -0x0C             ; the key, at the key handler's BP-0Ch: scan code, character
PICK_WAIT equ 9                 ; timer ticks
PICK_KEY_ON equ 2               ; (pick_on: P in a conversation too)
probe_pick:
        sti
        pushad
        push es
        mov bx, sp              ; the interrupt frame at BX+34: IP, CS, flags
        add word [ss:bx + 34], PICK_JUMP  ; go on where the JMP went
        test word [cs:pick_on], PICK_KEY_ON
        jz .out
        mov ax, [bp + PICK_KEY]
        and al, 0xDF            ; p or P
        cmp ax, 0x1950
        jne .out
        inc word [cs:pick_seq]
        xor ax, ax
        mov es, ax
        mov dx, [es:0x46C]      ; the BIOS timer
        call rtc_begin
.wait:  mov ax, [cs:pick_reply]
        cmp ax, [cs:pick_seq]
        je .ready
        mov ax, [es:0x46C]
        sub ax, dx
        cmp ax, PICK_WAIT
        jae .late_pick_reply
        call rtc_waited         ; (the BIOS clock can stand still: the game's timer
        jnc .wait               ;   doesn't always pass its ticks on; at most 2 s by the real-time clock)
.late_pick_reply:
        jmp .out                ; no answer: the companion isn't reading
.ready: cmp byte [cs:pick_text], 0
        je .out
        push word [ss:bx + 36]
        push word [ss:bx + 34]
        push bp
        mov bp, sp
        call give_xp
        call pick_show
        pop bp
        pop ax
        pop dx
        mov bx, sp
        mov [ss:bx + 34], ax
        mov [ss:bx + 36], dx
.out:   pop es
        popad
        iret

; PICK_TEXT in the dialogue window, which is up and waiting for a reply: the text, then the
; replies shown again; DS = the game's
pick_show:
        mov ax, ds
        add ax, DLG_STUB
        mov [cs:dlg + 2], ax
        mov word [cs:dlg], DLG_FEED
        push word 0
        push cs
        push word pick_text
        push word 2
        call far [cs:dlg]
        add sp, 8
        push word 0
        push dword 0
        push word 3
        call far [cs:dlg]
        add sp, 8
        ret

PICK_SIZE equ 240
pick_text times PICK_SIZE db 0

; GIVE_XP: the XP the companion asked for with PICK_TEXT (XP_WHO, XP_AMOUNT), given by the routine
; the game's scripts give one person XP with (GPL 21h; it sees to a level gained), and the sound
; of a quest done that they play with it ("... receives N experience points!" is in PICK_TEXT);
; DS = the game's. Called in the frame PROBE_PICK and PROBE_USE_ITEM make for the overlay manager.
XP_SEG    equ 0x4251            ; (DSUN.EXE segments, less the load segment) an overlay's entry:
XP_OFF    equ 0x005C            ;   (who, how many)
give_xp:
        cmp word [cs:xp_who], 0xFFFF
        je .ret
        mov ax, ds
        sub ax, DGROUP_SEG - XP_SEG
        mov [cs:xp_call + 2], ax
        push word [cs:xp_amount]
        push word [cs:xp_who]
        call far [cs:xp_call]
        add sp, 4
        mov ax, ds
        sub ax, DGROUP_SEG - DROP_SEG
        mov [cs:sound_call + 2], ax
        push word QUEST_SOUND
        call far [cs:sound_call]
        add sp, 2
        mov word [cs:xp_who], 0xFFFF
.ret:   ret

xp_call dw XP_OFF, 0
drop_call dw DROP_OFF, 0
sound_call dw SOUND_OFF, 0

; PROBE_USE_ITEM: INT VEC_USE_ITEM replaces "cmp si,-1 / jne +3" (5 bytes: INT + 3 NOPs) in the
; routine that uses the item on the pointer on whatever is under it on the map (SI: that
; object, -1 for none). When the companion wants picked pockets: count it, wait a moment for
; the companion to see whether the item is its thieving tools and the object someone to rob
; (USE_TAKEN), and if so show what came of it (PICK_TEXT) and skip the game's own handling (the
; routine's end). Otherwise on as the compare and the JNE would have gone. Overlay code: the
; way back is put in a frame the overlay manager can fix up, as for PROBE_NEXT.
USE_NONE  equ 0x7361A - 0x73617 ; (DSUN.EXE) SI = -1: "jmp", less the address after the INT
USE_SOME  equ 0x7361D - 0x73617 ; the JNE's target
USE_DONE  equ 0x7371D - 0x73617 ; the routine's end
USE_HELD_SEG equ 0x73A15 - 0x73617 ; the routine's "mov dx,<segment>" for the pointer's items,
                                ;   whose operand the game fixes up when it loads the code
HELD      equ 0x17A0            ; DS: the pointer's item (in that segment at HELD * 10 + 44h)
HELD_LIST equ 0x179E            ; DS: the object whose item list is the pointer's
DGROUP_SEG equ 0x4356           ; the game's DS, less its load segment
DROP_SEG  equ 0x1A0A            ; and the resident routine (21134h in DSUN.EXE) that empties an
DROP_OFF  equ 0x1C94            ;   object's item list, putting the items back on the free list
SOUND_OFF equ 0x0663            ; the resident routine there that plays a sound (GPL's 5Dh)
QUEST_SOUND equ 53              ; the sound of a quest done (the game's scripts, with their XP)
probe_use_item:
        sti
        pushad
        push es
        mov bx, sp              ; the interrupt frame at BX+34: IP, CS, flags
        mov dx, USE_NONE
        cmp si, -1
        je .go
        mov dx, USE_SOME
        cmp word [cs:pick_on], 0
        je .go
        mov [cs:use_who], si
        mov word [cs:use_taken], 0
        mov word [cs:use_item], 0xFFFF
        mov di, [HELD]          ; (DS: the game's)
        cmp di, -1
        je .asked
        imul di, di, 10
        push ds
        push si
        lds si, [ss:bx + 34]    ; DS:SI: the code after the INT
        mov ds, [si + USE_HELD_SEG]  ; the pointer's items' segment, as fixed up
        mov ax, [di + 0x44]
        pop si
        pop ds
        mov [cs:use_item], ax
.asked:
        inc word [cs:use_seq]
        xor ax, ax
        mov es, ax
        mov cx, [es:0x46C]      ; the BIOS timer
        call rtc_begin
.wait:  mov ax, [cs:use_reply]
        cmp ax, [cs:use_seq]
        je .ready
        mov ax, [es:0x46C]
        sub ax, cx
        cmp ax, PICK_WAIT
        jae .late_use_reply
        call rtc_waited         ; (the BIOS clock can stand still: the game's timer
        jnc .wait               ;   doesn't always pass its ticks on; at most 2 s by the real-time clock)
.late_use_reply:
        jmp .go                 ; no answer: the companion isn't reading
.ready: cmp word [cs:use_taken], 0
        je .go
        cmp word [cs:use_taken], 2
        jne .kept
        ; 2: the item is used up (the cooked vulture, eaten): let go of the pointer's items the
        ; way the game does once it has counted coins picked up, and hold nothing
        push bx
        mov ax, ds
        sub ax, DGROUP_SEG - DROP_SEG
        mov [cs:drop_call + 2], ax
        push word [HELD_LIST]
        call far [cs:drop_call]
        add sp, 2
        mov word [HELD], -1
        mov ax, ds              ; (the drop routine's AX is its own)
        sub ax, DGROUP_SEG - DROP_SEG
        mov [cs:sound_call + 2], ax  ; and the sound the game plays when a quest is done (with
        push word QUEST_SOUND   ;   "... receives N experience points!": its 5Dh command)
        call far [cs:sound_call]
        add sp, 2
        pop bx
.kept:
        mov dx, USE_DONE
        add [ss:bx + 34], dx
        cmp byte [cs:pick_text], 0
        je .out
        push word [ss:bx + 36]
        push word [ss:bx + 34]
        push bp
        mov bp, sp
        call give_xp
        mov word [cs:show_text], pick_text
        call show_window
        pop bp
        pop ax                  ; the way back, as the overlay manager has left it
        pop dx
        mov bx, sp
        mov [ss:bx + 34], ax
        mov [ss:bx + 36], dx
        jmp .out
.go:    add [ss:bx + 34], dx
.out:   pop es
        popad
        iret

; whose turn it is (DS:4979h) has changed since last seen: count it, wait a moment for the
; companion's summary of the turn that ended (MSG_BUF) and show it; DS = the game's
turn_check:
        cmp byte [cs:showing], 0
        jne .out                ; a summary is up: leave the game's loop alone meanwhile
        mov bx, [0x4979]        ; whose turn it is now
        xchg bx, [cs:last_turn]
        cmp bx, [cs:last_turn]
        je .out                 ; the same as last time
        cmp word [cs:popups_on], 0
        je .out
        mov [cs:ended], bx
        inc word [cs:turn_seq]
        xor ax, ax
        mov es, ax
        mov dx, [es:0x46C]      ; the BIOS timer
        call rtc_begin
.wait:  mov ax, [cs:reply_seq]
        cmp ax, [cs:turn_seq]
        je .ready
        mov ax, [es:0x46C]
        sub ax, dx
        cmp ax, TURN_WAIT
        jae .late_reply_seq
        call rtc_waited         ; (the BIOS clock can stand still: the game's timer
        jnc .wait               ;   doesn't always pass its ticks on; at most 2 s by the real-time clock)
.late_reply_seq:
        jmp .out                ; no answer: the companion isn't reading
.ready: cmp byte [cs:msg_buf], 0
        je .out                 ; nothing to say about that turn
        mov word [cs:show_text], msg_buf
        call show_window
.out:   ret

; the text at CS:[SHOW_TEXT] in the game's dialogue window, with "Continue" to click;
; DS = the game's
show_window:
        mov byte [cs:showing], 1
        mov ax, ds
        add ax, DLG_STUB
        mov [cs:dlg + 2], ax
        mov word [cs:dlg], DLG_FEED
        xor bx, bx
        push word 0             ; the emblem (portrait 0)
        push bx
        push bx
        push word 1
        call far [cs:dlg]
        add sp, 8
        push word 0             ; the text
        push cs
        push word [cs:show_text]
        push word 2
        call far [cs:dlg]
        add sp, 8
        push word 0             ; "Press continue": the replies' title, then the one reply
        push ds
        push word S_PRESS
        push word 0
        call far [cs:dlg]
        add sp, 8
        push word 0
        push ds
        push word S_CONT
        push word 0
        call far [cs:dlg]
        add sp, 8
        push word 0             ; show the reply
        push dword 0
        push word 3
        call far [cs:dlg]
        add sp, 8
        mov word [cs:dlg], DLG_WAIT
        call far [cs:dlg]       ; until it's clicked
        mov word [cs:dlg], DLG_FEED
        push word 0             ; and close the window
        push ds
        push word S_CLOSE
        push word 2
        call far [cs:dlg]
        add sp, 8
        mov byte [cs:showing], 0
        ret

show_text dw 0                  ; the text SHOW_WINDOW shows
dlg     dd 0                    ; the dialogue window routine being called
showing db 0                    ; 1 while PROBE_TURN has a summary up
last_turn dw 0xFFFF

; PROBE_USE: INT VEC_USE replaces "add sp,0Ch" (3 bytes: INT + NOP) in the USE (cast spells)
; screen's routine that labels its LEVEL button, which runs whenever the screen is drawn
; or the level changes. Draws the selected character's spell slots (SLOTS_TEXT, from the
; companion) in the empty panel under the spells, with the game's own text routine.
USE_TEXT_SEG equ 0x2B7A - 0x4356 ; the text routine's segment (DSUN.EXE: 2B7Ah) less DS's
USE_TEXT_OFF equ 0x16D
USE_WHO_SEG  equ 0x3931 - 0x4356 ; the selected character's number is at this segment:25Bh
SLOTS_SIZE   equ 96
MSG_SIZE     equ 900         ; the turn summary: the dialogue window keeps up to 1024 bytes
probe_use:
        push bp                 ; the replaced "add sp,0Ch": move the interrupt frame (and BP)
        mov bp, sp              ; up over the 12 bytes
        push ax
        mov ax, [bp + 6]
        mov [bp + 18], ax
        mov ax, [bp + 4]
        mov [bp + 16], ax
        mov ax, [bp + 2]
        mov [bp + 14], ax
        mov ax, [bp]
        mov [bp + 12], ax
        pop ax
        mov sp, bp
        add sp, 12
        pop bp
        sti
        pushad
        push es
        push fs
        mov eax, [0x11A4]       ; the USE screen is up: PROBE_WIN may redraw on this window
        mov [cs:use_win], eax
        call use_draw
        pop fs
        pop es
        popad
        iret

; PROBE_WIN: INT VEC_WIN replaces "xor ax,ax / pop si" (3 bytes: INT + NOP) at the end of the
; game's routine that brings a window to the front and redraws it (its window far pointer the
; argument, at the routine's BP+6), which repaints the USE screen's panels when a character or
; spell level is picked, after the LEVEL button's label (where PROBE_USE draws). For the USE
; screen's window, once PROBE_USE has drawn on it (not while the screen opens), the slots are
; drawn again. Everything is kept on the stack: drawing may run the routine again.
WIN_USE_SEG equ 0x40BC - 0x4356 ; the USE screen's window pointer is at this segment:0
probe_win:
        push bp
        mov bp, sp              ; BP+2 the interrupt frame, BP+8 the routine's saved SI
        sti
        pushad
        push es
        push fs
        mov di, [bp]            ; the routine's BP
        mov eax, [ss:di + 6]    ; its window
        or eax, eax
        jz .done
        call save_label         ; (the save/load window: which saves it shows)
        cmp eax, [cs:use_win]
        jne .done               ; not a USE screen PROBE_USE has drawn on
        cmp eax, [0x11A4]
        jne .done               ; not the window on show
        mov bx, ds
        add bx, WIN_USE_SEG
        mov es, bx
        cmp eax, [es:0]
        jne .done               ; not the USE screen
        call use_draw
.done:  pop fs
        pop es
        popad
        xor ax, ax              ; the replaced "xor ax,ax"
        mov si, [bp + 8]        ; ... and "pop si": move the interrupt frame up over it
        push dx
        mov dx, [bp + 6]
        mov [bp + 8], dx
        mov dx, [bp + 4]
        mov [bp + 6], dx
        mov dx, [bp + 2]
        mov [bp + 4], dx
        pop dx
        mov sp, bp
        pop bp
        add sp, 2
        iret

use_win dd 0                    ; the USE screen's window, as PROBE_USE last saw it

; PROBE_SAVE_PAGE: INT VEC_SAVE_PAGE replaces "jmp <the end>" (3 bytes: INT + NOP; DSUN.EXE 74901h),
; where the save/load window's event routine (74 8CAh; BP its frame) leaves a key it has no use
; for. The window shows ten saves, on four pages: the game's own (SAVE01.SAV to SAVE10.SAV), then
; SAVB, SAVC and SAVD01.SAV to 10.SAV. PgDn shows the next page, PgUp the one before; the PAGE 1
; to PAGE 4 buttons theirs (PROBE_SAVE_CLICK). The page is one letter of the game's two save
; names, the folder search's ("SAVE??.SAV") and the name a save is written to ("SAVE%.2d.SAV"):
; loading takes the name the search found. (The other pages' names don't match the game's
; search, so the game without the Ledger never sees them: its list has room for ten, and a
; SAVE11.SAV would run past it.) For the new page, the window's own routines (in its overlay, at
; CS: nothing they call is an overlay, so it stays put) search the folder again and draw the
; rows, the selected row stays (in the load window, the first save: PgUp and PgDn pass by pages
; with none; a button's page with none is shown, its first row chosen, and SAVE_LABEL greys
; LOAD while the row chosen has no save; Enter, which the game's key table no longer has, comes
; here too and goes on to LOAD only on a row with a save), and the window is drawn again (SAVE_LABEL greys the
; page's button); then on as the routine goes on after Up or Down.
SP_IGNORE  equ 0x74DEC - 0x74903 ; (DSUN.EXE, from the way back: the NOP after the INT) the routine's end
SP_DONE    equ 0x74A21 - 0x74903 ; ... after Up or Down: the screen updated, then the end
SP_KEY     equ 0x12             ; the key, at the routine's BP+12h
SP_SCAN    equ 0x743E2 - 0x74300 ; the overlay's routines (offsets in its segment): search the folder,
SP_ROW     equ 0x74E36 - 0x74300 ;   draw a row,
SP_PICK    equ 0x74ECD - 0x74300 ;   show a row as chosen (and its description below)
SAVE_SEG   equ 0x3BA7 - 0x4356  ; the window's data (DS-relative): the saves, 7Dh bytes each from +2
SAVE_SIZE  equ 0x7D             ;   (+2 the file's name, +52h the description), +4E4h the chosen
SAVE_CHOSEN equ 0x4E4           ;   row, +4E6h the window, +4EAh 1: the load window
SAVE_WIN   equ 0x4E6
SAVE_LOADING equ 0x4EA
BTN_SEG    equ 0x2A1D - 0x4356  ; (resident) a button's state: (window, id, state)
BTN_OFF    equ 0x71A
WIN_SEG    equ 0x25EC - 0x4356  ; (resident) bring a window to the front and draw it (PROBE_WIN's)
WIN_OFF    equ 0x618
NAME_FIND  equ 0x1DCF + 3       ; DS: "SAVE??.SAV": the letter that is the page
NAME_SAVE  equ 0x1DDA + 3       ; DS: "SAVE%.2d.SAV"
KEY_ENTER  equ 0x1C0D           ; (taken out of the window's key table: gamepatch.py's save_enter)
SP_ENTER   equ 0x74C68 - 0x74903 ; ... where the window's routine takes it: LOAD (or SAVE)
KEY_PGUP   equ 0x4900
KEY_PGDN   equ 0x5100
ROW_ID     equ 0x80B            ; the rows' buttons: 80Bh on
LOAD_ID    equ 0x809            ; LOAD (in the save window, SAVE)
probe_save_page:
        push bp
        mov bp, sp              ; BP+2 the way back, BP+4 its segment
        sti
        pushad
        push es
        mov word [cs:sp_done], SP_DONE
        mov word [cs:sp_ignore], SP_IGNORE
        mov byte [cs:sp_zero], 0
        mov si, [bp]            ; the routine's BP
        mov ax, [ss:si + SP_KEY]
        cmp al, 0xE0
        jne .scan
        xor al, al              ; (the grey keys': as the number pad's)
.scan:  cmp ax, KEY_ENTER
        jne .pages
        call sp_es              ; Enter: LOAD, as the game's own, but not on a row with no save
        cmp word [es:SAVE_LOADING], 0
        je .enter
        imul bx, [es:SAVE_CHOSEN], SAVE_SIZE
        cmp byte [es:bx + 2], 0
        je sp_skip
.enter: add word [bp + 2], SP_ENTER
        pop es
        popad
        pop bp
        iret
.pages: mov dl, -1              ; (the page before)
        cmp ax, KEY_PGUP
        je .step
        mov dl, 1               ; (the next)
        cmp ax, KEY_PGDN
        jne sp_skip
.step:  mov [cs:sp_step], dl
        call sp_now
        add al, dl
        mov bl, al
        jmp sp_want

; PROBE_SAVE_CLICK: INT VEC_SAVE_CLICK replaces "jmp <the end>" (3 bytes: INT + NOP; DSUN.EXE
; 74B9Fh), where the window's event routine leaves a click on a button it doesn't know: PAGE 1 to
; PAGE 4 (savepages.py) show that page (then the routine's end returns 0, as for the game's own
; buttons).
SC_DONE    equ 0x74A21 - 0x74BA1 ; (DSUN.EXE, from the way back) the screen updated, then the end
SC_IGNORE  equ 0x74DE5 - 0x74BA1 ; the end for the game's buttons
SC_ID      equ 8                ; the button, at the routine's BP+8
PAGE1_ID   equ 0x815            ; (PAGE 1 to PAGE 4: 815h to 818h)
PAGES      equ 4
probe_save_click:
        push bp
        mov bp, sp
        sti
        pushad
        push es
        mov word [cs:sp_done], SC_DONE
        mov word [cs:sp_ignore], SC_IGNORE
        mov byte [cs:sp_zero], 1
        mov si, [bp]
        mov ax, [ss:si + SC_ID]
        sub ax, PAGE1_ID
        cmp ax, PAGES
        jae sp_skip
        mov bl, al
        mov byte [cs:sp_step], 0  ; (that page or none)

sp_want:                        ; BL the page wanted (0 to 3), SP_STEP: where to look on from it
        cmp bl, PAGES           ;   if the load window has no saves on it (-1, 1; 0: nowhere)
        jae sp_skip             ; (no page before the first, or after the last)
        call sp_now
        cmp bl, al
        je sp_skip              ; (on that page already)
        mov [cs:sp_target], bl
        mov al, [NAME_FIND]
        mov [cs:sp_was], al     ; the page now, if no page wanted has anything to load
        mov ax, [bp + 4]
        mov [cs:sp_call + 2], ax
        call sp_es
        mov di, [es:SAVE_CHOSEN]
.try:   movzx bx, byte [cs:sp_target]
        mov bl, [cs:sp_letters + bx]
        call sp_page
        cmp word [es:SAVE_LOADING], 0
        je .rows
        xor di, di              ; loading: the first save on the page
.first: imul bx, di, SAVE_SIZE
        cmp byte [es:bx + 2], 0
        jne .rows
        inc di
        cmp di, 10
        jb .first
        mov al, [cs:sp_step]    ; none: the next page that way, if there is one
        add [cs:sp_target], al
        xor di, di              ; (a button's page: shown empty, its first row chosen)
        or al, al
        jz .rows
        cmp byte [cs:sp_target], PAGES
        jb .try
.none:  mov bl, [cs:sp_was]     ; none at all: back to the page there was
        call sp_page
        jmp sp_skip
.rows:  xor cx, cx
.row:   push cx
        push cx
        mov word [cs:sp_call], SP_ROW
        call far [cs:sp_call]
        add sp, 2
        pop cx
        inc cx
        cmp cx, 10
        jb .row
        call sp_es
        push word 5             ; the row chosen before: as the others
        mov ax, [es:SAVE_CHOSEN]
        call sp_button
        mov [es:SAVE_CHOSEN], di
        push word 4             ; ... and the one chosen now
        mov ax, di
        call sp_button
        push di
        mov word [cs:sp_call], SP_PICK
        call far [cs:sp_call]
        add sp, 2
        call sp_es
        push dword [es:SAVE_WIN]
        mov ax, ds
        add ax, WIN_SEG
        mov [cs:sp_call + 2], ax
        mov word [cs:sp_call], WIN_OFF
        call far [cs:sp_call]
        add sp, 4
        mov ax, [cs:sp_done]
        add [bp + 2], ax
        pop es
        popad
        cmp byte [cs:sp_zero], 0
        je .back
        xor si, si              ; (a click: the routine returns 0, as for its own buttons)
.back:  pop bp
        iret
sp_skip:
        mov ax, [cs:sp_ignore]
        add [bp + 2], ax
        pop es
        popad
        pop bp
        iret

sp_now:                         ; AL the page shown (0 to 3: the game's names' letter in SP_LETTERS)
        push bx
        mov al, [NAME_FIND]
        xor bx, bx
.find:  cmp al, [cs:sp_letters + bx]
        je .found
        inc bx
        cmp bx, PAGES
        jb .find
        xor bx, bx              ; (not one of them: as the first)
.found: mov al, bl
        pop bx
        ret

sp_page:                        ; BL the page's letter: the names, then the folder searched again
        mov [NAME_FIND], bl     ;   (ES the window's data again after)
        mov [NAME_SAVE], bl
        push word 10
        mov word [cs:sp_call], SP_SCAN
        call far [cs:sp_call]
        add sp, 2
sp_es:  mov ax, ds              ; ES: the window's data (the game's routines keep neither BX nor ES)
        add ax, SAVE_SEG
        mov es, ax
        ret

sp_button:                      ; AX a row, the state pushed (taken off): its button's state
        add ax, ROW_ID
sp_button_id:                   ; (AX the button)
        push bp
        mov bp, sp
        push word [bp + 4]      ; the state
        movsx eax, ax
        push eax
        push dword [es:SAVE_WIN]
        mov ax, ds
        add ax, BTN_SEG
        mov [cs:sp_btn + 2], ax
        call far [cs:sp_btn]
        add sp, 10
        call sp_es
        pop bp
        ret 2

sp_btn  dw BTN_OFF, 0           ; the game's routine for a button's state
sp_call dd 0
sp_was  db 0
sp_letters db 'EBCD'            ; the pages' letters (SAVE??.SAV, SAVB, SAVC, SAVD)
sp_target db 0
sp_step   db 0
sp_done   dw 0                  ; where the event routine goes on: after the page changes,
sp_ignore dw 0                  ;   or not (from the way back)
sp_zero   db 0                  ; 1: a click (the routine to return 0)

; SAVE_LABEL: (PROBE_WIN; EAX the window drawn, DS the game's) on the save/load window, the
; button of the page shown (PAGE 1 to PAGE 4, savepages.py) out of use: so it shows which; and in
; the load window, LOAD out of use while the row chosen has no save (an empty page's).
save_label:
        pushad
        push es
        cmp byte [cs:sl_busy], 0
        jne .out                ; (setting a button's state may draw the window again)
        mov bx, ds
        add bx, SAVE_SEG
        mov es, bx
        cmp eax, [es:SAVE_WIN]
        jne .out
        mov byte [cs:sl_busy], 1
        call sp_now
        mov [cs:sl_page], al
        xor si, si
.button:
        xor cx, cx              ; (states: 1 out of use, 0 in use)
        mov ax, si
        cmp al, [cs:sl_page]
        jne .state
        inc cx
.state: push cx
        lea ax, [si + PAGE1_ID]
        call sp_button_id
        inc si
        cmp si, PAGES
        jb .button
        cmp word [es:SAVE_LOADING], 0
        je .done
        imul bx, [es:SAVE_CHOSEN], SAVE_SIZE
        xor cx, cx
        cmp byte [es:bx + 2], 0
        jne .load
        inc cx                  ; (no save there)
.load:  push cx
        mov ax, LOAD_ID
        call sp_button_id
.done:  mov byte [cs:sl_busy], 0
.out:   pop es
        popad
        ret

sl_busy    db 0
sl_page    db 0

use_draw:                       ; the selected character's slots in the USE screen's panel
        mov ax, ds
        add ax, USE_WHO_SEG
        mov es, ax
        mov bx, [es:0x25B]      ; the character on show
        cmp bx, 3
        ja .done
        imul si, bx, SLOTS_SIZE
        add si, slots_text
        cmp byte [cs:si], 0
        je .done                ; no spells
        mov ax, ds
        add ax, USE_TEXT_SEG
        mov [cs:c_draw + 2], ax
        mov word [cs:c_draw], USE_TEXT_OFF
        mov eax, [0x11A4]       ; the screen's window
        mov [cs:c_winptr], eax
        mov dx, USE_FIRST_Y
.line:  mov di, u_line          ; copy one line (up to "|" or the end) and draw it
.copy:  mov al, [cs:si]
        cmp al, '|'
        je .cut
        cmp al, 0
        je .cut
        mov [cs:di], al
        inc si
        inc di
        cmp di, u_line + SLOTS_SIZE - 1
        jb .copy
.cut:   mov byte [cs:di], 0
        push si
        push dx
        push dx                 ; y
        push word USE_X         ; x
        push cs
        push word u_line
        call c_draw_line
        pop dx
        pop si
        add dx, USE_STEP
        cmp byte [cs:si], '|'
        jne .done
        inc si
        cmp dx, USE_LAST_Y
        jbe .line
.done:  ret
; PROBE_LOOK: INT VEC_LOOK replaces "mov si,ax / xor di,di" (4 bytes: INT + 2 NOPs) in the
; routine that fills the Look box (right-click to Look, then a monster in a fight), where the
; first of its four status rows have been drawn and AX says how many. Asks the companion
; for up to three short lines about the monster (LOOK_TEXT), prints them in the rows left
; with the game's text routine, as the box prints the monster's level, and moves the game's
; row count past them, so its own status lines follow in any row still free. If the
; companion also gave the whole description (LOOK_FULL), PROBE_TURN shows it in the dialogue
; window as the box closes (PROBE_UNLOOK).
LOOK_PATCH equ 0x5FCDA          ; DSUN.EXE offsets
LOOK_DRAW  equ 0x5FCB8          ; "lcall 0090h:0A40h" operand: the text routine
LOOK_ROWS  equ 4                ; the box's status rows: y = (row + 2) * 7 + 10h
LOOK_SIDE  equ 1                ; a line starting with this goes on LEVEL's row (row -1), to its right
LOOK_SIDE_X equ 60              ;   at this x (past LEVEL: 10)
probe_look:
        sti
        pushad
        push es
        mov si, sp              ; SS:SI: ES, then EDI, ESI, EBP, ESP, EBX, EDX, ECX, EAX, IP, CS, flags
        mov ax, [ss:si + 30]
        mov [cs:l_row], ax      ; the rows the game has used
        cmp word [cs:look_on], 0
        je .done
        mov bx, [bp + 0xA]      ; the combatant (BP: the Look routine's frame)
        mov [cs:look_who], bx
        mov byte [cs:look_text], 0
        mov byte [cs:look_full], 0
        inc word [cs:look_seq]
        xor ax, ax
        mov es, ax
        mov dx, [es:0x46C]      ; the BIOS timer
        call rtc_begin
.wait:  mov ax, [cs:look_reply]
        cmp ax, [cs:look_seq]
        je .ready
        mov ax, [es:0x46C]
        sub ax, dx
        cmp ax, TURN_WAIT
        jae .late_look_reply
        call rtc_waited         ; (the BIOS clock can stand still: the game's timer
        jnc .wait               ;   doesn't always pass its ticks on; at most 2 s by the real-time clock)
.late_look_reply:
        jmp .done               ; no answer: the companion isn't reading
.ready: mov es, [ss:si + 36]
        mov di, [ss:si + 34]
        sub di, 2               ; ES:DI = the patch
        mov eax, [es:di + LOOK_DRAW - LOOK_PATCH]
        mov [cs:l_draw], eax
        mov eax, [bp + 6]       ; the box's window
        mov [cs:l_win], eax
        mov di, look_text
.line:  mov byte [cs:l_side], 0
        cmp byte [cs:di], LOOK_SIDE
        jne .rows
        inc di
        mov byte [cs:l_side], 1
        jmp .take
.rows:  cmp word [cs:l_row], LOOK_ROWS
        jae .full
        cmp byte [cs:di], 0
        je .full
.take:  mov bx, l_line          ; the next line, up to "|", into L_LINE
.copy:  mov al, [cs:di]
        cmp al, '|'
        je .cut
        or al, al
        je .cut
        cmp bx, l_line + L_LINE_SIZE - 1
        jae .skip
        mov [cs:bx], al
        inc bx
.skip:  inc di
        jmp .copy
.cut:   mov byte [cs:bx], 0
        cmp byte [cs:di], '|'
        jne .draw
        inc di
.draw:  push di
        mov ax, [cs:l_row]
        mov cx, 6
        cmp byte [cs:l_side], 0
        je .at
        mov ax, -1              ; (LEVEL's row, above the status rows, to its right)
        mov cx, LOOK_SIDE_X
.at:    add ax, 2
        imul ax, ax, 7
        add ax, 0x10
        push word 0x11          ; as the box prints LEVEL
        push word 0x1F
        push ax                 ; y
        push cx                 ; x
        push cs
        push word l_line
        push dword [cs:l_win]
        call far [cs:l_draw]
        add sp, 16
        pop di
        cmp byte [cs:l_side], 0
        jne .line               ; (beside LEVEL: no row of its own)
        inc word [cs:l_row]
        jmp .line
.full:  cmp byte [cs:look_full], 0
        je .done
        mov byte [cs:look_pending], 1
.done:  mov si, sp              ; the replaced code: SI = the rows used, DI = 0
        mov ax, [cs:l_row]
        mov [ss:si + 6], ax
        mov dword [ss:si + 2], 0
        pop es
        popad
        iret

; PROBE_UNLOOK: INT VEC_UNLOOK replaces "mov word [0844h],270Fh" (6 bytes: INT + 4 NOPs) at the
; end of the routine that closes the Look box (the game forgets whom it was looking at). Does
; that, then shows the monster's whole description (LOOK_FULL) in the dialogue window.
probe_unlook:
        mov word [0x844], 0x270F  ; the replaced instruction (DS = the game's)
        sti
        pushad
        push es
        cmp byte [cs:look_pending], 0
        je .out
        cmp byte [cs:showing], 0
        jne .out
        mov byte [cs:look_pending], 0
        mov word [cs:show_text], look_full
        call show_window
.out:   pop es
        popad
        iret

; STATS: STATS_SIZE bytes for each party member, kept by the companion: +0 1 if in use, +1 THAC0
; with the main weapon (signed), +2 the five saves as the d20 needed now, +8 three words: the
; item numbers of the weapons ready, +14 three bytes: the THAC0 with each (signed), +17 1 for a
; thief (2 for a ranger: of the six, only move silently and hide in shadows count), +18 the six
; thief skills the panel shows, as they stand (equipment and effects too)
STATS_SIZE  equ 24
STATS_RANGER equ 2
STATS_FRESH equ 91              ; timer ticks (5 seconds)
STATS_WAIT  equ 9               ; ... (half a second): the longest a screen waits for fresh STATS
stats   times 4 * STATS_SIZE db 0

; A screen is being drawn: while the companion is keeping STATS, have it bring them up to date
; (an item just put on, say) and wait for that, once for all the screen's lines (not again
; within 2 timer ticks). The game stands still meanwhile, so its state is what's drawn.
stats_sync:
        push ax
        push dx
        push es
        xor ax, ax
        mov es, ax
        mov dx, [es:0x46C]
        mov ax, dx
        sub ax, [cs:sync_at]
        cmp ax, 2
        jb .out                 ; this screen's already
        mov ax, dx
        sub ax, [cs:stats_stamp]
        cmp ax, STATS_FRESH
        jae .out                ; the companion isn't running
        inc word [cs:stats_req]
        call rtc_begin
.wait:  mov ax, [cs:stats_reply]
        cmp ax, [cs:stats_req]
        je .done
        mov ax, [es:0x46C]
        sub ax, dx
        cmp ax, STATS_WAIT
        jae .done
        call rtc_waited         ; (as the waits for the companion: the BIOS clock can stand still)
        jnc .wait
.done:  mov ax, [es:0x46C]
        mov [cs:sync_at], ax
.out:   pop es
        pop dx
        pop ax
        ret

sync_at dw 0

; RTC_BEGIN, RTC_WAITED: a wait for the companion ends after its BIOS ticks, or, as the BIOS clock
; stands still while the game's timer handler keeps its ticks to itself (as in some fights), once
; the real-time clock's second has changed twice (1 to 2 seconds): never a hang.
rtc_begin:
        push ax
        call rtc_second
        mov [cs:w_sec], al
        mov byte [cs:w_flips], 0
        pop ax
        ret
rtc_waited:                             ; CF set: the wait is over
        push ax
        call rtc_second
        cmp al, [cs:w_sec]
        je .no
        mov [cs:w_sec], al
        inc byte [cs:w_flips]
        cmp byte [cs:w_flips], 2
        jb .no
        pop ax
        stc
        ret
.no:    pop ax
        clc
        ret
rtc_second:                             ; AL = the real-time clock's seconds (CMOS register 0)
        pushf
        cli
        mov al, 0
        out 0x70, al
        in al, 0x71
        popf
        ret
w_sec   db 0
w_flips db 0

; BX = a party member (0-3): CF clear and CS:BX = their STATS entry if the companion keeps it
; current, CF set if not
stats_for:
        cmp bx, 3
        ja .no
        push ax
        push es
        xor ax, ax
        mov es, ax
        mov ax, [es:0x46C]
        sub ax, [cs:stats_stamp]
        cmp ax, STATS_FRESH
        pop es
        pop ax
        jae .no
        imul bx, bx, STATS_SIZE
        add bx, stats
        cmp byte [cs:bx], 0
        je .no
        clc
        ret
.no:    stc
        ret

; THAC0 (AL) and the saves (sheet +37h..+3Bh at ES:SI) into C_VALS, or the companion's numbers
; for the character in C_WHO instead. Keeps ES, SI, DI.
c_load_stats:
        push bx
        mov [cs:c_vals], al
        mov eax, [es:si + 0x37]
        mov [cs:c_vals + 1], eax
        mov al, [es:si + 0x3B]
        mov [cs:c_vals + 5], al
        mov bx, [cs:c_who]
        call stats_sync
        call stats_for
        jc .own
        mov al, [cs:bx + 1]
        mov [cs:c_vals], al
        mov eax, [cs:bx + 2]
        mov [cs:c_vals + 1], eax
        mov al, [cs:bx + 6]
        mov [cs:c_vals + 5], al
.own:   pop bx
        ret

c_itoa_s:                       ; AL (signed) -> decimal at CS:DI, with "-" below 0; keeps BX, CX
        test al, al
        jns c_itoa
        mov byte [cs:di], '-'
        inc di
        neg al
        jmp c_itoa

; PROBE_WEAPON: INT VEC_WEAPON replaces "add sp,10h" (3 bytes: INT + NOP) straight after the
; routine that lists a creature's weapons (on the inventory screen, and in the Look box) has
; drawn one: its name, then its damage, AX lines from y = [BP+0Eh]. Does the add, then puts
; the THAC0 the companion worked out for that weapon at the right of its last line.
W_PATCH equ 0x7276E             ; DSUN.EXE offsets
W_DRAW  equ 0x72851             ; "lcall 0090h:0A40h" operand: the routine that draws the lines
W_X     equ 0x12A               ; (window coordinates: the lines start at 0ECh)
probe_weapon:
        pop word [cs:w_ip]
        pop word [cs:w_cs]
        pop word [cs:w_fl]
        add sp, 0x10            ; the replaced instruction
        sti
        pushad
        push es
        or ax, ax
        jz .done
        mov [cs:w_lines], ax
        mov bx, [bp + 0x0A]     ; the creature (its object number: the party's are 0-3)
        cmp bx, 3
        ja .done
        call stats_sync
        call stats_for
        jc .done
        mov si, [bp - 2]        ; the entry of the item list the weapon came from
        imul si, si, 10
        mov dx, [bp + si - 0x330]  ; its item number
        xor cx, cx
.find:  mov di, cx
        shl di, 1
        cmp [cs:bx + di + 8], dx
        je .found
        inc cx
        cmp cx, 3
        jb .find
        jmp .done
.found: mov di, cx
        mov al, [cs:bx + di + 14]
        mov di, w_text + 1
        call c_itoa_s
        mov es, [cs:w_cs]
        mov di, [cs:w_ip]
        mov eax, [es:di + W_DRAW - W_PATCH - 2]
        mov [cs:w_draw], eax
        mov ax, [cs:w_lines]
        dec ax
        imul ax, ax, 7
        add ax, [bp + 0x0E]     ; the weapon's last line
        push word [bp + 0x12]   ; the colours, as the routine draws its lines
        push word [bp + 0x10]
        push ax
        push word W_X
        push cs
        push word w_text
        push dword [bp + 6]     ; the window
        call far [cs:w_draw]
        add sp, 0x10
.done:  pop es
        popad
        push word [cs:w_fl]
        push word [cs:w_cs]
        push word [cs:w_ip]
        iret

w_ip    dw 0
w_cs    dw 0
w_fl    dw 0
w_lines dw 0
w_draw  dd 0
w_text  db 'T', 0, 0, 0, 0
c_who   dw 0
c_signed db 0

; RINGS: the game has rings (item type RING_TYPE, a plain "Ring") but nothing that makes
; one better AC or saves. The companion can put a Ring +1 in the arena; these two make its
; plus count, as a ring of protection's would.
RING_TYPE  equ 102
HELM_LEATHER equ 5              ; the helm item types: Helm, Dapartea's Helm; Helm of
HELM_METAL   equ 89             ; Contemplation; and a leather one no object uses (Helm of
HELM_OTHER   equ 109            ; Might, made by a script)
HELM_BONE    equ 117            ; (and the companion's bone helm, of TYPES)
FINGER     equ 4                ; the item's slot byte while worn on a finger (the left
FINGER2    equ 11               ; hand's, then the right's)
CLOAK      equ 12               ; ... and on the back
THINGS     equ 0xC36            ; the things table (3 bytes each: kind, index) in its segment
NO_THING   equ 0x270F
CREATURES  equ 0x1665           ; DS: far pointer to the creature records (3Ah bytes each)
ITEMS      equ 0x165D           ; DS: far pointer to the item records (15h bytes each)
ITEM_TYPES equ 0x1669           ; DS: far pointer to the item types (14h bytes each; +0Ah: 1 melee)
WS_MELEE   equ 0xFFFE           ; WS_TYPE: any melee weapon

; PROBE_RING_AC: INT VEC_RING_AC replaces "mov al,es:[bx+0Fh] / cbw" (5 bytes: INT + 3 NOPs)
; in the AC function, where ES:BX is a worn item's type, CX its number, DX the item and DI the
; creature's thing; bit 80h of AX says the type counts for AC (the plus less the type's AC).
; Does that, counting rings too. With RULE_PROTECTION, AD&D's: a ring of protection betters
; AC only without magical armour, and of two rings only the better (the left hand's if they're
; equal); a cloak of protection (the second of TYPES) counts only without magical or metal
; armour and without a shield (PROT_SCAN).
probe_ring_ac:
        mov al, [es:bx+0x0F]
        cbw
        cmp cx, RING_TYPE
        jne .cloak
        or al, 0x80
        test word [cs:rules], RULE_PROTECTION
        jz .done
        call prot_here
        jc .done
        test byte [cs:p_flags], P_MAGIC_ARMOUR
        jnz .off
        push ax
        push bx
        push es
        les bx, [ITEMS]
        mov ax, dx
        imul ax, ax, 0x15
        add bx, ax
        mov al, [es:bx+0x11]    ; the slot this ring is worn in
        mov ah, [cs:p_ring]
        cmp al, FINGER
        je .first
        cmp [cs:p_ring2], ah    ; (the other hand's) counts if better than the left's
        jmp .which
.first: cmp ah, [cs:p_ring2]    ; the left hand's counts unless the other is better
.which: pop es
        pop bx
        pop ax
        jg .done                ; (the better: counts)
        je .tie
        jmp .off
.tie:   push ax                 ; equal: the left hand's
        push bx
        push es
        les bx, [ITEMS]
        mov ax, dx
        imul ax, ax, 0x15
        add bx, ax
        cmp byte [es:bx+0x11], FINGER
        pop es
        pop bx
        pop ax
        je .done
.off:   and al, 0x7F
.done:  iret
.cloak: push ax
        mov ax, [cs:types_first]
        or ax, ax
        jz .other
        inc ax
        cmp cx, ax
        jne .other
        pop ax
        test word [cs:rules], RULE_PROTECTION
        jz .done
        call prot_here
        jc .done
        test byte [cs:p_flags], P_MAGIC_ARMOUR | P_METAL_ARMOUR | P_SHIELD
        jnz .off
        iret
.other: pop ax
        push ax                 ; bracers of defense: their plus counts, but not over armour
        mov ax, cx              ; on the arms, legs, head or chest (AD&D's; a shield, rings and
        call bracers_ax         ; cloaks go with them)
        pop ax
        jne .helm
        call prot_here
        jc .done
        test byte [cs:p_flags], P_ARMOUR
        jnz .off
        iret
.helm:  cmp cx, HELM_LEATHER    ; a helm: AC 1 with RULE_HELMS (the game's are all 0), 0 without
        je .is
        cmp cx, HELM_METAL
        je .is
        cmp cx, HELM_OTHER
        je .is
        cmp cx, HELM_BONE
        je .is
        iret
.is:    push ax
        xor al, al
        test byte [cs:rules], RULE_HELMS
        jz .set
        inc al
.set:   mov [es:bx+0x12], al    ; (the type's AC, read next)
        pop ax
        iret

; PROBE_RING_SAVE: INT VEC_RING_SAVE replaces "xor si,si" (2 bytes) at the start of the
; function that adds up a saving throw's modifiers into SI, DI being the one saving. Starts
; SI at the plus of the rings they wear instead of 0.
probe_ring_save:
        push bp
        mov bp, sp              ; SS:BP+2 = our return address
        push ax
        push bx
        push cx
        push dx
        push es
        xor si, si
        les bx, [bp+2]
        mov ax, [es:bx+6]       ; the things table's segment: the code after the patch is
        call ring_plus          ; "mov bx,di / imul bx,bx,3 / mov ax,<segment>"
        mov bx, [bp]            ; (the routine's frame: [BP+14h] the spell)
        mov ax, [ss:bx + 0x14]
        call kit_save
        pop es
        pop dx
        pop cx
        pop bx
        pop ax
        pop bp
        iret

ring_plus:                      ; DS = the game's, AX = the things table's segment, DI = a
        mov [cs:r_things], ax   ; creature's thing: SI += the pluses of the rings it wears
        test word [cs:rules], RULE_PROTECTION
        jnz .rules
        mov es, ax
        mov bx, di
        imul bx, bx, 3
        cmp byte [es:bx+THINGS], 2
        jne .done               ; not a creature
        mov ax, [es:bx+THINGS+1]
        mov [cs:r_who], ax
        mov word [cs:ws_slot], FINGER
        mov word [cs:ws_type], RING_TYPE
        call worn_scan
        add si, [cs:ws_plus]
        mov ax, [cs:r_who]
        mov word [cs:ws_slot], FINGER2
        call worn_scan
        add si, [cs:ws_plus]
.cloak: mov ax, [cs:types_first]  ; and a cloak of protection's (the second of TYPES), worn
        or ax, ax
        jz .done
        inc ax
        mov [cs:ws_type], ax
        mov word [cs:ws_slot], CLOAK
        mov ax, [cs:r_who]
        call worn_scan
        add si, [cs:ws_plus]
.done:  ret
.rules: call prot_scan          ; RULE_PROTECTION: the better ring only, whatever the armour;
        jc .done                ; the cloak only without magical or metal armour or a shield
        mov al, [cs:p_ring]
        cmp al, [cs:p_ring2]
        jge .ring
        mov al, [cs:p_ring2]
.ring:  cbw
        add si, ax
        test byte [cs:p_flags], P_MAGIC_ARMOUR | P_METAL_ARMOUR | P_SHIELD
        jnz .done
        mov es, [cs:r_things]
        mov bx, di
        imul bx, bx, 3
        mov ax, [es:bx+THINGS+1]
        mov [cs:r_who], ax
        jmp .cloak

; PROT_HERE: (called from PROBE_RING_AC, its INT's return address at SP+2) PROT_SCAN for the
; AC routine's creature DI, the things table's segment being in its "mov ax,<segment>" A8h
; bytes on (DSUN.EXE 58F66h).
prot_here:
        push bp
        mov bp, sp
        push es
        push bx
        les bx, [bp+4]
        mov bx, [es:bx+0xA8]
        mov [cs:r_things], bx
        pop bx
        pop es
        pop bp
        ; (on into PROT_SCAN)

; PROT_SCAN: what creature thing DI wears (DS = the game's, R_THINGS the things table's
; segment) that the protection rules weigh: P_FLAGS (P_MAGIC_ARMOUR: a worn armour piece,
; chest, arm, leg or helm, with a plus; P_METAL_ARMOUR: one of metal; P_SHIELD: a shield in a hand),
; P_RING and P_RING2 (the plus of the ring on each hand's finger, 0 for none or less). CF set
; if DI isn't a creature. Keeps every register.
prot_scan:
        pushad
        push es
        mov es, [cs:r_things]
        mov bx, di
        imul bx, bx, 3
        cmp byte [es:bx+THINGS], 2
        jne .not
        mov ax, [es:bx+THINGS+1]
        imul ax, ax, 0x3A
        mov [cs:r_creature], ax
        mov byte [cs:p_flags], 0
        mov word [cs:p_ring], 0         ; (and P_RING2)
        mov cx, 8                       ; its item lists, each a thing: +8, +0Ah, +0Ch
.list:  les bx, [CREATURES]
        add bx, [cs:r_creature]
        add bx, cx
        mov dx, [es:bx]
        cmp dx, NO_THING
        jae .next
        mov es, [cs:r_things]
        mov bx, dx
        imul bx, bx, 3
        cmp byte [es:bx+THINGS], 1
        jne .next                       ; not an item
        mov dx, [es:bx+THINGS+1]
        mov byte [cs:r_left], 100
.item:  cmp dx, NO_THING
        jae .next
        les bx, [ITEMS]
        mov ax, dx
        imul ax, ax, 0x15
        add bx, ax
        mov dx, [es:bx+4]               ; (the next)
        mov si, [es:bx+0x0A]            ; the type
        mov al, [es:bx+0x11]            ; the slot
        mov ah, [es:bx+0x14]            ; the plus
        cmp si, RING_TYPE
        jne .type
        cmp ah, 0
        jle .on
        cmp al, FINGER
        jne .ring2
        mov [cs:p_ring], ah
        jmp .on
.ring2: cmp al, FINGER2
        jne .on
        mov [cs:p_ring2], ah
        jmp .on
.type:  push ax
        mov ax, si
        call bracers_ax                 ; (bracers aren't armour)
        pop ax
        je .on
        les bx, [ITEM_TYPES]
        imul si, si, 0x14
        add bx, si
        test byte [es:bx], 4            ; a shield (the flag the game's AC reads for one)
        jz .body
        cmp al, HAND_RIGHT
        je .shield
        cmp al, HAND_LEFT
        jne .on
.shield: or byte [cs:p_flags], P_SHIELD
        jmp .on
.body:  cmp al, ARM_SLOT                ; armour: worn on the arms, legs, head or chest, and
        je .armour                      ; counting for AC
        cmp al, LEG_SLOT
        je .armour
        cmp al, HEAD_SLOT
        je .armour
        cmp al, CHEST_SLOT
        jne .on
.armour: test byte [es:bx+0x0F], 0x80
        jz .on
        or byte [cs:p_flags], P_ARMOUR  ; (a helm too, to bracers)
        cmp ah, 0
        jle .metal
        or byte [cs:p_flags], P_MAGIC_ARMOUR
.metal: mov al, [es:bx+8]               ; its material (no material: 40h with 0)
        and al, 0x4F
        cmp al, MATERIAL_METAL
        jne .on
        or byte [cs:p_flags], P_METAL_ARMOUR
.on:    dec byte [cs:r_left]
        jnz .item
.next:  add cx, 2
        cmp cx, 0x0E
        jb .list
        clc
        jmp .out
.not:   stc
.out:   pop es
        popad
        ret

; Weapon specialization (RULE_SPECIALIZE; dscompanion/specialize.py). A character's chosen weapon
; kinds are in its sheet's +14h..+17h (SPEC_SLOTS: the kind + 1 each, 0 none); a character with
; none there (monsters, and those who have not chosen) fights as the game has it. Two probes in
; the routine that makes a weapon attack (its [BP+0Ah] the THAC0 the d20 is held against, [BP+10h]
; the attacker's sheet, [BP+14h] the weapon's item type, [BP+16h] above 1 for a missile; its
; locals [BP-8] the attacks a round in halves, [BP-0Eh] the damage dice and [BP-10h] their
; sides, [BP-12h] the damage bonus):
; PROBE_ATTACKS: INT VEC_ATTACKS replaces "cbw / mov [bp-8],ax" (4 bytes: INT + 2 NOPs; DSUN.EXE
; 58892h), the attacks a round stored. The game gives every fighter, gladiator and ranger AD&D's
; specialist's rate (3/2, then 2 from 7th level, 5/2 from 13th); in melee a warrior (more than 2
; halves) with a weapon of a kind not chosen has AD&D's plain rate, half an attack less, and a
; grand master one more. Specialization +1 to hit, mastery +3 (the THAC0 less).
; PROBE_SPEC_DAMAGE: INT VEC_SPEC_DAMAGE replaces "add [bp-12h],ax" (3 bytes: INT + NOP; 588F2h),
; the strength bonus added to the damage bonus: specialization +2, mastery +3, and grand mastery
; the damage dice one size larger (2 more sides: d8 to d10, 2d4 to 2d6).
SPEC_SLOTS equ 0x14
SPEC_COUNT equ 4
FIGHTER_CLASS equ 9
GLADIATOR_CLASS equ 10
MASTERY equ 5                   ; (the fighter levels)
GRAND_MASTERY equ 9
SPEC_NONE equ 0                 ; (SPEC_OF's levels)
SPEC_PLAIN equ 1
SPEC_EXPERT equ 2
SPEC_SPECIAL equ 3
SPEC_MASTER equ 4
SPEC_GRAND equ 5
probe_attacks:
        cbw
        push ax
        call kit_to_hit         ; (a Ravager's, a Brute's, an Arena Champion's)
        sub [bp + 0x0A], ax
        pop ax
        test word [cs:rules], RULE_SPECIALIZE
        jz .store
        push dx
        call spec_of
        cmp word [bp + 0x16], 1
        jg .missile
        cmp ax, 2
        ja .warrior
        call expert_of          ; (not a warrior: a Battle Mage's expertise)
        jmp .hit
.warrior:
        cmp dl, SPEC_PLAIN
        jne .grand
        dec ax
        jmp .hit
.grand: cmp dl, SPEC_GRAND
        jne .hit
        add ax, 2
.hit:   cmp dl, SPEC_SPECIAL
        jb .done
        ja .three
        dec word [bp + 0x0A]
        jmp .done
.three: sub word [bp + 0x0A], 3
.done:  pop dx
.store: mov [bp - 8], ax
        iret
.missile:                       ; (a specialist's rate of fire, else the game's)
        push bx
        push si
        push es
        mov si, [bp + 0x10]
        imul si, si, 0x47
        les bx, [0x1661]
        add bx, si
        mov si, [bp + 0x14]
        call missile_rate
        pop es
        pop si
        pop bx
        jmp .hit

; MISSILE_RATE: AX (the attacks a round in halves: the weapon type's, +0Bh, as the game has a
; missile's) made a specialist's rate of fire when greater, for skill DL (SPEC_EXPERT or above: a
; fighter's or gladiator's chosen kind, a ranger's, a ranger's bow) with item type SI, the sheet at
; ES:BX, by the warrior's level (the highest fighter, gladiator or ranger level of the classes it
; has now: 1-6, 7-12, 13 on): AD&D's
; for the sling, 3/2, 2, 5/2 a round; the bow, staff sling and chatkcha a step above AD&D's, the bow
; 3, 4, 5, the staff sling and chatkcha 3/2, 2, 5/2; a grand master one more. A warrior's weapon of
; a kind not chosen (SPEC_PLAIN), from 7th level: the specialist's rate a band lower, as in melee
; (a bow 3 at 7-12, a sling 3/2).
MISSILE_KIND equ 13             ; (the chatkcha's kind + 1; then the bow, the sling, the staff sling)
missile_halves db 3, 4, 5, 6, 8, 10, 3, 4, 5, 3, 4, 5
missile_rate:
        cmp dl, SPEC_PLAIN
        jb .ret
        cmp si, KIND_TYPES
        jae .ret
        push cx
        push dx
        push si
        movzx cx, byte [cs:si + kind_of_type]
        sub cx, MISSILE_KIND
        jb .out
        cmp cx, 3
        ja .out
        xor dx, dx              ; DL the specialist's level
        xor si, si
.class: push ax
        mov al, [es:bx + si + 0x21]
        mov ah, [es:bx + si + 0x24]
        or si, si               ; (a human's earlier classes once the first's level has passed theirs)
        jz .on
        cmp byte [es:bx + 0x18], 1
        jne .on
        cmp ah, [es:bx + 0x24]
        jae .next
.on:    cmp al, FIGHTER_CLASS
        je .warrior
        cmp al, GLADIATOR_CLASS
        je .warrior
        cmp al, 13              ; (a ranger, 13-16)
        jb .next
        cmp al, 16
        ja .next
.warrior:
        cmp ah, dl
        jbe .next
        mov dl, ah
.next:  pop ax
        inc si
        cmp si, 3
        jb .class
        imul cx, cx, 3
        cmp dl, 7
        jb .tier
        inc cx
        cmp dl, 13
        jb .tier
        inc cx
.tier:  mov si, sp
        cmp byte [ss:si + 2], SPEC_PLAIN  ; (the skill: DX, pushed before SI)
        jne .rate
        cmp dl, 7
        jb .out                 ; (a kind not chosen before 7th level: the weapon's own rate)
        dec cx                  ; (from 7th: a specialist's a band lower)
.rate:  mov si, cx
        movzx cx, byte [cs:si + missile_halves]
        cmp cx, ax
        jbe .grand
        mov ax, cx
.grand: pop si                  ; (DX, pushed after it: the skill back in DL)
        pop dx
        push dx
        push si
        cmp dl, SPEC_GRAND
        jne .out
        add ax, 2               ; (a grand master one more shot, as in melee)
.out:   pop si
        pop dx
        pop cx
.ret:   ret

; EXPERT_OF: EXPERT_HALVES for the attack routine's attacker ([BP+10h] its sheet's number).
expert_of:
        push bx
        push es
        push ax
        mov ax, [bp + 0x10]
        imul ax, ax, 0x47
        les bx, [0x1661]
        add bx, ax
        pop ax
        call expert_halves
        pop es
        pop bx
        ret

; EXPERT_HALVES: AX the attacks a round (halves) of a character who isn't a warrior (the game's 2:
; 1 a round), DL its skill with the weapon (SPEC_OF_SHEET), ES:BX its sheet: with SPEC_EXPERT (a
; Battle Mage's chosen weapon spec, kits.py; no warrior has fewer than 3 halves) the expertise
; rate, 3/2 a round, 2 from 7th level (specialize.expert_attacks). Others kept.
expert_halves:
        cmp dl, SPEC_EXPERT
        jne .ret
        cmp ax, 2
        ja .ret
        push ax
        call kit_level
        cmp al, 7
        pop ax
        mov ax, 3
        jb .ret
        inc ax
.ret:   ret

probe_spec_damage:
        add [bp - 0x12], ax
        call kit_attack_damage
        test word [cs:rules], RULE_SPECIALIZE
        jz .done
        push dx
        call spec_of
        cmp dl, SPEC_SPECIAL
        jb .out
        ja .three
        add word [bp - 0x12], 2
        jmp .out
.three: add word [bp - 0x12], 3
        cmp dl, SPEC_GRAND
        jne .out
        add word [bp - 0x10], 2
.out:   pop dx
.done:  iret

; PROBE_DAM_LINE: INT VEC_DAM_LINE replaces "mov al,es:[bx+2Ah]" (4 bytes: INT + 2 NOPs; DSUN.EXE
; 72A77h) in the routine that writes a melee weapon's "DAM: 1.5x1D8+4" (View Character, the
; inventory screen), ES:BX the sheet, SI the weapon's item type, the damage bonus, the dice's sides
; and their count pushed (under the INT's return, in that order up). With RULE_SPECIALIZE, the
; attacks as PROBE_ATTACKS gives them, and the bonus and sides as PROBE_SPEC_DAMAGE does.
probe_dam_line:
        push bp
        mov bp, sp              ; BP+8 the count, +0Ah the sides, +0Ch the bonus
        push ax
        call kit_melee          ; (a Ravager's, a Brute's)
        add [bp + 0x0C], ax
        pop ax
        pop bp
        mov al, [es:bx + 0x2a]
        test word [cs:rules], RULE_SPECIALIZE
        jz .done
        push bp
        mov bp, sp
        push dx
        call spec_of_sheet
        cmp al, 2
        ja .warrior
        push cx                 ; (not a warrior: a Battle Mage's expertise; AH kept)
        mov ch, ah
        xor ah, ah
        call expert_halves
        mov ah, ch
        pop cx
        jmp .bonus
.warrior:
        cmp dl, SPEC_PLAIN
        jne .grand
        dec al
        jmp .bonus
.grand: cmp dl, SPEC_GRAND
        jne .bonus
        add al, 2
.bonus: cmp dl, SPEC_SPECIAL
        jb .out
        ja .three
        add word [bp + 0x0C], 2
        jmp .out
.three: add word [bp + 0x0C], 3
        cmp dl, SPEC_GRAND
        jne .out
        add word [bp + 0x0A], 2
.out:   pop dx
        pop bp
.done:  iret

; PROBE_VIEW_DAM: INT VEC_VIEW_DAM replaces "mov [bp-0Eh],dx" (3 bytes: INT + NOP; DSUN.EXE 64EB6h)
; in View Character's routine for its "DAM: 1.5x1D8+4": DX the damage bonus it stores, its [BP-6]
; the attacks a round (halves), [BP-2] the dice's count, [BP-4] their sides, [BP-0Ah] the weapon's
; item. With RULE_SPECIALIZE, the attacks (not a missile weapon's), the bonus and the sides as the
; attack has them (PROBE_ATTACKS, PROBE_SPEC_DAMAGE), for the character on show: its number in
; the segment the routine's "mov ax,seg" at 64E33h holds, +25Bh (as PROBE_VIEW's CH_WHO).
VIEW_DAM_WHO equ 0x84           ; that operand, back from the INT's return
probe_view_dam:
        mov [bp - 0x0E], dx
        call wp_rules
        jz .done
        push ax
        push bx
        push dx
        push si
        push di
        push es
        mov ax, [bp - 0x0A]     ; the item, its type
        imul ax, ax, 0x15
        les bx, [0x165D]
        add bx, ax
        mov si, [es:bx + 0x0A]
        mov di, sp
        mov bx, [ss:di + 12]    ; (the INT's return, past the six pushes)
        mov es, [ss:di + 14]
        mov es, [es:bx - VIEW_DAM_WHO]
        mov ax, [es:0x25B]      ; the character on show, its sheet
        imul ax, ax, 0x47
        les bx, [0x1661]
        add bx, ax
        push ax
        call kit_melee          ; (a Ravager's, a Brute's: the rule for kits being on)
        add [bp - 0x0E], ax
        pop ax
        mov dl, SPEC_NONE
        test word [cs:rules], RULE_SPECIALIZE
        jz .bonus
        call spec_of_sheet
        mov ax, si              ; a missile weapon (the type's +0, 2) keeps its rate
        imul ax, ax, 0x14
        push es
        push bx
        les bx, [0x1669]
        add bx, ax
        test byte [es:bx], 2
        pop bx
        pop es
        jnz .bonus
        cmp word [bp - 6], 2
        ja .warrior
        push ax                 ; (not a warrior: a Battle Mage's expertise)
        mov ax, [bp - 6]
        call expert_halves
        mov [bp - 6], ax
        pop ax
        jmp .bonus
.warrior:
        cmp dl, SPEC_PLAIN
        jne .grand
        dec word [bp - 6]
        jmp .bonus
.grand: cmp dl, SPEC_GRAND
        jne .bonus
        add word [bp - 6], 2
.bonus: cmp dl, SPEC_SPECIAL
        jb .out
        ja .three
        add word [bp - 0x0E], 2
        jmp .out
.three: add word [bp - 0x0E], 3
        cmp dl, SPEC_GRAND
        jne .out
        add word [bp - 4], 2
.out:   pop es
        pop di
        pop si
        pop dx
        pop bx
        pop ax
.done:  iret

; PROBE_CAN_USE: INT VEC_CAN_USE replaces "and ax,[es:bx+12h]" (4 bytes: INT + 2 NOPs; DSUN.EXE
; 6EF34h) in the routine that says whether a character may equip an item (its "Cannot use this
; item" when not; its only caller, the equip routine): AX the item type's mask of the classes that
; may use it, ES:BX the character's sheet, its +12h a bit for each of its classes, DX the item
; type. With RULE_RESTRICT, an item the game allows that the character's classes keep it from
; (CLASS_FORBIDS) is not allowed either. The kit's own (KIT_ALLOWS) are allowed whatever the
; game's lists and the restrictions; the kit's limits (KIT_FORBIDS) hold over everything.
probe_can_use:
        and ax, [es:bx + 0x12]
        jnz .game
        call kit_allows         ; (the game's lists say no: the kit's own, all the same)
        jnc .done
        inc ax
.game:  mov byte [cs:kf_spec], 0
        push si
        mov si, [bp]            ; (the equip routine's frame: its [BP+8] the slot, 14 the off hand)
        cmp word [ss:si + 8], EQUIP_OFF_HAND
        pop si
        sete [cs:kf_off_hand]
        call kit_forbids
        mov byte [cs:kf_off_hand], 0
        jc .no
        test word [cs:rules], RULE_RESTRICT
        jz .done
        call kit_allows         ; (the kit's own: whatever the classes' restrictions)
        jc .done
        call class_forbids
        jnc .done
.no:    xor ax, ax
.done:  iret

; KIT_FORBIDS: carry set if the kit of the character whose sheet is at ES:BX keeps it from item
; type DX (kits.forbids): a Ravager a shield, a missile or thrown weapon, and armour that isn't
; light; a Twin-blade a shield, and a two-handed weapon (but a half-giant's,
; with RULE_HALF_GIANT); a Brute a one-handed melee weapon, and a shield (but a half-giant's,
; with the rule), and with KF_SPEC set (a weapon spec chosen) a missile weapon; a Stalker armour
; that isn't light (leather, or of no material); a Grove Warden a metal weapon; a Lifebinder a
; weapon of a kind not blunt (KIT_BLUNT); a Shinobi a shield, armour that isn't light, and a
; weapon not of its kinds (KIT_SHINOBI); a Seeker a weapon its sphere doesn't allow (as a
; cleric's: SPHERE_ALLOWS), but the bow. DS the game's; all registers kept.
KT_MELEE   equ 0x01             ; (the item type's +0 flags, +0Fh kind flags)
KT_MISSILE equ 0x02
KT_SHIELD  equ 0x04
KT_THROWN  equ 0x10
KT_ARMOUR  equ 0x80
KT_TWO_HANDED equ 0x40
KIT_BLUNT  equ 0xC112           ; bits by kind: club, mace, quarterstaff, sling, staff sling
HALF_GIANT equ 5
kit_forbids:
        push ax
        push bx
        push cx
        push si
        push es
        call kit_id
        jz .ok
        mov cl, al              ; CL the kit, CH 1 for a half-giant with RULE_HALF_GIANT
        xor ch, ch
        cmp byte [es:bx + 0x18], HALF_GIANT
        jne .type
        test word [cs:rules], RULE_HALF_GIANT
        jz .type
        inc ch
.type:  call kit_class_of_sheet     ; (a ranger's sphere, for a Seeker: its class less 13)
        sub al, 13
        mov [cs:kf_sphere], al
        les bx, [ITEM_TYPES]
        imul ax, dx, 0x14
        add bx, ax
        mov al, [es:bx]         ; AL the flags, AH the kind flags
        mov ah, [es:bx + 0x0F]
        cmp byte [cs:kf_off_hand], 0    ; the off hand: nothing for a Battle Mage, no weapon for a Healer
        je .ravager
        cmp cl, KIT_BATTLE_MAGE
        je .no
        cmp cl, KIT_HEALER
        jne .ravager
        test al, KT_MELEE | KT_MISSILE | KT_THROWN
        jnz .no
.ravager:
        cmp cl, KIT_RAVAGER     ; a Ravager: no shield, no missile or thrown weapon, light armour only
        jne .twin
        test al, KT_SHIELD
        jnz .no
        test al, KT_MISSILE | KT_THROWN
        jnz .no
        jmp .light
.twin:  cmp cl, KIT_TWIN_BLADE
        jne .brute
        test al, KT_SHIELD
        jnz .no
        test al, KT_MELEE | KT_MISSILE
        jz .ok
        test ah, KT_TWO_HANDED
        jz .ok
        or ch, ch
        jnz .ok
        jmp .no
.brute: cmp cl, KIT_BRUTE
        jne .stalker
        test al, KT_SHIELD
        jz .melee
        or ch, ch
        jnz .ok
        jmp .no
.melee: test al, KT_MELEE
        jz .missile
        test ah, KT_TWO_HANDED
        jz .no
        jmp .ok
.missile:
        test al, KT_MISSILE
        jz .ok
        cmp byte [cs:kf_spec], 0
        jne .no
        jmp .ok
.stalker:
        cmp cl, KIT_STALKER
        jne .warden
.light: test ah, KT_ARMOUR
        jz .ok
        test al, KT_SHIELD
        jnz .ok
        mov al, [es:bx + 8]     ; (the material: leather, or none)
        and al, 0x4F
        cmp al, LEATHER
        je .ok
        cmp al, 0x40
        je .ok
        jmp .no
.warden:
        cmp cl, KIT_GROVE_WARDEN
        jne .lifebinder
        test al, KT_MELEE | KT_MISSILE
        jz .ok
        mov al, [es:bx + 8]
        and al, 0x4F
        cmp al, MATERIAL_METAL
        je .no
        jmp .ok
.lifebinder:
        cmp cl, KIT_SEEKER      ; a Seeker: its sphere's weapons (SPHERE_ALLOWS), but the bow
        jne .blunt
        test al, KT_MELEE | KT_MISSILE
        jz .ok
        cmp dx, KIND_TYPES
        jae .ok
        mov si, dx
        mov ah, [cs:si + kind_of_type]
        sub ah, 1
        jc .ok                  ; (no kind: as the game has it)
        cmp ah, BOW_KIND - 1
        je .ok
        mov [cs:cu_kind], ah
        mov [cs:cu_flags], al
        mov al, [es:bx + 8]     ; (the material, as CLASS_FORBIDS keeps it)
        mov ah, al
        and al, 0x0F
        jnz .mat
        test ah, 0x40
        jz .mat
        mov al, NO_MATERIAL
.mat:   mov [cs:cu_mat], al
        mov al, [cs:kf_sphere]
        call sphere_allows
        jc .no
        jmp .ok
.blunt: mov si, kit_blunt
        cmp cl, KIT_LIFEBINDER
        je .kind
        cmp cl, KIT_SHINOBI
        jne .ok
        test al, KT_SHIELD      ; a Shinobi: no shield, light armour, its own weapons
        jnz .no
        test ah, KT_ARMOUR
        jz .weapon
        mov al, [es:bx + 8]
        and al, 0x4F
        cmp al, LEATHER
        je .ok
        cmp al, 0x40
        je .ok
        jmp .no
.weapon:
        mov si, kit_shinobi
.kind:  cmp dx, KIND_TYPES      ; a weapon's kind in the mask at CS:SI (no kind: as the game has it)
        jae .ok
        push si
        mov si, dx
        movzx ax, byte [cs:si + kind_of_type]
        pop si
        dec ax
        js .ok
        bt word [cs:si], ax
        jc .ok
.no:    pop es
        pop si
        pop cx
        pop bx
        pop ax
        stc
        ret
.ok:    pop es
        pop si
        pop cx
        pop bx
        pop ax
        clc
        ret
; KIT_ALLOWS: carry set if the kit of the character whose sheet is at ES:BX lets it use item type
; DX whatever its classes' lists and restrictions (kits.allows): a Battle Mage the weapons of its
; chosen weapon spec (SPEC_SLOTS' first, with RULE_SPECIALIZE) and light armour (leather, or of no
; material; not a shield). DS the game's; all registers kept.
kit_allows:
        push ax
        push bx
        push cx
        push si
        push es
        call kit_id
        cmp al, KIT_BATTLE_MAGE
        jne .no
        mov cl, [es:bx + SPEC_SLOTS]
        les bx, [ITEM_TYPES]
        imul ax, dx, 0x14
        add bx, ax
        test byte [es:bx + 0x0F], KT_ARMOUR
        jz .weapon
        test byte [es:bx], KT_SHIELD
        jnz .no
        mov al, [es:bx + 8]
        and al, 0x4F
        cmp al, LEATHER
        je .yes
        cmp al, 0x40
        je .yes
        jmp .no
.weapon:
        test word [cs:rules], RULE_SPECIALIZE
        jz .no
        cmp dx, KIND_TYPES
        jae .no
        mov si, dx
        mov al, [cs:si + kind_of_type]
        or al, al
        jz .no
        cmp al, cl
        jne .no
.yes:   pop es
        pop si
        pop cx
        pop bx
        pop ax
        stc
        ret
.no:    pop es
        pop si
        pop cx
        pop bx
        pop ax
        clc
        ret

kit_blunt  dw KIT_BLUNT
kit_shinobi dw 0xF10C           ; bits by kind: dagger, short sword, quarterstaff, chatkcha, bow, sling,
                                ;   staff sling
kf_spec    db 0
kf_off_hand db 0               ; (KIT_FORBIDS: the item going to the off hand)
kf_sphere  db 0                 ; (KIT_FORBIDS: a ranger's sphere, 0 air to 3 water)
EQUIP_OFF_HAND equ 14          ; the equip routine's slot for the off (left) hand

; CLASS_FORBIDS: carry set if the classes of the character whose sheet is at ES:BX keep it from
; item type DX (dscompanion/restrict.py, which says why): a psionicist, a multiclass thief, a
; preserver of that class alone, a druid, a cleric. A human's first class alone holds it (the one
; it has now, if it has changed class); another race's every class. All registers kept.
RESTRICT_TYPE equ 0x14          ; (the item type record's size)
NO_MATERIAL equ 0xFF            ; (CU_MAT: none)
LEATHER equ 5
THIEF_BIT equ 0x400
PSI_KINDS equ 0x701E            ; bits by kind: club, dagger, short sword, mace, chatkcha, bow, sling
SPHERE_EARTH equ 0x1D           ; bits by material: stone, obsidian, metal, wood
SPHERE_FIRE equ 0x08            ; obsidian
SPHERE_WATER equ 0x03           ; bone, wood
class_forbids:
        push ax
        push bx
        push cx
        push dx
        push si
        push di
        push ds
        mov ax, dx
        imul ax, ax, RESTRICT_TYPE
        lds si, [0x1669]
        add si, ax
        mov al, [si]                    ; the flags: 1 melee, 2 missile, 4 shield, 10h thrown
        mov [cs:cu_flags], al
        mov al, [si + 8]                ; the material (its low nibble; 40h and 0 none)
        mov ah, al
        and al, 0x0F
        jnz .mat
        test ah, 0x40
        jz .mat
        mov al, NO_MATERIAL
.mat:   mov [cs:cu_mat], al
        xor al, al                      ; armour: +0Fh 80h, and not a shield (nor bracers)
        push ax
        mov ax, dx
        call bracers_ax
        pop ax
        je .armour
        test byte [si + 0x0F], 0x80
        jz .armour
        test byte [cs:cu_flags], 4
        jnz .armour
        inc ax
.armour:
        mov [cs:cu_armour], al
        mov ax, [si + 0x10]             ; the classes but thief that may use it
        and ax, [es:bx + 0x12]
        and ax, 0xFFFF - THIEF_BIT
        mov [cs:cu_others], ax
        mov byte [cs:cu_kind], 0xFF     ; a weapon's kind (0FFh none)
        test byte [cs:cu_flags], 3
        jz .kind
        cmp dx, KIND_TYPES
        jae .kind
        mov si, dx
        mov al, [cs:si + kind_of_type]
        dec al
        mov [cs:cu_kind], al
.kind:  pop ds
        call specialized_back   ; (a dual-classed warrior's own kinds: as the game has them)
        jnc .holds0
        mov byte [cs:cu_kind], 0xFF
.holds0:
        mov cx, 3                       ; CL the classes that hold, CH more than one (not human)
        cmp byte [es:bx + 0x18], 1
        jne .multi
        mov cl, 1
        jmp .holds
.multi: cmp byte [es:bx + 0x22], 0
        je .holds
        inc ch
.holds: xor di, di
.each:  mov al, [es:bx + di + 0x21]
        or al, al
        jz .next
        call class_forbids_one
        jc .out
.next:  inc di
        cmp di, cx                      ; (CH 1: DI never reaches it, and the third slot ends it)
        jae .out                        ; (CF clear)
        cmp di, 3
        jb .each
        clc
.out:   pop di
        pop si
        pop dx
        pop cx
        pop bx
        pop ax
        ret

; SPECIALIZED_BACK: carry set if CU_KIND is a kind the character (sheet ES:BX) specialized in as a
; fighter, gladiator or ranger, a human who has dual-classed and whose new class's level has
; passed the old; or the bow, for a ranger (a multiclass one always, a human while a ranger or
; once its new class's level has passed its ranger level): restrict.specialized_back. All
; registers kept.
specialized_back:
        pusha
        mov al, [cs:cu_kind]
        cmp al, 0xFF
        je .no
        cmp al, BOW_KIND - 1
        jne .spec
        xor si, si                      ; (the bow: a ranger's own)
.rclass:
        mov al, [es:bx + si + 0x21]
        cmp al, 13
        jb .rnext
        cmp al, 16
        ja .rnext
        cmp byte [es:bx + 0x18], 1      ; (not human: multiclass, always)
        jne .yes
        or si, si                       ; (a human ranger now)
        jz .yes
        mov al, [es:bx + si + 0x24]
        cmp al, [es:bx + 0x24]
        jb .yes
.rnext: inc si
        cmp si, 3
        jb .rclass
        mov al, [cs:cu_kind]
.spec:  cmp byte [es:bx + 0x18], 1
        jne .no
        inc al
        cmp [es:bx + SPEC_SLOTS], al
        je .kind
        cmp [es:bx + SPEC_SLOTS + 1], al
        je .kind
        cmp [es:bx + SPEC_SLOTS + 2], al
        je .kind
        cmp [es:bx + SPEC_SLOTS + 3], al
        jne .no
.kind:  mov si, 1
.class: mov al, [es:bx + si + 0x21]
        cmp al, FIGHTER_CLASS
        je .warrior
        cmp al, GLADIATOR_CLASS
        je .warrior
        cmp al, 13
        jb .next
        cmp al, 16
        ja .next
.warrior:
        mov al, [es:bx + si + 0x24]
        cmp al, [es:bx + 0x24]
        jb .yes
.next:  inc si
        cmp si, 3
        jb .class
.no:    popa
        clc
        ret
.yes:   popa
        stc
        ret

; CLASS_FORBIDS_ONE: carry set if class AL keeps the character (sheet ES:BX, CH 1 if multiclass)
; from the item CLASS_FORBIDS has described in CU_*.
class_forbids_one:
        cmp al, 12                      ; psionicist
        jne .thief
        call heavy
        jc .ret
        call shield_not_leather
        jc .ret
        mov dl, [cs:cu_kind]
        cmp dl, 0xFF
        je .ok
        push cx
        mov cl, dl
        mov dx, 1
        shl dx, cl
        pop cx
        test dx, PSI_KINDS
        jnz .ok
        stc
        ret
.thief: cmp al, 17
        jne .pres
        or ch, ch
        jz .ok
        call heavy
        jc .ret
        call shield_not_leather
        jc .ret
        test byte [cs:cu_flags], 4
        jz .ok
        cmp word [cs:cu_others], 0
        jne .ok
        stc
        ret
.pres:  cmp al, 11
        jne .druid
        or ch, ch
        jnz .ok
        jmp .bare
.druid: cmp al, 5
        jb .cleric
        cmp al, 8
        ja .ok
.bare:  cmp byte [cs:cu_armour], 0      ; no armour, no shield
        jne .no
        test byte [cs:cu_flags], 4
        jz .ok
.no:    stc
        ret
.cleric:                                ; (1-4)
        cmp byte [cs:cu_kind], 0xFF
        je .ok
        push si
        xor si, si
.sphere:
        mov al, [es:bx + si + 0x21]     ; a cleric's or ranger's sphere: its class less 1 (13 a ranger's), mod 4
        dec al
        cmp al, 4
        jb .is
        sub al, 12
        cmp al, 4
        jae .nexts
.is:    call sphere_allows
        jnc .yes
.nexts: inc si
        cmp si, 3
        jb .sphere
        call el_second          ; (an Elementalist's second sphere's too)
        jz .none
        call sphere_allows
        jnc .yes
.none:  pop si
        stc
        ret
.yes:   pop si
.ok:    clc
.ret:   ret

; HEAVY: carry set for armour not light (not leather, and of a material)
heavy:  cmp byte [cs:cu_armour], 0
        je .light
        cmp byte [cs:cu_mat], LEATHER
        je .light
        cmp byte [cs:cu_mat], NO_MATERIAL
        je .light
        stc
        ret
.light: clc
        ret

; SHIELD_NOT_LEATHER: carry set for a shield of a material not leather
shield_not_leather:
        test byte [cs:cu_flags], 4
        jz .ok
        cmp byte [cs:cu_mat], LEATHER
        je .ok
        stc
        ret
.ok:    clc
        ret

; SPHERE_ALLOWS: carry clear if sphere AL (0 air, 1 earth, 2 fire, 3 water) allows the weapon:
; air, missile and thrown weapons and daggers; the others by material.
sphere_allows:
        push cx
        or al, al
        jnz .mat
        test byte [cs:cu_flags], 0x12
        jnz .yes
        cmp byte [cs:cu_kind], 2        ; (a dagger)
        je .yes
        jmp .no
.mat:   mov ah, SPHERE_EARTH
        cmp al, 1
        je .test
        mov ah, SPHERE_FIRE
        cmp al, 2
        je .test
        mov ah, SPHERE_WATER
.test:  mov cl, [cs:cu_mat]
        cmp cl, 8
        jae .no
        mov ch, 1
        shl ch, cl
        test ah, ch
        jnz .yes
.no:    pop cx
        stc
        ret
.yes:   pop cx
        clc
        ret

cu_flags   db 0
cu_mat     db 0
cu_armour  db 0

; BRACERS_AX: ZF set if AX is the bracers of defense's type (TYPES_FIRST + BRACERS, once the
; types are in). All registers kept.
bracers_ax:
        push bx
        mov bx, [cs:types_first]
        or bx, bx
        jz .no
        add bx, BRACERS
        cmp ax, bx
        pop bx
        ret
.no:    inc bx                  ; (ZF clear)
        pop bx
        ret
cu_kind    db 0
cu_others  dw 0

; PROBE_NO_CAST: INT VEC_NO_CAST replaces "add sp,4" (3 bytes: INT + NOP; DSUN.EXE 89B84h) at the
; end of the game's test of whether a character can't cast spells, which the USE screen asks
; before it shows a spell as one to cast and before it casts one, wizard or priest (not a
; psionic power), and so does a spell queued in a fight: DX the character, AX not 0 if it can't
; (its "No spell use" effect). With RULE_RESTRICT, a multiclass preserver (not a human, who
; dual-classes) can't either while it wears armour, a helm too (ARMOUR_WORN; a shield doesn't
; count).
PRESERVER_CLASS equ 11
probe_no_cast:
        push bp                 ; the replaced "add sp,4": move the interrupt frame (and BP)
        mov bp, sp              ; up over the 4 bytes, so IRET returns with them gone
        push ax
        mov ax, [bp + 6]
        mov [bp + 10], ax       ; flags
        mov ax, [bp + 4]
        mov [bp + 8], ax        ; CS
        mov ax, [bp + 2]
        mov [bp + 6], ax        ; IP
        mov ax, [bp]
        mov [bp + 4], ax        ; BP
        pop ax
        mov sp, bp
        add sp, 4
        pop bp
        or ax, ax
        jnz .done
        test word [cs:rules], RULE_RESTRICT
        jz .done
        push bx
        push cx
        push es
        mov bx, dx
        imul bx, bx, 0x47
        les cx, [0x1661]
        add bx, cx
        cmp byte [es:bx + 0x18], 1      ; a human: none
        je .out
        cmp byte [es:bx + 0x22], 0      ; one class: none
        je .out
        cmp byte [es:bx + 0x21], PRESERVER_CLASS
        je .pres
        cmp byte [es:bx + 0x22], PRESERVER_CLASS
        je .pres
        cmp byte [es:bx + 0x23], PRESERVER_CLASS
        jne .out
.pres:  call armour_worn
        adc ax, 0                       ; (AX was 0)
.out:   pop es
        pop cx
        pop bx
.done:  iret

; ARMOUR_WORN: carry set if creature DX (DS = the game's) wears armour, a helm too: an item of
; a type the game marks armour (+0Fh 80h), not a shield, on its arms, legs, head or chest.
; All registers kept.
armour_worn:
        pushad
        push es
        mov ax, ds
        add ax, THINGS_SEG
        mov [cs:r_things], ax
        imul ax, dx, 0x3A
        mov [cs:r_creature], ax
        mov cx, 8               ; its item lists, each a thing: +8, +0Ah, +0Ch
.list:  les bx, [CREATURES]
        add bx, [cs:r_creature]
        add bx, cx
        mov dx, [es:bx]
        cmp dx, NO_THING
        jae .next
        mov es, [cs:r_things]
        mov bx, dx
        imul bx, bx, 3
        cmp byte [es:bx+THINGS], 1
        jne .next               ; not an item
        mov dx, [es:bx+THINGS+1]
        mov byte [cs:r_left], 100
.item:  cmp dx, NO_THING
        jae .next
        les bx, [ITEMS]
        imul ax, dx, 0x15
        add bx, ax
        mov dx, [es:bx+4]       ; (the next)
        mov al, [es:bx+0x11]    ; the slot
        cmp al, ARM_SLOT
        je .slot
        cmp al, LEG_SLOT
        je .slot
        cmp al, HEAD_SLOT
        je .slot
        cmp al, CHEST_SLOT
        jne .on
.slot:  mov si, [es:bx+0x0A]
        push ax
        mov ax, si
        call bracers_ax                 ; (bracers aren't armour)
        pop ax
        je .on
        imul si, si, 0x14
        les bx, [ITEM_TYPES]
        add bx, si
        test byte [es:bx], 4    ; a shield
        jnz .on
        test byte [es:bx+0x0F], 0x80
        jz .on
        pop es
        popad
        stc
        ret
.on:    dec byte [cs:r_left]
        jnz .item
.next:  add cx, 2
        cmp cx, 0x0E
        jb .list
        pop es
        popad
        clc
        ret

; Weapon pages in the character creation panel (RULE_SPECIALIZE; dscompanion/weaponpages.py, which
; puts the windows and their pictures in the Ledger's RESOURCE.GFF). The panel shows the psionic
; disciplines' window (3012, DS:EA2h its window) or the clerical spheres' (3013, DS:EA6h), which
; the game swaps through its routines at DSUN.EXE 640E4h (to the spheres) and 641B9h (back). For a
; fighter, gladiator or ranger it gets windows with WEAPON SPEC for its button (3018 for the
; disciplines if it has no sphere, 3019 for the spheres), and that opens the four weapon pages
; (3014-3017; kept at DS:EA6h, as the spheres' window is, so that the game closes it as one),
; each four kinds (specialize.KINDS), MORE SPECS to the next, the last's VIEW PSIONICS back.
; The kind marked is the creation sheet's (DS:119Ch) first SPEC_SLOTS byte, which goes with the
; sheet when DONE is pressed (the Ledger puts in the long sword for a warrior who marks none).
; PROBE_WP_DISC_WIN: INT replaces "push 0BC4h" (3 bytes: INT + NOP; 67BFBh), the disciplines'
; window being opened: pushes 3018's id instead for a warrior with no sphere.
; PROBE_WP_SPHERE_WIN: INT replaces "push 0BC5h" (6413Bh), the spheres' window being opened:
; 3019's id instead for a warrior.
; PROBE_WP_SHOWN: INT replaces "cmp ax,8" (3 bytes: INT + NOP; 6337Ah) where a new class has been
; picked and the game asks whether the spheres' window is up (AX 8 if it is, from the routine it
; has just called, 118:C36h, with the window's id) to close it: 8 too if a warrior's spheres or a
; weapon page is up. RETF 2 keeps the compare's flags.
; PROBE_WP_DISC_CLICK, PROBE_WP_SPHERE_CLICK: INT replaces "mov bx,[bp+8]" (3 bytes: INT + NOP;
; 64311h, 642B0h) in those windows' click routines, [BP+8] the button: WEAPON SPEC opens the
; first page, and the routine goes on at its end (6433Fh, 642E2h: the redraw). Both read
; the far addresses of the game's window routines from the calls in their own overlay, as the
; loader has fixed them (WP_CALLS), for WP_HANDLER to call.
WP_DISC_ID   equ 0xBC4              ; (windows' ids: 3012, 3013, 3014-3017, 3018, 3019)
WP_SPHERE_ID equ 0xBC5
WP_PAGE_ID   equ 0xBC6
WP_WDISC_ID  equ 0xBCA
WP_WSPHERE_ID equ 0xBCB
WP_ROW       equ 0x840              ; (buttons: the kinds' rows, MORE SPECS, VIEW PSIONICS, WEAPON SPEC)
WP_MORE      equ 0x850
WP_BACK      equ 0x851
WP_VIEW      equ 0x852
WP_PAGES     equ 4
SPHERE_TOGGLE equ 0x7FF             ; (the spheres' VIEW PSIONICS)
; Kits (RULE_HI_KITS; kitpages.py): one class's three, or none, chosen on a page of their own
; (3026-3033, by class, kept at DS:EA6h as the weapon pages are), its rows NO KIT and the
; class's kits (KIT_ROW + 3 * (class - 1) + kit - 1), its button VIEW PSIONICS back. KITS opens it:
; the button of the disciplines' window (3022) for a class with no sphere, of the spheres' (3023)
; for a cleric's, druid's or ranger's, of the fourth weapon page (3025) for a warrior choosing
; weapons. The kit marked is the creation sheet's KIT_BYTE (0 none, 1-3), which goes with the
; sheet when DONE is pressed. The game keeps that sheet from one character to
; the next: the disciplines' window opened other than on the way back to it (KIT_KEEP), for a
; new character or another class, puts the kit back to none.
KIT_DISC_ID  equ 3022
KIT_SPHERE_ID equ 3023
KIT_PAGE4_ID equ 3025
KIT_WIN_ID   equ 3026
KIT_ROW      equ 0x870
KIT_NONE     equ 0x888
KIT_VIEW     equ 0x889
KIT_BYTE     equ 0x43
KIT_OPEN     equ 0xFF               ; (KIT_BYTE while the player has taken the kit back: none chosen)
; The kits as KIT_ID numbers them (the creation screen's class x 4 + the kit)
KIT_ELEMENTALIST equ 5
KIT_HEALER   equ 6
KIT_CRUSADER equ 7
KIT_GROVE_WARDEN equ 9
KIT_LIFEBINDER equ 10
KIT_WANDERER equ 11
KIT_MYRMIDON equ 13
KIT_SENTINEL equ 14
KIT_RAVAGER  equ 15
KIT_CHAMPION equ 17
KIT_TWIN_BLADE equ 18
KIT_BRUTE    equ 19
KIT_SCHOLAR  equ 21
KIT_BATTLE_MAGE equ 22
KIT_ARCANIST equ 23
KIT_MIND_BENDER equ 25
KIT_MIND_WARRIOR equ 26
KIT_KINETICIST equ 27
KIT_STALKER  equ 29
KIT_JUSTIFIER equ 30
KIT_SEEKER   equ 31
KIT_SWASHBUCKLER equ 33
KIT_ASSASSIN equ 34
KIT_SHINOBI  equ 35
WP_DISC      equ 0xEA2              ; DS: the panel's windows (far)
WP_SPHERE    equ 0xEA6
WP_DISC_MASK equ 0x4980             ; DS: the disciplines and spheres marked, kept while hidden
WP_SPHERE_MASK equ 0x4982
WP_CREATION  equ 0x119C             ; DS: the sheet being made (far; its classes numbered 1-8)
WP_STUB      equ 0x421D - 0x4356    ; the creation overlay's stub, less DS: its entries
WP_MARKED    equ 0x66               ; (63EFAh: the rows marked, as a mask)
WP_TO_DISC   equ 0x7A               ; (641B9h: back to the disciplines)
WP_X         equ 0xD2               ; (the panel's place)
WP_Y         equ 0x58
CR_CLERIC    equ 1                  ; (classes at creation)
CR_DRUID     equ 2
CR_FIGHTER   equ 3
CR_GLADIATOR equ 4
CR_RANGER    equ 7
; WP_CALLS: the routines' far calls (9Ah) in the overlay, back from the click probes' return
; (the overlay's 642B3h and 64314h; the calls at 64127h close, 6413Eh open, 6416Fh print,
; 6417Dh set the text colour, 641AFh the panel's help line, 6422Fh set a button, 642E2h redraw)
WP_CALL_CLOSE  equ 0x64127
WP_CALL_OPEN   equ 0x6413E
WP_CALL_PRINT  equ 0x6416F
WP_CALL_COLOUR equ 0x6417D
WP_CALL_HELP   equ 0x641AF
WP_CALL_BUTTON equ 0x6422F
WP_CALL_REDRAW equ 0x642E2
WP_CALL_MARK   equ 0x63FC4          ; (the row's mark: A0:3180h, as 63FEEh draws it)
WP_CALL_BACKDROP equ 0x639E9        ; (a number's backdrop put back: A0:30C3h, as 639D5h shows the PSP)
WP_STAT_SEG    equ 0x639D5          ; ("mov ax,340h": the backdrops' words' segment)
WP_MARK_SEG    equ 0x63FB2          ; ("mov ax,338h": the marks' table's segment, +1ABh)
WP_MARK_WIN    equ 0xF32            ; DS: the window the marks are drawn through (far)
WP_SPHERE_SEG  equ 0x6412F          ; ("push 538h": the spheres' routine's stub segment, as fixed)
WP_SPHERE_ENTRY equ 0x57            ; (its entry there)
WP_SPHERE_RET  equ 0x642B2          ; (the click probes' return, and where they go on to)
WP_SPHERE_END  equ 0x642E2
WP_DISC_RET    equ 0x64313
WP_DISC_END    equ 0x6433F
WP_SHOWN_CALL  equ 0x63372          ; (118:C36h's call, and PROBE_WP_SHOWN's return)
WP_SHOWN_RET   equ 0x6337C

probe_wp_disc_win:
        sub sp, 2               ; push the window's id: the frame down a word
        push bp
        mov bp, sp
        push ax
        mov ax, [bp + 4]
        mov [bp + 2], ax
        mov ax, [bp + 6]
        mov [bp + 4], ax
        mov ax, [bp + 8]
        mov [bp + 6], ax
        push dx
        call wp_ids
        mov [bp + 8], ax
        pop dx
        cmp byte [cs:kit_keep], 0   ; (opened for a new character, or another class: no kit)
        jne .keep
        push es
        push bx
        les bx, [WP_CREATION]
        mov byte [es:bx + KIT_BYTE], 0
        pop bx
        pop es
.keep:  mov byte [cs:kit_keep], 0
        pop ax
        pop bp
        iret

probe_wp_sphere_win:
        sub sp, 2
        push bp
        mov bp, sp
        push ax
        mov ax, [bp + 4]
        mov [bp + 2], ax
        mov ax, [bp + 6]
        mov [bp + 4], ax
        mov ax, [bp + 8]
        mov [bp + 6], ax
        push dx
        call wp_ids
        mov [bp + 8], dx
        pop dx
        pop ax
        pop bp
        iret

; WP_IDS: the windows the panel shows for the sheet being made (kitpages.panel_windows): AX the
; disciplines' (3012, its button VIEW SPHERES; 3018, WEAPON SPEC, for a warrior with no sphere
; and weapon specialization; 3022, KITS, for one of no sphere with kits to choose), DX the
; spheres' (3013, VIEW PSIONICS; 3019, WEAPON SPEC, for a warrior and weapon specialization;
; 3023, KITS, for one with kits to choose). Others kept.
wp_ids:
        push bx
        push cx
        call wp_classes
        mov cx, ax              ; CL a warrior, CH a sphere
        call kit_class
        mov bl, al
        mov ax, WP_DISC_ID
        mov dx, WP_SPHERE_ID
        test word [cs:rules], RULE_SPECIALIZE
        jz .kits
        or cl, cl
        jz .kits
        mov dx, WP_WSPHERE_ID
        or ch, ch
        jnz .out
        mov ax, WP_WDISC_ID
        jmp .out
.kits:  or bl, bl
        jz .out
        mov dx, KIT_SPHERE_ID
        or ch, ch
        jnz .out
        mov ax, KIT_DISC_ID
.out:   pop cx
        pop bx
        ret

; KIT_CLASS: AL the class of the sheet being made (1-8, as the creation screen numbers them) if it
; has kits to choose (the rule on, and one class), else 0. Others kept.
kit_class:
        push bx
        push es
        xor al, al
        test word [cs:rules_hi], RULE_HI_KITS
        jz .out
        les bx, [WP_CREATION]
        cmp word [es:bx + 0x22], 0
        jne .out
        mov al, [es:bx + 0x21]
        cmp al, 8
        jbe .out
        xor al, al
.out:   pop es
        pop bx
        ret

; WP_RULES: ZF clear if weapon specialization or kits are on (the panel's own windows wanted).
; All registers kept.
wp_rules:
        test word [cs:rules], RULE_SPECIALIZE
        jnz .ret
        test word [cs:rules_hi], RULE_HI_KITS
.ret:   ret

; WP_ALLOWED: AX the kinds (bit 0 the long sword) the sheet being made can choose: those whose
; plain weapon (WP_PLAIN) the game's class lists and CLASS_FORBIDS let it use, its classes as the
; sheet numbers them (a cleric's or ranger's by the sphere marked). restrict.allowed_kinds.
wp_allowed:
        push bx
        push cx
        push dx
        push si
        push di
        push es
        les bx, [WP_CREATION]
        push ds
        push cs
        pop ds
        mov di, wp_sheet
        mov cx, 0x47 / 2 + 1
        xor ax, ax
.zero:  mov [di], ax
        add di, 2
        loop .zero
        pop ds
        mov al, [es:bx + 0x18]
        mov [cs:wp_sheet + 0x18], al
        xor si, si
        xor dx, dx              ; DX the class flags
.class: mov al, [es:bx + si + 0x24]
        mov [cs:wp_sheet + si + 0x24], al
        movzx di, byte [es:bx + si + 0x21]
        cmp di, 8
        ja .next
        shl di, 2
        mov al, [cs:di + wp_class_map]
        mov cx, [cs:di + wp_class_map + 2]
        cmp byte [cs:di + wp_class_map + 1], 0  ; (a sphere to add: 0-3 by the mark)
        je .put
        mov ah, 0x80
.sphere:
        test [WP_SPHERE_MASK], ah
        jnz .put
        inc al
        shl cx, 1
        shr ah, 1
        cmp ah, 0x08
        ja .sphere
        sub al, 4               ; (none marked: the first)
        shr cx, 4
.put:   mov [cs:wp_sheet + si + 0x21], al
        cmp byte [cs:di + wp_class_map + 1], 2  ; (a ranger's sphere doesn't change its flag)
        jne .flag
        mov cx, 0x200
.flag:  or dx, cx
.next:  inc si
        cmp si, 3
        jb .class
        mov [cs:wp_sheet + 0x12], dx
        mov al, [es:bx + KIT_BYTE]          ; (its kit: what it keeps the sheet from)
        mov [cs:wp_sheet + KIT_BYTE], al
        mov al, [es:bx + SPHERE2]           ; (an Elementalist's second sphere)
        mov [cs:wp_sheet + SPHERE2], al
        push cs
        pop es
        mov bx, wp_sheet
        call kinds_allowed
        pop es
        pop di
        pop si
        pop dx
        pop cx
        pop bx
        ret

; KINDS_ALLOWED: AX the kinds (bit 0 the long sword) the character whose sheet is at ES:BX can
; choose: those with an item type (KIND_OF_TYPE) the game's class lists and CLASS_FORBIDS let it
; use (a fire cleric's long sword the obsidian one, not the plain bone one); not the bow for a
; ranger (its class flags, +12h: 200h), who has expertise with it already; a Battle Mage the
; one-handed melee kinds (KIT_BM_KINDS), not thrown, whatever its class lets it use.
; DS = the game's. Others kept.
KIT_BM_KINDS equ 0x00BF         ; a Battle Mage's: long sword, club, dagger, short sword, mace, axe, pick
kinds_allowed:
        push cx
        push dx
        push si
        push di
        call kit_id             ; (a Battle Mage: its own, whatever its class)
        cmp al, KIT_BATTLE_MAGE
        jne .types
        mov di, KIT_BM_KINDS
        jmp .all
.types: xor dx, dx              ; DX each item type of a kind, CL its kind
        xor di, di
.type:  mov si, dx
        mov cl, [cs:si + kind_of_type]
        sub cl, 1
        jc .no
        bt di, cx
        jc .no                  ; (one of its types already let in)
        push es
        push bx
        imul ax, dx, 0x14
        les bx, [0x1669]
        add bx, ax
        mov ax, [es:bx + 0x10]
        pop bx
        pop es
        and ax, [es:bx + 0x12]
        jz .no
        call class_forbids
        jc .no
        mov byte [cs:kf_spec], 1
        call kit_forbids
        jc .no
        movzx cx, cl
        bts di, cx
.no:    inc dx
        cmp dx, KIND_TYPES
        jb .type
        test word [es:bx + 0x12], 0x200
        jz .all                 ; (a ranger)
        btr di, BOW_KIND - 1
.all:   mov ax, di
        pop di
        pop si
        pop dx
        pop cx
        ret

; PROBE_LV_PICK: INT VEC_LV_PICK replaces "cmp word [bp+8],0Bh" (4 bytes: INT + 2 NOPs, then the
; game's "jnz +7"; DSUN.EXE 87A9Bh) in the routine that raises a character a level in a class
; ([BP+8] the class, SI the character), where it goes on to have a preserver pick a new spell and a
; psionicist a new power. With weapon specialization, a fighter, gladiator or ranger with fewer
; weapon kinds than it is due (LV_DUE: a gladiator two, a third at 6th level and a fourth at 9th;
; a fighter or ranger one) picks the rest the way a psionicist picks a power: the game's own
; routine for that (its far call a little further on, 620:57h) is called in weapon mode (LV_MODE),
; in which the PROBE_PK_* probes in it show the weapon window (weaponpages.PICKER) in place of the
; powers' and take its clicks. Then on as the compare and the JNZ would have gone. The level-up
; routine is overlay code: the way back is put in a frame the overlay manager can fix up, as for
; PROBE_NEXT.
LV_SHEETS   equ 0x1661          ; DS: the sheets (far, 47h bytes each) and creatures (3Ah each)
LV_PARTY    equ 4
LV_PSI_CALL equ 0x87AB0 - 0x87A9D  ; the psionicists' pop-up's far call's address, less the INT's
LV_SPELL_CALL equ 0x87AA3 - 0x87A9D ; the preservers' CHOOSE A SPELL's (620:5Ch), as that
LV_THIEF    equ 17
probe_lv_pick:
        sti
        pushad
        push es
        mov bx, sp              ; the interrupt frame at BX+34: IP, CS, flags
        push ds
        push si
        lds si, [ss:bx + 34]
        mov eax, [si + LV_PSI_CALL]
        mov [cs:lv_psi], eax
        mov eax, [si + LV_SPELL_CALL]
        mov [cs:lv_spell], eax
        pop si
        pop ds
        mov ax, [ss:bx + 34]
        add ax, 4               ; past the NOPs and the JNZ for a preserver (and a Shinobi) ...
        mov byte [cs:lv_class], 0x0B
        cmp word [bp + 8], 0x0B
        je .frame
        mov byte [cs:lv_class], 0
        cmp word [bp + 8], LV_THIEF
        jne .other
        call lv_shinobi
        je .frame
.other: add ax, 7               ; ... and to its target for any other class
.frame: push word [ss:bx + 36]
        push ax
        push bp
        mov bp, sp
        call lv_check
        cmp byte [cs:lv_class], 0x0B
        jne .scholar_done
        call lv_scholar
.scholar_done:
        pop bp
        pop ax                  ; the way back, as the overlay manager has left it
        pop dx
        mov bx, sp
        mov [ss:bx + 34], ax
        mov [ss:bx + 36], dx
        pop es
        popad
        iret

; Character SI (DS the game's) asked for weapon kinds while it has fewer than it is due
lv_check:
        test word [cs:rules], RULE_SPECIALIZE
        jz .ret
        cmp si, LV_PARTY
        jae .ret
        cmp byte [cs:showing], 0
        jne .ret
        les bx, [LV_SHEETS]
        imul ax, si, 0x47
        add bx, ax
        call lv_due
        xor dx, dx              ; DX the kinds it has, DI the first slot empty
        mov di, SPEC_COUNT
        push si
        mov si, SPEC_COUNT - 1
.have:  movzx ax, byte [es:bx + si + SPEC_SLOTS]
        or ax, ax
        jnz .has
        mov di, si
        jmp .next
.has:   inc dx
.next:  dec si
        jns .have
        pop si
        cmp dx, cx
        jae .ret
        cmp di, SPEC_COUNT
        jae .ret
        mov [cs:lv_have], dl
        sub cl, dl              ; (the kinds due less those it has: those left)
        mov [cs:lv_left], cl
        call kinds_allowed
        push si
        xor si, si              ; less those it has
.mine:  movzx cx, byte [es:bx + si + SPEC_SLOTS]
        jcxz .mine_next
        dec cx
        btr ax, cx
.mine_next:
        inc si
        cmp si, SPEC_COUNT
        jb .mine
        pop si
        or ax, ax
        jz .ret
        mov cl, [cs:lv_left]    ; (no more than there are to pick)
        xor dx, dx
        xor di, di
.count: bt ax, di
        adc dl, 0
        inc di
        cmp di, 16
        jb .count
        cmp dl, cl
        jae .left
        mov cl, dl
.left:  mov [cs:lv_left], cl
        call lv_ask
.ret:   ret

; LV_SHINOBI: ZF set if character SI, gone up a level as a thief, is a Shinobi of SHINOBI_FIRST or
; more, who learns a spell of its own (as a preserver: CHOOSE A SPELL, with PROBE_PICK_LEVEL and
; PROBE_PICK_LIST). All registers kept.
lv_shinobi:
        push ax
        push bx
        push es
        cmp si, LV_PARTY
        jae .no
        les bx, [LV_SHEETS]
        imul ax, si, 0x47
        add bx, ax
        call kit_id
        jnz .kit
        or al, 1                ; (none: ZF clear)
        jmp .out
.kit:   cmp al, KIT_SHINOBI
        jne .out
        call kit_level
        cmp al, SHINOBI_FIRST
        jb .no
        cmp al, al              ; (ZF set)
        jmp .out
.no:    or al, 1
.out:   pop es
        pop bx
        pop ax
        ret

; LV_SCHOLAR: for character SI gone up a level as a preserver, a Scholar's spell more: the game's
; CHOOSE A SPELL (LV_SPELL) once before the game's own. All registers kept.
lv_scholar:
        pusha
        push es
        cmp si, LV_PARTY
        jae .out
        les bx, [LV_SHEETS]
        imul ax, si, 0x47
        add bx, ax
        call kit_id
        cmp al, KIT_SCHOLAR
        jne .out
        push si
        call far [cs:lv_spell]
        add sp, 2
.out:   pop es
        popa
        ret

; LV_DUE: CX the weapon kinds the character whose sheet is ES:BX is due: a gladiator 2, 3 from 6th
; level, 4 from 9th; a fighter or ranger 1; 0 for others. A human's earlier classes (dual-classed)
; count only once its first class's level has passed theirs. Others kept.
lv_due:
        push ax
        push si
        xor cx, cx
        xor si, si
.class: mov al, [es:bx + si + 0x21]
        mov ah, [es:bx + si + 0x24]
        or si, si
        jz .on
        cmp byte [es:bx + 0x18], 1
        jne .on
        cmp ah, [es:bx + 0x24]
        jae .next
.on:    cmp al, GLADIATOR_CLASS
        jne .warrior
        mov al, 2
        cmp ah, 6
        jb .most
        inc al
        cmp ah, 9
        jb .most
        inc al
        jmp .most
.warrior:
        cmp al, FIGHTER_CLASS
        je .one
        cmp al, 13
        jb .next
        cmp al, 16
        ja .next
.one:   mov al, 1
.most:  cmp cl, al
        jae .next
        mov cl, al
.next:  inc si
        cmp si, 3
        jb .class
        or cl, cl               ; (a Battle Mage: one)
        jnz .out
        call kit_id
        cmp al, KIT_BATTLE_MAGE
        jne .out
        mov cl, 1
.out:   pop si
        pop ax
        ret

; LV_ASK: the psionicists' pop-up (LV_PSI) in weapon mode for character SI (DS the game's): the
; kinds AX to pick from, LV_LEFT of them to pick. Each kind clicked goes in the sheet's first empty
; SPEC_SLOTS byte (PK_TAKE); the window closes once LV_LEFT are picked, or with EXIT.
lv_ask:
        pusha
        push es
        mov [cs:lv_avail], ax
        mov [cs:lv_member], si
        mov byte [cs:lv_mode], 1
        mov byte [cs:showing], 1
        push si
        call far [cs:lv_psi]
        add sp, 2
        mov byte [cs:lv_mode], 0
        mov byte [cs:showing], 0
        pop es
        popa
        ret

; The PROBE_PK_* probes, in the psionicists' pop-up (DSUN.EXE 85EECh, its window routine 85F6Eh,
; the window's drawing 86107h and clicks 862B6h); outside weapon mode each does what it replaced.
; PROBE_PK_COUNT: "mov dx,ax / or dx,dx" (85F01h; 4 bytes), the powers it has to pick: some.
; PROBE_PK_WIN: "push 445Dh" (85FF4h; 3 bytes), the window: the weapon window.
; PROBE_PK_LEFT: "mov al,[4AECh]" (8602Dh; 3 bytes), the number shown: the kinds left to pick.
; PROBE_PK_TITLE: "push ds / push 3026h" (860C3h; 4 bytes), "PICK A PSIONIC POWER,": the weapons',
;   and its colours near-white on black (the next pushes' bytes; the game's own put back otherwise).
; PROBE_PK_FILL: "xor di,di / mov si,di" (8610Fh; 4 bytes), at the start of the powers' pictures
;   put in: none; the rows of the kinds it can't pick out of use (the routine's end, 862A7h).
; PROBE_PK_CLICK: "mov ax,[bp+8]" (862D4h; 3 bytes), a button clicked: a row it can pick taken
;   (PK_TAKE), then the window closed (as for a close, 86370h) once none are left to pick, or left up
;   (86367h).
PICK_WIN      equ 0xBCD             ; (weaponpages.PICKER: 3021)
PICK_FIRST    equ 0x860             ; (weaponpages.PICK_FIRST: its rows)
PICK_COUNT_BUTTON equ 0x2C37
PK_FILL_DONE  equ 0x862A7 - 0x86111 ; (the drawing routine's end, less the address after the INT)
PK_STAY       equ 0x86367 - 0x862D6 ; (the click handler's way out, and its close's)
PK_CLOSE      equ 0x86370 - 0x862D6
PK_OP_CALL    equ 0x86069 - 0x86111 ; (140:71Ah's far address, in a call, less the address after
PK_LABEL_CALL equ 0x86054 - 0x86111 ;   PROBE_PK_FILL's INT; and 140:7FAh's)
PK_WINDOW     equ 0x11A4            ; DS: the window up (far)
PK_TITLE_SECOND equ 0x860CB - 0x860C5 ; ("push 0D000FEh" and "push 0D3009Fh", after PROBE_PK_TITLE's
PK_TITLE_INK  equ 0x860D1 - 0x860C5 ;   INT: their colour bytes, less the address after it)
PK_GAME_SECOND equ 0xD0
PK_GAME_INK   equ 0xD3              ; (dark grey)
PK_PICK_SECOND equ 0xD0             ; (as the game has it)
PK_PICK_INK   equ 0xD9              ; (near-white: 234, 234, 234)
probe_pk_count:
        mov dx, ax
        cmp byte [cs:lv_mode], 0
        je .flags
        mov dx, 2               ; (2: [4AECh] 1, as the routine counts; PROBE_PK_LEFT shows ours)
.flags: push bp
        mov bp, sp
        push ax
        or dx, dx
        pushf
        pop ax
        and ax, 0x08D5
        and word [bp + 6], 0xFFFF - 0x08D5
        or [bp + 6], ax
        pop ax
        pop bp
        iret

probe_pk_win:
        sub sp, 2               ; push the window's id: the frame down a word
        push bp
        mov bp, sp
        push ax
        mov ax, [bp + 4]
        mov [bp + 2], ax
        mov ax, [bp + 6]
        mov [bp + 4], ax
        mov ax, [bp + 8]
        mov [bp + 6], ax
        mov word [bp + 8], 0x445D
        cmp byte [cs:lv_mode], 0
        je .out
        mov word [bp + 8], PICK_WIN
.out:   pop ax
        pop bp
        iret

probe_pk_left:
        mov al, [0x4AEC]
        cmp byte [cs:lv_mode], 0
        je .out
        mov al, [cs:lv_left]
.out:   iret

probe_pk_title:
        sub sp, 4               ; push a far pointer: the frame down two words
        push bp
        mov bp, sp
        push ax
        mov ax, [bp + 6]
        mov [bp + 2], ax
        mov ax, [bp + 8]
        mov [bp + 4], ax
        mov ax, [bp + 10]
        mov [bp + 6], ax
        mov [bp + 10], ds
        mov word [bp + 8], 0x3026
        push ds
        push bx
        lds bx, [bp + 2]        ; the line's colours, in the pushes that come next: the game's ...
        mov byte [bx + PK_TITLE_SECOND], PK_GAME_SECOND
        mov byte [bx + PK_TITLE_INK], PK_GAME_INK
        cmp byte [cs:lv_mode], 0
        je .out
        mov byte [bx + PK_TITLE_SECOND], PK_PICK_SECOND  ; ... or near-white on black, as the rows
        mov byte [bx + PK_TITLE_INK], PK_PICK_INK
        mov [bp + 10], cs
        mov word [bp + 8], lv_title
.out:   pop bx
        pop ds
        pop ax
        pop bp
        iret

probe_pk_fill:
        xor di, di
        mov si, di
        cmp byte [cs:lv_mode], 0
        je .out
        push bp
        mov bp, sp
        push ds
        push bx
        lds bx, [bp + 2]        ; the code after the INT: the far calls' addresses, as fixed up
        mov eax, [bx + PK_OP_CALL]
        mov [cs:lv_op_call], eax
        mov eax, [bx + PK_LABEL_CALL]
        mov [cs:lv_label_call], eax
        pop bx
        pop ds
        add word [bp + 2], PK_FILL_DONE
        pop bp
        call pk_ops
.out:   iret

; every row in use if its kind is in LV_AVAIL, out of use if not (DS the game's)
pk_ops:
        pusha
        push es
        xor si, si
.row:   xor ax, ax
        bt [cs:lv_avail], si
        jc .op
        inc ax
.op:    push ax
        push word 0
        lea bx, [si + PICK_FIRST]
        push bx
        push dword [PK_WINDOW]
        call far [cs:lv_op_call]
        add sp, 10
        inc si
        cmp si, 16
        jb .row
        pop es
        popa
        ret

probe_pk_click:
        push bp
        mov bp, sp              ; ([BP]: the click handler's BP)
        push bx
        mov bx, [bp]
        mov ax, [ss:bx + 8]     ; the replaced "mov ax,[bp+8]": the button
        cmp byte [cs:lv_mode], 0
        je .out
        sub ax, PICK_FIRST
        cmp ax, 16
        jae .back
        bt [cs:lv_avail], ax
        jnc .back
        btr [cs:lv_avail], ax
        call pk_take
        dec byte [cs:lv_left]
        mov ax, PK_STAY
        jnz .go
        mov ax, PK_CLOSE
.go:    add [bp + 2], ax
        jmp .out
.back:  add ax, PICK_FIRST
.out:   pop bx
        pop bp
        iret

; PK_TAKE: kind AX into the sheet of LV_MEMBER (its first empty SPEC_SLOTS byte), its row out of
; use, and the number shown one less (DS the game's)
pk_take:
        pusha
        push es
        mov dx, ax
        les bx, [LV_SHEETS]
        imul ax, [cs:lv_member], 0x47
        add bx, ax
        xor si, si
.slot:  cmp byte [es:bx + si + SPEC_SLOTS], 0
        je .put
        inc si
        cmp si, SPEC_COUNT
        jb .slot
        jmp .shown
.put:   mov al, dl
        inc al
        mov [es:bx + si + SPEC_SLOTS], al
.shown: push word 1
        push word 0
        add dx, PICK_FIRST
        push dx
        push dword [PK_WINDOW]
        call far [cs:lv_op_call]
        add sp, 10
        mov al, [cs:lv_left]
        dec al
        add al, '0'
        mov [cs:lv_count_text + 2], al
        push cs
        push word lv_count_text
        push dword PICK_COUNT_BUTTON
        push dword [PK_WINDOW]
        call far [cs:lv_label_call]
        add sp, 12
        pop es
        popa
        ret

lv_psi    dd 0                  ; the psionicists' pop-up (620:57h, as fixed up)
lv_spell  dd 0                  ; the preservers' CHOOSE A SPELL (620:5Ch, as fixed up)
lv_class  db 0                  ; 0Bh while PROBE_LV_PICK has a preserver gone up a level
lv_op_call dd 0                 ; 140:71Ah, a button's state (0 in use, 1 out of use)
lv_label_call dd 0              ; 140:7FAh, a button's text
lv_mode   db 0                  ; 1 while LV_ASK has the pop-up up for weapons
lv_left   db 0
lv_have   db 0
lv_avail  dw 0
lv_member dw 0
lv_count_text db '  0', 0
lv_title  db 'PICK A WEAPON SPECIALTY,', 0

; PROBE_EF_ROWS: INT VEC_EF_ROWS replaces "pop di / pop si" (2 bytes; DSUN.EXE 7F13Eh) at the end of
; the Effects screen's routine that puts the selected character's effects in their cells (7EDFAh,
; run when the screen opens and when another character is picked; [BP-4] the cells it has used).
; With weapon specialization, and the lower panel free (the game puts effects there only past 21),
; the character's weapon kinds are listed there, in the game's text as the USE screen's spell slots
; are (C_DRAW_LINE, at the same place): a line for the skill, then a line a kind. Then the pops,
; from under the interrupt frame.
EF_SEL_SEG   equ 0x7EE07 - 0x7F140  ; ("mov ax,348h": the selected character's number's segment,
EF_SELECTED  equ 0x25B              ;   at +25Bh there; less the address after the INT)
EF_CELLS     equ 21                 ; (the upper panel's)
EF_LINES     equ 5
probe_ef_rows:
        sti
        pushad
        push es
        call wp_rules
        jz .pops
        cmp word [bp - 4], EF_CELLS
        jg .pops
        mov bx, sp
        les di, [ss:bx + 34]    ; the code after the INT
        mov es, [es:di + EF_SEL_SEG]
        mov ax, [es:EF_SELECTED]
        cmp ax, LV_PARTY
        jae .pops
        mov dx, ds
        add dx, USE_TEXT_SEG
        mov [cs:c_draw + 2], dx
        mov word [cs:c_draw], USE_TEXT_OFF
        mov edx, [PK_WINDOW]
        mov [cs:c_winptr], edx
        call ef_draw
.pops:  pop es
        popad
        push bp                 ; the replaced pops: DI and SI from under the interrupt frame,
        mov bp, sp              ;   the frame (and BP) moved up over them
        push ax
        mov di, [bp + 8]
        mov si, [bp + 10]
        mov ax, [bp + 6]
        mov [bp + 10], ax       ; flags
        mov ax, [bp + 4]
        mov [bp + 8], ax        ; CS
        mov ax, [bp + 2]
        mov [bp + 6], ax        ; IP
        mov ax, [bp]
        mov [bp + 4], ax        ; BP
        pop ax
        mov sp, bp
        add sp, 4
        pop bp
        iret

; the kinds of party member AX (DS the game's): the skill's line where it changes, then the kind's
ef_draw:
        les bx, [LV_SHEETS]
        imul ax, ax, 0x47
        add bx, ax
        mov word [cs:ef_y], USE_FIRST_Y
        mov byte [cs:ef_last], 0xFF
        call kit_of_sheet
        jz .specs
        call ef_line
        call kit_id             ; (a human's kit asleep: until its new class passes the old)
        jnz .specs
        mov si, kit_asleep
        call ef_line
.specs: test word [cs:rules], RULE_SPECIALIZE
        jz .ret
        xor di, di
.slot:  movzx si, byte [es:bx + di + SPEC_SLOTS]
        or si, si
        jz .next
        dec si
        mov [cs:ef_kind], si
        shl si, 1
        mov si, [cs:si + wp_plain]  ; (its plain weapon's type: SPEC_OF_SHEET's skill with it)
        call spec_of_sheet
        mov al, 3
        cmp dl, SPEC_EXPERT
        je .skill
        cmp dl, SPEC_SPECIAL
        jb .next                ; (a warrior class not back yet: none)
        mov al, dl
        sub al, SPEC_SPECIAL
.skill: cmp al, [cs:ef_last]
        je .kind
        mov [cs:ef_last], al
        movzx si, al
        shl si, 1
        mov si, [cs:si + ef_skills]
        call ef_line
.kind:  mov si, [cs:ef_kind]
        shl si, 1
        mov si, [cs:si + ef_kinds]
        call ef_line
.next:  inc di
        cmp di, SPEC_COUNT
        jb .slot
.ret:   ret

; KIT_OF_SHEET: CS:SI the line naming the kit of sheet ES:BX ("KIT: RAVAGER", in KIT_LINE), asleep
; or not (KIT_ANY), ZF clear; ZF set if it has none. Others kept.
kit_of_sheet:
        push ax
        push cx
        push di
        call kit_any
        jz .out
        movzx cx, al            ; (the kit's place in KIT_NAMES, from 1: (class - 1) * 3 + kit)
        and cl, 3
        shr al, 2
        dec al
        mov ah, 3
        mul ah
        add cx, ax
        mov si, kit_names
.skip:  dec cx
        jz .copy
.past:  cs lodsb
        or al, al
        jnz .past
        jmp .skip
.copy:  mov di, kit_line + 5    ; (after "KIT: ")
.char:  cs lodsb
        mov [cs:di], al
        inc di
        or al, al
        jnz .char
        mov si, kit_line
        or di, di               ; (ZF clear)
.out:   pop di
        pop cx
        pop ax
        ret

; KIT_ID: AL the kit of sheet ES:BX (KIT_RAVAGER...: the creation screen's class x 4 + the kit,
; kitpages.kit_id), ZF clear; 0 and ZF set if it has none (the rule off, more than one class but
; for a human, none chosen) or it sleeps (KIT_AWAKE). Others kept.
kit_id:
        call kit_any
        jz .ret
        push di
        call kit_place
        call kit_awake
        pop di
        jnc .on
        xor al, al
.on:    or al, al
.ret:   ret

; KIT_PLACE: DI the place (0-2) among sheet ES:BX's classes of the class its kit was chosen with:
; 0 for one class; for a human who has changed class (DUAL: the classes move down, the new one
; first), its first class, the last of its classes. CF set for more than one class but not a
; human's (no kit). Others kept.
kit_place:
        xor di, di
        cmp word [es:bx + 0x22], 0
        je .ok
        cmp byte [es:bx + 0x18], 1
        jne .none
        inc di
        cmp byte [es:bx + 0x23], 0
        je .ok
        inc di
.ok:    clc
        ret
.none:  stc
        ret

; KIT_AWAKE: CF set if the kit of sheet ES:BX, its class at place DI (KIT_PLACE), sleeps: a human's
; first class, until the class it has now is of a higher level (as the game counts its earlier
; classes). Others kept.
kit_awake:
        or di, di
        jz .yes
        push ax
        mov al, [es:bx + di + 0x24]
        cmp [es:bx + 0x24], al
        pop ax
        ja .yes
        stc
        ret
.yes:   clc
        ret

; KIT_LEVEL: AL the level of the class sheet ES:BX's kit was chosen with (KIT_PLACE), the kit's
; levels (a Ravager's AC, a Seeker's slots), 0 for none. KIT_CLASS_OF: AL that class. Others kept.
kit_level:
        push di
        call kit_place
        mov al, 0
        jc .out
        mov al, [es:bx + di + 0x24]
.out:   pop di
        ret
kit_class_of_sheet:
        push di
        call kit_place
        mov al, 0
        jc .out
        mov al, [es:bx + di + 0x21]
.out:   pop di
        ret

; KIT_ANY: AL the kit of sheet ES:BX as KIT_ID's, but asleep too. Others kept.
kit_any:
        push cx
        push di
        xor cl, cl
        test word [cs:rules_hi], RULE_HI_KITS
        jz .out
        call kit_place
        jc .out
        mov ch, [es:bx + KIT_BYTE]
        dec ch
        cmp ch, 2
        ja .out
        movzx di, byte [es:bx + di + 0x21]  ; (its class, 1-17: the creation screen's)
        dec di
        cmp di, 16
        ja .out
        mov cl, [cs:di + kit_class_of]
        shl cl, 2
        add cl, ch
        inc cl
.out:   mov al, cl
        pop di
        pop cx
        or al, al
        ret

; KIT_OF_CREATURE: AL the kit (KIT_ID) of creature AX (its record's number), ZF as KIT_ID's. DS
; the game's; others but AX kept.
kit_of_creature:
        push bx
        push es
        les bx, [CREATURES]
        imul ax, ax, 0x3A
        add bx, ax
        mov ax, [es:bx + 4]     ; (its sheet's number)
        les bx, [0x1661]
        imul ax, ax, 0x47
        add bx, ax
        call kit_id
        pop es
        pop bx
        ret

; KIT_AC: AX (the AC the game's AC routine has for its creature, whose thing is its [BP+6]) with
; the creature's kit's (kits.ac): a Ravager's base AC by its level (RAVAGER_AC) where it is better
; than its sheet's (+27h), its armour improving it as before; a Wanderer's 1 worse; a Sentinel's 2
; better with a shield in a hand, an Arena Champion's 1; a Grove Warden's 1 better for every 3 druid levels. DS the game's;
; others kept.
kit_ac:
        test word [cs:rules_hi], RULE_HI_KITS
        jz .ret
        push bx
        push cx
        push dx
        push es
        mov cx, ax
        mov ax, ds
        add ax, THINGS_SEG
        mov es, ax
        mov bx, [bp + 6]
        imul bx, bx, 3
        cmp byte [es:bx + THINGS], 2
        jne .out                ; (not a creature)
        mov ax, [es:bx + THINGS + 1]
        push ax
        call kit_of_creature
        pop bx                  ; (BX the creature)
        cmp al, KIT_WANDERER
        jne .levels
        inc cx
        jmp .out
.levels:
        cmp al, KIT_RAVAGER
        je .sheet
        cmp al, KIT_GROVE_WARDEN
        jne .sentinel
.sheet: mov dl, al              ; (DL the kit; ES:BX the sheet: KIT_LEVEL)
        push dx
        mov ax, bx
        les bx, [CREATURES]
        imul ax, ax, 0x3A
        add bx, ax
        mov ax, [es:bx + 4]
        les bx, [0x1661]
        imul ax, ax, 0x47
        add bx, ax
        pop dx
        call kit_level
        movzx ax, al
        cmp dl, KIT_GROVE_WARDEN
        jne .table
        mov dl, 3
        div dl
        movzx ax, al
        sub cx, ax
        jmp .out
.table: dec ax
        cmp ax, RAVAGER_LEVELS - 1
        jbe .level
        mov ax, RAVAGER_LEVELS - 1
.level: push si
        mov si, ax
        movsx dx, byte [cs:si + ravager_ac]
        pop si
        movsx ax, byte [es:bx + 0x27]
        sub ax, dx              ; (the sheet's base less the table's: what the table betters it by)
        jle .out
        sub cx, ax
        jmp .out
.sentinel:
        mov dx, 2               ; (a Sentinel's 2 with a shield, an Arena Champion's 1)
        cmp al, KIT_SENTINEL
        je .shield
        dec dx
        cmp al, KIT_CHAMPION
        jne .out
.shield:
        push di
        mov di, [bp + 6]
        mov [cs:r_things], es
        call prot_scan
        pop di
        jc .out
        test byte [cs:p_flags], P_SHIELD
        jz .out
        sub cx, dx
.out:   mov ax, cx
        pop es
        pop dx
        pop cx
        pop bx
.ret:   ret
ravager_ac db 7, 7, 6, 6, 5, 5, 4, 4, 3, 3, 3, 2, 2, 2, 1, 1, 1, 0  ; (by level, 1-18 and on)
RAVAGER_LEVELS equ $ - ravager_ac

; KIT_MELEE: AX what the kit of sheet ES:BX adds to hit and to damage with item type SI unless it
; is a missile weapon's (its type's +0, bit 2): a Ravager's 1; a Brute's 2 with a two-handed weapon
; (+0Fh, 40h); else 0 (kits.melee). DS the game's; others kept.
kit_melee:
        call kit_id
        push bx
        push es
        mov ah, al
        push ax
        les bx, [ITEM_TYPES]
        imul ax, si, 0x14
        add bx, ax
        pop ax
        test byte [es:bx], KT_MISSILE
        jnz .none
        cmp ah, KIT_RAVAGER
        je .one
        cmp ah, KIT_BRUTE
        jne .none
        test byte [es:bx + 0x0F], KT_TWO_HANDED
        jz .none
        mov ax, 2
        jmp .out
.one:   mov ax, 1
        jmp .out
.none:  xor ax, ax
.out:   pop es
        pop bx
        ret

; KIT_TO_HIT: AX what the attacker's kit adds to hit in the weapon attack routine (the sheet its
; [BP+10h], the item type [BP+14h], [BP+16h] above 1 for a missile): for a melee attack
; KIT_CHAMPION's and KIT_MELEE's, for a missile nothing. Others kept.
kit_to_hit:
        cmp word [bp + 0x16], 1
        jle .melee
        xor ax, ax              ; (nothing for a missile)
        ret
.melee:
        call kit_champion
        push bx
        push dx
        push si
        push es
        mov dx, ax
        les bx, [0x1661]
        imul ax, [bp + 0x10], 0x47
        add bx, ax
        mov si, [bp + 0x14]
        call kit_melee
        add ax, dx
        pop es
        pop si
        pop dx
        pop bx
.ret:   ret

; KIT_ATTACK_DAMAGE: the weapon attack's damage bonus ([BP-12h]) with the attacker's kit's
; (KIT_MELEE, for a melee attack: [BP+16h] 1 or less; the sheet [BP+10h], the item type
; [BP+14h]). All registers kept.
kit_attack_damage:
        cmp word [bp + 0x16], 1
        jg .ret
        push ax
        call kit_champion       ; (with a shield, +1 in melee)
        cmp ax, 1
        jne .kit
        inc word [bp - 0x12]
.kit:   pop ax
        push ax
        push bx
        push si
        push es
        les bx, [0x1661]
        imul ax, [bp + 0x10], 0x47
        add bx, ax
        mov si, [bp + 0x14]
        call kit_melee
        add [bp - 0x12], ax
        pop es
        pop si
        pop bx
        pop ax
.ret:   ret

; KIT_CHAMPION: AX 1 if the attacker in the weapon attack routine (its thing [BP+18h], its sheet
; [BP+10h]) is an Arena Champion with a shield in a hand, -1 if one without, else 0 (kits.champion:
; in melee, with one +1 to hit and damage, without one -1 to hit). DS the game's; others kept.
kit_champion:
        push bx
        push es
        les bx, [0x1661]
        imul ax, [bp + 0x10], 0x47
        add bx, ax
        call kit_id
        pop es
        pop bx
        cmp al, KIT_CHAMPION
        mov ax, 0
        jne .ret
        push di
        mov ax, ds
        add ax, THINGS_SEG
        mov [cs:r_things], ax
        mov di, [bp + 0x18]
        mov ax, -1
        call prot_scan
        jc .out
        test byte [cs:p_flags], P_SHIELD
        jz .out
        mov ax, 1
.out:   pop di
.ret:   ret

; PROBE_INIT: INT VEC_INIT replaces "add dx,14h" (3 bytes: INT + NOP; DSUN.EXE 5750Eh) where a
; combatant's initiative for the round is made (DX the 0-9 roll and its adjustments, SI the
; creature): the game's 20 added, and a Sentinel's 2 more (kits.initiative).
probe_init:
        add dx, 20
        push ax
        mov ax, si
        call kit_of_creature
        cmp al, KIT_SENTINEL
        jne .out
        add dx, 2
.out:   pop ax
        iret

; PROBE_THAC0: INT VEC_THAC0 replaces "mov ax,14h / sub ax,si" (5 bytes: INT + 3 NOPs; DSUN.EXE
; 876BBh) at the end of the game's THAC0 routine (SI the most any of the character's classes
; takes off 20, [BP+6] its sheet's number), which every write of a creature's THAC0 (+1Fh) uses:
; AX 20 less SI, and the kit's (kits.thac0): a Swashbuckler's, Crusader's, Battle Mage's or Mind
; Warrior's a warrior's (21 less its level) where that is better, a Scholar's 1 worse.
probe_thac0:
        push bx
        push cx
        push es
        mov cx, 20
        sub cx, si
        les bx, [0x1661]
        imul ax, [bp + 6], 0x47
        add bx, ax
        call kit_id
        jz .out
        cmp al, KIT_SCHOLAR
        jne .warrior
        inc cx
        jmp .out
.warrior:
        cmp al, KIT_SWASHBUCKLER
        je .level
        cmp al, KIT_CRUSADER
        je .level
        cmp al, KIT_BATTLE_MAGE
        je .level
        cmp al, KIT_MIND_WARRIOR
        jne .out
.level: call kit_level
        movzx ax, al
        neg ax
        add ax, 21
        cmp ax, 1
        jge .best
        mov ax, 1
.best:  cmp ax, cx
        jge .out
        mov cx, ax
.out:   mov ax, cx
        pop es
        pop cx
        pop bx
        iret

; PROBE_SLOTS: INT VEC_SLOTS replaces "mov ax,[bp-2]" (3 bytes: INT + NOP; DSUN.EXE 5E255h) at the
; end of the game's spell slot routine (the slots a combatant has at a spell level, on resting
; and wherever the most is wanted: [BP+6] the combatant, [BP+8] the kind of magic, 1 wizard and 2
; priest, [BP+0Ah] the spell level; [BP-2] the slots its classes give): AX the slots, with the
; kit's (kits.slots): an Arcanist's wizard slots 1 more at each spell level it has any, a Battle
; Mage's 1 fewer, a Crusader's priest slots 1 fewer; a Seeker's and a Justifier's priest slots
; their own tables' (SEEKER_SLOTS), by ranger level; a Shinobi's wizard slots the Seeker's, by
; thief level.
SLOT_WIZARD  equ 1
SLOT_PRIEST  equ 2
probe_slots:
        push bx
        push cx
        push es
        mov cx, [bp - 2]
        mov ax, ds
        add ax, THINGS_SEG
        mov es, ax
        imul bx, [bp + 6], 3
        mov ax, [es:bx + COMBATANT_CREATURE]
        push ax
        call kit_of_creature
        pop bx                  ; (BX the creature)
        jz .out
        cmp al, KIT_ARCANIST
        jne .battle
        cmp byte [bp + 8], SLOT_WIZARD
        jne .out
        or cx, cx
        jz .out
        inc cx
        jmp .out
.battle:
        cmp al, KIT_BATTLE_MAGE
        jne .crusader
        cmp byte [bp + 8], SLOT_WIZARD
        je .fewer
        jmp .out
.crusader:
        cmp al, KIT_CRUSADER
        jne .seeker
        cmp byte [bp + 8], SLOT_PRIEST
        jne .out
.fewer: or cx, cx
        jz .out
        dec cx
        jmp .out
.seeker:
        cmp al, KIT_SHINOBI     ; (a Shinobi's wizard slots: the Seeker's table, by thief level)
        jne .ranger
        cmp byte [bp + 8], SLOT_WIZARD
        jne .out
        mov al, KIT_SEEKER
        jmp .levels
.ranger:
        cmp al, KIT_SEEKER
        je .table
        cmp al, KIT_JUSTIFIER
        jne .out
.table: cmp byte [bp + 8], SLOT_PRIEST
        jne .out
.levels:
        push dx
        mov dl, al
        mov ax, bx
        les bx, [CREATURES]
        imul ax, ax, 0x3A
        add bx, ax
        mov ax, [es:bx + 4]
        les bx, [0x1661]
        imul ax, ax, 0x47
        add bx, ax
        call kit_level                  ; (the ranger level)
        movzx ax, al
        xor cx, cx
        movzx bx, byte [bp + 0x0A]
        dec bx
        cmp bx, 2
        ja .table_out           ; (spell levels 1 to 3 only)
        cmp dl, KIT_JUSTIFIER
        jne .seeker_level
        cmp ax, 10
        jb .table_out
        or bx, bx
        jnz .table_out
        inc cx                  ; (one 1st-level slot from 10th level)
        jmp .table_out
.seeker_level:
        cmp ax, 6
        jb .table_out
        cmp ax, 10
        jbe .row
        mov ax, 10
.row:   sub ax, 6
        imul ax, ax, 3
        add bx, ax
        mov cl, [cs:bx + seeker_slots]
.table_out:
        pop dx
.out:   mov ax, cx
        pop es
        pop cx
        pop bx
        iret
; a Seeker's priest slots at spell levels 1, 2 and 3, by ranger level 6 to 10 (and on: kits.SEEKER_SLOTS)
seeker_slots db 1, 0, 0,  2, 0, 0,  2, 1, 0,  2, 2, 0,  2, 2, 1

; PROBE_SLOT_LEVEL: INT VEC_SLOT_LEVEL replaces "mov al,es:[bx+24h]" (4 bytes: INT + 2 NOPs;
; DSUN.EXE 5E1F6h) in the game's spell slot routine, where it takes a class's level (ES:BX the
; sheet + DI, the class's place): AL that level, an Elementalist's 1 less (kits.slot_level: its
; slots a level behind), for its kit's own class (KIT_PLACE). Others kept.
probe_slot_level:
        push bx
        push cx
        push dx
        mov ch, ah
        mov dx, di
        sub bx, di
        call kit_id
        mov cl, al
        push di
        call kit_place          ; (the kit's own class only: a human's other classes as they are)
        cmp di, dx
        pop di
        je .own
        xor cl, cl
.own:   add bx, di
        mov al, [es:bx + 0x24]
        cmp cl, KIT_ELEMENTALIST
        jne .out
        or al, al
        jz .out
        dec al
.out:   mov ah, ch
        pop dx
        pop cx
        pop bx
        iret

; KIT_PSP: AX the PSP a power (BX, 0-33: psychokinesis to 5, psychometabolism to 19, telepathy
; from 20) costs combatant SI to use, from the game's AX (kits.psp_cost): a Mind Bender's telepathy
; 2 less, its psychokinesis 2 more, a Kineticist's the other way about, never below 1 (a cost of
; 0 or less as it was). KIT_PSP_OF: the same for the kit in CL. DS the game's; others kept.
PSP_PK_LAST  equ 5
WHOSE_TURN   equ 0x4979         ; DS: the combatant whose turn it is in a fight
PSP_TP_FIRST equ 20
kit_psp:
        push cx
        push es
        mov cx, ax
        mov ax, ds
        add ax, THINGS_SEG
        mov es, ax
        push bx
        imul bx, si, 3
        mov ax, [es:bx + COMBATANT_CREATURE]
        pop bx
        call kit_of_creature
        xchg ax, cx             ; (CL the kit, AX the cost)
        call kit_psp_of
        pop es
        pop cx
        ret
kit_psp_of:
        or ax, ax
        jle .ret
        push dx
        xor dx, dx              ; DX what the kit adds to telepathy (psychokinesis the opposite)
        cmp cl, KIT_MIND_BENDER
        jne .kineticist
        mov dx, -2
        jmp .discipline
.kineticist:
        cmp cl, KIT_KINETICIST
        jne .out
        mov dx, 2
.discipline:
        cmp bx, PSP_TP_FIRST
        jae .add
        neg dx
        cmp bx, PSP_PK_LAST
        jbe .add
        xor dx, dx              ; (psychometabolism: as it was)
.add:   add ax, dx
        cmp ax, 1
        jge .out
        mov ax, 1
.out:   pop dx
.ret:   ret

; PROBE_PSP_USE: INT VEC_PSP_USE replaces "or di,di / jge $+4 / xor di,di" (6 bytes: INT + 4
; NOPs; DSUN.EXE 5CBE7h) where the routine using a power has its cost in DI (SI the combatant,
; [BP+0Ah] the power: its table's, or worked out for Enhanced Strength and Domination): DI the
; kit's (KIT_PSP), no less than 0.
probe_psp_use:
        push ax
        push bx
        mov ax, di
        mov bx, [bp + 0x0A]
        call kit_psp
        or ax, ax
        jge .set
        xor ax, ax
.set:   mov di, ax
        pop bx
        pop ax
        iret

; PROBE_PSP_TABLE: INT VEC_PSP_TABLE replaces "mov al,es:[bx+1]" (5 bytes: INT + 3 NOPs) where the
; game reads a power's cost from its table (ES:BX the power's row, 8 bytes a power; SI the
; combatant): in the check whether a power can be used (DSUN.EXE 5CAA3h) and where half of it is
; taken for a power that fails (5CCA2h). AL the kit's (KIT_PSP); AH as it was.
probe_psp_table:
        push bx
        push cx
        mov ch, ah
        movzx ax, byte [es:bx + 1]
        shr bx, 3
        call kit_psp
        mov ah, ch
        pop cx
        pop bx
        iret

; PROBE_PSP_DEFENCE: INT VEC_PSP_DEFENCE replaces "sub es:[bx+2],ax" (4 bytes: INT + 2 NOPs;
; DSUN.EXE 5D820h) where a creature (ES:BX its record, [BP-6] its number) pays AX for the psionic
; defence it raises: the kit's (KIT_PSP_OF; the defences are all telepathy), then taken off.
probe_psp_defence:
        push bx
        push cx
        mov cx, ax
        mov ax, [bp - 6]
        call kit_of_creature
        xchg ax, cx             ; (CL the kit, AX the cost)
        mov bx, PSP_TP_FIRST
        call kit_psp_of
        pop cx
        pop bx
        sub [es:bx + 2], ax
        iret

; GAME_DIE: AX a roll of 1 to AX from the game's own rand() seed (as its "rand()*N/32768 + 1"),
; not recorded in the ring. DS the game's; others kept (EAX's upper half too).
game_die:
        push ebx
        push ecx
        push edx
        movzx ecx, ax
        push eax
        mov bx, [cs:seed_off]
        mov eax, [bx]
        imul eax, eax, 0x015A4E35
        inc eax
        mov [bx], eax
        shr eax, 16
        and eax, 0x7FFF
        imul eax, ecx
        shr eax, 15
        inc eax
        mov cx, ax
        pop eax
        mov ax, cx
        pop edx
        pop ecx
        pop ebx
        ret

; PROBE_CURE: INT VEC_CURE replaces "nop / push cs" (2 bytes; DSUN.EXE 79619h) in the handler for
; spells with rules of their own, where the healing it has rolled (the word pushed last but one;
; DI the target, the game's [BP+8] the caster, [BP+0Eh] the spell) goes to the routine that heals:
; the caster's kit's (kits.cure): a Healer's Cure Light, Serious and Critical Wounds 1 more a die,
; a Lifebinder's those and Blood Flow a die more (CURE_DICE). Then CS pushed, as the code was.
CURE_SPELLS: ; spell, dice, sides (kits.CURE_DICE)
        db 71, 1, 8,  112, 2, 8,  127, 3, 8,  108, 2, 6
CURE_SPELLS_END:
probe_cure:
        sub sp, 2
        push bp
        mov bp, sp              ; [BP+4] the INT's IP, CS, flags; [BP+0Ah] DI pushed, [BP+0Ch] the healing
        pusha
        mov ax, [bp + 4]
        mov [bp + 2], ax
        mov ax, [bp + 6]
        mov [bp + 4], ax
        mov ax, [bp + 8]
        mov [bp + 6], ax
        mov ax, [bp + 4]
        mov [bp + 8], ax        ; (the CS the code pushed)
        mov di, [bp]            ; the game's BP
        mov ax, [ss:di + 0x0E]
        mov bx, CURE_SPELLS
.find:  cmp [cs:bx], al
        je .spell
        add bx, 3
        cmp bx, CURE_SPELLS_END
        jb .find
        jmp .out
.spell: or ah, ah
        jnz .out
        push bx
        push es
        mov ax, ds
        add ax, THINGS_SEG
        mov es, ax
        imul bx, [ss:di + 8], 3
        mov ax, [es:bx + COMBATANT_CREATURE]
        pop es
        call kit_of_creature
        pop bx
        jz .out
        cmp al, KIT_HEALER
        jne .lifebinder
        cmp byte [cs:bx], 108   ; (Blood Flow: not a cure)
        je .out
        movzx ax, byte [cs:bx + 1]
        add [bp + 0x0C], ax
        jmp .out
.lifebinder:
        cmp al, KIT_LIFEBINDER
        jne .out
        movzx ax, byte [cs:bx + 2]
        call game_die
        add [bp + 0x0C], ax
.out:   popa
        pop bp
        iret

; PROBE_PSP_KEEP: INT VEC_PSP_KEEP replaces "mov al,es:[bx+2]" (5 bytes: INT + 3 NOPs; DSUN.EXE
; 5CE49h) where the game takes a power's cost to keep it up another round from its table (ES:BX
; the power's row; SI the combatant): AL the kit's (KIT_PSP), but 63h (none to keep up) as it is;
; AH as it was. PROBE_PSP_KEEP_DX: the same with the combatant in DX (5CB02h, the check whether it
; can be kept up).
PSP_NO_UPKEEP equ 0x63
probe_psp_keep_dx:
        push si
        mov si, dx
        call psp_keep
        pop si
        iret
probe_psp_keep:
        call psp_keep
        iret
psp_keep:
        push bx
        push cx
        mov ch, ah
        movzx ax, byte [es:bx + 2]
        cmp al, PSP_NO_UPKEEP
        je .out
        shr bx, 3
        call kit_psp
.out:   mov ah, ch
        pop cx
        pop bx
        ret

; PROBE_HIT_ROUND: INT VEC_HIT_ROUND replaces "mov byte es:[si+0AFh],1" (6 bytes: INT + 4 NOPs;
; DSUN.EXE 58733h) where the routine taking a hit's damage off marks creature SI hit this round
; (ES the segment of those marks), which keeps it from casting a spell until the next round (the
; USE screen's refusal, 892F3h; a queued spell dropped, 8991Bh): a Battle Mage isn't marked.
probe_hit_round:
        push ax
        mov ax, si
        call kit_of_creature
        cmp al, KIT_BATTLE_MAGE
        je .out
        mov byte [es:si + 0xAF], 1
.out:   pop ax
        iret

; The Shinobi's spells (kits.SHINOBI_SPELLS): spell, spell level. Wizard spells are 0 to WIZARD_LAST.
SHINOBI_SPELLS:
        db 6, 1,  2, 1,  9, 1,  4, 1,  11, 1           ; Gaze Reflection, Charm Person, Shield, Color Spray, Wall of Fog
        db 17, 2,  19, 2,  12, 2,  13, 2,  15, 2       ; Invisibility, Mirror Image, Blur, Detect Invisibility, Fog Cloud
        db 25, 3,  29, 3,  36, 3,  30, 3               ; Blink, Haste, Protection from Normal Missiles, Hold Person
SHINOBI_SPELLS_END:
WIZARD_LAST  equ 68
SHINOBI_FIRST equ 6             ; (the thief level its spells start at; it casts at that less 5)
KNOWN_FROM_DS equ 0x4356 - 0x3800  ; the spells each party member knows: DS less this, from KNOWN_OFF,
KNOWN_OFF    equ 0x168          ;   8Ah bytes a member, a byte a spell (not 0: known)
KNOWN_SIZE   equ 0x8A

; SHINOBI_CAST: AX the level a Shinobi of thief level AX casts at (less SHINOBI_FIRST - 1, no less
; than 0). Others kept.
shinobi_cast:
        sub ax, SHINOBI_FIRST - 1
        jns .ret
        xor ax, ax
.ret:   ret

; PROBE_CAST_LEVEL: INT VEC_CAST_LEVEL replaces "mov ax,[bp-2]" (3 bytes: INT + NOP; DSUN.EXE 81C06h)
; at the end of the caster level routine (its [BP+6] the combatant, [BP+8] the spell; [BP-2] the
; level its classes give, a ranger's 7 less: PROBE_RANGER_CAST), which sets the spell levels a
; caster may cast (half it rounded up: 81664h asks for spell 0) and weighs a spell against Dispel
; Magic: AX that, a Shinobi's for a wizard spell its thief level less 5 where more (SHINOBI_LEVEL).
probe_cast_level:
        mov ax, [bp - 2]
        jmp shinobi_wizard

; PROBE_SPELL_LEVEL: INT VEC_SPELL_LEVEL replaces "mov ax,di" (2 bytes; DSUN.EXE 5E3D1h) at the end of
; the routine giving the level a spell is cast at, for its duration and damage (5E25Ch; the same
; arguments; DI the best level of the caster's classes that cast it, PROBE_RANGER_LEVEL's for a
; ranger): AX that, for a wizard spell a Shinobi's as PROBE_CAST_LEVEL's.
probe_spell_level:
        mov ax, di
        jmp shinobi_wizard

; PROBE_RANGER_LEVEL: INT VEC_RANGER_LEVEL replaces "mov al,es:[bx+24h]" (4 bytes: INT + 2 NOPs;
; DSUN.EXE 5E3B8h) where that routine takes the level of a class of the caster's that casts the
; spell (ES:BX the sheet + the class's place; the combatant its [BP+6]), whole for a ranger (the
; caster level routine counts it 7 less: PROBE_RANGER_CAST): AL that, a ranger's 7 less with
; RULE_HI_RANGER, a Seeker's 5 less and a Justifier's 9 whatever the rule (no less than 0;
; kits.spell_class_level).
probe_ranger_level:
        mov al, [es:bx + 0x24]
        push cx
        mov cl, [es:bx + 0x21]
        sub cl, 13
        cmp cl, 3
        ja .out                 ; (not a ranger: classes 13-16)
        push ax
        push bx
        call combatant_kit
        mov cl, 5
        cmp al, KIT_SEEKER
        je .take
        mov cl, 9
        cmp al, KIT_JUSTIFIER
        je .take
        mov cl, 7
        test word [cs:rules_hi], RULE_HI_RANGER
        jnz .take
        xor cl, cl
.take:  pop bx
        pop ax
        sub al, cl
        jnc .out
        xor al, al
.out:   pop cx
        iret

; SHINOBI_WIZARD: AX (a level), for wizard spell [BP+8] the level a Shinobi (combatant [BP+6])
; casts it at where more; then IRET.
shinobi_wizard:
        cmp word [bp + 8], WIZARD_LAST
        ja .ret
        push cx
        mov cx, ax
        call shinobi_level
        cmp ax, cx
        jge .out
        mov ax, cx
.out:   pop cx
.ret:   iret

; COMBATANT_KIT: AL the kit (KIT_ID) of combatant [BP+6], BX its creature. DS the game's; others
; but AX and BX kept.
combatant_kit:
        push es
        mov ax, ds
        add ax, THINGS_SEG
        mov es, ax
        imul bx, [bp + 6], 3
        mov bx, [es:bx + COMBATANT_CREATURE]
        mov ax, bx
        call kit_of_creature
        pop es
        ret

; SHINOBI_LEVEL: AX the level a Shinobi casts at (SHINOBI_CAST) if combatant [BP+6] is one, else 0.
; DS the game's; others kept.
shinobi_level:
        push bx
        push es
        call combatant_kit
        cmp al, KIT_SHINOBI
        mov ax, 0
        jne .out
        mov ax, bx
        les bx, [CREATURES]
        imul ax, ax, 0x3A
        add bx, ax
        mov ax, [es:bx + 4]
        les bx, [0x1661]
        imul ax, ax, 0x47
        add bx, ax
        call kit_level
        movzx ax, al
        call shinobi_cast
.out:   pop es
        pop bx
        ret

; PROBE_PICK_ANY: INT VEC_PICK_ANY replaces "mov [bp-2],ax / or ax,ax" (5 bytes: INT + 3 NOPs;
; DSUN.EXE 85580h) in the routine a level up calls for a preserver's spell (620:5Ch, 85560h; SI the
; character, AX its preserver level), which goes on (the JG after) only for a level, and then opens
; CHOOSE A SPELL (85771h) if the game's list (500:2Ah) has a spell to learn. A Shinobi has no
; preserver level and nothing on that list: for one, CHOOSE A SPELL (DI 1, on at PICK_OPEN) if one
; of its spells (SHINOBI_SPELLS) up to the spell level it casts is unknown (SHINOBI_UNKNOWN), else
; nothing (AX 0). Flags as "or ax,ax".
PICK_OPEN equ 0x855CB - 0x85582   ; ("or di,di", then "push si / call 5771h")
probe_pick_any:
        mov [bp - 2], ax
        push bp
        mov bp, sp              ; (the INT's frame: [BP+2] IP, [BP+6] flags)
        push bx
        push cx
        push es
        mov cx, ax
        les bx, [0x1661]
        imul ax, si, 0x47
        add bx, ax
        call kit_id
        xchg ax, cx             ; (CL the kit)
        cmp cl, KIT_SHINOBI
        jne .flags
        call kit_level
        movzx ax, al
        call shinobi_cast
        inc ax
        shr ax, 1
        mov cl, al              ; (CL the highest spell level it casts)
        xor ax, ax
        call shinobi_unknown
        jnc .flags
        mov di, 1
        add word [bp + 2], PICK_OPEN
.flags: or ax, ax
        pushf
        pop cx
        and cx, 0x08D5          ; (OF, SF, ZF, AF, PF, CF)
        and word [bp + 6], ~0x08D5
        or [bp + 6], cx
        pop es
        pop cx
        pop bx
        pop bp
        iret

; SHINOBI_UNKNOWN: CF set if character SI doesn't know one of the Shinobi's spells (SHINOBI_SPELLS)
; of spell level CL or less. DS the game's; all registers kept.
shinobi_unknown:
        push ax
        push bx
        push si
        push ds
        imul bx, si, KNOWN_SIZE
        add bx, KNOWN_OFF
        mov ax, ds
        sub ax, KNOWN_FROM_DS
        mov ds, ax
        mov si, SHINOBI_SPELLS
.spell: cmp [cs:si + 1], cl
        ja .next
        movzx ax, byte [cs:si]
        push bx
        add bx, ax
        cmp byte [bx], 0
        pop bx
        stc
        je .out
.next:  add si, 2
        cmp si, SHINOBI_SPELLS_END
        jb .spell
        clc
.out:   pop ds
        pop si
        pop bx
        pop ax
        ret

; PROBE_PICK_LEVEL: INT VEC_PICK_LEVEL replaces "inc al" (2 bytes; DSUN.EXE 85861h) in the CHOOSE A
; SPELL window, where AL is the character's preserver level and the spell levels on offer are up to
; (AL + 1) / 2 (DS:[119Ch] its sheet, far): a Shinobi's its casting level (SHINOBI_CAST). Then
; AL + 1, as the code was.
PICK_SHEET   equ 0x119C
probe_pick_level:
        push bx
        push es
        les bx, [PICK_SHEET]
        push ax
        call kit_id
        cmp al, KIT_SHINOBI
        pop ax
        jne .out
        call kit_level
        movzx ax, al
        call shinobi_cast
.out:   inc al
        pop es
        pop bx
        iret

; PROBE_PICK_LIST: INT VEC_PICK_LIST replaces "mov di,ax" (2 bytes; DSUN.EXE 8563Fh) in the CHOOSE A
; SPELL window, after the game's routine put the spells the character may learn in the list (the
; segment of "mov ax,348h" before, PICK_LIST_SEG, at 7, a word each; AX how many; the character at
; that segment's 25Bh; the highest spell level DS:[4AECh]): for a Shinobi, its own spells
; (SHINOBI_SPELLS) up to that level that it doesn't know yet. DI how many, as the code had it.
PICK_LIST_SEG equ 0x8562E - 0x85641   ; (the segment's word in "mov ax,348h", less the INT's way back)
PICK_LIST    equ 7
PICK_WHO     equ 0x25B
PICK_MOST    equ 0x4AEC
probe_pick_list:
        mov di, ax
        push ax
        push bx
        push cx
        push dx
        push si
        push es
        push bp
        les bx, [PICK_SHEET]
        call kit_id
        cmp al, KIT_SHINOBI
        jne .out
        mov bp, sp
        les si, [ss:bp + 14]    ; (the INT's way back: CS:IP)
        mov es, [es:si + PICK_LIST_SEG]
        mov cx, [es:PICK_WHO]   ; the character's known spells: CX their offset
        imul cx, cx, KNOWN_SIZE
        add cx, KNOWN_OFF
        mov dl, [PICK_MOST]     ; (DL the highest spell level on offer)
        mov ax, ds
        sub ax, KNOWN_FROM_DS
        push ds
        mov ds, ax
        xor di, di              ; (the list's length)
        mov si, SHINOBI_SPELLS
.spell: cmp [cs:si + 1], dl
        ja .next
        movzx bx, byte [cs:si]
        add bx, cx
        cmp byte [bx], 0
        jne .next               ; (known)
        sub bx, cx
        mov [es:PICK_LIST + di], bx
        add di, 2
.next:  add si, 2
        cmp si, SHINOBI_SPELLS_END
        jb .spell
        pop ds
        shr di, 1
.out:   pop bp
        pop es
        pop si
        pop dx
        pop cx
        pop bx
        pop ax
        iret

; PROBE_SCROLL_LEARN: INT VEC_SCROLL_LEARN replaces "or ax,ax" (2 bytes; DSUN.EXE 8B6D3h, then the
; game's "jz", to "CANNOT LEARN FROM THIS ITEM") after the game's check whether the character on
; show (the segment of "mov ax,348h" before, its 25Bh) may learn a scroll's spell: none for a
; Shinobi, who learns only at a level up. ZF as "or ax,ax" leaves it.
SCROLL_WHO_SEG equ 0x8B6C2 - 0x8B6D5
probe_scroll_learn:
        or ax, ax
        jz .flags
        push bx
        push si
        push es
        push bp
        mov bp, sp
        les si, [ss:bp + 8]     ; (the INT's way back)
        mov es, [es:si + SCROLL_WHO_SEG]
        mov bx, [es:PICK_WHO]
        imul bx, bx, 0x47
        les si, [0x1661]
        add bx, si
        push ax
        call kit_id
        cmp al, KIT_SHINOBI
        pop ax
        jne .keep
        xor ax, ax
.keep:  pop bp
        pop es
        pop si
        pop bx
.flags: push bp                 ; (ZF in the flags IRET gives back)
        mov bp, sp
        and word [bp + 6], ~0x40
        or ax, ax
        jnz .done
        or word [bp + 6], 0x40
.done:  pop bp
        iret

; PROBE_RANGER_CAST: INT VEC_RANGER_CAST replaces "sub dx,7" (3 bytes: INT + NOP; DSUN.EXE 81B6Ah)
; in the game's caster level routine (its [BP+6] the combatant, [BP+8] the spell), where a ranger's
; level counts 7 less: DX that much less, a Seeker's 5, a Justifier's 9 (kits.ranger_cast_drop),
; which also sets the spell levels it may cast (half the caster level, rounded up). Others kept.
probe_ranger_cast:
        push ax
        push bx
        push es
        push dx
        mov ax, ds
        add ax, THINGS_SEG
        mov es, ax
        imul bx, [bp + 6], 3
        mov ax, [es:bx + COMBATANT_CREATURE]
        call kit_of_creature
        pop dx
        mov bx, 7
        cmp al, KIT_SEEKER
        jne .justifier
        mov bx, 5
.justifier:
        cmp al, KIT_JUSTIFIER
        jne .take
        mov bx, 9
.take:  sub dx, bx
        pop es
        pop bx
        pop ax
        iret

; KIT_SAVE: SI (a saving throw's modifiers) with the kit's of the one saving (thing DI) against
; spell AX: a Sentinel's -1 against a wizard's or priest's spell (0-137, not a psionic power or a
; monster's), a Myrmidon's -4 against a charm (KIT_CHARMS), a Wanderer's +3 against fire and cold
; (kits.save). DS the game's; others kept.
KIT_SPELL_LAST equ 137
KIT_SPELLS equ 256              ; (the spell records: DS - SPELLS_FROM_DS, +40h, 20h each)
SPELLS_FROM_DS equ 0x4356 - 0x3CB4
kit_save:
        push ax
        push bx
        push cx
        push es
        mov cx, ax
        mov ax, ds
        add ax, THINGS_SEG
        mov es, ax
        mov bx, di
        imul bx, bx, 3
        cmp byte [es:bx + THINGS], 2
        jne .out                ; (not a creature)
        mov ax, [es:bx + THINGS + 1]
        call kit_of_creature
        cmp al, KIT_SENTINEL
        jne .myrmidon
        cmp cx, KIT_SPELL_LAST
        ja .out
        dec si
        jmp .out
.myrmidon:
        cmp al, KIT_MYRMIDON
        jne .wanderer
        mov bx, kit_charms
.charm: cmp [cs:bx], cx
        je .charmed
        add bx, 2
        cmp bx, kit_charms_end
        jb .charm
        jmp .out
.charmed:
        sub si, 4
        jmp .out
.wanderer:
        cmp al, KIT_WANDERER    ; +3 against a fire or cold spell (its record's +1Ah: 2 fire, 4 cold),
        jne .out                ;   as the game's Resist Fire and Resist Cold
        cmp cx, KIT_SPELLS
        jae .out
        push ds
        mov ax, ds
        sub ax, SPELLS_FROM_DS
        mov ds, ax
        mov bx, cx
        shl bx, 5
        test byte [bx + 0x40 + 0x1A], 0x06
        pop ds
        jz .out
        add si, 3
.out:   pop es
        pop cx
        pop bx
        pop ax
        ret
; the charms (kits.CHARMS): Charm Person, Charm Monster, Domination, Charm Person or Mammal, and
; the psionic Domination and Mass Domination
kit_charms dw 2, 40, 61, 82, 158, 159
kit_charms_end:

; KIT_MOVE: AX (a creature's movement for its turn in a fight, its Move x 10; SI the creature)
; with its kit's: a Stalker's 2 more. Others kept.
kit_move:
        push ax
        mov ax, si
        call kit_of_creature
        cmp al, KIT_STALKER
        jne .none
        pop ax
        add ax, 20
        ret
.none:  pop ax
        ret

; CS:SI on the panel's next line (all registers kept: the game's text routine changes ES)
ef_line:
        pusha
        push es
        cmp word [cs:ef_y], USE_FIRST_Y + (EF_LINES - 1) * USE_STEP
        ja .out
        push word [cs:ef_y]
        push word USE_X
        push cs
        push si
        call c_draw_line
.out:   add word [cs:ef_y], USE_STEP
        pop es
        popa
        ret

; Each class's kits (kitpages.KITS), the creation screen's classes in order, three each
kit_names    db 'ELEMENTALIST', 0, 'HEALER', 0, 'CRUSADER', 0
             db 'GROVE WARDEN', 0, 'LIFEBINDER', 0, 'WANDERER', 0
             db 'MYRMIDON', 0, 'SENTINEL', 0, 'RAVAGER', 0
             db 'ARENA CHAMPION', 0, 'TWIN-BLADE', 0, 'BRUTE', 0
             db 'SCHOLAR', 0, 'BATTLE MAGE', 0, 'ARCANIST', 0
             db 'MIND BENDER', 0, 'MIND WARRIOR', 0, 'KINETICIST', 0
             db 'STALKER', 0, 'JUSTIFIER', 0, 'SEEKER', 0
             db 'SWASHBUCKLER', 0, 'ASSASSIN', 0, 'SHINOBI', 0
kit_class_of db 1, 1, 1, 1, 2, 2, 2, 2, 3, 4, 5, 6, 7, 7, 7, 7, 8   ; (a sheet's class, 1-17: the screen's)
kit_asleep   db 'DORMANT', 0
kit_line     db 'KIT: '
             times 15 db 0
ef_y         dw 0
ef_kind      dw 0
ef_last      db 0
ef_skills    dw .s0, .s1, .s2, .s3
.s0     db 'SPECIALIZED IN', 0
.s1     db 'MASTER OF', 0
.s2     db 'GRAND MASTER OF', 0
.s3     db 'EXPERT IN', 0
ef_kinds     dw .k0, .k1, .k2, .k3, .k4, .k5, .k6, .k7, .k8, .k9, .k10, .k11, .k12, .k13, .k14, .k15
.k0     db '  LONG SWORD', 0
.k1     db '  CLUB', 0
.k2     db '  DAGGER', 0
.k3     db '  SHORT SWORD', 0
.k4     db '  MACE', 0
.k5     db '  AXE', 0
.k6     db '  GREAT AXE', 0
.k7     db '  PICK', 0
.k8     db '  QUARTERSTAFF', 0
.k9     db '  POLEARM', 0
.k10    db '  GYTHKA', 0
.k11    db '  CAHULAKS', 0
.k12    db '  CHATKCHA', 0
.k13    db '  BOW', 0
.k14    db '  SLING', 0
.k15    db '  STAFF SLING', 0

; By class as the creation screen numbers it (0-8): the sheet's number (the first of four, by
; sphere), 1 if a sphere is added (2 for a ranger, whose flag is one), the class's flag
wp_class_map db 0, 0
             dw 0
             db 1, 1
             dw 0x01            ; cleric (air, earth, fire, water: flags 1, 2, 4, 8)
             db 5, 0
             dw 0x10            ; druid
             db 9, 0
             dw 0x20            ; fighter
             db 10, 0
             dw 0x40            ; gladiator
             db 11, 0
             dw 0x80            ; preserver
             db 12, 0
             dw 0x100           ; psionicist
             db 13, 2
             dw 0x200           ; ranger
             db 17, 0
             dw 0x400           ; thief
wp_plain   dw 81, 18, 17, 115, 20, 22, 2, 112, 3, 19, 44, 21, 48, 1, 64, 0   ; (weaponchoice.PLAIN's types, by kind)
wp_ok      dw 0
wp_sheet   times 0x48 db 0

; WP_GLADIATOR: ZF clear if the sheet being made (ES:BX) is a gladiator's (two kinds to choose).
; All registers kept.
wp_gladiator:
        cmp byte [es:bx + 0x21], CR_GLADIATOR
        je .yes
        cmp byte [es:bx + 0x22], CR_GLADIATOR
        je .yes
        cmp byte [es:bx + 0x23], CR_GLADIATOR
        je .yes
        cmp al, al              ; (ZF set)
        ret
.yes:   push ax
        or al, 1                ; (ZF clear)
        pop ax
        ret

; WP_TWO: ZF clear if the sheet being made (ES:BX) chooses two kinds: a gladiator, or a Myrmidon
; (a fighter of one class, its kit the first; kits.py). All registers kept.
wp_two:
        call wp_gladiator
        jnz .ret
        push ax
        call kit_class
        cmp al, CR_FIGHTER
        jne .no
        cmp byte [es:bx + KIT_BYTE], 1
        jne .no
        or al, 1                ; (ZF clear)
        pop ax
        ret
.no:    cmp al, al              ; (ZF set)
        pop ax
.ret:   ret

; WP_CLASSES: AL 1 if the sheet being made has a fighter, gladiator or ranger class, AH 1 if a
; cleric, druid or ranger one (a sphere). Others kept.
wp_classes:
        push bx
        push cx
        push es
        les bx, [WP_CREATION]
        xor ax, ax
        mov cx, 3
.class: mov ch, [es:bx + 0x21]
        cmp ch, CR_FIGHTER
        je .warrior
        cmp ch, CR_GLADIATOR
        je .warrior
        cmp ch, CR_RANGER
        jne .sphere
        mov ax, 0x0101
        jmp .next
.warrior:
        mov al, 1
        jmp .next
.sphere:
        cmp ch, CR_CLERIC
        je .has
        cmp ch, CR_DRUID
        jne .next
.has:   mov ah, 1
.next:  inc bx
        dec cl
        jnz .class
        or al, al               ; (a Battle Mage, with weapon specialization: a warrior here)
        jnz .out
        test word [cs:rules], RULE_SPECIALIZE
        jz .out
        push ax
        call kit_made
        cmp al, KIT_BATTLE_MAGE
        pop ax
        jne .out
        mov al, 1
.out:   pop es
        pop cx
        pop bx
        ret

probe_wp_shown:
        sti
        cmp ax, 8
        je .ret
        call wp_rules
        jz .ret
        push bp
        mov bp, sp
        push bx
        push cx
        push dx
        push es
        mov es, [bp + 4]        ; 118:C36h's far address, from the call just made
        mov bx, [bp + 2]
        add bx, (WP_SHOWN_CALL + 1 - WP_SHOWN_RET) & 0xFFFF
        mov ax, [es:bx]
        mov [cs:wp_far], ax
        mov ax, [es:bx + 2]
        mov [cs:wp_far + 2], ax
        mov bx, wp_mine
.one:   push word 0
        push word [cs:bx]
        call far [cs:wp_far]
        add sp, 4
        cmp ax, 8
        je .out
        add bx, 2
        cmp bx, wp_mine_end
        jb .one
.out:   pop es
        pop dx
        pop cx
        pop bx
        pop bp
.ret:   cmp ax, 8
        retf 2

; PROBE_WP_CLASS: INT replaces "mov cx,1" (3 bytes: INT + NOP; 66406h) at the start of the routine
; that runs after each click on a class (it puts DONE in or out of use): the disciplines' window
; up is opened again if it is the other one for the class now (WEAPON SPEC or VIEW SPHERES); a
; weapon page up is marked again for the class now (a gladiator's two), or, for one with no
; weapon to choose, goes back to the disciplines.
probe_wp_class:
        mov cx, 1
        call wp_rules
        jz .ret
        push eax                ; (the classes changed: their defaults, until a kind is clicked;
        push es                 ;   the routine runs after every click on the screen)
        push bx
        les bx, [WP_CREATION]
        mov eax, [es:bx + 0x21]
        and eax, 0x00FFFFFF
        cmp eax, [cs:wp_classes_seen]
        je .same
        mov [cs:wp_classes_seen], eax
        mov byte [cs:wp_touched], 0
        mov byte [es:bx + KIT_BYTE], 0
.same:  pop bx
        pop es
        pop eax
        push ax
        push bx
        push es
        mov ax, [WP_DISC]       ; the disciplines up: the right window for the class now?
        or ax, [WP_DISC + 2]
        jz .page
        push dx
        call wp_ids
        pop dx
        mov [cs:wp_button], ax
        les bx, [WP_DISC]
        mov ax, [es:bx + 8]
        cmp ax, [cs:wp_button]
        je .out
        sti
        pushad
        push es
        push word 0x7F8         ; (its marks kept, as 640E4h keeps them)
        push word 0x7F6
        push dword [WP_DISC]
        call wp_marked
        add sp, 8
        mov [WP_DISC_MASK], ax
        mov byte [cs:kit_keep], 1
        mov ax, ds              ; and the game opens it again (641B9h), as for the class now
        add ax, WP_STUB
        mov [cs:wp_far + 2], ax
        mov word [cs:wp_far], WP_TO_DISC
        call far [cs:wp_far]
        jmp .done
.page:  mov ax, [WP_SPHERE]     ; a weapon page up: the window's id (+8)
        or ax, [WP_SPHERE + 2]
        jz .out
        les bx, [WP_SPHERE]
        mov ax, [es:bx + 8]
        cmp ax, KIT_SPHERE_ID   ; (the spheres: an Elementalist's marked again, EL_MARKS)
        je .spheres
        cmp ax, GAME_SPHERES_ID
        je .spheres
        sub ax, KIT_WIN_ID
        cmp ax, 8
        jb .kit
        add ax, KIT_WIN_ID - WP_PAGE_ID
        cmp ax, KIT_PAGE4_ID - WP_PAGE_ID
        jne .weapons
        mov al, WP_PAGES - 1    ; (the last page, its button KITS)
.weapons:
        cmp ax, WP_PAGES
        jae .out
        mov [cs:wp_page], al
        sti
        pushad
        push es
        call wp_classes
        or al, al
        jz .back
        call wp_marks
        jmp .done
.back:  mov ax, WP_BACK
        call wp_page_button
        jmp .done
.kit:   sti                     ; a kit page up: marked again if its class is still the one made,
        pushad                  ;   the class's own opened if another is, else back
        push es
        mov cl, al
        inc cl
        call kit_class
        or al, al
        jz .back
        cmp al, cl
        je .kit_marks
        mov byte [cs:wp_from], 2
        call kit_open
        jmp .done
.kit_marks:
        call kit_marks
        jmp .done
.spheres:
        sti
        pushad
        push es
        call el_remark
.done:  pop es
        popad
.out:   pop es
        pop bx
        pop ax
.ret:   iret

probe_wp_disc_click:
        push bp
        mov bp, sp
        push ax
        mov word [cs:wp_ret_file], WP_DISC_RET & 0xFFFF
        mov word [cs:wp_end_file], WP_DISC_END & 0xFFFF
        mov byte [cs:wp_from], 0
        jmp wp_click

probe_wp_sphere_click:
        push bp
        mov bp, sp
        push ax
        mov word [cs:wp_ret_file], WP_SPHERE_RET & 0xFFFF
        mov word [cs:wp_end_file], WP_SPHERE_END & 0xFFFF
        mov byte [cs:wp_from], 1
; (BP: the frame, [BP] the game's BP, [BP+2] the return; AX pushed)
wp_click:
        push bx
        mov bx, [bp]
        mov bx, [ss:bx + 8]     ; the button
        mov [cs:wp_button], bx
        cmp bx, SPHERE_TOGGLE   ; (VIEW PSIONICS: back to the disciplines, the kit kept)
        jne .which
        mov byte [cs:kit_keep], 1
.which: pop bx
        call wp_rules
        jz .game
        call wp_harvest
        mov ax, [cs:wp_button]
        cmp ax, WP_VIEW
        je .view
        cmp ax, KIT_VIEW
        je .kits
        cmp byte [cs:wp_from], 1    ; (a weapon or kit page's button, in the spheres' routine)
        jne .game
        cmp ax, WP_ROW
        jb .sphere
        cmp ax, KIT_VIEW
        ja .game
        sti
        pushad
        push es
        call wp_page_button
        pop es
        popad
        jmp .end
.view:  sti
        pushad
        push es
        xor al, al
        call wp_open
        pop es
        popad
        jmp .end
.kits:  sti                     ; KITS: from the disciplines, the spheres or the last weapon page
        pushad
        push es
        cmp byte [cs:wp_from], 0
        je .kit_open
        les bx, [WP_SPHERE]
        cmp word [es:bx + 8], KIT_SPHERE_ID
        je .kit_open
        mov byte [cs:wp_from], 2
.kit_open:
        call kit_open
        pop es
        popad
.sphere:                        ; a sphere's row: an Elementalist's second (EL_CLICK)
        cmp ax, EL_ROW
        jb .game
        cmp ax, EL_ROW + 3
        ja .game
        sti
        pushad
        push es
        call el_click
        pop es
        popad
        jnc .game
.end:
        mov ax, [cs:wp_end_file]  ; go on at the routine's end
        sub ax, [cs:wp_ret_file]
        add [bp + 2], ax
        cmp byte [cs:kit_reroll_due], 0
        je .game
        sti
        pushad
        push es
        call kit_reroll
        pop es
        popad
.game:  pop ax
        pop bp
        mov bx, [cs:wp_button]
        iret

; WP_HARVEST: the window routines' far addresses from the overlay's calls (WP_CALLS), the
; probe's frame at BP.
wp_harvest:
        push ax
        push bx
        push cx
        push si
        push es
        mov es, [bp + 4]
        mov si, wp_calls
        mov bx, (WP_MARK_SEG + 1) & 0xFFFF
        sub bx, [cs:wp_ret_file]
        add bx, [bp + 2]
        mov ax, [es:bx]
        mov [cs:wp_mark_seg], ax
        mov bx, (WP_SPHERE_SEG + 1) & 0xFFFF
        sub bx, [cs:wp_ret_file]
        add bx, [bp + 2]
        mov ax, [es:bx]
        mov [cs:wp_sphere_seg], ax
        mov bx, (WP_STAT_SEG + 1) & 0xFFFF
        sub bx, [cs:wp_ret_file]
        add bx, [bp + 2]
        mov ax, [es:bx]
        mov [cs:wp_stat_seg], ax
        mov cx, (wp_calls_end - wp_calls) / 6
.one:   mov bx, [cs:si]         ; a call's file offset (low word), less the return's
        sub bx, [cs:wp_ret_file]
        add bx, [bp + 2]
        mov ax, [es:bx + 1]
        mov [cs:si + 2], ax
        mov ax, [es:bx + 3]
        mov [cs:si + 4], ax
        add si, 6
        loop .one
        pop es
        pop si
        pop cx
        pop bx
        pop ax
        ret

; WP_OPEN: page AL of the weapons in the panel, after closing what it shows (WP_FROM: 0 the
; disciplines, 1 the spheres, 2 a weapon or kit page), as 640E4h opens the spheres. DS = the
; game's.
wp_open:
        mov [cs:wp_page], al
        call wp_close_panel
        movzx si, byte [cs:wp_page]
        lea ax, [si + WP_PAGE_ID]
        imul si, si, WP_TITLE_SIZE
        add si, wp_titles
        cmp ax, WP_PAGE_ID + WP_PAGES - 1
        jne .show
        push ax
        call kit_class
        or al, al
        pop ax
        jz .show
        mov ax, KIT_PAGE4_ID    ; (the last, its button KITS)
.show:  call wp_show
        call wp_marks
wp_help_line:
        push dword 0x11771
        call far [cs:wp_help]
        add sp, 4
        ret

; KIT_OPEN: the kit page of the class being made in the panel, after closing what it shows
; (WP_FROM as for WP_OPEN). DS = the game's.
kit_open:
        call wp_close_panel
        call kit_class
        movzx ax, al
        add ax, KIT_WIN_ID - 1
        mov si, kit_title
        call wp_show
        call kit_marks
        jmp wp_help_line

; WP_CLOSE_PANEL: what the panel shows (WP_FROM) closed, the disciplines' or spheres' marks kept
; as the game keeps them
wp_close_panel:
        cmp byte [cs:wp_from], 0
        jne .sphere
        push word 0x7F8
        push word 0x7F6
        push dword [WP_DISC]
        call wp_marked
        add sp, 8
        mov [WP_DISC_MASK], ax
        push dword [WP_DISC]
        call far [cs:wp_close]
        add sp, 4
        mov dword [WP_DISC], 0
        ret
.sphere:
        cmp byte [cs:wp_from], 1
        jne .page
        push word 0x7FD
        push word 0x7FA
        push dword [WP_SPHERE]
        call wp_marked
        add sp, 8
        mov [WP_SPHERE_MASK], ax
.page:  mov ax, [WP_SPHERE]
        or ax, [WP_SPHERE + 2]
        jz .ret
        push dword [WP_SPHERE]
        call far [cs:wp_close]
        add sp, 4
        mov dword [WP_SPHERE], 0
.ret:   ret

; WP_SHOW: window AX in the panel, kept at DS:EA6h, the spheres' own routine (538h:57h) answering
; its buttons (PROBE_WP_SPHERE_CLICK), and its title CS:SI, as the spheres' ("%C%C%C%s" at
; DS:E11h)
wp_show:
        push word [cs:wp_sphere_seg]
        push word WP_SPHERE_ENTRY
        push word WP_Y
        push word WP_X
        push ax
        call far [cs:wp_open_fn]
        add sp, 10
        mov [WP_SPHERE], ax
        mov [WP_SPHERE + 2], dx
        push cs
        push si
        push dword 0x3C0014
        push dword 0xFE00FE
        push dword 0xFF0000
        push ds
        push word 0xE11
        push dword 0x6000E
        push dx
        push ax
        call far [cs:wp_print]
        add sp, 0x1C
        push dword 0x3400FE
        call far [cs:wp_colour]
        add sp, 4
        push word [0x3270]
        push word 0x14
        call far [cs:wp_colour]
        add sp, 4
        ret

; WP_MARKED: the game's 63EFAh through its stub (the window, the first and last row on the stack
; as for it): AX the rows marked.
wp_marked:
        push bp
        mov bp, sp
        push word [bp + 10]
        push word [bp + 8]
        push dword [bp + 4]
        mov ax, ds
        add ax, WP_STUB
        mov [cs:wp_far + 2], ax
        mov word [cs:wp_far], WP_MARKED
        call far [cs:wp_far]
        add sp, 8
        pop bp
        ret

; WP_MARKS: the page's rows, the kinds marked (the creation sheet's: a gladiator's two, anyone
; else's one; none yet: the long sword, and a gladiator's club, put in) chosen and the rest not,
; as 63FEEh marks the spheres.
wp_marks:
        push es
        push bx
        call wp_allowed
        mov [cs:wp_ok], ax
        les bx, [WP_CREATION]
        movzx cx, byte [es:bx + SPEC_SLOTS]     ; none yet, or one its classes don't allow: the
        jcxz .none                              ; long sword, or else the first allowed (unless
        dec cx                                  ; the player has taken it back: WP_TOUCHED)
        bt ax, cx
        jc .first
        mov word [es:bx + SPEC_SLOTS], 0
.none:  cmp byte [cs:wp_touched], 0
        jne .first
        xor cx, cx
        bt ax, 0
        jc .put
        bsf cx, ax
        jnz .put
        mov cx, -1                              ; (none at all)
.put:   inc cx
        mov [es:bx + SPEC_SLOTS], cl
.first: mov word [es:bx + SPEC_SLOTS + 2], 0
        call wp_two
        jz .one
        call wp_gladiator                       ; (a Myrmidon's second: none put in)
        jz .marked
        cmp byte [es:bx + SPEC_SLOTS + 1], 0
        jne .marked
        cmp byte [cs:wp_touched], 0
        jne .marked
        mov al, 2                               ; (the club; or the long sword, if the club is first)
        cmp byte [es:bx + SPEC_SLOTS], al
        jne .second
        dec al
.second:
        mov [es:bx + SPEC_SLOTS + 1], al
        jmp .marked
.one:   mov byte [es:bx + SPEC_SLOTS + 1], 0
.marked:
        mov ax, [es:bx + SPEC_SLOTS]
        mov [cs:wp_kind], ax
        mov byte [cs:wp_full], 0                ; all its picks made (a gladiator's two, another's
        or al, al                               ;   one): the other kinds out of use until one is
        jz .count                               ;   taken back, as the game's classes and disciplines
        call wp_two
        jz .full
        or ah, ah
        jz .count
.full:  mov byte [cs:wp_full], 1
.count:
        movzx bx, byte [cs:wp_page]
        shl bx, 2               ; (four to a page)
        mov cx, 4
.row:   inc bx                  ; (the kind + 1)
        lea ax, [bx + WP_ROW - 1]
        lea dx, [bx - 1]
        bt [cs:wp_ok], dx
        jc .open
        push 3                  ; a kind its other class doesn't allow: out of use
        call wp_button_op
        push 1
        call wp_button_op
        push 0
        jmp .next
.open:  cmp bl, [cs:wp_kind]
        je .chosen
        cmp bl, [cs:wp_kind + 1]
        je .chosen
        push 3                  ; one not chosen: in use, or greyed once all are picked
        call wp_button_op
        movzx dx, byte [cs:wp_full]
        push dx
        call wp_button_op
        push 0
        jmp .next
.chosen:
        push 0
        call wp_button_op
        push 2
        call wp_button_op
        push 1
.next:  call wp_mark
        loop .row
        pop bx
        pop es
        ret

; WP_BUTTON_OP: the game's 140:71Ah on button AX of the page (DS:EA6h), with the operation pushed
; (0 in use, 1 out of use, 2 marked, 3 not marked); takes it off the stack. AX, BX, CX kept.
wp_button_op:
        push bp
        mov bp, sp
        pusha
        push word [bp + 4]
        push word 0
        push ax
        push dword [WP_SPHERE]
        call far [cs:wp_button_fn]
        add sp, 10
        popa
        pop bp
        ret 2

; WP_MARK: the mark (pushed: 1) or none (0) at the page's row 4 - CX, as 63FEEh draws it; takes
; the flag off the stack. AX, BX, CX kept.
wp_mark:
        push bp
        mov bp, sp
        pusha
        push es
        push word [bp + 4]
        push dword [WP_MARK_WIN]
        push word 0
        mov bx, 4
        sub bx, cx
        shl bx, 1
        mov es, [cs:wp_mark_seg]
        mov ax, [es:bx + 0x1AB]
        inc ax
        push ax
        push dword 0xDA0001
        call far [cs:wp_mark_fn]
        add sp, 0x0E
        pop es
        popa
        pop bp
        ret 2

; WP_PAGE_BUTTON: button AX of a weapon page, clicked: a kind's row marked (on the creation sheet),
; MORE SPECS the next page, VIEW PSIONICS back to the disciplines (641B9h). DS = the game's.
wp_page_button:
        cmp ax, KIT_ROW
        jae kit_row
        cmp ax, WP_MORE
        je .more
        cmp ax, WP_BACK
        je .back
        sub ax, WP_ROW
        cmp ax, 16
        jae .ret
        bt [cs:wp_ok], ax       ; (one its classes don't allow: nothing)
        jnc .ret
        inc ax
        mov byte [cs:wp_touched], 1
        les bx, [WP_CREATION]
        cmp al, [es:bx + SPEC_SLOTS]            ; a kind chosen: taken back (a gladiator's second
        je .first_off                           ;   moving up), as the game's classes are
        cmp al, [es:bx + SPEC_SLOTS + 1]
        je .second_off
        cmp byte [es:bx + SPEC_SLOTS], 0        ; another: chosen, where a pick is free
        je .put_first
        call wp_two
        jz .ret
        cmp byte [es:bx + SPEC_SLOTS + 1], 0
        jne .ret
        mov [es:bx + SPEC_SLOTS + 1], al
        jmp wp_marks
.put_first:
        mov [es:bx + SPEC_SLOTS], al
        jmp wp_marks
.first_off:
        mov al, [es:bx + SPEC_SLOTS + 1]
        mov [es:bx + SPEC_SLOTS], al
.second_off:
        mov byte [es:bx + SPEC_SLOTS + 1], 0
        jmp wp_marks
.more:  mov byte [cs:wp_from], 2
        mov al, [cs:wp_page]
        inc al
        cmp al, WP_PAGES
        jb .open
        xor al, al
.open:  jmp wp_open
.back:  push dword [WP_SPHERE]
        call far [cs:wp_close]
        add sp, 4
        mov dword [WP_SPHERE], 0
        mov byte [cs:kit_keep], 1
        mov ax, ds
        add ax, WP_STUB
        mov [cs:wp_far + 2], ax
        mov word [cs:wp_far], WP_TO_DISC
        call far [cs:wp_far]
.ret:   ret

; KIT_ROW: kit row AX of the kit page clicked (KIT_NONE: NO KIT), as the game's spheres: with
; none chosen, the row's kit chosen; the one chosen, taken back (KIT_OPEN: the rows all in use,
; the sheet with no kit); another, nothing. DS = the game's.
kit_row:
        les bx, [WP_CREATION]
        xor dl, dl
        cmp ax, KIT_NONE
        je .row
        sub ax, KIT_ROW
        mov cl, 3
        div cl
        mov dl, ah
        inc dl
.row:   mov dh, [es:bx + KIT_BYTE]
        cmp dh, KIT_OPEN        ; none chosen: this one
        je .put
        cmp dl, dh              ; another, while one is chosen: out of use (nothing)
        jne kit_marks
        mov dl, KIT_OPEN        ; the one chosen: taken back, the rest in use again
.put:   call kit_made           ; (a Battle Mage or Mind Warrior before or after: rolls again)
        call kit_rolls
        mov [es:bx + KIT_BYTE], dl
        call kit_made
        call kit_rolls
        call wp_two             ; (no Myrmidon now: its second kind gone)
        jnz kit_marks
        mov byte [es:bx + SPEC_SLOTS + 1], 0
; KIT_MARKS: the kit page's rows (NO KIT, then the class's three kits) marked as the creation
; sheet's kit, the rest not, as WP_MARKS marks a weapon page's
kit_marks:
        push es
        push bx
        call kit_class
        or al, al
        jz .out
        les bx, [WP_CREATION]
        mov dl, [es:bx + KIT_BYTE]
        movzx bx, al
        imul bx, bx, 3
        add bx, KIT_ROW - 4     ; (NO KIT first, then the class's three)
        xor dh, dh              ; (the row's kit)
        mov cx, 4
.row:   mov ax, bx
        cmp cx, 4
        jne .kit
        mov ax, KIT_NONE
.kit:   cmp dh, dl
        je .chosen
        push 3                  ; not chosen: greyed while another is, as the game's spheres
        call wp_button_op
        cmp dl, KIT_OPEN
        je .open
        push 1
        jmp .op
.open:  push 0
.op:    call wp_button_op
        push 0
        jmp .next
.chosen:
        push 0
        call wp_button_op
        push 2
        call wp_button_op
        push 1
.next:  call wp_mark
        inc bx
        inc dh
        loop .row
.out:   pop bx
        pop es
        ret

; KIT_ROLLS: kit AL one with its own hit die or PSP (a Battle Mage, a Mind Warrior, an Arcanist):
; KIT_REROLL due.
kit_rolls:
        cmp al, KIT_BATTLE_MAGE
        je .due
        cmp al, KIT_MIND_WARRIOR
        je .due
        cmp al, KIT_ARCANIST
        jne .ret
.due:   mov byte [cs:kit_reroll_due], 1
.ret:   ret

; KIT_REROLL: (WP_CLICK, at its end, BP the probe's frame, the way back to the game's in it as the
; overlay manager needs it) the sheet being made's hit points rolled again and its PSP worked out
; again, by the game's own routines for a click on a class (655D6h, flag 1, and 65B39h through the
; creation overlay's stub, as 63E44h and 639C0h call them), with its kit now: the hit points kept
; within the range 655D6h gives, as 63E5Fh keeps them, and both put on the creature and shown
; again (the game's 64C6Bh and 64CEDh, as 63E9Ch and 639D5h). DS the game's.
CR_STUB      equ 0x422E - 0x4356    ; the creation rolls' overlay's stub, less DS: its entries
CR_HP        equ 0x66               ; (655D6h: the hit points, rolled with a flag)
CR_PSP       equ 0x6B               ; (65B39h: the most PSP)
CR_HP_MIN    equ 0x4998             ; DS: the least and most hit points 655D6h works out
CR_HP_MAX    equ 0x4996
CR_CREATURE  equ 0x11A0             ; DS: the creature being made (far)
CR_PORTRAIT  equ 0x11A4             ; DS: (far; 64C6Bh's first)
CR_HP_SHOW   equ 0x3E               ; (64C6Bh, through WP_STUB: a number shown)
CR_PSP_SHOW  equ 0x43               ; (64CEDh)
kit_reroll:
        mov byte [cs:kit_reroll_due], 0
        push word 1
        push word CR_HP_MAX
        push word CR_HP_MIN
        les bx, [WP_CREATION]
        lea ax, [bx + 8]
        push es
        push ax
        mov ax, CR_HP
        call cr_far
        call far [cs:wp_far]
        add sp, 10
        les bx, [WP_CREATION]
        mov ax, [es:bx + 8]
        cmp ax, [CR_HP_MAX]
        jle .not_over
        mov ax, [CR_HP_MAX]
.not_over:
        cmp ax, [CR_HP_MIN]
        jge .not_under
        mov ax, [CR_HP_MIN]
.not_under:
        mov [es:bx + 8], ax
        les bx, [CR_CREATURE]
        mov [es:bx], ax
        mov si, 0x3F0           ; (the backdrop's words, 340h:3F0h and 404h)
        mov di, 0xF92           ; (the number's place, DS: x then y)
        mov ax, CR_HP_SHOW
        call cr_show
        mov ax, CR_PSP
        call cr_far
        call far [cs:wp_far]
        les bx, [WP_CREATION]
        mov ax, [es:bx + 0x0C]
        les bx, [CR_CREATURE]
        mov [es:bx + 2], ax
        mov si, 0x3F2
        mov di, 0xF9A
        mov ax, CR_PSP_SHOW
        jmp cr_show

; CR_FAR: WP_FAR the creation rolls' overlay's entry AX. DS the game's.
cr_far:
        mov [cs:wp_far], ax
        mov ax, ds
        add ax, CR_STUB
        mov [cs:wp_far + 2], ax
        ret

; CR_SHOW: a number of the creation screen shown again, as the game does: its backdrop put back
; (WP_BACKDROP, with the words at 340h:SI and SI+14h), then the creation overlay's entry AX with the
; number's place (DS:DI, x then y).
cr_show:
        push ax
        mov es, [cs:wp_stat_seg]
        push word [es:si]
        push word [es:si + 0x14]
        call far [cs:wp_backdrop]
        add sp, 4
        pop ax
        mov [cs:wp_far], ax
        mov ax, [di + 2]
        sub ax, 2
        push ax
        mov ax, [di]
        add ax, 0x15
        push ax
        push dword [CR_CREATURE]
        push dword [WP_CREATION]
        push dword [CR_PORTRAIT]
        mov ax, ds
        add ax, WP_STUB
        mov [cs:wp_far + 2], ax
        call far [cs:wp_far]
        add sp, 16
        ret

kit_reroll_due db 0

; The Elementalist (kits.py): a cleric with a second sphere, its spells and its weapons. The sheet's
; SPHERE2 (+45h): 0 none, 1-4 the sphere (air, earth, fire, water) + 1. On the creation panel's
; spheres (the game's, mask-driven: WP_SPHERE_MASK its own sphere's row, 80h air to 10h water), an
; Elementalist with its own sphere chosen clicks another for its second (EL_CLICK), the rows kept in
; use and both marked (EL_MARKS, after every click: PROBE_WP_CLASS); its own taken back, the
; second goes too.
SPHERE2    equ 0x45
EL_ROW     equ 0x7FA                ; (the spheres' rows: AIR, EARTH, FIRE, WATER)
GAME_SPHERES_ID equ 3013
; EL_SECOND: AL the second sphere (0-3) of the Elementalist whose sheet is at ES:BX, ZF clear; ZF
; set if none (not one, or none chosen). Others kept.
el_second:
        push cx
        mov cl, [es:bx + SPHERE2]
        call kit_id
        cmp al, KIT_ELEMENTALIST
        jne .none
        mov al, cl
        dec al
        cmp al, 3
        ja .none
        or cl, 1                ; (ZF clear)
        pop cx
        ret
.none:  cmp al, al
        pop cx
        ret

; EL_CLICK: sphere row AX clicked on the creation panel (DS the game's): carry set if it was an
; Elementalist's second (taken, or taken back) and the game is to do nothing; its own taken back
; (the game's), the second cleared too.
el_click:
        mov si, ax
        sub si, EL_ROW
        call kit_made
        cmp al, KIT_ELEMENTALIST
        jne .game
        les bx, [WP_CREATION]
        mov ax, [WP_SPHERE_MASK]
        mov cx, si
        mov dx, 0x80
        shr dx, cl
        or ax, ax
        jz .own                 ; (none chosen: the game's, its own)
        test ax, dx
        jnz .own                ; (its own: taken back)
        inc si
        mov ax, si
        cmp [es:bx + SPHERE2], al
        jne .put
        xor al, al              ; (the second again: taken back)
.put:   mov [es:bx + SPHERE2], al
        call el_marks
        stc
        ret
.own:   mov byte [es:bx + SPHERE2], 0
.game:  clc
        ret

; EL_REMARK: for an Elementalist with its own sphere chosen, the spheres' rows as EL_MARKS has
; them. DS the game's.
el_remark:
        cmp word [cs:wp_button_fn + 2], 0   ; (the window routines not yet read from the overlay)
        je .ret
        call kit_made
        cmp al, KIT_ELEMENTALIST
        jne .ret
        cmp word [WP_SPHERE_MASK], 0
        jne el_marks
.ret:   ret

; EL_MARKS: the spheres' rows (the page at DS:EA6h) all in use, its own sphere's and its second's
; marked, the others not.
el_marks:
        les bx, [WP_CREATION]
        mov dx, [WP_SPHERE_MASK]
        mov cl, [es:bx + SPHERE2]
        or cl, cl
        jz .draw
        dec cl
        mov ax, 0x80
        shr ax, cl
        or dx, ax
.draw:  mov bx, EL_ROW
        mov si, 0x80
        mov cx, 4
.row:   mov ax, bx
        push 0
        call wp_button_op
        test dx, si
        jz .open
        push 2
        call wp_button_op
        push 1
        jmp .mark
.open:  push 3
        call wp_button_op
        push 0
.mark:  call wp_mark
        inc bx
        shr si, 1
        loop .row
        ret

; The Elementalist's spells. The game gives each class a mask (the load segment + 3800h, +118h, a
; dword a class: 1 wizard, 2 priest, then a bit a class, 4 the air cleric to 20h the water cleric)
; and each spell one (+3FB9h:19Dh, 7 bytes a spell): a class casts the spells whose masks meet its.
; PROBE_EL_GRANT: INT VEC_EL_GRANT replaces "xor si,si" (2 bytes; DSUN.EXE 5E489h) in the routine
; that gives a priest the spells of its spheres (5E401h; [BP+6] the character, [BP-6] the mask of
; its priest classes, then every spell meeting it that it may cast learnt): an Elementalist's
; second sphere's cleric bit added. PROBE_EL_CAST: INT VEC_EL_CAST replaces "mov edx,es:[si+19Dh]"
; (6 bytes; 81B42h) in the caster level routine (81B16h, [BP+6] the caster), and PROBE_EL_LEVEL
; "mov ebx,es:[bx+19Dh]" (6 bytes; 5E375h) in the effect level routine (5E25Ch, [BP+6] too), where
; the spell's mask is read: for an Elementalist, a spell of its second sphere counts as its own
; sphere's (its cleric bit added; kits.spell_spheres).
probe_el_grant:
        xor si, si
        push ax
        push bx
        push cx
        push es
        call el_caster
        jz .out
        mov bx, 4
        xchg cl, al             ; (CL the second sphere)
        shl bx, cl
        xchg cl, al
        or [bp - 6], bx
.out:   pop es
        pop cx
        pop bx
        pop ax
        iret

probe_el_cast:
        mov edx, [es:si + 0x19D]
        push eax
        mov eax, edx
        call el_spheres
        mov edx, eax
        pop eax
        iret

probe_el_level:
        mov ebx, [es:bx + 0x19D]
        push eax
        mov eax, ebx
        call el_spheres
        mov ebx, eax
        pop eax
        iret

; PROBE_EL_KNOW: INT VEC_EL_KNOW replaces "test dword es:[bx+19Dh],eax" (6 bytes; DSUN.EXE 66FC9h,
; on making a character, and 86E56h, when a human changes class) in the loops that mark each priest spell
; (45h-89h) known or not for party member SI by its mask (ES:BX the spell's place in the table)
; meeting the class's bit (EAX): for an Elementalist, its second sphere's cleric bit too
; (as kits.spell_spheres). The flags of the test go back to the JZ after (RETF 2: the interrupt's own
; dropped), interrupts on again.
probe_el_know:
        sti
        push eax
        push ecx
        push bx
        push es
        mov ecx, eax            ; (ECX the class's bit)
        imul ax, si, 0x3A
        les bx, [CREATURES]
        add bx, ax
        mov ax, [es:bx + 4]
        les bx, [0x1661]
        imul ax, ax, 0x47
        add bx, ax
        call el_second
        jz .test
        movzx eax, al           ; (the second sphere's cleric bit: 4 the air cleric's)
        add al, 2
        bts ecx, eax
.test:  pop es
        pop bx
        test [es:bx + 0x19D], ecx
        pop ecx
        pop eax
        retf 2

; PROBE_DUAL_BAN: INT VEC_DUAL_BAN replaces "or ax,ax" (2 bytes; DSUN.EXE 866FFh) after the test
; whether a human may change to a class (86E94h: AX 1 if so), made for each class (SI, 1-17) as
; the DUAL window (866C7h, for member [BP+6]) greys those it can't: AX 0 for a class its kit bars
; (kits.dual_banned): a Seeker or Justifier a cleric or druid (1-8), a Shinobi a preserver, whose
; slot tables would take the new class's place. The test's flags back to the JZ (RETF 2).
probe_dual_ban:
        sti
        or ax, ax
        jz .out
        push bx
        push es
        les bx, [0x1661]
        imul ax, [bp + 6], 0x47
        add bx, ax
        call kit_any
        mov ah, al
        mov al, 1
        cmp ah, KIT_SHINOBI
        jne .ranger
        cmp si, PRESERVER_CLASS
        jne .back
        jmp .barred
.ranger:
        cmp ah, KIT_SEEKER
        je .priest
        cmp ah, KIT_JUSTIFIER
        jne .back
.priest:
        cmp si, 8
        ja .back
.barred:
        xor al, al
.back:  xor ah, ah
        pop es
        pop bx
.out:   or ax, ax
        retf 2

; EL_SPHERES: EAX a spell's mask, for caster [BP+6]: an Elementalist's second sphere's spells its
; own sphere's too. Others kept.
el_spheres:
        push bx
        push cx
        push edx
        push es
        mov edx, eax
        call el_caster          ; AL the second sphere, CL the cleric class (1-4)
        jz .out
        push cx
        mov cl, al
        mov eax, 4
        shl eax, cl
        pop cx
        test edx, eax
        jz .out
        mov eax, 2
        shl eax, cl
        or edx, eax
.out:   mov eax, edx
        pop es
        pop edx
        pop cx
        pop bx
        ret

; EL_CASTER: for combatant [BP+6]: AL its second sphere (EL_SECOND), CL its class, ZF clear if an
; Elementalist with one; ZF set if not. ES, BX changed.
el_caster:
        push ax
        call combatant_kit
        cmp al, KIT_ELEMENTALIST
        pop ax
        jne .no
        mov ax, bx
        les bx, [CREATURES]
        imul ax, ax, 0x3A
        add bx, ax
        mov ax, [es:bx + 4]
        les bx, [0x1661]
        imul ax, ax, 0x47
        add bx, ax
        push ax
        call kit_class_of_sheet
        mov cl, al
        pop ax
        jmp el_second
.no:    cmp al, al
        ret
kit_title  db 'KITS', 0
kit_keep   db 0                 ; 1 while the panel goes back to the disciplines (the kit kept)
WP_TITLE_SIZE equ 16
wp_titles  db 'WEAPONS 1 OF 4', 0, 0
           db 'WEAPONS 2 OF 4', 0, 0
           db 'WEAPONS 3 OF 4', 0, 0
           db 'WEAPONS 4 OF 4', 0, 0
wp_page    db 0
wp_from    db 0
wp_kind    db 0, 0
wp_full    db 0
wp_touched db 0                 ; 1 once the player has clicked a kind (no defaults put back after)
wp_classes_seen dd 0xFFFFFFFF   ; the classes being made, when WP_TOUCHED was last cleared
wp_button  dw 0
wp_ret_file dw 0
wp_end_file dw 0
wp_far     dd 0
wp_mine    dw WP_WSPHERE_ID, WP_PAGE_ID, WP_PAGE_ID + 1, WP_PAGE_ID + 2, WP_PAGE_ID + 3
           dw KIT_SPHERE_ID, KIT_PAGE4_ID, KIT_WIN_ID, KIT_WIN_ID + 1, KIT_WIN_ID + 2
           dw KIT_WIN_ID + 3, KIT_WIN_ID + 4, KIT_WIN_ID + 5, KIT_WIN_ID + 6, KIT_WIN_ID + 7
wp_mine_end:
wp_calls:                   ; each: the call's file offset (low word), then its far address
           dw WP_CALL_CLOSE & 0xFFFF
wp_close   dd 0
           dw WP_CALL_OPEN & 0xFFFF
wp_open_fn dd 0
           dw WP_CALL_PRINT & 0xFFFF
wp_print   dd 0
           dw WP_CALL_COLOUR & 0xFFFF
wp_colour  dd 0
           dw WP_CALL_HELP & 0xFFFF
wp_help    dd 0
           dw WP_CALL_BUTTON & 0xFFFF
wp_button_fn dd 0
           dw WP_CALL_REDRAW & 0xFFFF
wp_redraw  dd 0
           dw WP_CALL_MARK & 0xFFFF
wp_mark_fn dd 0
           dw WP_CALL_BACKDROP & 0xFFFF
wp_backdrop dd 0
wp_calls_end:
wp_stat_seg dw 0
wp_mark_seg dw 0
wp_sphere_seg dw 0

; SPEC_OF: DL the attacker's skill with the attack's weapon (SPEC_OF_SHEET, for the attack
; routine's [BP+10h] sheet and [BP+14h] item type).
spec_of:
        push bx
        push si
        push es
        mov si, [bp + 0x10]
        imul si, si, 0x47
        les bx, [0x1661]
        add bx, si
        mov si, [bp + 0x14]
        call spec_of_sheet
        pop es
        pop si
        pop bx
        ret

; SPEC_OF_SHEET: DL the skill with item type SI of the character whose sheet is at ES:BX:
; SPEC_NONE (it has chosen no kinds), SPEC_PLAIN (not this kind), SPEC_EXPERT (a ranger's: no
; fighter or gladiator class; and every ranger's with the bow, chosen or not), SPEC_SPECIAL,
; SPEC_MASTER (a fighter's own kind, the first, from 5th level; a Myrmidon's second too),
; SPEC_GRAND (9th). A Battle Mage's chosen kind SPEC_EXPERT; a Justifier's expertise (its chosen
; kind and the bow) SPEC_SPECIAL.
BOW_KIND equ 14                 ; (the bow's kind + 1)
spec_of_sheet:
        push ax
        push cx
        push si
        mov dl, SPEC_NONE
        mov al, [es:bx + SPEC_SLOTS]
        or al, [es:bx + SPEC_SLOTS + 1]
        or al, [es:bx + SPEC_SLOTS + 2]
        or al, [es:bx + SPEC_SLOTS + 3]
        jz .ret
        call kit_any            ; (a Battle Mage who has changed class: its weapon spec the kit's
        cmp al, KIT_BATTLE_MAGE ; alone, nothing while it sleeps, expertise when awake)
        jne .kinds
        cmp word [es:bx + 0x22], 0
        je .kinds
        call kit_id
        mov dl, SPEC_NONE
        jz .out
        mov dl, SPEC_PLAIN
        cmp si, KIND_TYPES
        jae .out
        mov al, [cs:si + kind_of_type]
        cmp al, [es:bx + SPEC_SLOTS]
        jne .out
        mov dl, SPEC_EXPERT
        jmp .out
.kinds: mov dl, SPEC_PLAIN
        cmp si, KIND_TYPES
        jae .ret
        mov cl, [cs:si + kind_of_type]
        or cl, cl
        jz .ret
        xor si, si
.slot:  cmp [es:bx + si + SPEC_SLOTS], cl
        je .found
        inc si
        cmp si, SPEC_COUNT
        jb .slot
        cmp cl, BOW_KIND        ; (not chosen: a ranger's bow all the same)
        jne .ret
        xor si, si
.rbow:  mov al, [es:bx + si + 0x21]
        or si, si               ; (a human's earlier classes once the first's level has passed theirs)
        jz .ron
        cmp byte [es:bx + 0x18], 1
        jne .ron
        mov ah, [es:bx + si + 0x24]
        cmp ah, [es:bx + 0x24]
        jae .rnext
.ron:   cmp al, 13
        jb .rnext
        cmp al, 16
        ja .rnext
        mov dl, SPEC_EXPERT
        jmp .ret
.rnext: inc si
        cmp si, 3
        jb .rbow
        jmp .ret
.found: mov dl, SPEC_EXPERT
        xor ax, ax              ; AH a fighter or gladiator, AL any warrior, CH the fighter level
        xor ch, ch
        push si
        xor si, si
.class: mov cl, [es:bx + si + 0x21]
        or si, si               ; a human's earlier classes (dual-classed) count only once the
        jz .on                  ; first's level has passed theirs
        cmp byte [es:bx + 0x18], 1
        jne .on
        push ax
        mov al, [es:bx + si + 0x24]
        cmp al, [es:bx + 0x24]
        pop ax
        jae .next
.on:    cmp cl, FIGHTER_CLASS
        jne .glad
        mov ax, 0x0101
        mov ch, [es:bx + si + 0x24]
.glad:  cmp cl, GLADIATOR_CLASS
        jne .ranger
        mov ax, 0x0101
.ranger:
        cmp cl, 13
        jb .next
        cmp cl, 16
        ja .next
        mov al, 1
.next:  inc si
        cmp si, 3
        jb .class
        pop si
        or al, al
        jnz .warrior
        mov dl, SPEC_PLAIN      ; (a warrior class not back yet)
        call kit_id             ; (a Battle Mage's expertise)
        cmp al, KIT_BATTLE_MAGE
        jne .ret
        mov dl, SPEC_EXPERT
        jmp .ret
.warrior:
        or ah, ah
        jz .ret
        mov dl, SPEC_SPECIAL
        or si, si
        jz .master
        cmp si, 1               ; (a Myrmidon's second kind too)
        jne .ret
        push ax
        call kit_id
        cmp al, KIT_MYRMIDON
        pop ax
        jne .ret
.master:
        cmp ch, MASTERY
        jb .ret
        mov dl, SPEC_MASTER
        cmp ch, GRAND_MASTERY
        jb .ret
        mov dl, SPEC_GRAND
.ret:   cmp dl, SPEC_EXPERT     ; (a Justifier's expertise, the bow's too: specialization)
        jne .out
        call kit_id
        cmp al, KIT_JUSTIFIER
        jne .out
        mov dl, SPEC_SPECIAL
.out:   pop si
        pop cx
        pop ax
        ret

; By item type (the game's 115, then the companion's own: 115 its short sword), the weapon kind
; + 1 (dscompanion/specialize.py's KIND_OF_TYPE; 0 none)
KIND_TYPES equ 140
kind_of_type  db 16, 14, 7, 9, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 3, 2, 10, 5, 12, 6, 0, 0, 0, 0, 0, 0, 0, 0, 0
              db 0, 3, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 11, 1, 5, 1, 13, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1
              db 15, 0, 0, 0, 0, 14, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 9, 1, 0, 0, 3, 1, 0, 0, 0, 0, 0, 0, 0, 0, 3, 0
              db 0, 1, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 10, 8, 0, 0, 4, 0, 0, 4, 6, 4, 6, 4, 0, 3, 5, 7, 8
              db 10, 0, 0, 0, 0, 0, 0, 0, 3   ; (136: the air clerics' metal dagger, Galefang's)
              db 7, 7, 3                      ; (137, 138: the bone and obsidian great axes; 139 the bone dagger)

; PROBE_XP_NEXT: INT VEC_XP_NEXT replaces "push 10F4h" (3 bytes: INT + NOP; DSUN.EXE 67DBEh) in
; View Character's line "EXP:10301 (16000)", where the game adds ")" (DS:10F4h) after the XP the
; next level needs: the least of its classes' ([BP-6], a dword, never 0 here), each class's from
; its table (a word, x100, at the class x 40 + its level x 2 + 27Ch, in the segment the game's
; "mov ax,seg" at 67D35h holds) unless it is at the level cap. The routine's character ([BP+0Ah])
; is the screen's copy of the sheet, its classes numbered 1-8 as at character creation (cleric,
; druid, fighter, gladiator, preserver, psionicist, ranger, thief), not the sheet's 1-17; its +18h
; is the race. For a character of more than one class (a second one in the second slot; not a
; human, who dual-classes and whose first class alone the game counts here), it pushes DSCLOG's
; XP_SUFFIX instead (the game's DS, pushed before, becomes CS): the class whose next level that
; is, then ")": " F)", " Pr/T)" if two are due at once. Preserver and psionicist are "Pr" and
; "Ps", the rest a letter (CLASS_LETTERS).
XP_SEG_BACK equ 0x8A            ; the "mov ax,seg"'s immediate, back from the INT's return
XP_CLOSE equ 0x10F4             ; DS: the game's ")"
probe_xp_next:
        pop word [cs:x_ip]
        pop word [cs:x_cs]
        pop word [cs:x_fl]
        push ax
        push bx
        push cx
        push dx
        push si
        push di
        push es
        push gs
        mov byte [cs:xp_ours], 0
        les bx, [bp + 0x0A]     ; the screen's copy of the sheet
        cmp byte [es:bx + 0x18], 1
        je .out                 ; (a human: the game's ")")
        cmp byte [es:bx + 0x22], 0
        je .out                 ; (one class: the game's ")")
        mov si, [cs:x_ip]
        mov gs, [cs:x_cs]
        mov ax, [gs:si - XP_SEG_BACK]
        mov gs, ax
        mov di, xp_suffix + 1
        mov cl, 9               ; the level cap (as PROBE_LEVEL)
        test byte [cs:rules], RULE_LEVEL_10
        jz .slots
        inc cl
.slots: xor dx, dx
.slot:  push bx
        add bx, dx
        movzx si, byte [es:bx + 0x21]   ; the class
        mov al, [es:bx + 0x24]          ; its level
        pop bx
        or si, si
        jz .next
        cmp si, 8
        ja .next
        cmp al, cl
        je .next
        mov ch, al
        imul ax, si, 40
        push si
        movzx si, ch
        add si, si
        add si, ax
        movzx eax, word [gs:si + 0x27C]
        pop si
        imul eax, eax, 100
        cmp eax, [bp - 6]
        jne .next
        cmp di, xp_suffix + 1
        je .letters
        mov byte [cs:di], '/'
        inc di
.letters:
        add si, si
        mov ax, [cs:si + class_letters]
        mov [cs:di], al
        inc di
        or ah, ah
        jz .one
        mov [cs:di], ah
        inc di
.one:   mov byte [cs:xp_ours], 1
.next:  inc dx
        cmp dx, 3
        jb .slot
        mov word [cs:di], ')'   ; (and its NUL)
.out:   pop gs
        pop es
        pop di
        pop si
        pop dx
        pop cx
        pop bx
        pop ax
        cmp byte [cs:xp_ours], 0
        je .game
        add sp, 2               ; the game's DS
        push cs
        push xp_suffix
        jmp .back
.game:  push XP_CLOSE
.back:  push word [cs:x_fl]
        push word [cs:x_cs]
        push word [cs:x_ip]
        iret
x_ip    dw 0
x_cs    dw 0
x_fl    dw 0
xp_ours db 0
xp_suffix db ' ', 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0
; by class, 1-8 (CREATION_CLASS_NAMES in dscompanion/game.py)
class_letters db 0, 0, 'C', 0, 'D', 0, 'F', 0, 'G', 0, 'P', 'r', 'P', 's', 'R', 0, 'T', 0

; PROBE_SCRIPT_RAND: INT VEC_SCRIPT_RAND replaces "inc eax / mov edx,eax" (5 bytes: INT + 3 NOPs;
; DSUN.EXE B300h) in the scripts' random command (0 to N, each as likely: rand() * (N + 1) /
; 32768), EAX its N, the rand() on the stack under the INT's return. Does the two instructions
; and records the command (kind KIND_SCRIPT): AL the result, AH N (its low byte), EXTRA where
; the script is (the running script's position, past the command's number: what dscompanion's
; searches.py knows the junk, haystack and wardrobe searches by), and the frame's BP+2, +4 and
; +6 the searches' counts (the scripts' variables 135,21 to 23: junk and hay finds, wardrobe
; searches; words in the table DS:1356h points to).
KIND_SCRIPT equ 5
SCRIPT_DATA equ 0x3781 - 0x4356 ; (DS-relative) the interpreter's: +192h the running script's slot,
SCRIPT_SLOT equ 0x192           ;   +295h each slot's position (words)
SCRIPT_POS equ 0x295
SCRIPT_COUNTS equ 0x1356        ; DS: far pointer to the scripts' variables 135 (words)
SEARCH_FIRST equ 21             ; (junk, hay, wardrobe: 21 to 23)
probe_script_rand:
        sti
        inc eax
        mov edx, eax
        push bp
        mov bp, sp              ; BP+2 the INT's return, BP+8 the rand() pushed
        pushad
        push es
        movsx ebx, word [bp + 8]
        imul ebx, eax
        sar ebx, 15             ; (the game's: / 32768, the product never negative)
        mov ax, dx
        dec ax
        mov ah, al              ; N
        mov al, bl              ; the result
        mov bx, ds
        add bx, SCRIPT_DATA
        mov es, bx
        movsx bx, byte [es:SCRIPT_SLOT]
        add bx, bx
        mov cx, [es:bx + SCRIPT_POS]
        mov [cs:extra], cx
        les bx, [SCRIPT_COUNTS]
        push word [es:bx + (SEARCH_FIRST + 2) * 2]
        push word [es:bx + (SEARCH_FIRST + 1) * 2]
        push word [es:bx + SEARCH_FIRST * 2]
        push bp
        mov bp, sp              ; BP+2, +4, +6: the counts
        mov word [cs:kind], KIND_SCRIPT
        lea si, [bp + 38]       ; (SS:SI+6: the INT's return address, past ES, PUSHAD's and BP)
        call record
        pop bp
        add sp, 6
        pop es
        popad
        pop bp
        iret

; ITEM SAVES. The game's acid and corroding touch (its special attacks 178, 186 and 187, the
; Rampager's and the Babau's hits, after a failed save) can destroy an item (DSUN.EXE 7AA5Ah,
; the first melee weapon held; 7ABC4h, the first worn arm, leg or chest piece that fails):
; a weapon on a d20 under 8 less its plus; armour with no magical power (the item's +0Fh, as
; the game's weapon breaking reads it) at once, without a roll, else on a d20 under 10 less
; that byte. With RULE_ITEM_SAVES the item needs the lower (the easier) of the game's number
; and AD&D's save against acid for its material (the DMG's table: wood 8, bone 11, metal 13,
; leather 10, cloth 12; glass's 5 for stone and obsidian), less its plus, and 1 less again
; for a magical power. Each check is recorded (kind KIND_ITEM: the d20 in the low byte, the
; number needed in the high, 0 for none rolled; EXTRA the item, 8000h for armour).
KIND_ITEM equ 4
ITEM_ARMOUR equ 0x8000

; PROBE_ITEM_WEAPON: INT VEC_ITEM_WEAPON replaces "mov dx,8 / sub dx,[bp-2]" (6 bytes: INT + 4
; NOPs; DSUN.EXE 7AABCh), AX the d20, [BP-2] the weapon's plus, SI its entry in the list at
; BP-324h (the item at BP-320h + SI*0Ah). DX: the number needed (a roll under it corrodes).
probe_item_weapon:
        push si
        push ax
        push bx
        push cx
        push di
        mov dx, 8
        sub dx, [bp-2]
        mov di, si
        imul di, di, 0x0A
        mov bx, [bp+di-0x320]
        test word [cs:rules], RULE_ITEM_SAVES
        jz .log
        call adnd_needed
        cmp cx, dx
        jge .log
        mov dx, cx
.log:   xor cx, cx                      ; (a weapon)
        jmp item_record

; PROBE_ITEM_SKIP: INT VEC_ITEM_SKIP replaces "cmp word [bp-2],0" (4 bytes: INT + 2 NOPs;
; 7AC45h) before the game's "jz <destroyed>": [BP-2] the armour's magical power, SI its entry
; in the list at BP-322h (the item at BP-31Eh + SI*0Ah). ZF as the game's (destroyed without a
; roll, recorded so), or with RULE_ITEM_SAVES clear: it rolls.
probe_item_skip:
        push bp
        mov bp, sp
        and word [bp+6], ~0x40          ; (the flags IRET takes back)
        pop bp
        test word [cs:rules], RULE_ITEM_SAVES
        jnz .roll
        cmp word [bp-2], 0
        jne .roll
        push bp
        mov bp, sp
        or word [bp+6], 0x40
        pop bp
        push si
        push ax
        push bx
        push cx
        push di
        mov di, si
        imul di, di, 0x0A
        mov bx, [bp+di-0x31E]
        xor ax, ax                      ; no roll
        mov dx, 21                      ; (any d20 is under it)
        mov cx, ITEM_ARMOUR
        jmp item_record
.roll:  iret

; PROBE_ITEM_ARMOUR: INT VEC_ITEM_ARMOUR replaces "mov dx,0Ah / sub dx,[bp-2]" (6 bytes: INT
; + 4 NOPs; 7AC59h), AX the d20, [BP-2] the armour's magical power, SI as for PROBE_ITEM_SKIP.
; DX: the number needed.
probe_item_armour:
        push si
        push ax
        push bx
        push cx
        push di
        mov dx, 10
        sub dx, [bp-2]
        cmp word [bp-2], 0
        jne .game
        mov dx, 21                      ; (no power: the game's, destroyed whatever the roll)
.game:  mov di, si
        imul di, di, 0x0A
        mov bx, [bp+di-0x31E]
        test word [cs:rules], RULE_ITEM_SAVES
        jz .log
        call adnd_needed
        cmp cx, dx
        jge .log
        mov dx, cx
.log:   mov cx, ITEM_ARMOUR
        ; (on into ITEM_RECORD)

; ITEM_RECORD: (jumped to with SI, AX, BX, CX, DI pushed, in that order, on the INT's return
; address) records the check: AL the d20 (0: none), DL the number needed, BX the item, CX
; ITEM_ARMOUR or 0; then IRETs with those registers back and DX as given.
item_record:
        or cx, bx
        mov [cs:extra], cx
        mov ah, dl
        mov si, sp
        add si, 4                       ; (SS:SI+6: the INT's return address)
        mov word [cs:kind], KIND_ITEM
        call record
        pop di
        pop cx
        pop bx
        pop ax
        pop si
        iret

; ADND_NEEDED: CX = AD&D's save against acid for item BX (DS the game's): its material's
; number, less its plus, less 1 for a magical power. Keeps every other register.
adnd_needed:
        push ax
        push bx
        push di
        push es
        mov di, bx
        les bx, [ITEMS]
        imul ax, di, 0x15
        add bx, ax
        mov al, [es:bx+0x14]            ; its plus
        cbw
        mov cx, ax
        cmp byte [es:bx+0x0F], 0
        je .type
        inc cx                          ; a magical power: one plus more
.type:  mov ax, [es:bx+0x0A]
        les bx, [ITEM_TYPES]
        imul ax, ax, 0x14
        add bx, ax
        mov al, [es:bx+8]               ; its material (40h with 0: none, cloth)
        mov ah, al
        and al, 0x0F
        test ah, 0x40
        jz .known
        or al, al
        jnz .known
        mov al, MATERIAL_CLOTH
.known: cmp al, MATERIAL_CLOTH
        jbe .look
        mov al, MATERIAL_METAL          ; (no other is used)
.look:  xor ah, ah
        mov bx, ax
        mov al, [cs:acid_saves+bx]
        sub ax, cx
        mov cx, ax
        pop es
        pop di
        pop bx
        pop ax
        ret

MATERIAL_CLOTH equ 6
acid_saves db 8, 11, 5, 5, 13, 10, 12   ; wood, bone, stone, obsidian, metal, leather; cloth

P_MAGIC_ARMOUR equ 1
P_METAL_ARMOUR equ 2
P_SHIELD       equ 4
P_ARMOUR       equ 8                    ; any armour on the arms, legs, head or chest
ARM_SLOT       equ 0                    ; the item slots of armour (the game's: arm, legs,
LEG_SLOT       equ 6                    ; head, chest)
HEAD_SLOT      equ 7
CHEST_SLOT     equ 9
HAND_RIGHT     equ 3
HAND_LEFT      equ 10
MATERIAL_METAL equ 4
p_flags    db 0
p_ring     db 0
p_ring2    db 0

; The items creature AX (DS = the game's, R_THINGS the things table's segment) wears in slot
; WS_SLOT, of type WS_TYPE (0FFFFh: any): WS_COUNT of them, their positive pluses adding up to
; WS_PLUS. Keeps SI, DI, BP.
worn_scan:
        push cx
        imul ax, ax, 0x3A
        mov [cs:r_creature], ax
        mov word [cs:ws_count], 0
        mov word [cs:ws_plus], 0
        mov cx, 8               ; its item lists, each a thing: +8, +0Ah, +0Ch
.list:  les bx, [CREATURES]
        add bx, [cs:r_creature]
        add bx, cx
        mov dx, [es:bx]
        cmp dx, NO_THING
        jae .next
        mov es, [cs:r_things]
        mov bx, dx
        imul bx, bx, 3
        cmp byte [es:bx+THINGS], 1
        jne .next               ; not an item
        mov dx, [es:bx+THINGS+1]
        mov byte [cs:r_left], 100
.item:  cmp dx, NO_THING
        jae .next
        les bx, [ITEMS]
        mov ax, dx
        imul ax, ax, 0x15
        add bx, ax
        mov al, [es:bx+0x11]
        cmp al, [cs:ws_slot]
        jne .on
        mov ax, [cs:ws_type]
        cmp ax, 0xFFFF
        je .match
        cmp ax, WS_MELEE
        je .melee
        cmp [es:bx+0x0A], ax
        jne .on
        jmp .match
.melee: push es               ; WS_MELEE: a melee weapon (its type's class 1)
        push bx
        mov ax, [es:bx+0x0A]
        imul ax, ax, 0x14
        les bx, [ITEM_TYPES]
        add bx, ax
        cmp byte [es:bx+0x0A], 1
        pop bx
        pop es
        jne .on
.match: inc word [cs:ws_count]
        mov al, [es:bx+0x14]    ; the plus
        cbw
        or ax, ax
        jle .on
        add [cs:ws_plus], ax
.on:    mov dx, [es:bx+4]       ; the next item in the list
        dec byte [cs:r_left]
        jnz .item
.next:  add cx, 2
        cmp cx, 0x0E
        jb .list
        pop cx
        ret

r_things   dw 0
r_creature dw 0
r_left     db 0
r_who      dw 0
ws_slot    dw 0
ws_type    dw 0
ws_count   dw 0
ws_plus    dw 0

; RULES (set by the companion, from its Options): a helm counts AC 1, boots add 1 to movement
; in a fight
RULE_HELMS equ 1
RULE_BOOTS equ 2
RULE_TWO_WEAPONS equ 4
RULE_SPELL_SAVE equ 8           ; (the companion writes the game's save table for this one)
RULE_NO_DOUBLE equ 16
RULE_CATS_GRACE equ 32          ; (the companion also gives Flaming Sphere Strength's record and the name)
RULE_STEALTH equ 64             ; (the companion rolls the hiding and moving silently, and sets STEALTH)
RULE_LEVEL_10 equ 128          ; class levels go up to 10, not 9
RULE_THIEF_TABLE equ 256        ; thief skills from AD&D's table and Dark Sun's DEX adjustments
RULE_HALF_GIANT equ 512         ; half-giants wield two-handed weapons in one hand
RULE_PROTECTION equ 1024        ; AD&D's rings and cloaks of protection (PROBE_RING_AC, RING_PLUS)
RULE_ITEM_SAVES equ 2048        ; items save against acid as in AD&D where better (PROBE_ITEM_*)
RULE_SPECIALIZE equ 4096        ; weapon specialization (PROBE_ATTACKS, PROBE_SPEC_DAMAGE)
RULE_RESTRICT equ 8192          ; class restrictions on armour, shields and weapons (PROBE_CAN_USE)
RULE_MULTI_HP equ 16384         ; multiclass hit points as in AD&D (PROBE_MC_*)
RULE_HP_BEST equ 32768          ; a hit die rolled twice, the better kept (PROBE_HP_BEST)
RULE_HI_KITS equ 1              ; (RULES_HI) kits, chosen on the creation panel's KIT page (KIT_*)
RULE_HI_RANGER equ 2            ; (RULES_HI) a ranger's spells' durations and damage at its level less 7 (PROBE_RANGER_LEVEL)
FOOT      equ 13               ; the item's slot byte while worn on the feet
THINGS_SEG equ 0x3972 - 0x4356  ; the things table's segment, relative to DS

; PROBE_MOVE: INT VEC_MOVE replaces "mov es:[bx+22Bh],ax" (5 bytes: INT + 3 NOPs) where a
; creature's turn in a fight starts: AX = its movement for the turn (its Move x 10), SI the
; creature. Does the move, with 10 more for boots on its feet when RULE_BOOTS is on.
probe_move:
        call kit_move
        test byte [cs:rules], RULE_BOOTS
        jz .store
        push ax
        push bx
        push cx
        push dx
        push es
        mov ax, ds
        add ax, THINGS_SEG
        mov [cs:r_things], ax
        mov ax, si
        mov word [cs:ws_slot], FOOT
        mov word [cs:ws_type], 0xFFFF
        call worn_scan
        pop es
        pop dx
        pop cx
        pop bx
        pop ax
        cmp word [cs:ws_count], 0
        je .store
        add ax, 10
.store: mov [es:bx+0x22B], ax
        iret

; PROBE_TWO: INT VEC_TWO replaces "neg ax / mov dx,ax / or dx,dx / jge +2 / xor dx,dx" (10
; bytes: INT + 8 NOPs) in the routine that gives an attack's to-hit adjustment for two weapons
; ready, after the call that reads the attacker's DEX in the game's initiative table (AX), for
; a non-ranger; [BP+0Ah] is the attack's item, CX the attacker's object. Leaves the
; adjustment in DX: the game's, -AX and no less than 0; with RULE_TWO_WEAPONS, AD&D's: -2 (the
; item in the right hand) or -4 (in the left) plus AX (the same numbers as the reaction
; adjustment), no more than 0, and only with a melee weapon in the other hand too (else 0:
; a two-handed weapon, a shield, a sling or bow in the missile slot don't count).
RIGHT_HAND equ 3
LEFT_HAND  equ 10
probe_two:
        test byte [cs:rules], RULE_TWO_WEAPONS
        jnz .rule
        neg ax
        mov dx, ax
        or dx, dx
        jge .done
        xor dx, dx
.done:  iret
.rule:  push bx
        push es
        push cx
        mov [cs:t_adjust], ax
        xor dx, dx
        mov bx, [bp+0x0A]
        cmp bx, NO_THING
        jae .out
        imul bx, bx, 0x15
        mov ax, bx
        les bx, [ITEMS]
        add bx, ax
        mov al, [es:bx+0x11]    ; the attack's hand, and the other
        mov dx, -2
        mov ah, LEFT_HAND
        cmp al, RIGHT_HAND
        je .hand
        mov dx, -4
        mov ah, RIGHT_HAND
        cmp al, LEFT_HAND
        je .hand
        xor dx, dx
        jmp .out
.hand:  mov [cs:t_base], dx
        mov [cs:ws_slot], ah
        mov byte [cs:ws_slot+1], 0
        mov word [cs:ws_type], WS_MELEE
        mov ax, ds
        add ax, THINGS_SEG
        mov [cs:r_things], ax
        mov es, ax
        mov bx, cx
        imul bx, bx, 3
        xor dx, dx
        cmp byte [es:bx+THINGS], 2
        jne .out                ; not a creature
        mov ax, [es:bx+THINGS+1]
        push ax                 ; (a Twin-blade: none, as a ranger; kits.py)
        call kit_of_creature
        cmp al, KIT_TWIN_BLADE
        pop ax
        je .out
        call worn_scan
        xor dx, dx
        cmp word [cs:ws_count], 0
        je .out                 ; nothing to fight with in the other hand
        mov dx, [cs:t_base]
        add dx, [cs:t_adjust]
        jle .out
        xor dx, dx
.out:   pop cx
        pop es
        pop bx
        iret
t_base   dw 0
t_adjust dw 0

; PROBE_DOUBLE: INT VEC_DOUBLE replaces "shl al,1" (2 bytes), where the saving throw doubles
; its d20 against fire, cold and electricity spells; not with RULE_NO_DOUBLE.
probe_double:
        test byte [cs:rules], RULE_NO_DOUBLE
        jnz .done
        shl al, 1
.done:  iret

; PROBE_LEVEL: INT VEC_LEVEL replaces "cmp byte es:[bx+24h],9" (5 bytes: INT + 3 NOPs) in the
; two places the game holds a class level (ES:BX+24h, a sheet's) against its cap of 9: where a
; character goes up a level (only while below it) and where View Character shows the XP for the
; next level (not at it). With RULE_LEVEL_10 the cap is 10: the game's XP tables, hit points,
; THAC0, saves, spell slots and thief skills all go on past 9 (the tables have 20 levels, the
; rest are formulas). RETF 2 keeps the compare's flags.
probe_level:
        sti
        push ax
        mov al, 9
        test byte [cs:rules], RULE_LEVEL_10
        jz .cmp
        inc al
.cmp:   cmp [es:bx+0x24], al
        pop ax
        retf 2

; A thief's hit dice. The game keeps one "dice up to this level" for thieves and psionicists
; together (9: after it, +2 a level), but an AD&D thief rolls a d6 up to 10th (a psionicist
; stops at 9th). With RULE_LEVEL_10 a thief's goes to 10, in the two places it counts.
THIEF_CLASS equ 17
; PROBE_HD_ROLL: INT VEC_HD_ROLL replaces "mov al,es:[bx+1]" (5 bytes: INT + 3 NOPs) where a new
; level's hit points are a roll or the fixed gain: ES:BX the class's hit point group, the
; game's [BP+8] the class.
probe_hd_roll:
        mov al, [es:bx+1]
        test byte [cs:rules], RULE_LEVEL_10
        jz .done
        cmp al, 9
        jne .done
        cmp word [bp+8], THIEF_CLASS
        jne .done
        inc al
.done:  iret

; PROBE_HD_CON: INT VEC_HD_CON replaces "cmp al,es:[bx+1]" (5 bytes: INT + 3 NOPs) where the game
; counts the levels CON's hit point bonus applies to: AL a class's level, ES:BX its group, the
; game's [BP-4] the sheet (far) and CX which of its classes. RETF 2 keeps the compare's flags.
probe_hd_con:
        sti
        push dx
        mov dl, [es:bx+1]
        test byte [cs:rules], RULE_LEVEL_10
        jz .cmp
        cmp dl, 9
        jne .cmp
        push es
        push bx
        les bx, [bp-4]
        add bx, cx
        cmp byte [es:bx+0x21], THIEF_CLASS
        pop bx
        pop es
        jne .cmp
        inc dl
.cmp:   cmp al, dl
        pop dx
        retf 2

; Multiclass hit points as in AD&D (RULE_MULTI_HP). The game adds each new level's die (or fixed
; gain) to a sheet's base (+0Ah), and a character's most hit points are that base divided by its
; number of classes (a human's, who dual-classes, counts one), plus CON's bonus for its levels:
; the warrior's full bonus, the rest's up to +2, undivided. With the rule, of a character of more
; than one class (not a human) each level's die counts its share, at least 1 (the base gains
; that times the classes, which the game divides again), and CON's bonus is shared out too.
; PROBE_MC_ROLL: INT VEC_MC_ROLL replaces "add es:[bx+0Ah],cx" (4 bytes: INT + 2 NOPs; DSUN.EXE
; 8735Eh) where a new level's hit points go into the base: ES:BX the sheet, CX the gain.
; PROBE_HIT_DIE: INT VEC_HIT_DIE replaces "mov al,es:[bx+0]" (5 bytes: INT + 3 NOPs; DSUN.EXE 87308h)
; where a new level's hit die is taken from its class's hit point group (ES:BX; the routine's
; [BP-4] the sheet, far), at a level up and at creation: AL that, a Battle Mage's a d6 (a
; preserver's d4), a Mind Warrior's a d8 (a psionicist's d6), an Arcanist's a d3 (kits.hit_die),
; for the kit's class (one class: a human who has changed class rolls for its new one).
probe_hit_die:
        mov al, [es:bx]
        push bx
        push cx
        push es
        mov cl, al
        les bx, [bp - 4]
        cmp word [es:bx + 0x22], 0      ; (a human who has changed class rolls for the new one)
        jne .out
        call kit_id
        cmp al, KIT_BATTLE_MAGE
        jne .warrior
        mov cl, 6
.warrior:
        cmp al, KIT_MIND_WARRIOR
        jne .arcanist
        mov cl, 8
.arcanist:
        cmp al, KIT_ARCANIST
        jne .out
        mov cl, 3
.out:   mov al, cl
        pop es
        pop cx
        pop bx
        iret

; PROBE_MAX_PSP: INT VEC_MAX_PSP replaces "les bx,[bp-8]" (3 bytes: INT + NOP; DSUN.EXE 8748Fh) where
; the level-up routine has a character's most PSP worked out (SI) and puts it in the sheet ([BP-8],
; far: its +0Ch) when more than it was, the gain on the creature's PSP too: ES:BX the sheet, and a
; Mind Warrior's SI a tenth less (kits.max_psp), its kit asleep or not (KIT_ANY).
probe_max_psp:
        les bx, [bp - 8]
        push ax
        call kit_any            ; (asleep too: its psionicist levels are the PSP's)
        cmp al, KIT_MIND_WARRIOR
        jne .out
        push cx
        push dx
        mov ax, si
        xor dx, dx
        mov cx, 10
        div cx
        sub si, ax
        pop dx
        pop cx
.out:   pop ax
        iret

; At creation the game rolls a character's hit points and works out its PSP when a class is
; clicked, before a kit can be chosen; picking or taking back a Battle Mage, a Mind Warrior or an
; Arcanist on the kit page has KIT_REROLL roll them again (KIT_ROW). The sheet being made has its
; classes numbered 1-8; the game copies it to the party's sheet (66AC4h, which numbers them 1-17)
; before the rolls.
; PROBE_CR_DIE: INT VEC_CR_DIE replaces "mov al,es:[bx+14Ah]" (5 bytes: INT + 3 NOPs; DSUN.EXE
; 65677h) where the creation hit points' routine (655D6h) takes the class's die for the most hit
; points it can give (ES:BX the table, by class): AL that, a Battle Mage's 6, a Mind Warrior's 8,
; an Arcanist's 3 (kits.hit_die). A kit left from the classes before a click on a class
; (PROBE_WP_CLASS takes it away only after the rolls) is taken away first, from the party's sheet
; too, so the rolls go by the class's own die.
CR_MEMBER_SEG equ 0x65689 - 0x65679   ; ("mov ax,348h": the member's number's segment, +25Bh)
CR_MEMBER     equ 0x25B
probe_cr_die:
        push cx
        mov cl, [es:bx + 0x14A]
        call kit_stale
        call kit_made
        cmp al, KIT_BATTLE_MAGE
        jne .warrior
        mov cl, 6
.warrior:
        cmp al, KIT_MIND_WARRIOR
        jne .arcanist
        mov cl, 8
.arcanist:
        cmp al, KIT_ARCANIST
        jne .out
        mov cl, 3
.out:   mov al, cl
        pop cx
        iret

; KIT_STALE: (PROBE_CR_DIE, its interrupt frame at SP+6) a kit on the sheet being made while its
; classes are not those PROBE_WP_CLASS last saw: taken away, from the party's sheet too. DS the
; game's; all registers kept.
kit_stale:
        test word [cs:rules_hi], RULE_HI_KITS
        jz .ret
        push eax
        push bx
        push es
        les bx, [WP_CREATION]
        mov eax, [es:bx + 0x21]
        and eax, 0x00FFFFFF
        cmp eax, [cs:wp_classes_seen]
        je .out
        mov byte [es:bx + KIT_BYTE], 0
        mov bx, sp
        les bx, [ss:bx + 12]            ; (the INT's return, in the overlay, past CX and the call)
        mov ax, [es:bx + CR_MEMBER_SEG]
        mov es, ax
        imul ax, [es:CR_MEMBER], 0x47
        les bx, [0x1661]
        add bx, ax
        mov byte [es:bx + KIT_BYTE], 0
.out:   pop es
        pop bx
        pop eax
.ret:   ret

; KIT_MADE: AL the kit (KIT_ID's numbers) of the sheet being made, ZF clear; 0 and ZF set if none
; (the rule off, more than one class, none chosen, or its classes changed since PROBE_WP_CLASS last
; saw them). DS the game's; others kept.
kit_made:
        push bx
        push es
        call kit_class
        or al, al
        jz .out
        les bx, [WP_CREATION]
        push eax
        mov eax, [es:bx + 0x21]
        and eax, 0x00FFFFFF
        cmp eax, [cs:wp_classes_seen]
        pop eax
        jne .none
        mov bl, [es:bx + KIT_BYTE]
        dec bl
        cmp bl, 2
        ja .none
        shl al, 2
        add al, bl
        inc al
        jmp .out
.none:  xor al, al
.out:   pop es
        pop bx
        or al, al
        ret

; PROBE_CR_PSP: INT VEC_CR_PSP replaces "pop bp; retf" (2 bytes; DSUN.EXE 65C3Dh), the end of the
; creation PSP routine (65B39h), which has put the sheet being made's most PSP in its +0Ch: a Mind
; Warrior's a tenth fewer, rounded down (kits.max_psp). Then the routine's own end, the interrupt
; frame dropped.
probe_cr_psp:
        add sp, 6
        push ax
        call kit_made
        cmp al, KIT_MIND_WARRIOR
        jne .out
        push bx
        push cx
        push dx
        push es
        les bx, [WP_CREATION]
        mov ax, [es:bx + 0x0C]
        xor dx, dx
        mov cx, 10
        div cx
        sub [es:bx + 0x0C], ax
        pop es
        pop dx
        pop cx
        pop bx
.out:   pop ax
        pop bp
        retf

; PROBE_MC_CON: INT VEC_MC_CON replaces "add di,ax" (2 bytes; 87523h) where the most hit points
; get CON's bonus: AX the bonus, SI the sheet's number.
; PROBE_MC_UNCON: INT VEC_MC_UNCON replaces "sub dx,ax" (2 bytes; 877DAh) where the game takes
; CON's bonus off the hit points to work back to the base (when CON changes): AX, SI as above.
probe_mc_roll:
        test word [cs:rules], RULE_MULTI_HP
        jz .add
        push ax
        push dx
        call class_share        ; AX the classes sharing, 1 for none
        cmp ax, 1
        jbe .out
        xchg ax, cx             ; CX the classes, AX the gain
        cwd
        idiv cx
        cmp ax, 1
        jge .share
        mov ax, 1
.share: imul cx                 ; (the classes' times the share: the game divides it back)
        mov cx, ax
.out:   pop dx
        pop ax
.add:   add [es:bx + 0x0A], cx
        iret

; PROBE_HP_BEST: INT VEC_HP_BEST replaces "mov cx,ax" (2 bytes; DSUN.EXE 87319h) after the game's
; roll of a level's hit die (at creation too), AX the roll. With RULE_HP_BEST the game rolls it
; twice and keeps the better: the first time the roll is kept and the INT returns to the start of
; the game's roll (872FDh: the die from the class's table, then the call), so the dice log sees
; both rolls as the game's; the second time CX gets the better of the two.
HP_BEST_BACK equ 0x8731B - 0x872FD
probe_hp_best:
        test word [cs:rules], RULE_HP_BEST
        jz .game
        cmp byte [cs:hp_again], 0
        jne .second
        mov [cs:hp_first], ax
        mov byte [cs:hp_again], 1
        push bp
        mov bp, sp
        sub word [bp + 2], HP_BEST_BACK ; (the INT's return IP)
        pop bp
        iret
.second:
        mov byte [cs:hp_again], 0
        cmp ax, [cs:hp_first]
        jge .game
        mov ax, [cs:hp_first]
.game:  mov cx, ax
        iret

hp_first dw 0
hp_again db 0

; PROBE_TOME: INT VEC_TOME replaces "push ds / push 3440h" (4 bytes: INT + 2 NOPs; DSUN.EXE
; 8B80Ch) in the routine run when a scroll's icon is clicked in its box, where a scroll of the
; game's (object 1400-1499) whose spell byte, less one ([BP-3]), is 172 to 195 has the game show
; its message 3440h ("CANNOT LEARN FROM THIS ITEM") and keep the scroll. The Tome of Understanding (dscompanion/tome.py) is such
; a scroll, of TOME_SPELL: the one whose scroll it is (the character on show, at 25Bh of the
; segment the game names in its "mov ax,348h" at 8B7E3h) gains a point of WIS, in the sheet
; (+1Fh) and the creature record (+26h), at most TOME_MOST, and the game goes on as for a psionic
; power taught (8B7E2h): it uses the scroll up and shows the message in its buffer at [BP-54h],
; here "<name> reads the tome: WIS <n>." At TOME_MOST already, only the message ("... can grow no
; wiser."), shown as the game shows its own (8B810h), and the tome is kept. Any other scroll: the
; game's message.
TOME_SPELL   equ 0xB0             ; (172-195: the game lets the icon be clicked for below 196)
TOME_MOST    equ 25
TOME_WHO     equ 0x8B7E4 - 0x8B80E   ; (the segment in that "mov ax", less the INT's way back)
TOME_USE     equ 0x8B7E2 - 0x8B80E
TOME_SHOW    equ 0x8B810 - 0x8B80E
probe_tome:
        cmp byte [bp - 3], TOME_SPELL
        je .tome
        pop word [cs:tm_ip]
        pop word [cs:tm_cs]
        pop word [cs:tm_fl]
        push ds
        push 0x3440
        jmp .show
.tome:  pushad
        push es
        push fs
        mov si, sp
        mov es, [ss:si + 38]            ; (the INT's CS:IP, above FS, ES and PUSHAD's 32 bytes)
        mov di, [ss:si + 36]
        mov fs, [es:di + TOME_WHO]
        mov bx, [fs:0x25B]              ; the reader
        mov byte [cs:tm_used], 0
        les di, [0x1661]
        imul ax, bx, 0x47
        add di, ax                      ; ES:DI: their sheet
        lfs si, [0x1665]
        imul ax, bx, 0x3A
        add si, ax                      ; FS:SI: their creature record
        mov al, [es:di + 0x1F]
        cmp al, TOME_MOST
        jae .text
        inc al
        mov [es:di + 0x1F], al
        mov byte [cs:tm_used], 1
        cmp byte [fs:si + 0x26], TOME_MOST
        jae .text
        inc byte [fs:si + 0x26]
.text:  mov [cs:tm_wis], al
        push ss
        pop es
        lea di, [bp - 0x54]             ; ES:DI: the game's message buffer
        mov cx, 16
.name:  mov al, [fs:si + 0x28]
        or al, al
        jz .named
        stosb
        inc si
        loop .name
.named: push ds
        push cs
        pop ds
        mov si, tm_reads
        cmp byte [cs:tm_used], 0
        jne .copy
        mov si, tm_wiser
.copy:  lodsb
        stosb
        or al, al
        jnz .copy
        pop ds
        cmp byte [cs:tm_used], 0
        je .kept
        dec di                          ; (the number, after "WIS ")
        mov al, [cs:tm_wis]
        aam                             ; AH tens, AL ones
        or ax, 0x3030
        cmp ah, 0x30
        je .ones
        mov [es:di], ah
        inc di
.ones:  mov [es:di], al
        mov word [es:di + 1], '.'
        mov si, sp
        add word [ss:si + 36], TOME_USE ; on as for a power taught: the tome used up, the message
        pop fs
        pop es
        popad
        iret
.kept:  pop fs
        pop es
        popad
        pop word [cs:tm_ip]
        pop word [cs:tm_cs]
        pop word [cs:tm_fl]
        mov [cs:tm_ax], ax
        lea ax, [bp - 0x54]
        push ss
        push ax
        mov ax, [cs:tm_ax]
.show:  push word [cs:tm_fl]
        push word [cs:tm_cs]
        mov [cs:tm_ax], ax
        mov ax, [cs:tm_ip]
        add ax, TOME_SHOW
        push ax
        mov ax, [cs:tm_ax]
        iret

tm_ip    dw 0
tm_cs    dw 0
tm_fl    dw 0
tm_ax    dw 0
tm_wis   db 0
tm_used  db 0
tm_reads db " reads the tome: WIS ", 0
tm_wiser db " can grow no wiser.", 0

probe_mc_con:
        call con_share
        add di, ax
        iret

probe_mc_uncon:
        call con_share
        sub dx, ax
        iret

; CON_SHARE: AX (CON's hit point bonus) divided by the classes of sheet SI (dropping fractions)
; with RULE_MULTI_HP. Other registers kept.
con_share:
        test word [cs:rules], RULE_MULTI_HP
        jz .ret
        push bx
        push cx
        push dx
        push es
        push ax
        les bx, [0x1661]
        imul ax, si, 0x47
        add bx, ax
        call class_share
        mov cx, ax
        pop ax
        cmp cx, 1
        jbe .out
        cwd
        idiv cx
.out:   pop es
        pop dx
        pop cx
        pop bx
.ret:   ret

; CLASS_SHARE: AX the number of classes of the sheet at ES:BX, 1 for a human's. Others kept.
class_share:
        mov ax, 1
        cmp byte [es:bx + 0x18], 1
        je .ret
        cmp byte [es:bx + 0x22], 0
        je .ret
        inc ax
        cmp byte [es:bx + 0x23], 0
        je .ret
        inc ax
.ret:   ret

; PROBE_THIEF_SKILL: INT VEC_THIEF_SKILL, then "jc" past the game's DEX formula, replaces
; "mov ax,si / shl ax,2 / add dx,ax / mov si,dx" (9 bytes) in the game's thief skill routine,
; where DX is the skill's base plus the race's adjustment, SI the thief level, ES:0 the game's
; thief tables (the base at +skill), and the game's [BP+8] the skill, [BP-0Ch] DEX. Without
; RULE_THIEF_TABLE (or a thief level), the game's sum (SI = DX + 4 a level), CF clear: on to
; the game's DEX formula. With it, SI = AD&D's average for the level (THIEF_TABLE, levels past
; 10 as 10) + the race's adjustment + Dark Sun's DEX adjustment (DEX_TABLE, DEX 9-22), CF set:
; past the game's DEX formula, to its armour and effects (dscompanion/game.py does the same sums).
probe_thief_skill:
        sti
        test word [cs:rules], RULE_THIEF_TABLE
        jz .game
        cmp si, 1
        jl .game
        push ax
        push bx
        mov bx, [bp+8]
        and bx, 7
        mov al, [es:bx]         ; the game's base, out
        cbw
        sub dx, ax
        mov ax, si
        cmp ax, 10
        jbe .level
        mov ax, 10
.level: dec ax
        imul bx, bx, 10
        add bx, ax
        mov al, [cs:thief_table+bx]
        mov ah, 0
        add dx, ax
        mov bx, [bp+8]
        and bx, 7
        cmp bx, 5               ; DEX adjusts the first five
        jae .done
        mov ax, [bp-0Ch]
        cmp ax, 9
        jge .low
        mov ax, 9
.low:   cmp ax, 22
        jle .high
        mov ax, 22
.high:  sub ax, 9
        imul ax, ax, 5
        add bx, ax
        mov al, [cs:dex_table+bx]
        cbw
        add dx, ax
.done:  mov si, dx
        pop bx
        pop ax
        push bp
        mov bp, sp
        or word [bp+6], 1       ; CF in the flags IRET restores: past the game's DEX formula
        pop bp
        iret
.game:  shl si, 2
        add si, dx
        push bp
        mov bp, sp
        and word [bp+6], 0FFFEh
        pop bp
        iret
; PROBE_TWO_HANDED: INT VEC_TWO_HANDED replaces "test byte es:[bx+0Fh],40h" (5 bytes: INT + 3
; NOPs), the two-handed bit of an item type (ES:BX), where the inventory screen puts a weapon in
; a hand: of the other hand's ("Two handed weapon in use") and of the one going in ("Need two
; free hands"). With RULE_HALF_GIANT, for a half-giant on show the answer is "not two-handed"
; (ZF set); otherwise the game's test. RETF 2 keeps the flags. (The game's own check that both
; hands don't hold heavy weapons, over 30 each, still stands.)
WHO_SEG equ USE_WHO_SEG          ; the segment of the character on show's number (+25Bh), from DS
                                ; (the overlay's code says 0348h, which its loader relocates there)
HALF_GIANT equ 5
probe_two_handed:
        sti
        test word [cs:rules], RULE_HALF_GIANT
        jz .test
        push ax
        push es
        push bx
        mov ax, ds
        add ax, WHO_SEG
        mov es, ax
        imul ax, [es:0x25B], 0x47
        les bx, [0x1661]
        add bx, ax              ; ES:BX = the sheet of the character on show
        cmp byte [es:bx + 0x18], HALF_GIANT
        pop bx
        pop es
        pop ax
        jne .test
        cmp ax, ax              ; ZF: not two-handed, for a half-giant
        retf 2
.test:  test byte [es:bx + 0x0F], 0x40
        retf 2

; PROBE_SPELL_TEXT: INT VEC_SPELL_TEXT replaces "add sp,0Ch" (3 bytes: INT + NOP) after the game
; reads a spell's description (RESOURCE.GFF's SPIN chunk DI, the spell's number + 1) into the
; buffer at the game's [BP-8] (far), AX its length or FFFFh. With RULE_CATS_GRACE, Flaming
; Sphere's (SPIN 15) is Cat's Grace's instead, in the game's words for Strength's.
SPIN_SPHERE equ 15
probe_spell_text:
        pop word [cs:n_ip]
        pop word [cs:n_cs]
        pop word [cs:n_fl]
        add sp, 0x0C
        push word [cs:n_fl]
        push word [cs:n_cs]
        push word [cs:n_ip]
        test byte [cs:rules], RULE_CATS_GRACE
        jz .out
        cmp di, SPIN_SPHERE
        jne .out
        cmp ax, 0xFFFF
        je .out
        push cx
        push si
        push di
        push ds
        push es
        les di, [bp-8]
        push cs
        pop ds
        mov si, grace_text
        mov cx, GRACE_TEXT_LEN
        cld
        rep movsb
        pop es
        pop ds
        pop di
        pop si
        pop cx
        mov ax, GRACE_TEXT_LEN - 1  ; (its length, as the game's read gives it)
.out:   iret
grace_text:
        db "CAT'S GRACE:  Raises the target's dexterity by 1 to 6 pts. Maximum dexterity is 24.", 13, 10, 0
GRACE_TEXT_LEN equ $ - grace_text

; PROBE_CHUNK_ID: INT VEC_CHUNK_ID replaces "cmp [9Ch],sp" (4 bytes: INT + 2 NOPs; the stack
; check) at the start of the game's two routines that load a GFF chunk, whose [BP+6] is the
; chunk's type and [BP+0Ah] its number (dwords). With RULE_CATS_GRACE, and the companion's copy
; of RESOURCE.GFF open, Flaming Sphere's icon (ICON 21014) is asked for as Cat's Grace's
; (GRACE_ICON, which only the copy has). Then the stack check, with the routine's SP; RETF 2
; keeps its flags.
SPHERE_ICON equ 21014
GRACE_ICON  equ 21900
probe_chunk_id:
        sti                     ; (RETF 2 keeps these flags: interrupts on, as the game had them)
        test byte [cs:rules], RULE_CATS_GRACE
        jz .check
        cmp word [cs:resources_on], 1
        jne .check
        cmp dword [bp+6], 0x4E4F4349    ; 'ICON'
        jne .check
        cmp dword [bp+0x0A], SPHERE_ICON
        jne .check
        mov dword [bp+0x0A], GRACE_ICON
.check: push ax
        mov ax, sp
        add ax, 8               ; (the routine's SP: past this push and INT's IP, CS and flags)
        cmp [0x9C], ax
        pop ax
        retf 2

; AD&D's average thief skills, levels 1-10, a row per skill (game.AD_D_THIEF)
thief_table:
        db 30, 35, 40, 45, 50, 55, 60, 65, 70, 80     ; pick pockets
        db 25, 29, 33, 37, 42, 47, 52, 57, 62, 67     ; open locks
        db 20, 25, 30, 35, 40, 45, 50, 55, 60, 65     ; find/remove traps
        db 15, 21, 27, 33, 40, 47, 55, 62, 70, 78     ; move silently
        db 10, 15, 20, 25, 31, 37, 43, 49, 56, 63     ; hide in shadows
        db 10, 10, 15, 15, 20, 20, 25, 25, 30, 30     ; hear noise
        db 85, 86, 87, 88, 90, 92, 94, 96, 98, 99     ; climb walls
        db 0, 0, 0, 20, 25, 30, 35, 40, 45, 50        ; read languages
; Dark Sun's DEX adjustments, DEX 9-22, a row per DEX: pick, open, traps, move, hide (game.DEX_ADJUST)
dex_table:
        db -15, -10, -10, -20, -10
        db -10, -5, -10, -15, -5
        db -5, 0, -5, -10, 0
        db 0, 0, 0, -5, 0
        db 0, 0, 0, 0, 0
        db 0, 0, 0, 0, 0
        db 0, 0, 0, 0, 0
        db 0, 5, 0, 0, 0
        db 5, 10, 0, 5, 5
        db 10, 15, 5, 10, 10
        db 15, 20, 10, 15, 15
        db 20, 25, 12, 20, 17
        db 25, 27, 15, 25, 20
        db 27, 30, 17, 30, 22

; PROBE_DOS_OPEN: the DOS services (INT 21h), hooked. Opening a file (AH=3Dh) whose name ends in
; one of COPIES' (SEGOBJEX.GFF, the game's objects and their pictures; RESOURCE.GFF, its screens'
; pictures and texts; GPLDATA.GFF, its scripts; RGN29.GFF, the slave pens; RGN1C.GFF, RGN1E.GFF
; and RGN08.GFF, the Upper Castle, the Undermountain and the Gemfields, a person there with an
; object of their own for a new item) opens the companion's
; copy instead (D:\..., which the launcher writes with
; the companion's icons added; the game folder is never changed), and notes that it has; with no
; copy there, the game's own. Everything else goes on to DOS.
OBJ_NAME_LEN equ 12
probe_dos_open:
        cmp ah, 3Dh
        jne .chain
        push ax
        push bx
        push cx
        push si
        push di
        mov si, dx
        mov cx, 128
.end:   cmp byte [si], 0
        je .at_end
        inc si
        loop .end
        jmp .no
;
; Each copy's name (its length first) is matched against the end of the path, where it starts the
; path or follows a \, : or /.
.at_end:
        mov ax, si
        sub ax, dx              ; AX: the path's length
        mov bx, copies
.file:  mov cl, [cs:bx]         ; this copy's name length (0: no more)
        xor ch, ch
        jcxz .no
        cmp ax, cx
        jb .next
        push ax
        push si
        sub si, cx              ; where the name would start
        cmp ax, cx
        je .compare             ; (the path is just the name)
        mov al, [si - 1]
        cmp al, '\'
        je .compare
        cmp al, ':'
        je .compare
        cmp al, '/'
        jne .differ
.compare:
        lea di, [bx + 1]
.cmp:   mov al, [si]
        cmp al, 'a'
        jb .upper
        cmp al, 'z'
        ja .upper
        sub al, 20h
.upper: cmp al, [cs:di]
        jne .differ
        inc si
        inc di
        loop .cmp
        pop si
        pop ax
        jmp .found
.differ:
        pop si
        pop ax
.next:  add bx, COPY_SIZE
        jmp .file
.found: mov [cs:copy_at], bx
        pop di
        pop si
        pop cx
        pop bx
        pop ax
        sti                     ; (RETF 2 keeps these flags: interrupts on, as the game had them)
        push ax                 ; (the game's AX, for its own file if there's no copy)
        push ds
        push dx
        push cs
        pop ds
        mov dx, [cs:copy_at]
        add dx, 1 + OBJ_NAME_LEN  ; its copy's path
        pushf
        call far [cs:old21]
        pop dx
        pop ds
        jc .own
        pop word [cs:obj_scratch]  ; (POP keeps the flags: CF clear, AX the handle)
        push bx
        mov bx, [cs:copy_at]
        mov bx, [cs:bx + 1 + OBJ_NAME_LEN + COPY_PATH]  ; (its flag: set to 1)
        mov word [cs:bx], 1
        pop bx
        retf 2
.own:   pop ax
        jmp .chain
.no:    pop di
        pop si
        pop cx
        pop bx
        pop ax
.chain: jmp far [cs:old21]
old21       dd 0
obj_scratch dw 0
copy_at     dw 0
resources_on dw 0               ; 1 once the game has opened D:\RESOURCE.GFF (PROBE_CHUNK_ID)
scripts_on dw 0                 ; 1 once the game has opened D:\GPLDATA.GFF (Kalzith's conversation)
region_on dw 0                  ; 1 once the game has opened D:\RGN29.GFF (Kalzith in the pens)
others_on dw 0                  ; 1 once it has opened one of the other regions' copies
; the files with copies: the name's length, the game's name (up to 12 letters), the copy's path,
; the flag set when opened
COPY_PATH equ 16
COPY_SIZE equ 1 + OBJ_NAME_LEN + COPY_PATH + 2
%macro COPY 3
        db %strlen(%1), %1
        times OBJ_NAME_LEN - %strlen(%1) db 0
        db %2
        times COPY_PATH - %strlen(%2) db 0
        dw %3
%endmacro
copies:
        COPY 'SEGOBJEX.GFF', 'D:\SEGOBJEX.GFF', objects_on
        COPY 'RESOURCE.GFF', 'D:\RESOURCE.GFF', resources_on
        COPY 'GPLDATA.GFF', 'D:\GPLDATA.GFF', scripts_on
        COPY 'RGN29.GFF', 'D:\RGN29.GFF', region_on
        COPY 'RGN1C.GFF', 'D:\RGN1C.GFF', others_on   ; (a Castle Guard of its own: worldgear.py)
        COPY 'RGN1E.GFF', 'D:\RGN1E.GFF', others_on   ; (an Undermountain miner of its own)
        COPY 'RGN08.GFF', 'D:\RGN08.GFF', others_on   ; (a Magera of their own, Drakejaw's)
        db 0

; CAT'S GRACE (RULE_CATS_GRACE): Flaming Sphere (spell 14), given Strength's record and the
; name by the companion, works as Strength does but for DEX: its own effect (54, a number the
; game leaves unused) holds the 1d6, which the routine that works out a creature's abilities
; adds to DEX, keeping it between 3 and 24.
GRACE_SPELL    equ 14
STRENGTH_SPELL equ 23
GRACE_EFFECT   equ 54
ADRENALIN      equ 0x94         ; the psionic Adrenalin Control, which Strength's code also serves

; PROBE_GRACE_CAST: INT VEC_GRACE_CAST replaces "mov ax,[bp+0Eh] / mov [bp-1Ah],ax" (6 bytes:
; INT + 4 NOPs) where the code for spells with handlers of their own picks the handler by the
; spell's number: Cat's Grace goes to Strength's.
probe_grace_cast:
        mov ax, [bp+0x0E]
        test byte [cs:rules], RULE_CATS_GRACE
        jz .store
        cmp ax, GRACE_SPELL
        jne .store
        mov ax, STRENGTH_SPELL
.store: mov [bp-0x1A], ax
        iret

; PROBE_GRACE_EFFECT: INT VEC_GRACE_EFFECT replaces Strength's handler choosing its effect
; (19 bytes: INT + 17 NOPs): Adrenalin Control 42h, Strength 43h, and Cat's Grace its own.
probe_grace_effect:
        mov word [bp-0x14], 0x43
        cmp word [bp+0x0E], ADRENALIN
        jne .grace
        mov word [bp-0x14], 0x42
        iret
.grace: test byte [cs:rules], RULE_CATS_GRACE
        jz .out
        cmp word [bp+0x0E], GRACE_SPELL
        jne .out
        mov word [bp-0x14], GRACE_EFFECT
.out:   iret

; PROBE_GRACE_ABILITY: INT VEC_GRACE_ABILITY replaces "mov [bp-0Ah],ax / mov cx,7" (6 bytes:
; INT + 4 NOPs) in the routine that works out a creature's abilities, as it looks at one of its
; effects (AX its number; ES:BX the effect, its amount at +10Fh): Cat's Grace adds the amount
; to DEX in its sums (words from [BP-342h], STR first), as Strength's adds to STR.
probe_grace_ability:
        mov [bp-0x0A], ax
        mov cx, 7
        cmp ax, GRACE_EFFECT
        jne .out
        push ax
        mov al, [es:bx+0x10F]
        cbw
        add ax, [bp-0x340]
        cmp ax, 24
        jle .low
        mov ax, 24
.low:   cmp ax, 3
        jge .set
        mov ax, 3
.set:   mov [bp-0x340], ax
        pop ax
.out:   iret

; NAMES: the game's name table (GPLDATA's NAME chunk: 322 names of 25 bytes, which items name
; by number) gets NAMES_EXTRA more, for the companion's own items (the Ring of Protection, the
; Thieves' Tools...). Where the game reserves memory for the chunk (as it starts, and as a game
; is loaded) PROBE_NAMES_SIZE makes the room for them; once it has read the chunk in,
; PROBE_NAMES_FILL copies EXTRA_NAMES after the game's own. Nothing in the game limits the
; numbers to its own 322.
NAMES_OWN    equ 0x142
NAME_SIZE    equ 25
NAMES_EXTRA  equ 32
NAMES_PTR    equ 0x166D         ; DS: far pointer to the name table

; PROBE_NAMES_SIZE: INT VEC_NAMES_SIZE replaces "push dword 1" (3 bytes: INT + NOP) just before
; the game reserves memory for the NAME chunk, its size the dword at [BP-4]: adds the room,
; then does the push (under the interrupt's return frame).
probe_names_size:
        add word [bp-4], NAMES_EXTRA * NAME_SIZE
        adc word [bp-2], 0
        pop word [cs:n_ip]
        pop word [cs:n_cs]
        pop word [cs:n_fl]
        push dword 1
        push word [cs:n_fl]
        push word [cs:n_cs]
        push word [cs:n_ip]
        iret

; PROBE_NAMES_FILL: INT VEC_NAMES_FILL replaces "add sp,0Ch" (3 bytes: INT + NOP) after the call
; that reads the NAME chunk into the table (AX 0: read). Does the add, then, if it was read,
; copies EXTRA_NAMES after the game's names and notes the table in NAMES_PTR.
probe_names_fill:
        pop word [cs:n_ip]
        pop word [cs:n_cs]
        pop word [cs:n_fl]
        add sp, 0x0C
        push word [cs:n_fl]
        push word [cs:n_cs]
        push word [cs:n_ip]
        or ax, ax
        jnz .out
        push cx
        push si
        push di
        push ds
        push es
        les di, [NAMES_PTR]
        mov [cs:names_ptr], di
        mov [cs:names_ptr+2], es
        add di, NAMES_OWN * NAME_SIZE
        push cs
        pop ds
        mov si, extra_names
        mov cx, NAMES_EXTRA * NAME_SIZE
        cld
        rep movsb
        pop es
        pop ds
        pop di
        pop si
        pop cx
.out:   iret
n_ip    dw 0
n_cs    dw 0
n_fl    dw 0

; TYPES: the game's item types (GPLDATA's IT1R chunk, 20 bytes each, which items name by
; number), read in just before the names, get TYPES_EXTRA more in the same way: for the
; companion's own items that no type of the game's fits (a metal short sword, a cloak of
; protection). Nothing in the game limits the numbers to its own.
TYPE_SIZE   equ 20
TYPES_EXTRA equ 25
TYPES_PTR   equ 0x1669          ; DS: far pointer to the item types
BRACERS     equ 8               ; (the bracers of defense: the ninth of them)
ELVEN_CLOAK equ 19              ; (the Cloak and Boots of Elvenkind)
ELVEN_BOOTS equ 20
GREYS_ARMS  equ 54              ; Grey's Scale's arm and leg armour: the game's AC 2 each, made 3
GREYS_LEGS  equ 24              ; (PROBE_TYPES_FILL)

; PROBE_TYPES_SIZE: INT VEC_TYPES_SIZE replaces "push dword 1" (3 bytes: INT + NOP) before the
; game reserves memory for the IT1R chunk, its size the dword at [BP-4]: adds the room.
probe_types_size:
        add word [bp-4], TYPES_EXTRA * TYPE_SIZE
        adc word [bp-2], 0
        pop word [cs:n_ip]
        pop word [cs:n_cs]
        pop word [cs:n_fl]
        push dword 1
        push word [cs:n_fl]
        push word [cs:n_cs]
        push word [cs:n_ip]
        iret

; PROBE_TYPES_FILL: INT VEC_TYPES_FILL replaces "add sp,0Ch" (3 bytes: INT + NOP) after the
; call that reads the chunk in (AX 0: read). Does the add, then, if it was read, copies
; EXTRA_TYPES after the game's own (the room made, [BP-4], less theirs) and notes where; and
; raises Grey's Scale's AC (its arm and leg armour, the game's own types) to 3.
probe_types_fill:
        pop word [cs:n_ip]
        pop word [cs:n_cs]
        pop word [cs:n_fl]
        add sp, 0x0C
        push word [cs:n_fl]
        push word [cs:n_cs]
        push word [cs:n_ip]
        or ax, ax
        jnz .out
        push ax
        push cx
        push dx
        push si
        push di
        push ds
        push es
        les di, [TYPES_PTR]
        mov [cs:types_ptr], di
        mov [cs:types_ptr+2], es
        mov byte [es:di+GREYS_ARMS*TYPE_SIZE+0x12], 3
        mov byte [es:di+GREYS_LEGS*TYPE_SIZE+0x12], 3
        mov ax, [bp-4]
        sub ax, TYPES_EXTRA * TYPE_SIZE
        add di, ax              ; after the game's own
        xor dx, dx
        mov cx, TYPE_SIZE
        div cx
        mov [cs:types_first], ax
        push cs
        pop ds
        mov si, extra_types
        mov cx, TYPES_EXTRA * TYPE_SIZE
        cld
        rep movsb
        pop es
        pop ds
        pop di
        pop si
        pop dx
        pop cx
        pop ax
.out:   iret
; the types, numbered from the game's count (115): the companion's (the same as TYPES in
; dscompanion/npcitems.py), the rest unused
extra_types:
        ; a metal short sword: the metal long sword's type (63), 1d6
        db 0x01, 0x00, 0x30, 0x00, 0x1E, 0x00, 0xFA, 0x00, 0x04, 0x05, 0x01, 0x01
        db 0x06, 0x01, 0x00, 0x00, 0x72, 0x16, 0x00, 0x01
        ; a cloak of protection: the Cloak's type (65), no material shown, its plus counting
        ; for AC (bit 80h of +0Fh, as armour's) with an AC of its own of 0
        db 0x00, 0x00, 0x00, 0x00, 0x0A, 0x00, 0x0A, 0x00, 0x40, 0x08, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x80, 0xFF, 0x1F, 0x00, 0x01
        ; a bone helm: the Helm's type (5), of bone (to go with the bone scale armour), and like
        ; it for the classes that can wear it (+10h: no thieves, where the Helm allows them)
        db 0x00, 0x00, 0x00, 0x00, 0x0F, 0x00, 0xFA, 0x00, 0x01, 0x06, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x80, 0x6F, 0x12, 0x00, 0x00
        ; a bone short sword (a new warrior's, weaponchoice.py): the short sword's, of bone (+8),
        ; half its weight (+4), for the classes of the bone long sword and psionicists (+10h)
        db 0x01, 0x00, 0x30, 0x00, 0x0F, 0x00, 0xFA, 0x00, 0x01, 0x05, 0x01, 0x01
        db 0x06, 0x01, 0x00, 0x00, 0x78, 0x17, 0x00, 0x01
        ; a bone axe: the Axe's (22), of bone, half its weight, for the bone weapons' classes (a
        ; water cleric's, not an earth cleric's)
        db 0x01, 0x00, 0x10, 0x00, 0x23, 0x00, 0xFA, 0x00, 0x01, 0x05, 0x01, 0x01
        db 0x08, 0x01, 0x00, 0x00, 0x78, 0x17, 0x00, 0x01
        ; an obsidian short sword: the short sword's, of obsidian, for the obsidian long sword's
        ; classes and psionicists
        db 0x01, 0x00, 0x30, 0x00, 0x1E, 0x00, 0xFA, 0x00, 0x03, 0x05, 0x01, 0x01
        db 0x06, 0x01, 0x00, 0x00, 0x7E, 0x17, 0x00, 0x01
        ; an obsidian axe: the Axe's, of obsidian, for the obsidian weapons' classes
        db 0x01, 0x00, 0x10, 0x00, 0x46, 0x00, 0xFA, 0x00, 0x03, 0x05, 0x01, 0x01
        db 0x08, 0x01, 0x00, 0x00, 0x7E, 0x17, 0x00, 0x01
        ; a plain metal short sword: the first's (Kurzak's, which is his alone: Shadowseeker)
        db 0x01, 0x00, 0x30, 0x00, 0x1E, 0x00, 0xFA, 0x00, 0x04, 0x05, 0x01, 0x01
        db 0x06, 0x01, 0x00, 0x00, 0x72, 0x16, 0x00, 0x01
        ; bracers of defense (BRACERS): the cloak of protection's, worn on the arms (+9: 3, the
        ; arm armour's slot), their plus counting for AC; not armour to anything else here
        db 0x00, 0x00, 0x00, 0x00, 0x0A, 0x00, 0x0A, 0x00, 0x40, 0x03, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x80, 0xFF, 0x1F, 0x00, 0x01
        ; metal versions of the game's plain weapons that have none (dscompanion/worldgear.py):
        ; each its type of metal (+8: 4), for the metal long sword's clerics (+10h's low nibble:
        ; 2, earth's): the Dagger's (33)
        db 0x01, 0x00, 0x20, 0x00, 0x0A, 0x00, 0xFA, 0x00, 0x04, 0x05, 0x01, 0x01
        db 0x04, 0x01, 0x00, 0x00, 0xF2, 0x1F, 0x00, 0x00
        ; the Mace's (20)
        db 0x01, 0x00, 0x08, 0x00, 0x64, 0x00, 0xFA, 0x00, 0x04, 0x05, 0x01, 0x01
        db 0x06, 0x01, 0x01, 0x00, 0x72, 0x16, 0x00, 0x01
        ; the Great Axe's (2)
        db 0x01, 0x00, 0x10, 0x00, 0x46, 0x00, 0xFA, 0x00, 0x04, 0x05, 0x01, 0x01
        db 0x0A, 0x01, 0x00, 0x40, 0x62, 0x16, 0x00, 0x02
        ; the pick's (112)
        db 0x01, 0x00, 0x20, 0x00, 0x28, 0x00, 0xFA, 0x00, 0x04, 0x05, 0x01, 0x01
        db 0x04, 0x01, 0x01, 0x00, 0x72, 0x17, 0x00, 0x00
        ; the Polearm's (19)
        db 0x01, 0x00, 0x30, 0x00, 0x96, 0x00, 0xFA, 0x00, 0x04, 0x05, 0x01, 0x01
        db 0x0A, 0x01, 0x00, 0x40, 0x72, 0x16, 0x00, 0x06
        ; a circlet and a crown (dscompanion/worldgear.py): worn on the head (+9: 6), as the
        ; Necklace's (36) of no material, not armour (+0Fh: no 80h), for every class
        db 0x00, 0x00, 0x00, 0x00, 0x01, 0x00, 0xFA, 0x00, 0x40, 0x06, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x00, 0xFF, 0x1F, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x00, 0x05, 0x00, 0xFA, 0x00, 0x40, 0x06, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x00, 0xFF, 0x1F, 0x00, 0x00
        ; plate mail (the Warden's Plate, dscompanion/worldgear.py): the Chain's chest, arm and
        ; leg armour (57, 58, 59), heavier (+4: 250, 75, 75), AC 3, 2, 2 (+12h; chain's 2, 2, 1)
        db 0x00, 0x00, 0x00, 0x00, 0xFA, 0x00, 0xFA, 0x00, 0x04, 0x01, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x80, 0x6F, 0x12, 0x03, 0x01
        db 0x00, 0x00, 0x00, 0x00, 0x4B, 0x00, 0xFA, 0x00, 0x04, 0x03, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x80, 0x6F, 0x12, 0x02, 0x00
        db 0x00, 0x00, 0x00, 0x00, 0x4B, 0x00, 0xFA, 0x00, 0x04, 0x0A, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x80, 0x6F, 0x12, 0x02, 0x00
        ; the Cloak and Boots of Elvenkind (ELVEN_CLOAK, ELVEN_BOOTS: dscompanion/worldgear.py,
        ; their stealth stealth.py): the Cloak's (65) and the Boots' (68), for thieves and rangers
        ; only (+10h: 600h, their class bits; a multiclass with either may wear them)
        db 0x00, 0x00, 0x00, 0x00, 0x0A, 0x00, 0x0A, 0x00, 0x05, 0x08, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x00, 0x00, 0x06, 0x00, 0x01
        db 0x00, 0x00, 0x00, 0x00, 0x01, 0x00, 0x0A, 0x00, 0x85, 0x04, 0x00, 0x00
        db 0x00, 0x00, 0x00, 0x00, 0x00, 0x06, 0x00, 0x00
        ; a metal dagger air clerics may use too (Galefang, dscompanion/worldgear.py): the metal
        ; Dagger's, with the air cleric's class bit (+10h: 1FF3h; fire and water clerics still not)
        db 0x01, 0x00, 0x20, 0x00, 0x0A, 0x00, 0xFA, 0x00, 0x04, 0x05, 0x01, 0x01
        db 0x04, 0x01, 0x00, 0x00, 0xF3, 0x1F, 0x00, 0x00
        ; bone and obsidian great axes (dscompanion/weaponchoice.py, worldgear.py): the metal one's,
        ; of bone (+8: 1; lighter, +4: 50, still too heavy to backstab with; water clerics, 1668h)
        ; and of obsidian (+8: 3; earth, fire and water clerics, 166Eh)
        db 0x01, 0x00, 0x10, 0x00, 0x32, 0x00, 0xFA, 0x00, 0x01, 0x05, 0x01, 0x01
        db 0x0A, 0x01, 0x00, 0x40, 0x68, 0x16, 0x00, 0x02
        db 0x01, 0x00, 0x10, 0x00, 0x46, 0x00, 0xFA, 0x00, 0x03, 0x05, 0x01, 0x01
        db 0x0A, 0x01, 0x00, 0x40, 0x6E, 0x16, 0x00, 0x02
        ; a bone dagger (dscompanion/weaponchoice.py, worldgear.py): the obsidian Dagger's (17), of
        ; bone (+8: 1), for water clerics and not fire ones (+10h: 1FFAh; the game's daggers 1FF6h)
        db 0x01, 0x00, 0x20, 0x00, 0x0A, 0x00, 0xFA, 0x00, 0x01, 0x05, 0x01, 0x01
        db 0x04, 0x01, 0x00, 0x00, 0xFA, 0x1F, 0x00, 0x00
; the names, numbered from NAMES_OWN (322): the companion's items' (the same as the companion's
; NAMES in dscompanion/names.py), the rest blank until it writes more
extra_names:
        db "Ring/Protection"
        times NAME_SIZE - 15 db 0
        db "Thieves' Tools"
        times NAME_SIZE - 14 db 0
        db "Short Sword"                ; (Kurzak's, of type TYPES' first)
        times NAME_SIZE - 11 db 0
        db "Cloak/Protectn"             ; (Pehtucl's, the game's way of shortening)
        times NAME_SIZE - 14 db 0
        db "Ring/Protection"            ; (Pehtucl's ring: the arena's is the first; their icons differ)
        times NAME_SIZE - 15 db 0
        db "Shadowseeker"               ; (Kurzak's Short Sword made +1: MAGIC_ARMS)
        times NAME_SIZE - 12 db 0
        db "Kreenfang"                  ; (the arena's Bone Gythka made +1)
        times NAME_SIZE - 9 db 0
        db "Bracers/Defense"            ; (bracers of defense: BRACERS, dscompanion/worldgear.py)
        times NAME_SIZE - 15 db 0
        db "Gutterknot"                 ; (the magic weapons of dscompanion/worldgear.py: a club +1,
        times NAME_SIZE - 10 db 0
        db "Deepbiter"                  ; a stone pick +1,
        times NAME_SIZE - 9 db 0
        db "Windlash"                   ; a staff sling +1,
        times NAME_SIZE - 8 db 0
        db "Greenbright"                ; a metal short sword +2)
        times NAME_SIZE - 11 db 0
        db "Arrowbane"                  ; (the circlet and the crown of dscompanion/worldgear.py)
        times NAME_SIZE - 9 db 0
        db "Sunking Crown"
        times NAME_SIZE - 13 db 0
        db "Warden's Chest"             ; (the Warden's Plate of dscompanion/worldgear.py)
        times NAME_SIZE - 14 db 0
        db "Warden's Arms"
        times NAME_SIZE - 13 db 0
        db "Warden's Legs"
        times NAME_SIZE - 13 db 0
        db "Warden's Helm"
        times NAME_SIZE - 13 db 0
        db "Cloak/Elvenkind"            ; (the Cloak and Boots of Elvenkind)
        times NAME_SIZE - 15 db 0
        db "Boots/Elvenkind"
        times NAME_SIZE - 15 db 0
        db "Flame Blade"                ; (an obsidian long sword +1, Focus Heat on what it hits)
        times NAME_SIZE - 11 db 0
        db "Tome/Understand"            ; (the Tome of Understanding: dscompanion/tome.py)
        times NAME_SIZE - 15 db 0
        db "Inixhide"                   ; (Legcrusher's Leather Chest Armor +1: dscompanion/npcitems.py)
        times NAME_SIZE - 8 db 0
        db "Drakejaw"                   ; (the magic axes of dscompanion/worldgear.py: a bone axe +1,
        times NAME_SIZE - 8 db 0
        db "Glasshewer"                 ; an obsidian axe +2,
        times NAME_SIZE - 10 db 0
        db "Headsman"                   ; a metal great axe +2)
        times NAME_SIZE - 8 db 0
        db "Galefang"                   ; (more magic weapons of dscompanion/worldgear.py: an air
        times NAME_SIZE - 8 db 0        ; cleric's dagger +2,
        db "Mindshard"                  ; an obsidian short sword +1,
        times NAME_SIZE - 9 db 0
        db "Stillwater"                 ; a bone short sword +1,
        times NAME_SIZE - 10 db 0
        db "Linebreaker"                ; a metal polearm +2,
        times NAME_SIZE - 11 db 0
        db "Thornwall"                  ; a bone polearm +1)
        times NAME_SIZE - 9 db 0
        times (NAMES_EXTRA - 31) * NAME_SIZE db 0

; STEALTH (RULE_STEALTH): a thief who starts a turn with no enemy next to them may hide in
; shadows and move silently up to someone; the companion rolls both and, when both succeed,
; sets the thief's bit in STEALTH. Their next attack then counts as one from behind, and so
; as a backstab when the game's own conditions for one hold; the bit is cleared (attacking
; gives the thief away).
;
; PROBE_STEALTH: INT VEC_STEALTH replaces "push word [bp-1Ah]" (3 bytes: INT + NOP) in the
; routine that sets up an attack, straight after it has worked out whether the attacker (SI)
; is behind the target (DI) and whether that makes a backstab: [BP-1Ah] from behind (+2 to
; hit, the target's DEX and shield don't count), [BP-24h] a backstab (+2 more, and the damage
; multiplied), [BP-20h] the THAC0 they lower; [BP-1Ch] set when the target is an object, which
; has no back. Does the push (under the interrupt's return frame).
probe_stealth:
        pop word [cs:s_ip]
        pop word [cs:s_cs]
        pop word [cs:s_fl]
        test byte [cs:rules], RULE_STEALTH
        jz .push
        cmp si, 3
        ja .push
        btr word [cs:stealth], si
        jnc .push
        inc word [cs:stealth_used]
        cmp word [bp-0x1C], 0
        jne .push
        push ax
        push bx
        push es
        cmp word [bp-0x1A], 0
        jne .stab
        mov word [bp-0x1A], 1
        sub word [bp-0x20], 2
.stab:  cmp word [bp-0x24], 0
        jne .done
        ; the game's own conditions: a thief (the sheet's word +12h, bit 400h), in melee
        ; ([BP+0Eh] 1), with a weapon whose type weighs 40 or less (the type's word +4)
        mov ax, [bp-0x12]
        imul ax, ax, 0x47
        les bx, [0x1661]
        add bx, ax
        test word [es:bx+0x12], 0x400
        jz .done
        cmp word [bp+0x0E], 1
        jne .done
        mov ax, [bp-4]
        imul ax, ax, 0x14
        les bx, [0x1669]
        add bx, ax
        cmp word [es:bx+4], 0x28
        jg .done
        sub word [bp-0x20], 2
        mov word [bp-0x24], 1
.done:  pop es
        pop bx
        pop ax
.push:  push word [bp-0x1A]
        push word [cs:s_fl]
        push word [cs:s_cs]
        push word [cs:s_ip]
        iret
s_ip    dw 0
s_cs    dw 0
s_fl    dw 0

L_LINE_SIZE equ 24
LOOK_SIZE   equ 80
LOOK_FULL_SIZE equ 700
l_row   dw 0
l_draw  dd 0
l_win   dd 0
l_line  times L_LINE_SIZE db 0
l_side  db 0                    ; the line being drawn goes beside LEVEL (LOOK_SIDE)
look_pending db 0
look_text times LOOK_SIZE db 0
look_full times LOOK_FULL_SIZE db 0

USE_X      equ 0x96             ; the panel under the spells (window coordinates): its top,
USE_FIRST_Y equ 0x6C            ; three lines above where the icons of usable items (fruit,
USE_STEP   equ 7                ; wands...) go, along the panel's bottom from 0x81
USE_LAST_Y equ 0x7A
u_line  times SLOTS_SIZE db 0
slots_text times 4 * SLOTS_SIZE db 0
msg_buf times MSG_SIZE db 0

tput:                           ; AL -> text buffer at position BX
        push bx
        and bx, TSIZE - 1
        mov [cs:tbuf+bx], al
        pop bx
        inc bx
        ret

tputw:
        call tput
        mov al, ah
        jmp tput

t_ret   dw 0
t_ip    dw 0
t_cs    dw 0
t_fl    dw 0
kind    dw 0
extra   dw 0
SPELL_SEG equ 2 + 0x79BB7 - 0x79A85  ; return address - (DSUN.EXE offsets: patch, mov ax's operand)

; SHADOWS (SHADOWS_ON): every figure the companion marks (SHADOW_TAB) casts a see-through shadow
; on the floor, its outline laid down toward the lower right (the light on Athas's maps comes from
; the upper left, as the walls' and bones' shadows show), drawn after the floor and before anything else, so
; walls and figures, its own and everyone else's, stand on it.
;
; The game draws a view of the map into a page of its planar (mode X) video memory: first the floor
; (DSUN.EXE 2700Eh: all of the view; 27162h: a rectangle of it), then, row by row, the walls and
; things standing on it and the figures (1DBD:0002). The floor routines are hooked at their start
; (PROBE_FLOOR): their way back is pointed at FLOOR_POST, which draws the shadows over the floor
; they have just drawn, clipped to it, then goes back (not when the routine draws nothing: then
; whatever is there was shadowed already). Each pixel of a shadow is darkened once, by DARK (each
; colour's nearest darker one, at 3/4 strength, among the colours the game doesn't animate). A
; figure's outline is its picture's: the runs of each row of the frame it is drawn with (no colours
; needed), its picture loaded into the cache first if it isn't in memory (as drawing it is about to;
; in a fight most aren't till then).
;
; A figure that moves is drawn again within a rectangle round where it was and where it is; its
; shadow reaches beyond its picture, so the routines redrawing rectangles (PROBE_REDRAW: 2475Fh, a
; rectangle; PROBE_REDRAW_ALL: 24B18h, the one round everything that moved) make theirs bigger by
; as far as a shadow reaches (SHADOW_RIGHT, SHADOW_DOWN), or the old shadow would be left behind.

SHADOW_ROWS  equ 64             ; rows of a figure, up from its feet, that cast a shadow
SHADOW_RIGHT equ 72             ; how far right a shadow reaches past its figure (SHADOW_ROWS * 1.05,
                                ;   and a step of 4: rectangles are kept to whole bytes)
SHADOW_DOWN  equ 44             ; and how far down (SHADOW_ROWS / 2, and a figure's height off the
                                ;   ground)
PAGES_SEG    equ 0x0980         ; (DSUN.EXE segments, less the load segment) the page routines',
                                ;   whose code segment holds the pages' table
CACHE_SEG    equ 0x3E60         ; the picture cache's table: 16 bytes a slot
OBJ_LIST     equ 0x6690         ; DS: far pointer to the things on the map, 8 bytes each (x, y,
OBJ_COUNT    equ 0x1F72         ;   height, flags, which thing), and how many
MAP_THINGS   equ 0x6694         ; DS: each thing on the map, 32 bytes (sprites.py)
MAP_COUNT    equ 520            ; how many things the game has
CACHE_LAST   equ 0x1F84         ; DS: the last slot of the picture cache
FLOOR_ON     equ 0x2F44         ; DS: 0 when the floor routines draw nothing
LOADER_SEG   equ 0x1DF3         ; (DSUN.EXE 25D6Ah) the routine that loads a picture into the cache
LOADER_OFF   equ 0x2A3A         ;   (slot, 0), as drawing one does when it isn't in memory
FIGURE_MOST  equ 128            ; (the widest and tallest a figure's picture may be, for the test of
                                ;   whether its shadow can be where the floor was drawn)

; PROBE_REDRAW: INT VEC_REDRAW replaces "push bp / mov bp,sp / sub sp,8" (6 bytes: INT + 4 NOPs)
; at the start of the routine that draws a rectangle of the view again (camera x, y, page, x0,
; y0, x1, y1, ...): with shadows on, the rectangle reaches further right and down.
probe_redraw:
        push bp
        mov bp, sp
        cmp word [cs:shadows_on], 0
        jne .wide
        cmp word [cs:rings_on], 0
        je .go
.wide:
        add word [bp+22], SHADOW_RIGHT  ; x1 (after BP, the interrupt's IP, CS and flags, and the
        add word [bp+24], SHADOW_DOWN   ;   caller's way back); y1 (the routine keeps both on screen)
.go:    pop bp
        pop word [cs:resume]
        pop word [cs:resume + 2]
        popf
        push bp
        mov bp, sp
        sub sp, 8
        jmp far [cs:resume]

; PROBE_REDRAW_ALL: INT VEC_REDRAW_ALL replaces "push word [bp+8] / push word [bp+6]" (6 bytes:
; INT + 4 NOPs) where the routine drawing again what moved, having made the rectangle round it all
; (x1 at [BP-0Ah], y1 at [BP-0Eh]), starts to draw: with shadows on, it reaches further right and down.
probe_redraw_all:
        pop word [cs:resume]
        pop word [cs:resume + 2]
        pop word [cs:resume_fl]
        cmp word [cs:shadows_on], 0
        jne .wide
        cmp word [cs:rings_on], 0
        je .dust
.wide:  add word [bp-0x0A], SHADOW_RIGHT
        cmp word [bp-0x0A], 0x13F       ; (the routine draws to x1 + 1: kept on screen)
        jle .x
        mov word [bp-0x0A], 0x13F
.x:     add word [bp-0x0E], SHADOW_DOWN
        cmp word [bp-0x0E], 0xC7
        jle .dust
        mov word [bp-0x0E], 0xC7
.dust:  call dust_union
.go:    push word [bp+8]
        push word [bp+6]
        push word [cs:resume_fl]
        push word [cs:resume + 2]
        push word [cs:resume]
        iret

; PROBE_FLOOR: INT VEC_FLOOR_ALL and INT VEC_FLOOR_RECT replace "push bp / mov bp,sp / sub sp,N"
; (6 bytes: INT + 4 NOPs) at the start of the routines drawing the floor of the view (page, camera
; x, y) and of a rectangle of it (page, x0, y0, x1 + 1, y1, camera x, y). With shadows on, their
; way back is FLOOR_POST.
probe_floor_all:
        mov word [cs:floor_frame], 0x1C
        mov byte [cs:floor_kind], 0
        jmp floor_enter
probe_floor_rect:
        mov word [cs:floor_frame], 0x1E
        mov byte [cs:floor_kind], 1
floor_enter:
        pop word [cs:resume]
        pop word [cs:resume + 2]
        popf
        cmp word [cs:shadows_on], 0
        jne .on
        cmp word [cs:dust_on], 0
        jne .on
        cmp word [cs:rings_on], 0
        je .plain
.on:    cmp byte [cs:floor_busy], 0
        jne .plain
        cmp word [FLOOR_ON], 0          ; (DS: the game's) the routine draws nothing without it
        je .plain
        mov byte [cs:floor_busy], 1
        push bp
        mov bp, sp
        push ax
        mov ax, [bp+2]                  ; the caller's way back, kept, and FLOOR_POST in its place
        mov [cs:floor_ret], ax
        mov ax, [bp+4]
        mov [cs:floor_ret + 2], ax
        mov word [bp+2], floor_post
        mov [bp+4], cs
        pop ax
        pop bp
.plain: push bp
        mov bp, sp
        sub sp, [cs:floor_frame]
        jmp far [cs:resume]

; the floor drawn: the shadows on it, then back to the caller (its arguments still on the stack)
floor_post:
        pushf
        pushad
        push ds
        push es
        mov bp, sp
        call shadow_pass
        pop es
        pop ds
        popad
        popf
        mov byte [cs:floor_busy], 0
        jmp far [cs:floor_ret]

F_ARGS equ 38                   ; (FLOOR_POST's frame: ES, DS, PUSHAD, flags, then the arguments)

shadow_pass:
        cld
        inc word [cs:shadow_passes]
        mov [cs:game_ds], ds
        mov ax, ds
        sub ax, DGROUP_SEG
        mov [cs:load_seg], ax
        ; the page: where it is (its bounds, the clip), and the page it is part of (its memory)
        add ax, PAGES_SEG
        mov es, ax
        mov bx, [bp + F_ARGS]
        shl bx, 1
        mov ax, [es:bx + 0x404]
        mov [cs:clip_x0], ax
        mov ax, [es:bx + 0x604]
        mov [cs:clip_y0], ax
        mov ax, [es:bx + 0x804]
        mov [cs:clip_x1], ax
        mov ax, [es:bx + 0xA04]
        mov [cs:clip_y1], ax
.parent:
        test word [es:bx + 0xC04], 0x40
        jz .root
        mov bx, [es:bx + 4]
        jmp .parent
.root:  mov ax, [es:bx + 4]
        mov [cs:v_seg], ax
        mov ax, [es:bx + 0x604]
        mov [cs:v_y0], ax
        mov ax, [es:bx + 0x404]
        shr ax, 2
        mov [cs:v_x0], ax
        mov cx, [es:bx + 0x804]
        shr cx, 2
        sub cx, ax
        inc cx
        mov [cs:v_row], cx
        ; the camera, and for a rectangle's floor the rectangle
        cmp byte [cs:floor_kind], 0
        jne .rect
        mov ax, [bp + F_ARGS + 2]
        mov [cs:cam_x], ax
        mov ax, [bp + F_ARGS + 4]
        mov [cs:cam_y], ax
        jmp .clipped
.rect:  mov ax, [bp + F_ARGS + 10]
        mov [cs:cam_x], ax
        mov ax, [bp + F_ARGS + 12]
        mov [cs:cam_y], ax
        mov ax, [bp + F_ARGS + 2]       ; x0
        cmp ax, [cs:clip_x0]
        jle .rx1
        mov [cs:clip_x0], ax
.rx1:   mov ax, [bp + F_ARGS + 6]       ; x1 + 1
        dec ax
        cmp ax, [cs:clip_x1]
        jge .ry0
        mov [cs:clip_x1], ax
.ry0:   mov ax, [bp + F_ARGS + 4]       ; y0
        cmp ax, [cs:clip_y0]
        jle .ry1
        mov [cs:clip_y0], ax
.ry1:   mov ax, [bp + F_ARGS + 8]       ; y1
        cmp ax, [cs:clip_y1]
        jge .clipped
        mov [cs:clip_y1], ax
.clipped:
        cmp word [cs:dark_build], 0
        je .dark
        call build_dark
        jc .dark                        ; (not yet: the screen is fading)
        mov word [cs:dark_build], 0
.dark:  call vga_save
        cmp word [cs:shadows_on], 0
        je .dust
        cmp byte [cs:dark_ready], 0
        je .dust                        ; (no colours to darken with yet)
        mov ds, [cs:game_ds]
        les di, [OBJ_LIST]
        mov cx, [OBJ_COUNT]
        jcxz .done
.thing: push cx
        push di
        push es
        call shadow_of
        pop es
        pop di
        pop cx
        add di, 8
        loop .thing
.dust:  call ring_pass
        call dust_pass
.done:  call vga_restore
        ret

; the shadow of the thing at ES:DI (DS = the game's), if it casts one
shadow_of:
        mov bx, [es:di + 6]
        cmp bx, MAP_COUNT - 1
        ja .no                          ; (none)
        cmp byte [cs:shadow_tab + bx], 0
        je .no
        shl bx, 5
        add bx, MAP_THINGS
        test byte [bx], 0x80
        jnz .no                         ; (not drawn)
        mov si, [bx + 0x0F]             ; its picture's place in the cache
        cmp si, 0xFFFF
        je .no
        cmp si, [CACHE_LAST]
        ja .no
        ; where its picture is drawn (as the game works it out), on the ground
        mov al, [es:di + 5]
        mov [cs:mirror], al
        xor dx, dx
        test al, 0x20
        jz .fixed
        mov ax, [es:di]
        mov dl, [bx + 7]
        sub ax, dx
        sub ax, [cs:cam_x]
        mov [cs:fig_x], ax
        mov ax, [es:di + 2]
        mov dl, [bx + 8]
        sub ax, dx                      ; (its height off the ground left out: the shadow is on it)
        sub ax, [cs:cam_y]
        mov [cs:fig_y], ax
        jmp .frame
.fixed: mov ax, [bx + 3]
        sub ax, [cs:cam_x]
        mov [cs:fig_x], ax
        mov ax, [bx + 5]
        sub ax, [cs:cam_y]
        mov [cs:fig_y], ax
.frame: mov al, [bx + 0x11]
        xor ah, ah
        mov [cs:frame], ax
        ; (none of its shadow where the floor was drawn: nothing to do)
        mov ax, [cs:fig_x]
        cmp ax, [cs:clip_x1]
        jg .no
        add ax, FIGURE_MOST + SHADOW_RIGHT
        cmp ax, [cs:clip_x0]
        jl .no
        mov ax, [cs:fig_y]
        cmp ax, [cs:clip_y1]
        jg .no
        add ax, FIGURE_MOST + SHADOW_DOWN
        cmp ax, [cs:clip_y0]
        jl .no
        ; its picture, if in the cache
        mov ax, [cs:load_seg]
        add ax, CACHE_SEG
        mov es, ax
        shl si, 4
        cmp dword [es:si + 6], 0
        jl .no
        test byte [es:si + 0x0E], 2
        jnz .loaded
        shr si, 4                       ; (not in memory: loaded, as drawing it is about to)
        push si
        mov ax, [cs:load_seg]
        add ax, LOADER_SEG
        mov [cs:loader + 2], ax
        push word 0
        push si
        call far [cs:loader]
        add sp, 4
        pop si
        or ax, ax
        jz .no
        mov ax, [cs:load_seg]
        add ax, CACHE_SEG
        mov es, ax
        shl si, 4
.loaded:
        les si, [es:si + 0x0A]
        mov bx, [cs:frame]
        cmp bx, [es:si + 4]
        jae .no
        shl bx, 2
        movzx eax, word [cs:zero]       ; (EAX: the frame's address, linear)
        mov ax, es
        shl eax, 4
        movzx ecx, si
        add eax, ecx
        add eax, [es:si + bx + 6]
        mov si, ax
        and si, 0x0F
        shr eax, 4
        mov es, ax
        ; the frame: width, height, then its rows
        mov ax, [es:si]
        mov [cs:f_w], ax
        mov ax, [es:si + 2]
        dec ax
        mov [cs:f_bottom], ax
        add si, 4
.row:   mov al, [es:si]
        inc si
        cmp al, 0xFF
        je .drawn
        xor ah, ah
        mov [cs:f_y], ax
.run:   mov ax, [es:si]                 ; a run: x (8000h: the row's last), its pixels, its data
        mov [cs:r_x], ax
        mov cl, [es:si + 2]
        mov [cs:r_n], cl
        mov dl, [es:si + 3]
        xor dh, dh
        add si, 4
        add si, dx
        mov ax, [cs:f_bottom]
        sub ax, [cs:f_y]                ; how far up from the feet
        jl .next
        cmp ax, SHADOW_ROWS
        jg .next
        test al, 1
        jnz .next                       ; (every other row: the shadow is half as tall)
        push es
        push si
        call cast_run
        pop si
        pop es
.next:  test byte [cs:r_x + 1], 0x80
        jz .run
        jmp .row
.drawn: push es                         ; (its runs: drawn)
        call spans_flush
        pop es
.no:    ret

; the shadow of the current run, AX rows up from the feet
cast_run:
        mov bx, ax
        shr bx, 1
        add bx, [cs:f_bottom]
        add bx, [cs:fig_y]              ; BX: the screen row it falls on
        mov dx, [cs:r_x]
        and dx, 0x7FFF
        xor ch, ch
        mov cl, [cs:r_n]
        test byte [cs:mirror], 0x80
        jz .lean
        mov di, [cs:f_w]                ; (drawn mirrored: from the other side)
        sub di, dx
        sub di, cx
        mov dx, di
.lean:  add dx, [cs:fig_x]
        mov di, ax                      ; leaning right by 1.05 times as far as it is up
        add ax, 10
        push dx
        xor dx, dx
        push bx
        mov bx, 20
        div bx
        pop bx
        pop dx
        add di, ax
        add dx, di                      ; DX: its first x
        mov di, dx
        add di, cx
        dec di                          ; DI: its last
        call darken
        ret

; darken DX..DI on row BX (screen), within the clip: noted (SPANS), and drawn with the figure's
; others a plane at a time (SPANS_FLUSH: the VGA's registers set once a plane, not once a run; a
; figure's runs never overlap, so the order makes no difference)
SPANS_MOST equ 256
darken: cmp bx, [cs:clip_y0]
        jl .out
        cmp bx, [cs:clip_y1]
        jg .out
        cmp dx, [cs:clip_x0]
        jge .x1
        mov dx, [cs:clip_x0]
.x1:    cmp di, [cs:clip_x1]
        jle .span
        mov di, [cs:clip_x1]
.span:  cmp dx, di
        jg .out
        cmp word [cs:n_spans], SPANS_MOST
        jb .room
        push dx
        push di
        push bx
        call spans_flush                ; (full: those so far drawn first)
        pop bx
        pop di
        pop dx
.room:  push dx
        mov ax, bx                      ; the row's place in the page
        sub ax, [cs:v_y0]
        mul word [cs:v_row]
        sub ax, [cs:v_x0]
        pop dx
        mov bx, [cs:n_spans]
        imul bx, bx, 6
        mov [cs:spans + bx], ax
        mov [cs:spans + bx + 2], dx
        mov [cs:spans + bx + 4], di
        inc word [cs:n_spans]
.out:   ret

spans_flush:                            ; the noted runs darkened, a plane at a time
        cmp word [cs:n_spans], 0
        je .ret
        mov es, [cs:v_seg]
        xor cx, cx                      ; the plane
.plane: mov dx, 0x3C4                   ; write to this plane, read from it
        mov al, 2
        mov ah, 1
        shl ah, cl
        out dx, ax
        mov dx, 0x3CE
        mov al, 4
        mov ah, cl
        out dx, ax
        xor si, si
.span:  mov ax, cx
        sub ax, [cs:spans + si + 2]
        and ax, 3
        add ax, [cs:spans + si + 2]     ; the first x on this plane
        mov dx, [cs:spans + si + 4]
        sub dx, ax
        jl .nspan
        shr dx, 2
        inc dx                          ; DX: how many
        shr ax, 2
        add ax, [cs:spans + si]
        mov di, ax
        xor bh, bh
.pix:   mov bl, [es:di]
        mov bl, [cs:dark + bx]
        mov [es:di], bl
        inc di
        dec dx
        jnz .pix
.nspan: add si, 6
        mov ax, [cs:n_spans]
        imul ax, ax, 6
        cmp si, ax
        jb .span
        inc cx
        cmp cx, 4
        jb .plane
        mov word [cs:n_spans], 0
.ret:   ret

n_spans dw 0
spans   times SPANS_MOST * 6 db 0       ; (row's place in the page, first x, last x)

; the VGA's registers the shadows change, kept and put back
vga_save:
        mov dx, 0x3C4
        in al, dx
        mov [cs:sc_index], al
        mov al, 2
        out dx, al
        inc dx
        in al, dx
        mov [cs:sc_mask], al
        mov dx, 0x3CE
        in al, dx
        mov [cs:gc_index], al
        xor bx, bx
.save:  mov al, [cs:gc_regs + bx]
        out dx, al
        inc dx
        in al, dx
        dec dx
        mov [cs:gc_saved + bx], al
        inc bx
        cmp bx, 5
        jb .save
        mov ax, 0x0001                  ; no set/reset, no rotation, all bits, write mode 0
        out dx, ax
        mov ax, 0x0003
        out dx, ax
        mov ah, [cs:gc_saved + 3]
        and ah, 0xF4
        mov al, 5
        out dx, ax
        mov ax, 0xFF08
        out dx, ax
        ret

vga_restore:
        mov dx, 0x3CE
        xor bx, bx
.put:   mov al, [cs:gc_regs + bx]
        mov ah, [cs:gc_saved + bx]
        out dx, ax
        inc bx
        cmp bx, 5
        jb .put
        mov al, [cs:gc_index]
        out dx, al
        mov dx, 0x3C4
        mov al, 2
        mov ah, [cs:sc_mask]
        out dx, ax
        mov al, [cs:sc_index]
        out dx, al
        ret

; DARK: for each colour of the palette as it is now, the nearest to it at 3/4 strength among the
; colours the game doesn't animate (DARK_FIRST-DARK_LAST: 11-15 and 224-255 cycle, for fire and
; water); made again when the area, so the palette, changes
DARK_FIRST equ 16
DARK_LAST  equ 223
DARK_PARTS equ 3                ; (a shadow's strength: 3/4, as the walls' own shadows on the floor)
DARK_WHOLE equ 4
DARK_HUE   equ 4                ; (how much a change of hue counts against a colour)
DARK_LIT   equ 4000             ; (the palette's 768 values, out of 63 each, add up to less in a fade)
build_dark:
        push ds
        push cs
        pop ds
        mov dx, 0x3C7
        xor al, al
        out dx, al
        mov dx, 0x3C9
        mov di, dac
        mov cx, 768
        xor bx, bx                      ; (BX: how bright it is in all)
.read:  in al, dx
        mov [di], al
        xor ah, ah
        add bx, ax
        inc di
        loop .read
        cmp bx, DARK_LIT
        jae .lit
        pop ds                          ; (nearly black: a fade, not the area's colours)
        stc
        ret
.lit:
        xor bx, bx                      ; the colour
.colour:
        mov si, bx
        imul si, si, 3
        add si, dac
        xor di, di                      ; its darker self
.chan:  lodsb
        mov ah, DARK_PARTS
        mul ah
        mov dl, DARK_WHOLE
        div dl
        mov [want + di], al
        inc di
        cmp di, 3
        jb .chan
        mov si, bx                      ; (only clearly darker ones: brightness under 7/8 of its own)
        imul si, si, 3
        add si, dac
        xor ax, ax
        xor dx, dx
        mov di, 3
.own:   lodsb
        add dx, ax
        dec di
        jnz .own
        imul dx, dx, 7
        shr dx, 3
        mov [lit_most], dx
        mov dword [best_d], 0xFFFFFFFF
        mov [best_c], bl
        mov cx, DARK_FIRST
.cand:  mov word [lit_sum], 0
        mov si, cx                      ; how far a colour is from the one wanted: the differences
        imul si, si, 3                  ;   squared, and those between them weighted (DARK_HUE),
        add si, dac                     ;   so that it keeps its hue (sand stays sand, not pink)
        xor di, di
.diff:  lodsb
        mov ah, 0
        add [lit_sum], ax
        sub al, [want + di]
        cbw
        mov [diffs + di], al
        inc di
        cmp di, 3
        jb .diff
        mov ax, [lit_sum]
        cmp ax, [lit_most]
        jae .worse                      ; (not darker enough)
        xor edx, edx
        xor di, di
.sq:    movsx eax, byte [diffs + di]
        imul eax, eax
        add edx, eax
        inc di
        cmp di, 3
        jb .sq
        movsx eax, byte [diffs]
        movsx edi, byte [diffs + 1]
        sub eax, edi
        imul eax, eax
        imul eax, eax, DARK_HUE
        add edx, eax
        movsx eax, byte [diffs + 1]
        movsx edi, byte [diffs + 2]
        sub eax, edi
        imul eax, eax
        imul eax, eax, DARK_HUE
        add edx, eax
        cmp edx, [best_d]
        jae .worse
        mov [best_d], edx
        mov [best_c], cl
.worse: inc cx
        cmp cx, DARK_LAST
        jbe .cand
        mov al, [best_c]
        mov [dark + bx], al
        inc bx
        cmp bx, 256
        jb .colour
        mov byte [dark_ready], 1
        pop ds
        clc
        ret

; SCROLLING (SCROLL_ON): holding the right mouse button and moving scrolls the map with the pointer,
; as if dragging it; a right click (released before the pointer has moved DRAG_START pixels) is
; still the game's (it changes what the pointer does: walk, use, look).
;
; The game is told of the mouse's buttons by the mouse driver calling its handler (INT 33h, 0Ch).
; INT33 puts MOUSE_EVENT in its place, which keeps the right button from the game while it is
; held: if it was a click, the game gets the press and the release when it is let go; if it was a
; drag, nothing. The game's main loop reads where the pointer is (DSUN.EXE 1CAACh, the call to the
; driver's 03h), to scroll the map when it is at the screen's edge; PROBE_SCROLL makes that call
; itself and, while dragging, has the game centre its view where the drag puts it (191F:0281h, as
; clicking on the overview map does: the view is then drawn again), and the pointer is kept off
; the edges. It also scrolls by what the companion adds to PAN_X and PAN_Y.

DRAG_START  equ 4               ; pixels the pointer moves before a right click is a drag
CAM_X       equ 0x1178          ; DS: the view's top left on the map
CAM_Y       equ 0x117A
CAM_X_MOST  equ 0x6C0           ; (as far as the game lets it go: the map less the view)
CAM_Y_MOST  equ 0x558
CENTRE_SEG  equ 0x191F          ; (DSUN.EXE 1E871h) centre the view on (x, y, 1: draw it again)
CENTRE_OFF  equ 0x0281
EVENT_PRESS equ 8               ; the driver's events: right button pressed, released
EVENT_LEAVE equ 16
RIGHT       equ 2               ; the right button, in the buttons held
MIDDLE      equ 4               ; the middle one (pressing the wheel)
EVENT_MID_PRESS equ 0x20
EVENT_MID_LEAVE equ 0x40
SCROLL_MIDDLE equ 1             ; SCROLL_ON's bits: dragging with the wheel pressed,
SCROLL_RIGHT  equ 2             ;   with the right button held

int33:
        cmp ax, 0x0C
        jne .chain
        or cx, cx
        jz .chain                       ; (taking the handler away)
        mov [cs:game_handler], dx
        mov [cs:game_handler + 2], es
        mov [cs:game_mask], cx
        push es
        push dx
        push cx
        or cx, 0x7F                     ; (moves, and all three buttons)
        push cs
        pop es
        mov dx, mouse_event
        pushf
        call far [cs:old33]
        pop cx
        pop dx
        pop es
        iret
.chain: jmp far [cs:old33]

mouse_event:                            ; AX = events, BX = buttons held, CX, DX = where
        cmp word [cs:scroll_on], 0
        je .pass
        test byte [cs:scroll_on], SCROLL_MIDDLE
        jz .right
        test al, EVENT_MID_PRESS        ; the wheel pressed: a drag at once (the game has no use
        jz .mheld                       ;   for that button)
        cmp byte [cs:drag], 0
        jne .mheld
        mov byte [cs:drag], 2
        mov byte [cs:drag_mid], 1
        mov byte [cs:drag_new], 1
        mov [cs:drag_x], cx
        mov [cs:drag_y], dx
.mheld: cmp byte [cs:drag_mid], 0
        je .right
        and bx, ~MIDDLE
        test al, EVENT_MID_LEAVE
        jz .mdone
        mov byte [cs:drag], 0
        mov byte [cs:drag_mid], 0
.mdone: and al, ~(EVENT_MID_PRESS | EVENT_MID_LEAVE)
        jmp .pass
.right: test byte [cs:scroll_on], SCROLL_RIGHT
        jz .pass
        test al, EVENT_PRESS
        jz .held
        cmp byte [cs:drag], 0
        jne .held
        mov byte [cs:drag], 1
        mov [cs:drag_x], cx
        mov [cs:drag_y], dx
        and al, ~EVENT_PRESS
.held:  cmp byte [cs:drag], 0
        je .pass
        and bx, ~RIGHT
        cmp byte [cs:drag], 1
        jne .leave
        push ax                         ; moved far enough to be a drag?
        mov ax, cx
        sub ax, [cs:drag_x]
        call .apart
        jnc .y
        mov ax, dx
        sub ax, [cs:drag_y]
        call .apart
        jc .near
.y:     mov byte [cs:drag], 2
        mov byte [cs:drag_new], 1
.near:  pop ax
.leave: test al, EVENT_LEAVE
        jz .pass
        and al, ~EVENT_LEAVE
        cmp byte [cs:drag], 1
        mov byte [cs:drag], 0
        jne .pass                       ; (a drag: the game never knows)
        pusha                           ; a click: pressed and released, where it was pressed
        or bx, RIGHT
        mov cx, [cs:drag_x]
        mov dx, [cs:drag_y]
        mov ax, EVENT_PRESS
        call .give
        popa
        pusha
        mov ax, EVENT_LEAVE
        call .give
        popa
.pass:  call .give
        retf
.give:  and ax, [cs:game_mask]          ; the events the game asked for, to its handler
        jz .none
        pusha
        push ds
        push es
        call far [cs:game_handler]
        pop es
        pop ds
        popa
.none:  ret
.apart: or ax, ax                       ; CF clear when |AX| >= DRAG_START
        jns .pos
        neg ax
.pos:   cmp ax, DRAG_START
        ret

; PROBE_SCROLL: INT VEC_SCROLL replaces the start of "call far 3118:002E" (9Ah 2Eh 00h, then the
; segment: A9h makes it a harmless "test ax,<segment>" the loader may relocate), the game's main
; loop asking where the pointer is (its arguments, far pointers to x and y, on the stack).
probe_scroll:
        pop word [cs:s_resume]
        pop word [cs:s_resume + 2]
        pop word [cs:s_resume_fl]
        add word [cs:s_resume], 3       ; (past the segment)
        push bp
        mov bp, sp
        push es
        push bx
        push cx
        push dx
        push si
        push di
        inc word [cs:main_ticks]
        call dust_tick
        call target_click
        cmp word [cs:view_redraw], 0
        je .read
        mov word [cs:view_redraw], 0
        call view_again
.read:  mov ax, 3
        int 0x33                        ; BX = buttons, CX, DX = where
        cmp word [cs:scroll_on], 0
        je .wheel                       ; (the companion's scrolling, as for a chosen enemy, anyway)
        cmp byte [cs:drag], 2
        jne .wheel
        cmp byte [cs:drag_new], 0
        je .follow
        mov byte [cs:drag_new], 0
        mov ax, [CAM_X]
        mov [cs:cam0_x], ax
        mov ax, [CAM_Y]
        mov [cs:cam0_y], ax
.follow:
        mov ax, [cs:cam0_x]             ; where the drag puts the view: the map moves with it
        sub ax, cx
        add ax, [cs:drag_x]
        mov si, [cs:cam0_y]
        sub si, dx
        add si, [cs:drag_y]
        call pan_to
.wheel: mov ax, [cs:pan_x]              ; the companion's
        sub ax, [cs:pan_x_done]
        mov si, [cs:pan_y]
        sub si, [cs:pan_y_done]
        mov di, ax
        or di, si
        jz .report
        add [cs:pan_x_done], ax
        add [cs:pan_y_done], si
        add [cs:cam0_x], ax             ; (a drag under way goes on from there)
        add [cs:cam0_y], si
        add ax, [CAM_X]
        add si, [CAM_Y]
        call pan_to
.report:
        cmp byte [cs:drag], 0
        je .put
        and bx, ~(RIGHT | MIDDLE)
        cmp cx, 1                       ; (off the edges: no scrolling of the game's own)
        jge .x1
        mov cx, 1
.x1:    cmp cx, 0x13D
        jle .y0
        mov cx, 0x13D
.y0:    cmp dx, 1
        jge .y1
        mov dx, 1
.y1:    cmp dx, 0xC6
        jle .put
        mov dx, 0xC6
.put:   les di, [bp + 2]
        mov [es:di], cx
        les di, [bp + 6]
        mov [es:di], dx
        mov ax, bx
        pop di
        pop si
        pop dx
        pop cx
        pop bx
        pop es
        pop bp
        push word [cs:s_resume_fl]
        push word [cs:s_resume + 2]
        push word [cs:s_resume]
        iret

pan_to:                                 ; the view's top left to (AX, SI) as far as the map goes;
        cmp ax, 0                       ;   keeps CX, DX
        jge .x1
        xor ax, ax
.x1:    cmp ax, CAM_X_MOST
        jle .y0
        mov ax, CAM_X_MOST
.y0:    cmp si, 0
        jge .y1
        xor si, si
.y1:    cmp si, CAM_Y_MOST
        jle .snap
        mov si, CAM_Y_MOST
.snap:  and ax, 0xFFF8                  ; (as the game keeps it)
        and si, 0xFFF8
        cmp ax, [CAM_X]
        jne .go
        cmp si, [CAM_Y]
        je .done
.go:    call centre_on
.done:  ret

centre_on:                              ; the view's top left to (AX, SI), drawn again; keeps CX, DX
        push cx
        push dx
        push bx
        mov bx, ds
        sub bx, DGROUP_SEG - CENTRE_SEG
        mov [cs:centre + 2], bx
        push 1
        add si, 100
        push si
        add ax, 160
        push ax
        call far [cs:centre]
        add sp, 6
        pop bx
        pop dx
        pop cx
        ret

; DUST (DUST_ON): a figure walking raises puffs of dust behind it, which spread, rise a little and
; fade away in about a second (DUST_LIFE ticks of the BIOS clock). Each is drawn on the floor as the
; shadows are (DUST_PASS: before the walls and figures, which stand in it), lightening the ground's
; colours through LIGHT (made by the companion from the palette: 0 for colours that aren't ground
; dust shows on, so only sand and dirt take it), dithered thinner toward its edge and as it fades,
; by a pattern fixed to the map (so a puff looks the same however much of it is drawn again).
;
; DUST_TICK, from the main loop (PROBE_SCROLL), notes how far each figure that casts a shadow
; (SHADOW_TAB) has walked, and raises a puff behind its feet every DUST_STEP pixels. While puffs
; change (each tick of the clock), their ground is to be drawn again: the rectangle the game draws
; again round what moved takes them in (DUST_UNION, from PROBE_REDRAW_ALL), and when no one is
; walking, DUST_NUDGE has the game draw the view again. Nothing of the game's is changed (marking a
; figure changed to have it drawn again can set one in a fight walking again).

DUST_LIFE  equ 18               ; ticks of the BIOS clock (18.2 a second) a puff lasts
DUST_STEP  equ 6                ; pixels walked for each puff
JUMP_MOST  equ 40               ; a move further than this (across or up and down) is a jump, not a walk
PUFFS      equ 24               ; puffs at most at once
PUFF_SIZE  equ 12               ; a puff: x, y (on the map), born (ticks), owner (thing), seed, live
BIOS_TICKS equ 0x46C

dust_tick:                      ; DS = the game's
        cmp word [cs:dust_on], 0
        jne .on
        ret
.on:    push es
        push bp
        xor ax, ax
        mov es, ax
        mov ax, [es:BIOS_TICKS]
        mov [cs:d_now], ax
        cmp ax, [cs:d_last]
        je .walk
        mov [cs:d_last], ax
        mov si, puffs                   ; a tick on: each puff looks different, or is gone
        mov cx, PUFFS
.age:   cmp byte [cs:si + 10], 0
        je .anext
        call puff_dirty
        mov ax, [cs:d_now]
        sub ax, [cs:si + 4]
        cmp ax, DUST_LIFE
        jbe .anext
        mov byte [cs:si + 10], 0        ; (drawn once more, gone: now free)
.anext: add si, PUFF_SIZE
        loop .age
.walk:  les di, [OBJ_LIST]
        mov cx, [OBJ_COUNT]
        or cx, cx
        jz .done
.thing: push cx
        mov bx, [es:di + 6]
        cmp bx, MAP_COUNT - 1
        ja .tnext
        cmp byte [cs:shadow_tab + bx], 0
        je .tnext
        mov [cs:t_thing], bx
        mov ax, [es:di]
        mov [cs:t_x], ax
        mov dx, [es:di + 2]
        mov [cs:t_y], dx
        shl bx, 1
        sub ax, [cs:last_x + bx]
        mov [cs:t_dx], ax
        sub dx, [cs:last_y + bx]
        mov [cs:t_dy], dx
        mov ax, [cs:t_x]
        mov [cs:last_x + bx], ax
        mov ax, [cs:t_y]
        mov [cs:last_y + bx], ax
        mov ax, [cs:t_dx]               ; walked a little (not a jump: an area loaded, someone placed)
        call abs16
        cmp ax, JUMP_MOST
        jae .tnext
        mov cx, ax
        mov ax, [cs:t_dy]
        call abs16
        cmp ax, JUMP_MOST
        jae .tnext
        add ax, cx
        jz .tnext
        mov ax, [cs:d_now]
        mov [cs:d_walked_at], ax
        mov bx, [cs:t_thing]
        add [cs:walked + bx], al
        cmp byte [cs:walked + bx], DUST_STEP
        jb .tnext
        sub byte [cs:walked + bx], DUST_STEP
        cmp byte [cs:walked + bx], DUST_STEP
        jb .one
        mov byte [cs:walked + bx], 0        ; (one puff a tick at most)
.one:
        call puff_raise
.tnext: pop cx
        add di, 8
        dec cx
        jnz .thing
.done:  call dust_nudge
        pop bp
        pop es
        ret

abs16:  or ax, ax
        jns .pos
        neg ax
.pos:   ret

sgn16:  or ax, ax                       ; AX -> -1, 0 or 1
        jz .z
        mov ax, 1
        jg .z
        neg ax
.z:     ret

puff_raise:                             ; behind T_THING's feet at (T_X, T_Y), walking (T_DX, T_DY)
        mov ax, [cs:d_next]             ; the next in turn (the oldest, if all are in use)
        inc word [cs:d_next]
        cmp word [cs:d_next], PUFFS
        jb .slot
        mov word [cs:d_next], 0
.slot:  imul si, ax, PUFF_SIZE
        add si, puffs
        cmp byte [cs:si + 10], 0
        je .free
        call puff_dirty                 ; (its ground drawn again without it)
.free:  xor byte [cs:d_side], 1         ; (the feet in turn: a little to one side, then the other)
        mov ax, [cs:t_dx]
        call sgn16
        mov [cs:t_sx], ax
        mov ax, [cs:t_dy]
        call sgn16
        mov [cs:t_sy], ax
        mov ax, [cs:t_sx]               ; x: behind, and to the side across the way it goes
        imul ax, ax, -3
        add ax, [cs:t_x]
        mov dx, [cs:t_sy]
        shl dx, 1
        cmp byte [cs:d_side], 0
        je .x
        neg dx
.x:     add ax, dx
        mov [cs:si], ax
        mov ax, [cs:t_sy]               ; y: behind, and to the side
        imul ax, ax, -2
        add ax, [cs:t_y]
        mov dx, [cs:t_sx]
        cmp byte [cs:d_side], 0
        jne .y
        neg dx
.y:     add ax, dx
        mov [cs:si + 2], ax
        mov ax, [cs:d_now]
        mov [cs:si + 4], ax
        mov ax, [cs:t_thing]
        mov [cs:si + 6], ax
        inc word [cs:dust_puffs]
        imul ax, [cs:dust_puffs], 0x3D7
        mov [cs:si + 8], ax
        mov byte [cs:si + 10], 1
        call puff_dirty
        ret

puff_dirty:                             ; the puff at CS:SI: its ground to be drawn again (DS = the game's)
        mov ax, [cs:si]
        sub ax, 12
        mov dx, [cs:si + 2]
        sub dx, 12
        cmp byte [cs:d_dirty], 0
        je .first
        cmp ax, [cs:d_x0]
        jge .x1
.first: mov [cs:d_x0], ax
.x1:    add ax, 24
        cmp byte [cs:d_dirty], 0
        je .fx1
        cmp ax, [cs:d_x1]
        jle .y0
.fx1:   mov [cs:d_x1], ax
.y0:    cmp byte [cs:d_dirty], 0
        je .fy0
        cmp dx, [cs:d_y0]
        jge .y1
.fy0:   mov [cs:d_y0], dx
.y1:    add dx, 19
        cmp byte [cs:d_dirty], 0
        je .fy1
        cmp dx, [cs:d_y1]
        jle .mark
.fy1:   mov [cs:d_y1], dx
.mark:  mov byte [cs:d_dirty], 1
        mov ax, [cs:si + 6]
        mov [cs:d_owner], ax
        ret

STILL_TICKS equ 3              ; how long no one has walked (nor the view moved) before DUST_NUDGE
NUDGE_TICKS equ 4               ; ... and between its drawings of the view

; DUST_NUDGE: with puffs to draw again while no one walks and the view stands still (when the
; game draws again round what moved anyway, DUST_UNION taking them in), the view drawn again
; (VIEW_AGAIN) every NUDGE_TICKS, till they are gone
dust_nudge:
        mov ax, [CAM_X]                 ; (the view: when it last moved)
        cmp ax, [cs:d_cam_x]
        jne .cam
        mov ax, [CAM_Y]
        cmp ax, [cs:d_cam_y]
        je .still
.cam:   mov ax, [CAM_X]
        mov [cs:d_cam_x], ax
        mov ax, [CAM_Y]
        mov [cs:d_cam_y], ax
        mov ax, [cs:d_now]
        mov [cs:d_walked_at], ax
        ret
.still: cmp byte [cs:d_dirty], 0
        je .ret
        mov ax, [cs:d_now]
        sub ax, [cs:d_walked_at]
        cmp ax, STILL_TICKS
        jb .ret
        mov ax, [cs:d_now]
        sub ax, [cs:d_nudged]
        cmp ax, NUDGE_TICKS
        jb .ret
        mov ax, [cs:d_now]
        mov [cs:d_nudged], ax
        mov byte [cs:d_dirty], 0
        call view_again
.ret:   ret

; VIEW_AGAIN: the view drawn again, all of it, as the game does when the view is centred where it
; is (DS = the game's). The figures are never marked changed to have them drawn again: in a fight
; that can set one walking again.
view_again:
        push ax
        push bx
        push cx
        push dx
        push si
        mov si, [CAM_Y]
        mov ax, [CAM_X]
        call centre_on
        pop si
        pop dx
        pop cx
        pop bx
        pop ax
        ret

; DUST_UNION: in PROBE_REDRAW_ALL (x0 [BP-8], x1 [BP-0Ah], y0 [BP-0Ch], y1 [BP-0Eh]: on screen),
; the puffs' ground taken in, as far as it is on screen
dust_union:
        cmp byte [cs:d_dirty], 0
        je .ret
        mov byte [cs:d_dirty], 0
        mov ax, [cs:d_x0]
        sub ax, [CAM_X]
        mov cx, [cs:d_x1]
        sub cx, [CAM_X]
        mov dx, [cs:d_y0]
        sub dx, [CAM_Y]
        mov bx, [cs:d_y1]
        sub bx, [CAM_Y]
        cmp cx, 0                       ; (off screen: nothing)
        jl .ret
        cmp ax, 0x13F
        jg .ret
        cmp bx, 0
        jl .ret
        cmp dx, 0xC7
        jg .ret
        cmp ax, 0
        jge .ax
        xor ax, ax
.ax:    and ax, 0xFFFC
        cmp ax, [bp-8]
        jge .cx
        mov [bp-8], ax
.cx:    cmp cx, 0x13F
        jle .cx3
        mov cx, 0x13F
.cx3:   or cx, 3
        cmp cx, [bp-0x0A]
        jle .dx
        mov [bp-0x0A], cx
.dx:    cmp dx, 0
        jge .dx0
        xor dx, dx
.dx0:   cmp dx, [bp-0x0C]
        jge .bx
        mov [bp-0x0C], dx
.bx:    cmp bx, 0xC7
        jle .bx1
        mov bx, 0xC7
.bx1:   cmp bx, [bp-0x0E]
        jle .ret
        mov [bp-0x0E], bx
.ret:   ret

; DUST_PASS: in SHADOW_PASS (the VGA's registers kept; the clip, camera and page known), each puff
dust_pass:
        cmp word [cs:dust_on], 0
        je .ret
        push ds
        xor ax, ax
        mov ds, ax
        mov ax, [BIOS_TICKS]
        pop ds
        mov [cs:d_now], ax
        mov si, puffs
        mov cx, PUFFS
.puff:  cmp byte [cs:si + 10], 0
        je .next
        push cx
        push si
        call puff_draw
        pop si
        pop cx
.next:  add si, PUFF_SIZE
        loop .puff
.ret:   ret

puff_draw:                              ; the puff at CS:SI
        mov ax, [cs:d_now]
        sub ax, [cs:si + 4]
        cmp ax, DUST_LIFE
        jb .young
        ret
.young: mov bx, ax                      ; BX: its age
        shl ax, 3                       ; across: 3, growing to 11
        xor dx, dx
        mov cx, DUST_LIFE
        div cx
        add ax, 3
        mov [cs:d_rx], ax
        imul ax, ax, 9                  ; up and down: a little over half that
        add ax, 8
        shr ax, 4
        mov [cs:d_ry], ax
        mov al, [cs:dust_dens + bx]
        xor ah, ah
        mov [cs:d_dens], ax
        mov al, [cs:dust_core + bx]
        mov [cs:d_core], ax
        imul ax, bx, 5                  ; rising, 5 pixels in all
        xor dx, dx
        div cx
        mov dx, [cs:si + 2]
        sub dx, ax
        mov [cs:d_wy], dx
        mov ax, [cs:si]
        mov [cs:d_wx], ax
        mov ax, [cs:si + 8]
        mov [cs:d_seed], ax
        ; (none of it where the floor was drawn: nothing to do)
        mov ax, [cs:d_wx]
        sub ax, [cs:cam_x]
        mov [cs:d_sx], ax               ; its middle on screen
        mov dx, ax
        sub dx, [cs:d_rx]
        cmp dx, [cs:clip_x1]
        jle .inx
        ret
.inx:   add ax, [cs:d_rx]
        cmp ax, [cs:clip_x0]
        jge .iny
        ret
.iny:   mov ax, [cs:d_wy]
        sub ax, [cs:cam_y]
        mov dx, ax
        sub dx, [cs:d_ry]
        cmp dx, [cs:clip_y1]
        jle .iny1
        ret
.iny1:  add ax, [cs:d_ry]
        cmp ax, [cs:clip_y0]
        jge .cols
        ret
.cols:  mov ax, [cs:clip_x0]            ; the columns within the clip
        sub ax, [cs:d_sx]
        mov dx, [cs:d_rx]
        neg dx
        cmp ax, dx
        jge .from
        mov ax, dx
.from:  mov [cs:d_xfrom], ax
        mov ax, [cs:clip_x1]
        sub ax, [cs:d_sx]
        cmp ax, [cs:d_rx]
        jle .to
        mov ax, [cs:d_rx]
.to:    mov [cs:d_xto], ax
        mov cx, [cs:d_rx]               ; how far out it is across, (x / rx)^2 of 256, by |x|
        imul cx, cx
        xor si, si
.tab:   mov ax, si
        imul ax, ax
        shl ax, 8
        xor dx, dx
        div cx
        mov bx, si
        shl bx, 1
        mov [cs:d_extab + bx], ax
        inc si
        cmp si, [cs:d_rx]
        jbe .tab
        mov ax, [cs:d_ry]
        neg ax
        mov [cs:d_yy], ax
        push bp
.row:   mov ax, [cs:d_yy]
        cmp ax, [cs:d_ry]
        jle .inrow
        pop bp
        ret
.inrow: mov bx, [cs:d_wy]
        add bx, ax
        mov [cs:d_wrow], bx
        sub bx, [cs:cam_y]
        cmp bx, [cs:clip_y0]
        jl .nrow
        cmp bx, [cs:clip_y1]
        jg .nrow
        mov ax, bx                      ; the row's place in the page
        sub ax, [cs:v_y0]
        mul word [cs:v_row]
        sub ax, [cs:v_x0]
        mov [cs:row_at], ax
        mov ax, [cs:d_yy]               ; how far out it is, up and down: (y / ry)^2, of 256
        imul ax, ax
        shl ax, 8
        mov cx, [cs:d_ry]
        imul cx, cx
        xor dx, dx
        div cx
        mov [cs:d_ey], ax
        mov word [cs:d_xmax], -1        ; (the widest |x| inside, on this row)
        xor si, si                      ; for each |x| on this row: how thick (0: outside), and
.thr:   mov bx, si                      ;   whether in the core
        shl bx, 1
        mov ax, [cs:d_extab + bx]       ; and across
        add ax, [cs:d_ey]
        mov bx, si
        shl bx, 1
        mov word [cs:d_thrcore + bx], 0
        cmp ax, 256
        ja .nthr
        mov [cs:d_xmax], si
        cmp ax, [cs:d_core]
        setb byte [cs:d_thrcore + bx + 1]
        imul ax, ax, 154                ; thinner toward the edge
        shr ax, 8
        neg ax
        add ax, 256
        mul word [cs:d_dens]
        shr ax, 8
        mov [cs:d_thrcore + bx], al
.nthr:  inc si
        cmp si, [cs:d_rx]
        jbe .thr
        mov ax, [cs:d_xmax]             ; the row's columns: within the clip and the ellipse
        or ax, ax
        js .nrow
        mov dx, [cs:d_xto]
        cmp dx, ax
        jle .rto
        mov dx, ax
.rto:   mov [cs:d_rto], dx
        neg ax
        mov dx, [cs:d_xfrom]
        cmp dx, ax
        jge .rfrom
        mov dx, ax
.rfrom: mov [cs:d_rfrom], dx
        cmp dx, [cs:d_rto]
        jg .nrow
        mov ax, [cs:d_wrow]             ; the pattern, fixed to the map: the row's part
        imul ax, ax, 0x3B1
        mov [cs:d_rowhash], ax
        mov es, [cs:v_seg]
        mov word [cs:d_plane], 0        ; a plane at a time (the VGA's registers set once for each)
.plane: mov ax, [cs:d_sx]
        add ax, [cs:d_rfrom]
        mov si, [cs:d_plane]
        sub si, ax
        and si, 3
        add si, [cs:d_rfrom]            ; SI: the first column on this plane
        cmp si, [cs:d_rto]
        jg .nplane
        mov cx, [cs:d_plane]
        mov dx, 0x3C4                   ; write to this plane, read from it
        mov al, 2
        mov ah, 1
        shl ah, cl
        out dx, ax
        mov dx, 0x3CE
        mov al, 4
        mov ah, cl
        out dx, ax
        mov di, [cs:d_wx]               ; DI: the column's part of the pattern
        add di, si
        imul di, di, 0x9E5
        mov bp, [cs:d_sx]               ; BP: where it is in the page
        add bp, si
        shr bp, 2
        add bp, [cs:row_at]
.px:    mov bx, si
        or bx, bx
        jns .abs
        neg bx
.abs:   shl bx, 1
        mov cx, [cs:d_thrcore + bx]     ; CL: how thick, CH: whether in the core
        mov ax, di
        xor ax, [cs:d_rowhash]
        add ax, [cs:d_seed]
        mov dx, ax
        shr dx, 7
        xor ax, dx
        imul ax, ax, 0x2C5
        shr ax, 4
        cmp al, cl
        jae .npx
        xor bh, bh                      ; lightened (twice in the core)
        mov bl, [es:bp]
        mov al, [cs:light + bx]
        or al, al
        jz .npx
        or ch, ch
        jz .put
        mov bl, al
        mov ah, [cs:light + bx]
        or ah, ah
        jz .put
        mov al, ah
.put:   mov [es:bp], al
.npx:   add si, 4
        add di, 4 * 0x9E5
        inc bp
        cmp si, [cs:d_rto]
        jle .px
.nplane:
        inc word [cs:d_plane]
        cmp word [cs:d_plane], 4
        jb .plane
.nrow:  inc word [cs:d_yy]
        jmp .row

dust_dens  db 255, 255, 255, 255, 247, 238, 229, 219, 209, 198, 187, 174, 161, 147, 132, 114, 93, 66   ; how thick, by age (of 256)
dust_core  db 140, 132, 125, 117, 109, 101, 93, 86, 78, 70, 62, 54, 46, 39, 31, 23, 15, 7   ; the core (twice as light), by age
d_now      dw 0
d_nudged   dw 0
d_cam_x    dw 0
d_cam_y    dw 0
d_owner    dw 0
d_walked_at dw 0                ; when someone last took a step
d_last     dw 0
d_next     dw 0
d_side     db 0
d_dirty    db 0
d_x0       dw 0
d_x1       dw 0
d_y0       dw 0
d_y1       dw 0
d_rx       dw 0
d_ry       dw 0
d_dens     dw 0
d_core     dw 0
d_wx       dw 0
d_wy       dw 0
d_seed     dw 0
d_yy       dw 0
d_xx       dw 0
d_wrow     dw 0
d_ey       dw 0
d_sx       dw 0
d_xfrom    dw 0
d_xto      dw 0
d_plane    dw 0
d_extab    times 12 dw 0         ; (x / rx)^2 of 256, by |x| (rx at most 11)
d_thrcore  times 12 dw 0         ; on the row being drawn, by |x|: how thick (0: outside), and
                                ;   (high byte) whether in the core
d_xmax     dw 0
d_rfrom    dw 0
d_rto      dw 0
d_rowhash  dw 0
t_thing    dw 0
t_x        dw 0
t_y        dw 0
t_dx       dw 0
t_dy       dw 0
t_sx       dw 0
t_sy       dw 0
puffs      times PUFFS * PUFF_SIZE db 0
last_x     times MAP_COUNT dw 0
last_y     times MAP_COUNT dw 0
walked     times MAP_COUNT db 0
light      times 256 db 0

; RINGS (RINGS_ON): a ring on the ground under each thing RING_TAB marks (the companion marks the
; enemies in a fight, and the one chosen with Tab), drawn in the floor pass as the shadows are, so
; the figures stand in it, the ground's colours reddened through RED (made by the companion from the
; palette): two pixels thick, the chosen one's three, and redder.
ring_pass:
        cmp word [cs:rings_on], 0
        je .ret
        mov ds, [cs:game_ds]
        les di, [OBJ_LIST]
        mov cx, [OBJ_COUNT]
        or cx, cx
        jz .ret
.thing: push cx
        push di
        push es
        mov bx, [es:di + 6]
        cmp bx, MAP_COUNT - 1
        ja .next
        mov al, [cs:ring_tab + bx]
        or al, al
        jz .next
        mov [cs:r_kind], al
        mov ax, [es:di]
        sub ax, [cs:cam_x]
        mov [cs:r_fx], ax
        mov ax, [es:di + 2]
        sub ax, [cs:cam_y]
        mov [cs:r_fy], ax
        mov si, ring_outer
        mov cx, RING_OUTER
        call ring_points
        mov si, ring_inner
        mov cx, RING_INNER
        call ring_points
        cmp byte [cs:r_kind], 2
        jne .next
        mov si, ring_extra
        mov cx, RING_EXTRA
        call ring_points
.next:  pop es
        pop di
        pop cx
        add di, 8
        loop .thing
.ret:   ret

ring_points:                            ; CX points (dx, dy bytes) at CS:SI, round (R_FX, R_FY)
.pt:    push cx
        mov al, [cs:si]
        cbw
        add ax, [cs:r_fx]
        mov bx, ax
        mov al, [cs:si + 1]
        cbw
        add ax, [cs:r_fy]
        add si, 2
        cmp ax, [cs:clip_y0]
        jl .skip
        cmp ax, [cs:clip_y1]
        jg .skip
        cmp bx, [cs:clip_x0]
        jl .skip
        cmp bx, [cs:clip_x1]
        jg .skip
        sub ax, [cs:v_y0]               ; the row's place in the page
        mul word [cs:v_row]
        sub ax, [cs:v_x0]
        mov [cs:row_at], ax
        push si
        call ring_px
        pop si
.skip:  pop cx
        loop .pt
        ret

ring_px:                                ; redden screen x BX on the row at ROW_AT (twice for the chosen)
        mov cx, bx
        and cx, 3
        mov dx, 0x3C4
        mov al, 2
        mov ah, 1
        shl ah, cl
        out dx, ax
        mov dx, 0x3CE
        mov al, 4
        mov ah, cl
        out dx, ax
        shr bx, 2
        add bx, [cs:row_at]
        mov es, [cs:v_seg]
        xor ax, ax
        mov al, [es:bx]
        mov cl, [cs:r_kind]
        inc cl                          ; (reddened twice, the chosen one three times)
.again: mov di, ax
        mov ah, [cs:red + di]
        or ah, ah
        jz .put
        mov al, ah
        xor ah, ah
        dec cl
        jnz .again
.put:   mov [es:bx], al
        ret

RING_OUTER equ 76
RING_INNER equ 68
RING_EXTRA equ 84
ring_outer db 13, 0, 13, 1, 13, 2, 12, 2, 12, 3, 11, 3, 11, 4, 10, 4, 9, 4, 9, 5, 8, 5, 7, 5, 6, 5, 5, 5, 5, 6, 4, 6, 3, 6, 2, 6, 1, 6, 0, 6, 255, 6, 254, 6, 253, 6, 252, 6, 251, 6, 251, 5, 250, 5, 249, 5, 248, 5, 247, 5, 247, 4, 246, 4, 245, 4, 245, 3, 244, 3, 244, 2, 243, 2, 243, 1, 243, 0, 243, 255, 243, 254, 244, 254, 244, 253, 245, 253, 245, 252, 246, 252, 247, 252, 247, 251, 248, 251, 249, 251, 250, 251, 251, 251, 251, 250, 252, 250, 253, 250, 254, 250, 255, 250, 0, 250, 1, 250, 2, 250, 3, 250, 4, 250, 5, 250, 5, 251, 6, 251, 7, 251, 8, 251, 9, 251, 9, 252, 10, 252, 11, 252, 11, 253, 12, 253, 12, 254, 13, 254, 13, 255
ring_inner db 12, 0, 12, 1, 11, 1, 11, 2, 10, 2, 10, 3, 9, 3, 9, 4, 8, 4, 7, 4, 6, 4, 5, 4, 5, 5, 4, 5, 3, 5, 2, 5, 1, 5, 0, 5, 255, 5, 254, 5, 253, 5, 252, 5, 251, 5, 251, 4, 250, 4, 249, 4, 248, 4, 247, 4, 247, 3, 246, 3, 246, 2, 245, 2, 245, 1, 244, 1, 244, 0, 244, 255, 245, 255, 245, 254, 246, 254, 246, 253, 247, 253, 247, 252, 248, 252, 249, 252, 250, 252, 251, 252, 251, 251, 252, 251, 253, 251, 254, 251, 255, 251, 0, 251, 1, 251, 2, 251, 3, 251, 4, 251, 5, 251, 5, 252, 6, 252, 7, 252, 8, 252, 9, 252, 9, 253, 10, 253, 10, 254, 11, 254, 11, 255, 12, 255
ring_extra db 14, 0, 14, 1, 14, 2, 13, 2, 13, 3, 12, 3, 12, 4, 11, 4, 11, 5, 10, 5, 9, 5, 9, 6, 8, 6, 7, 6, 6, 6, 5, 6, 5, 7, 4, 7, 3, 7, 2, 7, 1, 7, 0, 7, 255, 7, 254, 7, 253, 7, 252, 7, 251, 7, 251, 6, 250, 6, 249, 6, 248, 6, 247, 6, 247, 5, 246, 5, 245, 5, 245, 4, 244, 4, 244, 3, 243, 3, 243, 2, 242, 2, 242, 1, 242, 0, 242, 255, 242, 254, 243, 254, 243, 253, 244, 253, 244, 252, 245, 252, 245, 251, 246, 251, 247, 251, 247, 250, 248, 250, 249, 250, 250, 250, 251, 250, 251, 249, 252, 249, 253, 249, 254, 249, 255, 249, 0, 249, 1, 249, 2, 249, 3, 249, 4, 249, 5, 249, 5, 250, 6, 250, 7, 250, 8, 250, 9, 250, 9, 251, 10, 251, 11, 251, 11, 252, 12, 252, 12, 253, 13, 253, 13, 254, 14, 254, 14, 255
r_kind     db 0
r_fx       dw 0
r_fy       dw 0
ring_tab   times MAP_COUNT db 0
red        times 256 db 0

; TARGETING (TARGET_ON): Tab and Shift+Tab, counted for the companion, choose an enemy in a fight
; (its ring brighter: RINGS); Enter attacks it, as a click on it does, even where another figure
; stands in front of it. The game reads keys through INT 16h: INT16 takes Tab, Shift+Tab and (with
; an enemy chosen) Enter out of what it sees. For Enter, the main loop (TARGET_CLICK, from
; PROBE_SCROLL) puts the pointer at the enemy's feet and gives the game a left click there, while
; the routine that finds what is under the pointer (PROBE_HIT, DSUN.EXE 25B52h) answers with the
; chosen enemy for a moment (HIT_PASSES), whatever is in front of it.

KEY_TAB     equ 0x0F09
KEY_BACKTAB equ 0x0F00
KEY_ENTER   equ 0x1C0D
KEY_PAD_ENTER equ 0xE00D
HIT_PASSES  equ 600             ; passes of the map's main loop the hit is the chosen enemy, from the
                                ;   click (not BIOS ticks: the BIOS clock can stand still in a fight,
                                ;   and the hit stayed the enemy, the walk to it stalling)
EVENT_LEFT_PRESS equ 2
EVENT_LEFT_LEAVE equ 4
LEFT        equ 1

int16:
        cmp word [cs:target_on], 0
        je .chain
        cmp ah, 0x00
        je .read
        cmp ah, 0x10
        je .read
        cmp ah, 0x01
        je .peek
        cmp ah, 0x11
        je .peek
.chain: jmp far [cs:old16]
.peek:  push ax                         ; a key waiting: if it is ours, take it and say none is
        pushf
        call far [cs:old16]
        jz .none
        call our_key
        jc .took
        add sp, 2                       ; (not ours: as the BIOS said)
        push bp
        mov bp, sp
        and word [bp + 6], ~0x40        ; ZF clear
        pop bp
        iret
.took:  pop ax
        push ax
        xor ah, ah
        pushf
        call far [cs:old16]
        pop ax
        jmp .peek
.none:  pop ax
        push bp
        mov bp, sp
        or word [bp + 6], 0x40          ; ZF set: no key
        pop bp
        iret
.read:  push ax                         ; reading: ours are taken out and the next one read
.again: pop ax
        push ax
        pushf
        call far [cs:old16]
        call our_key
        jc .again
        add sp, 2
        iret

our_key:                                ; AX a key: CF set (and counted) if it is one of ours
        cmp ax, KEY_TAB
        jne .back
        inc word [cs:tab_seq]
        stc
        ret
.back:  cmp ax, KEY_BACKTAB
        jne .enter
        inc word [cs:back_seq]
        stc
        ret
.enter: cmp word [cs:hit_target], 0xFFFF
        je .no
        cmp ax, KEY_ENTER
        je .go
        cmp ax, KEY_PAD_ENTER
        jne .no
.go:    inc word [cs:attack_seq]
        mov byte [cs:t_click], 1
        stc
        ret
.no:    clc
        ret

; TARGET_CLICK: (in PROBE_SCROLL, DS = the game's) Enter on the chosen enemy: a left click at its feet
target_click:
        cmp byte [cs:t_click], 0
        je .ret
        mov byte [cs:t_click], 0
        cmp word [cs:game_mask], 0
        je .ret
        mov bx, [cs:hit_target]
        cmp bx, MAP_COUNT - 1
        ja .ret
        shl bx, 5
        mov cx, [bx + MAP_THINGS + 9]   ; its feet, on the map
        mov dx, [bx + MAP_THINGS + 11]
        sub cx, [CAM_X]
        sub dx, [CAM_Y]
        sub dx, 8                       ; (a little above them)
        cmp cx, 1
        jl .ret
        cmp cx, 0x13E
        jg .ret
        cmp dx, 1
        jl .ret
        cmp dx, 0xC6
        jg .ret
        mov [cs:t_x], cx
        mov [cs:t_y], dx
        mov ax, [cs:main_ticks]
        add ax, HIT_PASSES
        mov [cs:hit_until], ax
        mov byte [cs:hit_forced], 1
        mov ax, 4                       ; the pointer there
        mov cx, [cs:t_x]
        mov dx, [cs:t_y]
        int 0x33
        pusha                           ; and a click
        push ds
        push es
        mov ax, EVENT_LEFT_PRESS
        mov bx, LEFT
        mov cx, [cs:t_x]
        mov dx, [cs:t_y]
        xor si, si
        xor di, di
        call far [cs:game_handler]
        pop es
        pop ds
        popa
        pusha
        push ds
        push es
        mov ax, EVENT_LEFT_LEAVE
        xor bx, bx
        mov cx, [cs:t_x]
        mov dx, [cs:t_y]
        xor si, si
        xor di, di
        call far [cs:game_handler]
        pop es
        pop ds
        popa
.ret:   ret

; PROBE_HIT: INT VEC_HIT replaces "push bp / mov bp,sp / sub sp,10h" (6 bytes: INT + 4 NOPs) at the
; start of the routine that finds the thing under the pointer (camera x, y, x, y on screen; the
; thing, or FFFFh): for a moment after TARGET_CLICK, the chosen enemy, wherever the pointer is.
probe_hit:
        cmp byte [cs:hit_forced], 0
        je .plain
        cmp word [cs:hit_target], 0xFFFF
        je .over                        ; (no enemy chosen any more: a new turn, the click's done)
        push ax
        mov ax, [cs:main_ticks]
        sub ax, [cs:hit_until]
        pop ax
        jns .over
        push bp                         ; (the caller's flags back: the INT turned interrupts off,
        mov bp, sp                      ;   and a RETF leaves them so; the game then ran on without
        push word [bp + 6]              ;   its timer, a walk to the enemy frozen till something
        popf                            ;   turned them on again)
        pop bp
        add sp, 6                       ; (the interrupt's frame: back to the caller, the enemy)
        mov ax, [cs:hit_target]
        retf
.over:  mov byte [cs:hit_forced], 0
.plain: pop word [cs:h_resume]
        pop word [cs:h_resume + 2]
        popf
        push bp
        mov bp, sp
        sub sp, 0x10
        jmp far [cs:h_resume]

; PROBE_ITEM_BOX: INT VEC_ITEM_BOX replaces "push 0" (2 bytes) at the end of the routine that fills
; an item's box (right-click an item: its picture, price, name, damage, HEAVY, AC BONUS; DSUN.EXE
; 8C1A1h), its lines drawn (SI: the item's type, DI: the row after them, but for AC BONUS's).
; With SKILLS_ON's bits, a cloak's or boots' bonus to hiding in shadows or moving silently (the
; Ledger's rule: stealth.py), or a belt's to picking pockets and opening locks (PROBE_BELT), in the
; next row, with the routine's own text routine, as it draws AC BONUS; then the push, as the code
; would have.
IB_DRAW   equ 0x8C19A - 0x8C1A3 ; (DSUN.EXE) the text routine's far address in the call before,
                                ;   less the way back
TYPES_PTR equ 0x1669            ; DS: far pointer to the item types, 20 bytes each
TYPE_WORN equ 9                 ; in one: where it is worn (8: as a cloak, 4: on the feet, 2: as a belt)
SKILLS_STEALTH equ 1            ; (SKILLS_ON's bits)
SKILLS_BELT equ 2
SKILLS_ELVEN equ 4              ; (the Cloak and Boots of Elvenkind's: the stealth rule on)
TYPE_ARMOUR equ 0x0F            ; ... 80h: armour (AC BONUS drawn)
probe_item_box:
        pushad
        push es
        cmp word [cs:skills_on], 0
        je .push
        cmp si, 0x270F
        jae .push
        les bx, [TYPES_PTR]             ; (DS: the game's)
        imul ax, si, 20
        add bx, ax
        mov ax, si                      ; the Cloak and Boots of Elvenkind: their own lines
        sub ax, [cs:types_first]
        mov cl, SKILLS_ELVEN
        mov dx, ib_elf_hide
        cmp ax, ELVEN_CLOAK
        je .want
        mov dx, ib_elf_quiet
        cmp ax, ELVEN_BOOTS
        je .want
        mov al, [es:bx + TYPE_WORN]
        mov cl, SKILLS_STEALTH
        mov dx, ib_hide
        cmp al, 8
        je .want
        mov dx, ib_quiet
        cmp al, 4
        je .want
        mov cl, SKILLS_BELT
        mov dx, ib_belt
        cmp al, 2
        jne .push
.want:  test [cs:skills_on], cl
        jz .push
        mov ax, di                      ; the row: after AC BONUS's, if the box drew it
        test byte [es:bx + TYPE_ARMOUR], 0x80
        jz .row
        add ax, 7
.row:   mov si, sp
        mov bx, [ss:si + 34]            ; the way back (after ES and the PUSHAD)
        mov es, [ss:si + 36]
        mov ecx, [es:bx + IB_DRAW]
        mov [cs:ib_draw], ecx
        push dword 0x00960081           ; (as the box draws its lines)
        push ax                         ; y
        push word [bp - 0x0C]           ; x
        push cs
        push dx
        push dword [bp + 8]             ; the box's window
        call far [cs:ib_draw]
        add sp, 16
.push:  pop es
        popad
        sub sp, 2                       ; the PUSH 0: the interrupt's frame moved down a word
        push bp
        mov bp, sp
        push ax
        mov ax, [bp + 4]
        mov [bp + 2], ax                ; IP
        mov ax, [bp + 6]
        mov [bp + 4], ax                ; CS
        mov ax, [bp + 8]
        mov [bp + 6], ax                ; flags
        mov word [bp + 8], 0            ; the word pushed
        pop ax
        pop bp
        iret

ib_draw    dd 0
ib_hide    db 'Hide +10', 0     ; (the skills' short names, as the inventory screen's thief rows
ib_quiet   db 'Move +10', 0     ;   have them: HIDE, MOVE, PICK, LOCK; mixed case, as item names)
ib_belt    db 'Pick +5, Lock +5', 0
ib_elf_hide  db 'Hide 90-95%', 0    ; (the Cloak of Elvenkind's chance: stealth.py)
ib_elf_quiet db 'Move 95%', 0

; PROBE_BELT: INT VEC_BELT replaces "mov ax,si" (2 bytes) at the end of the game's thief skill
; routine (DSUN.EXE 803B2h: SI the chance, armour and effects counted; DI the thief's object; the
; game's [BP+8] the skill, a dword). With SKILLS_BELT, a thief wearing a belt (the waist slot)
; gets BELT_BONUS more to pick pockets (0) and open locks (1); an Assassin (kits.py) 15 less to
; those, a Swashbuckler 10 less to every skill, no less than 0, as the Ledger counts them (game.py's thief_skills_now); then AX = the chance, as
; the code would have.
WAIST      equ 5                ; the item's slot byte while worn as a belt
BELT_BONUS equ 5
COMBATANT_CREATURE equ 0xC37    ; in the things table: an object's creature (3 bytes an object)
probe_belt:
        sti
        mov ax, si
        cmp word [bp + 0x0A], 0
        jne .out
        push bx
        push cx
        push dx
        push es
        mov dx, si              ; DX the chance
        mov ax, ds
        add ax, THINGS_SEG
        mov [cs:r_things], ax
        mov es, ax
        imul bx, di, 3
        mov ax, [es:bx + COMBATANT_CREATURE]
        mov cx, ax
        cmp word [bp + 8], 1
        ja .kit                 ; (the belt: pick pockets and open locks only)
        test byte [cs:skills_on], SKILLS_BELT
        jz .kit
        mov word [cs:ws_slot], WAIST
        mov word [cs:ws_type], 0xFFFF
        push dx
        call worn_scan
        pop dx
        cmp word [cs:ws_count], 0
        je .kit
        add dx, BELT_BONUS
.kit:   mov ax, cx              ; the kit's (kits.thief_skill), no less than 0
        call kit_of_creature
        cmp al, KIT_SWASHBUCKLER
        jne .assassin
        sub dx, 10
        jmp .least
.assassin:
        cmp al, KIT_ASSASSIN
        jne .done
        cmp word [bp + 8], 1
        ja .done
        sub dx, 15
.least: or dx, dx
        jns .done
        xor dx, dx
.done:  mov ax, dx
        pop es
        pop dx
        pop cx
        pop bx
.out:   iret

old16      dd 0
t_click    db 0
hit_forced db 0
hit_until  dw 0
h_resume   dd 0

old33        dd 0
game_handler dd 0
game_mask    dw 0
centre       dw CENTRE_OFF, 0
s_resume     dd 0
s_resume_fl  dw 0
drag         db 0               ; 0: no drag; 1: the right button held, not moved yet; 2: dragging
drag_mid     db 0               ; 1: dragging with the wheel pressed
drag_new     db 0
drag_x       dw 0               ; where it was pressed
drag_y       dw 0
cam0_x       dw 0               ; the view's top left then
cam0_y       dw 0
pan_x_done   dw 0
pan_y_done   dw 0

loader     dw LOADER_OFF, 0
resume     dd 0
resume_fl  dw 0
floor_ret  dd 0
floor_frame dw 0
floor_kind db 0
floor_busy db 0
dark_ready db 0
mirror     db 0
r_n        db 0
sc_index   db 0
sc_mask    db 0
gc_index   db 0
gc_regs    db 1, 3, 4, 5, 8
gc_saved   times 5 db 0
zero       dw 0
game_ds    dw 0
load_seg   dw 0
clip_x0    dw 0
clip_y0    dw 0
clip_x1    dw 0
clip_y1    dw 0
v_seg      dw 0
v_x0       dw 0
v_y0       dw 0
v_row      dw 0
cam_x      dw 0
cam_y      dw 0
fig_x      dw 0
fig_y      dw 0
frame      dw 0
f_w        dw 0
f_bottom   dw 0
f_y        dw 0
r_x        dw 0
row_at     dw 0
best_d     dd 0
diffs      times 3 db 0
lit_sum    dw 0
lit_most   dw 0
best_c     db 0
want       times 3 db 0
shadow_tab times MAP_COUNT db 0
dark       times 256 db 0
dac        times 768 db 0

tbuf:   times TSIZE db 0

; KEEP: the last of installing, from here, as the ring is where the install code was: clear the
; ring and stay resident, the ring with the image (DX the paragraphs). The stack goes to the PSP's
; command tail first, out of the ring's way.
keep:   mov ax, [cs:psp]
        mov ss, ax
        mov sp, 0x100
        mov es, [cs:ring_seg]
        xor di, di
        xor ax, ax
        mov cx, RING_BYTES / 2
        cld
        rep stosw
        mov ax, 3100h
        int 21h

align 16
resident_end:                   ; (the ring follows, in a segment of its own)
RING_BYTES equ NENT * ESIZE

install:                        ; DS = ES = PSP, CS = the image
        mov [cs:psp], es
        push cs
        pop ds
        mov si, all_vectors     ; the vectors must be free
        mov cx, all_vectors_end - all_vectors
.check:
        lodsb
        mov ah, 35h
        int 21h                 ; ES:BX = the vector
        mov dx, es
        or dx, bx
        jz .free
        mov dx, busy
        mov ah, 9
        int 21h
        mov ax, 4C01h
        int 21h
.free:
        loop .check

        mov ax, 2500h + VEC_RAND
        mov dx, int_rand
        int 21h
        mov ax, 2500h + VEC_SAVE
        mov dx, probe_save
        int 21h
        mov ax, 2500h + VEC_AC
        mov dx, probe_ac
        int 21h
        mov ax, 2500h + VEC_TEXT
        mov dx, probe_text
        int 21h
        mov ax, 2500h + VEC_MSG
        mov dx, probe_msg
        int 21h
        mov ax, 2500h + VEC_CHAR
        mov dx, probe_char
        int 21h
        mov ax, 2500h + VEC_TURN
        mov dx, probe_turn
        int 21h
        mov ax, 2500h + VEC_USE
        mov dx, probe_use
        int 21h
        mov ax, 2500h + VEC_VIEW
        mov dx, probe_view
        int 21h
        mov ax, 2500h + VEC_WIN
        mov dx, probe_win
        int 21h
        mov ax, 2500h + VEC_LOOK
        mov dx, probe_look
        int 21h
        mov ax, 2500h + VEC_UNLOOK
        mov dx, probe_unlook
        int 21h
        mov ax, 2500h + VEC_NEXT
        mov dx, probe_next
        int 21h
        mov ax, 2500h + VEC_RING_AC
        mov dx, probe_ring_ac
        int 21h
        mov ax, 2500h + VEC_RING_SAVE
        mov dx, probe_ring_save
        int 21h
        mov ax, 2500h + VEC_WEAPON
        mov dx, probe_weapon
        int 21h
        mov ax, 2500h + VEC_MOVE
        mov dx, probe_move
        int 21h
        mov ax, 2500h + VEC_PICK
        mov dx, probe_pick
        int 21h
        mov ax, 2500h + VEC_USE_ITEM
        mov dx, probe_use_item
        int 21h
        mov ax, 2500h + VEC_TWO
        mov dx, probe_two
        int 21h
        mov ax, 2500h + VEC_DOUBLE
        mov dx, probe_double
        int 21h
        mov ax, 2500h + VEC_GRACE_CAST
        mov dx, probe_grace_cast
        int 21h
        mov ax, 2500h + VEC_GRACE_EFFECT
        mov dx, probe_grace_effect
        int 21h
        mov ax, 2500h + VEC_GRACE_ABILITY
        mov dx, probe_grace_ability
        int 21h
        mov ax, 2500h + VEC_NAMES_SIZE
        mov dx, probe_names_size
        int 21h
        mov ax, 2500h + VEC_NAMES_FILL
        mov dx, probe_names_fill
        int 21h
        mov ax, 2500h + VEC_STEALTH
        mov dx, probe_stealth
        int 21h
        mov ax, 2500h + VEC_TYPES_SIZE
        mov dx, probe_types_size
        int 21h
        mov ax, 2500h + VEC_TYPES_FILL
        mov dx, probe_types_fill
        int 21h
        mov ax, 2500h + VEC_LEVEL
        mov dx, probe_level
        int 21h
        mov ax, 2500h + VEC_HD_ROLL
        mov dx, probe_hd_roll
        int 21h
        mov ax, 2500h + VEC_HD_CON
        mov dx, probe_hd_con
        int 21h
        mov ax, 2500h + VEC_THIEF_SKILL
        mov dx, probe_thief_skill
        int 21h
        mov ax, 2500h + VEC_TWO_HANDED
        mov dx, probe_two_handed
        int 21h
        mov ax, 2500h + VEC_SPELL_TEXT
        mov dx, probe_spell_text
        int 21h
        mov ax, 2500h + VEC_CHUNK_ID
        mov dx, probe_chunk_id
        int 21h
        mov ax, 2500h + VEC_FLOOR_ALL
        mov dx, probe_floor_all
        int 21h
        mov ax, 2500h + VEC_FLOOR_RECT
        mov dx, probe_floor_rect
        int 21h
        mov ax, 2500h + VEC_REDRAW
        mov dx, probe_redraw
        int 21h
        mov ax, 2500h + VEC_REDRAW_ALL
        mov dx, probe_redraw_all
        int 21h
        mov ax, 2500h + VEC_SCROLL
        mov dx, probe_scroll
        int 21h
        mov ax, 2500h + VEC_HIT
        mov dx, probe_hit
        int 21h
        mov ax, 2500h + VEC_ITEM_BOX
        mov dx, probe_item_box
        int 21h
        mov ax, 2500h + VEC_BELT
        mov dx, probe_belt
        int 21h
        mov ax, 2500h + VEC_SAVE_PAGE
        mov dx, probe_save_page
        int 21h
        mov ax, 2500h + VEC_SAVE_CLICK
        mov dx, probe_save_click
        int 21h
        mov ax, 2500h + VEC_ITEM_WEAPON
        mov dx, probe_item_weapon
        int 21h
        mov ax, 2500h + VEC_ITEM_SKIP
        mov dx, probe_item_skip
        int 21h
        mov ax, 2500h + VEC_ITEM_ARMOUR
        mov dx, probe_item_armour
        int 21h
        mov ax, 2500h + VEC_SCRIPT_RAND
        mov dx, probe_script_rand
        int 21h
        mov ax, 2500h + VEC_XP_NEXT
        mov dx, probe_xp_next
        int 21h
        mov ax, 2500h + VEC_ATTACKS
        mov dx, probe_attacks
        int 21h
        mov ax, 2500h + VEC_SPEC_DAMAGE
        mov dx, probe_spec_damage
        int 21h
        mov ax, 2500h + VEC_DAM_LINE
        mov dx, probe_dam_line
        int 21h
        mov ax, 2500h + VEC_VIEW_DAM
        mov dx, probe_view_dam
        int 21h
        mov ax, 2500h + VEC_CAN_USE
        mov dx, probe_can_use
        int 21h
        mov ax, 2500h + VEC_NO_CAST
        mov dx, probe_no_cast
        int 21h
        mov ax, 2500h + VEC_MC_ROLL
        mov dx, probe_mc_roll
        int 21h
        mov ax, 2500h + VEC_MC_CON
        mov dx, probe_mc_con
        int 21h
        mov ax, 2500h + VEC_MC_UNCON
        mov dx, probe_mc_uncon
        int 21h
        mov ax, 2500h + VEC_WP_DISC_WIN
        mov dx, probe_wp_disc_win
        int 21h
        mov ax, 2500h + VEC_WP_SPHERE_WIN
        mov dx, probe_wp_sphere_win
        int 21h
        mov ax, 2500h + VEC_WP_DISC_CLICK
        mov dx, probe_wp_disc_click
        int 21h
        mov ax, 2500h + VEC_WP_SPHERE_CLICK
        mov dx, probe_wp_sphere_click
        int 21h
        mov ax, 2500h + VEC_WP_SHOWN
        mov dx, probe_wp_shown
        int 21h
        mov ax, 2500h + VEC_WP_CLASS
        mov dx, probe_wp_class
        int 21h
        mov ax, 2500h + VEC_LV_PICK
        mov dx, probe_lv_pick
        int 21h
        mov ax, 2500h + VEC_PK_COUNT
        mov dx, probe_pk_count
        int 21h
        mov ax, 2500h + VEC_PK_WIN
        mov dx, probe_pk_win
        int 21h
        mov ax, 2500h + VEC_PK_LEFT
        mov dx, probe_pk_left
        int 21h
        mov ax, 2500h + VEC_PK_TITLE
        mov dx, probe_pk_title
        int 21h
        mov ax, 2500h + VEC_PK_FILL
        mov dx, probe_pk_fill
        int 21h
        mov ax, 2500h + VEC_PK_CLICK
        mov dx, probe_pk_click
        int 21h
        mov ax, 2500h + VEC_HP_BEST
        mov dx, probe_hp_best
        int 21h
        mov ax, 2500h + VEC_TOME
        mov dx, probe_tome
        int 21h
        mov ax, 2500h + VEC_EF_ROWS
        mov dx, probe_ef_rows
        int 21h
        mov ax, 2500h + VEC_INIT
        mov dx, probe_init
        int 21h
        mov ax, 2500h + VEC_THAC0
        mov dx, probe_thac0
        int 21h
        mov ax, 2500h + VEC_SLOTS
        mov dx, probe_slots
        int 21h
        mov ax, 2500h + VEC_SLOT_LEVEL
        mov dx, probe_slot_level
        int 21h
        mov ax, 2500h + VEC_PSP_USE
        mov dx, probe_psp_use
        int 21h
        mov ax, 2500h + VEC_PSP_TABLE
        mov dx, probe_psp_table
        int 21h
        mov ax, 2500h + VEC_PSP_DEFENCE
        mov dx, probe_psp_defence
        int 21h
        mov ax, 2500h + VEC_CURE
        mov dx, probe_cure
        int 21h
        mov ax, 2500h + VEC_PSP_KEEP
        mov dx, probe_psp_keep
        int 21h
        mov ax, 2500h + VEC_PSP_KEEP_DX
        mov dx, probe_psp_keep_dx
        int 21h
        mov ax, 2500h + VEC_HIT_ROUND
        mov dx, probe_hit_round
        int 21h
        mov ax, 2500h + VEC_CAST_LEVEL
        mov dx, probe_cast_level
        int 21h
        mov ax, 2500h + VEC_SPELL_LEVEL
        mov dx, probe_spell_level
        int 21h
        mov ax, 2500h + VEC_PICK_ANY
        mov dx, probe_pick_any
        int 21h
        mov ax, 2500h + VEC_RANGER_LEVEL
        mov dx, probe_ranger_level
        int 21h
        mov ax, 2500h + VEC_HIT_DIE
        mov dx, probe_hit_die
        int 21h
        mov ax, 2500h + VEC_MAX_PSP
        mov dx, probe_max_psp
        int 21h
        mov ax, 2500h + VEC_CR_DIE
        mov dx, probe_cr_die
        int 21h
        mov ax, 2500h + VEC_CR_PSP
        mov dx, probe_cr_psp
        int 21h
        mov ax, 2500h + VEC_EL_GRANT
        mov dx, probe_el_grant
        int 21h
        mov ax, 2500h + VEC_EL_CAST
        mov dx, probe_el_cast
        int 21h
        mov ax, 2500h + VEC_EL_LEVEL
        mov dx, probe_el_level
        int 21h
        mov ax, 2500h + VEC_EL_KNOW
        mov dx, probe_el_know
        int 21h
        mov ax, 2500h + VEC_DUAL_BAN
        mov dx, probe_dual_ban
        int 21h
        mov ax, 2500h + VEC_PICK_LEVEL
        mov dx, probe_pick_level
        int 21h
        mov ax, 2500h + VEC_PICK_LIST
        mov dx, probe_pick_list
        int 21h
        mov ax, 2500h + VEC_SCROLL_LEARN
        mov dx, probe_scroll_learn
        int 21h
        mov ax, 2500h + VEC_RANGER_CAST
        mov dx, probe_ranger_cast
        int 21h
        mov ax, 3516h           ; the keyboard's (TARGETING)
        int 21h
        mov [old16], bx
        mov [old16 + 2], es
        mov ax, 2516h
        mov dx, int16
        int 21h
        mov ax, 3533h           ; the mouse driver's (SCROLLING), if there is one
        int 21h
        mov ax, es
        or ax, bx
        jz .no_mouse
        mov [old33], bx
        mov [old33 + 2], es
        mov ax, 2533h
        mov dx, int33
        int 21h
.no_mouse:
        mov ax, 3521h           ; DOS itself last: opening the objects file (PROBE_DOS_OPEN)
        int 21h
        mov [old21], bx
        mov [old21 + 2], es
        mov ax, 2521h
        mov dx, probe_dos_open
        int 21h
        mov byte [hooked], 1

        mov es, [cs:psp]
        mov es, [es:0x2C]       ; free our copy of the environment
        mov ah, 49h
        int 21h
        mov dx, msg
        mov ah, 9
        int 21h
        mov ax, cs              ; the ring: the paragraphs after the resident image
        add ax, (resident_end - hdr) / 16
        mov [cs:ring_seg], ax
        mov dx, 0x10 + (resident_end - hdr) / 16 + RING_BYTES / 16  ; PSP, image and ring, in paragraphs
        jmp keep

msg     db 'Dark Sun companion dice log helper loaded.', 13, 10, '$'
psp     dw 0
busy    db 'DSCLOG: interrupts 60h-65h or 9Dh-FEh are in use (already loaded?). Not loaded.', 13, 10, '$'
all_vectors db VEC_RAND, VEC_SAVE, VEC_AC, VEC_TEXT, VEC_MSG, VEC_CHAR, VEC_TURN, VEC_USE, VEC_VIEW, VEC_WIN, VEC_LOOK, VEC_UNLOOK, VEC_NEXT, VEC_RING_AC, VEC_RING_SAVE, VEC_WEAPON, VEC_MOVE, VEC_PICK, VEC_USE_ITEM, VEC_TWO, VEC_DOUBLE, VEC_GRACE_CAST, VEC_GRACE_EFFECT, VEC_GRACE_ABILITY, VEC_NAMES_SIZE, VEC_NAMES_FILL, VEC_STEALTH, VEC_TYPES_SIZE, VEC_TYPES_FILL, VEC_LEVEL, VEC_HD_ROLL, VEC_HD_CON, VEC_THIEF_SKILL, VEC_TWO_HANDED, VEC_SPELL_TEXT, VEC_CHUNK_ID, VEC_FLOOR_ALL, VEC_FLOOR_RECT, VEC_REDRAW, VEC_REDRAW_ALL, VEC_SCROLL, VEC_HIT, VEC_ITEM_BOX, VEC_BELT, VEC_SAVE_PAGE, VEC_SAVE_CLICK, VEC_ITEM_WEAPON, VEC_ITEM_SKIP, VEC_ITEM_ARMOUR, VEC_SCRIPT_RAND, VEC_XP_NEXT, VEC_ATTACKS, VEC_SPEC_DAMAGE, VEC_DAM_LINE, VEC_VIEW_DAM, VEC_CAN_USE, VEC_NO_CAST, VEC_MC_ROLL, VEC_MC_CON, VEC_MC_UNCON, VEC_WP_DISC_WIN, VEC_WP_SPHERE_WIN, VEC_WP_DISC_CLICK, VEC_WP_SPHERE_CLICK, VEC_WP_SHOWN, VEC_WP_CLASS, VEC_LV_PICK, VEC_PK_COUNT, VEC_PK_WIN, VEC_PK_LEFT, VEC_PK_TITLE, VEC_PK_FILL, VEC_PK_CLICK, VEC_EF_ROWS, VEC_HP_BEST, VEC_TOME, VEC_INIT, VEC_THAC0, VEC_SLOTS, VEC_SLOT_LEVEL
            db VEC_PSP_USE, VEC_PSP_TABLE, VEC_PSP_DEFENCE, VEC_CURE, VEC_PSP_KEEP, VEC_RANGER_CAST, VEC_PSP_KEEP_DX
            db VEC_HIT_ROUND, VEC_CAST_LEVEL, VEC_PICK_LEVEL, VEC_PICK_LIST, VEC_SCROLL_LEARN, VEC_SPELL_LEVEL, VEC_PICK_ANY, VEC_RANGER_LEVEL, VEC_HIT_DIE, VEC_MAX_PSP, VEC_CR_DIE, VEC_CR_PSP, VEC_EL_GRANT, VEC_EL_CAST, VEC_EL_LEVEL, VEC_EL_KNOW, VEC_DUAL_BAN
all_vectors_end:

        align 16, db 0
image_len equ $ - $$
file_len  equ image_len + 32
; the memory past the image to load in: up to the ring's end (the ring follows the resident part,
; over the install code), and the install's stack beyond it (resident_end + RING_BYTES is the
; larger: the install code and its stack are smaller than the ring)
LOAD_EXTRA equ (resident_end - hdr + RING_BYTES - image_len + STACK + 15) / 16
